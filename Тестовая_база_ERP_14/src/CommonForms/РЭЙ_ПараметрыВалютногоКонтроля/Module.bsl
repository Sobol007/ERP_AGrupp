
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// Лимит оформления паспорта сделки
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Регистр.Период КАК Период
	|ИЗ
	|	РегистрСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля КАК Регистр
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		НаДату = Выборка.Период;
	Иначе
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	ПолучитьНастройкиНаДату();
	Если Не ЗначениеЗаполнено(ВалютаЛимитов) Тогда
		ВалютаЛимитов = Справочники.Валюты.НайтиПоКоду(643);
	КонецЕсли;
	
	// Вид обработки кода ВО из входящего платежа
	ВидОбработкиКодаВОИзВходящегоПлатежа = Константы.РЭЙ_ВидОбработкиКодаВОИзВходящегоПлатежа.Получить();
	
	// Контроль сумм в справках ВК
	Если Константы.РЭЙ_НеКонтролироватьСуммыВСправкахВалютногоКонтроля.Получить() Тогда
		НеКонтролироватьСуммыВСправкахВалютногоКонтроля = 1;
	Иначе
		НеКонтролироватьСуммыВСправкахВалютногоКонтроля = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	Если ПроверитьЗаполнение() Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	// Лимит оформления паспорта сделки
	Запись = РегистрыСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.СоздатьМенеджерЗаписи();
	Запись.Период = НаДату;
	Запись.ВалютаЛимитов = ВалютаЛимитов;
	Запись.ЛимитБезПредоставленияДокументовВБанк = ЛимитБезПредоставленияДокументовВБанк;
	Запись.ЛимитБезПостановкиНаУчетКонтрактаИмпорт = ЛимитБезПостановкиНаУчетКонтрактаИмпорт;
	Запись.ЛимитБезПостановкиНаУчетКонтрактаЭкспорт = ЛимитБезПостановкиНаУчетКонтрактаЭкспорт;
	Запись.ЛимитБезПостановкиНаУчетКредитногоДоговора = ЛимитБезПостановкиНаУчетКредитногоДоговора;
	Запись.Записать();
	РегистрыСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.ОчиститьРегистр(КонецДня(НаДату) + 1);
	
	// Вид обработки кода ВО из входящего платежа
	Константы.РЭЙ_ВидОбработкиКодаВОИзВходящегоПлатежа.Установить(ВидОбработкиКодаВОИзВходящегоПлатежа);
	
	// Контроль сумм в справках ВК
	Если НеКонтролироватьСуммыВСправкахВалютногоКонтроля = 1 Тогда
		Константы.РЭЙ_НеКонтролироватьСуммыВСправкахВалютногоКонтроля.Установить(Истина);
	Иначе
		Константы.РЭЙ_НеКонтролироватьСуммыВСправкахВалютногоКонтроля.Установить(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройкиНаДату()
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Регистр.ВалютаЛимитов КАК ВалютаЛимитов,
	|	Регистр.ЛимитБезПредоставленияДокументовВБанк КАК ЛимитБезПредоставленияДокументовВБанк,
	|	Регистр.ЛимитБезПостановкиНаУчетКонтрактаИмпорт КАК ЛимитБезПостановкиНаУчетКонтрактаИмпорт,
	|	Регистр.ЛимитБезПостановкиНаУчетКонтрактаЭкспорт КАК ЛимитБезПостановкиНаУчетКонтрактаЭкспорт,
	|	Регистр.ЛимитБезПостановкиНаУчетКредитногоДоговора КАК ЛимитБезПостановкиНаУчетКредитногоДоговора
	|ИЗ
	|	РегистрСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.СрезПоследних(&НаДату, ) КАК Регистр";
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВалютаЛимитов = Выборка.ВалютаЛимитов;
		ЛимитБезПредоставленияДокументовВБанк = Выборка.ЛимитБезПредоставленияДокументовВБанк;
		ЛимитБезПостановкиНаУчетКонтрактаИмпорт = Выборка.ЛимитБезПостановкиНаУчетКонтрактаИмпорт;
		ЛимитБезПостановкиНаУчетКонтрактаЭкспорт = Выборка.ЛимитБезПостановкиНаУчетКонтрактаЭкспорт;
		ЛимитБезПостановкиНаУчетКредитногоДоговора = Выборка.ЛимитБезПостановкиНаУчетКредитногоДоговора;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура НаДатуПриИзменении(Элемент)
	ПолучитьНастройкиНаДату();
КонецПроцедуры


