
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьДополнительныеРеквизитыИСведения = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения");
	ИспользоватьКлассификаторы					 = ПолучитьФункциональнуюОпцию("CRM_ИспользоватьКлассификаторы");
	
	ИнциализироватьПараметрыКонтроляРеквизитов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоОбъектов

&НаКлиенте
Процедура ДеревоОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовПередУдалением(Элемент, Отказ)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ДеревоОбъектовПриАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРеквизитыКонтроля

&НаКлиенте
Процедура РеквизитыКонтроляПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыКонтроляПередУдалением(Элемент, Отказ)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыКонтроляПометкаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.РеквизитыКонтроля.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
		
	КонецЕсли;
	
	ЗаполнитьПометкиРекурсивно(ТекущиеДанные.ПолучитьЭлементы(), ТекущиеДанные.Пометка, "Пометка");
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыКонтроляПометкаПроцессаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РеквизитыКонтроля.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
		
	КонецЕсли;
	
	ЗаполнитьПометкиРекурсивно(ТекущиеДанные.ПолучитьЭлементы(), ТекущиеДанные.ПометкаПроцесса, "ПометкаПроцесса");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	СохранитьПараметрыКонтроля();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	СохранитьПараметрыКонтроля();	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключаемыеПроцедурыФункции

&НаКлиенте
Процедура Подключаемый_ДеревоОбъектовПриАктивизацииСтроки()
	
	ТекущиеДанные = Элементы.ДеревоОбъектов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	
	КонецЕсли;
	
	РазвернутьСтрокиДерева(ТекущиеДанные, "РеквизитыКонтроля");	
	
КонецПроцедуры // Подключаемый_ДеревоОбъектовПриАктивизацииСтроки()

#КонецОбласти

#Область ВспомогательныеПроцедурыФункции

&НаСервере
Процедура ИнциализироватьПараметрыКонтроляРеквизитов()

	ПараметрыКонтроля = Константы.CRM_ПараметрыКонтроляЗаполненностиРеквизитовОбъектов.Получить().Получить();
	Если ПараметрыКонтроля = Неопределено Тогда
		ПараметрыКонтроляРеквизитов = Новый Соответствие;
		ПараметрыКонтроляПроцесса   = Новый Соответствие;
	// +Костыль
	ИначеЕсли ТипЗнч(ПараметрыКонтроля) = Тип("Соответствие") Тогда
		ПараметрыКонтроляРеквизитов = ПараметрыКонтроля;
		ПараметрыКонтроляПроцесса   = Новый Соответствие;
	// -Костыль
	Иначе
		ПараметрыКонтроляРеквизитов = ПараметрыКонтроля.ПараметрыКонтроляРеквизитов;
		ПараметрыКонтроляПроцесса   = ПараметрыКонтроля.ПараметрыКонтроляПроцесса;
	КонецЕсли;
	
	// Таблица объектов
	ЭлементыДерева = ДеревоОбъектов.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	// Справочники
	ЗаполнитьОбъектыМетаданных(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, ЭлементыДерева, "Справочники", БиблиотекаКартинок.Справочник);	
	
КонецПроцедуры // ИнциализироватьПараметрыКонтроляРеквизитов()

&НаСервереБезКонтекста
Процедура ЗаполнитьОбъектыМетаданных(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, ЭлементыДерева, ТипОбъекта, Картинка)

	СтрокаДерева = ЭлементыДерева.Добавить();
	
	СтрокаДерева.ТипОбъекта		  = ТипОбъекта;
	СтрокаДерева.ПолноеИмяОбъекта = ТипОбъекта;
	СтрокаДерева.Синоним		  = ТипОбъекта;
	СтрокаДерева.Картинка		  = Картинка;
	
	МассивИсключаемыхСтрок = Новый Массив;
	Для каждого ОбъектМетаданных Из Метаданные.Справочники Цикл
		ЗаполнитьСтрокуОбъектаМетаданных(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, СтрокаДерева.ПолучитьЭлементы(), ТипОбъекта, ОбъектМетаданных, МассивИсключаемыхСтрок, Картинка);			
	КонецЦикла;
	
	Для каждого ИсключаемаяСтрока Из МассивИсключаемыхСтрок Цикл
		СтрокаДерева.ПолучитьЭлементы().Удалить(ИсключаемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьОбъектыМетаданных()

&НаСервереБезКонтекста
Процедура ЗаполнитьСтрокуОбъектаМетаданных(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, ЭлементыДерева, ТипОбъекта, ОбъектМетаданных, МассивИсключаемыхСтрок, Картинка)

	СтрокаДерева = ЭлементыДерева.Добавить();
	
	ПолноеИмяОбъекта = ОбъектМетаданных.ПолноеИмя();
	
	СтрокаДерева.ТипОбъекта		  = ТипОбъекта;
	СтрокаДерева.ПолноеИмяОбъекта = ПолноеИмяОбъекта;
	СтрокаДерева.Синоним		  = ОбъектМетаданных.Синоним;
	СтрокаДерева.Картинка		  = Картинка;
	
	// Контроль реквизитов
	ПараметрыКонтроляОбъекта = ПараметрыКонтроляРеквизитов.Получить(ПолноеИмяОбъекта);
	Если ПараметрыКонтроляОбъекта <> Неопределено Тогда
		СтрокаДерева.РассчетПриЗаписи = ПараметрыКонтроляОбъекта.РассчетПриЗаписи;
		
	КонецЕсли;
	
	// Контроль запуска процесса
	ПараметрыКонтроляПроцессаОбъекта = ПараметрыКонтроляПроцесса.Получить(ПолноеИмяОбъекта);
	Если ПараметрыКонтроляПроцессаОбъекта <> Неопределено Тогда
		СтрокаДерева.РассчетПриЗаписи = ПараметрыКонтроляПроцессаОбъекта.РассчетПриЗаписи;
		
	КонецЕсли;
	
	ЗаполнитьРеквизитыКонтроляОбъекта(ПараметрыКонтроляОбъекта, ПараметрыКонтроляПроцессаОбъекта, СтрокаДерева.РеквизитыКонтроля, ОбъектМетаданных);
	Если СтрокаДерева.РеквизитыКонтроля.ПолучитьЭлементы().Количество() = 0 Тогда
		МассивИсключаемыхСтрок.Добавить(СтрокаДерева);
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтрокуОбъектаМетаданных()

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыКонтроляОбъекта(ПараметрыКонтроляОбъекта, ПараметрыКонтроляПроцессаОбъекта, РеквизитыКонтроля, ОбъектМетаданных)
	
	ЭлементыРеквизитыКонтроля = РеквизитыКонтроля.ПолучитьЭлементы();
	
	// Реквизиты
	Если ОбъектМетаданных.Реквизиты.Количество() > 0 Тогда
		СтрокаТипаРеквизита = ЭлементыРеквизитыКонтроля.Добавить();
		
		ТипРеквизита = "РеквизитОбъекта";
		СтруктураРеквизита = Новый Структура;	
		СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
		СтруктураРеквизита.Вставить("СсылкаРеквизита", ТипРеквизита);
		СтруктураРеквизита.Вставить("Представление"  , "Реквизиты");
		СтруктураРеквизита.Вставить("Пометка"		 , Ложь);
		ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита, СтруктураРеквизита);
		
		ТекущиеПараметрыКонтроля 		 = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта        , ТипРеквизита);
		ТекущиеПараметрыКонтроляПроцесса = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляПроцессаОбъекта, ТипРеквизита);
		Для каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
			СтруктураРеквизита = Новый Структура;	
			СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
			СтруктураРеквизита.Вставить("СсылкаРеквизита", Реквизит.Имя);
			СтруктураРеквизита.Вставить("Представление"  , Реквизит.Синоним);
			СтруктураРеквизита.Вставить("ИндексКартинки" , 22);
			СтруктураРеквизита.Вставить("Пометка"		 , ТекущиеПараметрыКонтроля.Найти(Реквизит.Имя) <> Неопределено);
			СтруктураРеквизита.Вставить("ПометкаПроцесса", ТекущиеПараметрыКонтроляПроцесса.Найти(Реквизит.Имя) <> Неопределено);
			ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита.ПолучитьЭлементы().Добавить(), СтруктураРеквизита);
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Табличные части
	Если ОбъектМетаданных.ТабличныеЧасти.Количество() > 0 Тогда	
		МассивТабличныйЧастей = Новый Массив;
		
		Исключения = Новый Соответствие;
		Исключения.Вставить("КонтактнаяИнформация"   , Истина);
		Исключения.Вставить("ДополнительныеРеквизиты", Истина);
		Для каждого Реквизит Из ОбъектМетаданных.ТабличныеЧасти Цикл
			Если Исключения.Получить(Реквизит.Имя) = Истина Тогда
				Продолжить;
				
			КонецЕсли;
			
			МассивТабличныйЧастей.Добавить(Реквизит);
			
		КонецЦикла;
		
		Если МассивТабличныйЧастей.Количество() > 0 Тогда
			СтрокаТипаРеквизита = ЭлементыРеквизитыКонтроля.Добавить();
			
			ТипРеквизита = "ТабличнаяЧастьОбъекта";
			СтруктураРеквизита = Новый Структура;	
			СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
			СтруктураРеквизита.Вставить("СсылкаРеквизита", ТипРеквизита);
			СтруктураРеквизита.Вставить("Представление"  , "Табличные части");
			СтруктураРеквизита.Вставить("Пометка"		 , Ложь);
			ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита, СтруктураРеквизита);
			
			ТекущиеПараметрыКонтроля 		 = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта		, ТипРеквизита);
			ТекущиеПараметрыКонтроляПроцесса = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляПроцессаОбъекта, ТипРеквизита);
			Для каждого Реквизит Из МассивТабличныйЧастей Цикл
				СтруктураРеквизита = Новый Структура;	
				СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
				СтруктураРеквизита.Вставить("СсылкаРеквизита", Реквизит.Имя);
				СтруктураРеквизита.Вставить("Представление"  , Реквизит.Синоним);
				СтруктураРеквизита.Вставить("ИндексКартинки" , 23);
				СтруктураРеквизита.Вставить("Пометка"		 , ТекущиеПараметрыКонтроля.Найти(Реквизит.Имя) <> Неопределено);
				СтруктураРеквизита.Вставить("ПометкаПроцесса", ТекущиеПараметрыКонтроляПроцесса.Найти(Реквизит.Имя) <> Неопределено);
				ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита.ПолучитьЭлементы().Добавить(), СтруктураРеквизита);
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// Доп. реквизиты 
	Если ИспользоватьДопРеквизиты(ОбъектМетаданных) Тогда
		ОписаниеОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя()).ПустаяСсылка();
		НаборыСвойствОбъекта = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(ОписаниеОбъекта);
		Если НаборыСвойствОбъекта.Количество() > 0 Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("НаборыСвойств", НаборыСвойствОбъекта.ВыгрузитьКолонку("Набор"));
			
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ДополнительныеРеквизиты.Свойство,
				|	ДополнительныеРеквизиты.Свойство.Представление КАК Представление
				|ИЗ
				|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
				|ГДЕ
				|	ДополнительныеРеквизиты.Ссылка В (&НаборыСвойств)
				|
				|УПОРЯДОЧИТЬ ПО
				|	ДополнительныеРеквизиты.НомерСтроки";
					
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				СтрокаТипаРеквизита = ЭлементыРеквизитыКонтроля.Добавить();
				
				ТипРеквизита = "ДополнительныйРеквизитОбъекта";
				СтруктураРеквизита = Новый Структура;	
				СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
				СтруктураРеквизита.Вставить("СсылкаРеквизита", ТипРеквизита);
				СтруктураРеквизита.Вставить("Представление"  , "Доп. реквизиты");
				СтруктураРеквизита.Вставить("Пометка"		 , Ложь);
				СтруктураРеквизита.Вставить("ПометкаПроцесса", Ложь);
				ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита, СтруктураРеквизита);
				
				ТекущиеПараметрыКонтроля 		 = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта		, ТипРеквизита);
				ТекущиеПараметрыКонтроляПроцесса = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляПроцессаОбъекта, ТипРеквизита);
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					СтруктураРеквизита = Новый Структура;	
					СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
					СтруктураРеквизита.Вставить("СсылкаРеквизита", Выборка.Свойство);
					СтруктураРеквизита.Вставить("Представление"  , Выборка.Представление);
					СтруктураРеквизита.Вставить("ИндексКартинки" , 24);
					СтруктураРеквизита.Вставить("Пометка"		 , ТекущиеПараметрыКонтроля.Найти(Выборка.Свойство) <> Неопределено);
					СтруктураРеквизита.Вставить("ПометкаПроцесса", ТекущиеПараметрыКонтроляПроцесса.Найти(Выборка.Свойство) <> Неопределено);
					ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита.ПолучитьЭлементы().Добавить(), СтруктураРеквизита);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
				
	КонецЕсли;
	
	// Виды КИ
	Если ИспользоватьКИ(ОбъектМетаданных) Тогда
		ГруппаВидовКИ = CRM_УправлениеКонтактнойИнформацией.ГруппаВидовКонтактнойИнформацииПоМетаданным(ОбъектМетаданных);
		Если ГруппаВидовКИ <> Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ГруппаВидовКИ", ГруппаВидовКИ);
			
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	СправочникВидыКонтактнойИнформации.Ссылка КАК ВидКИ,
				|	ВЫБОР
				|		КОГДА СправочникВидыКонтактнойИнформации.ЭтоГруппа
				|			ТОГДА ВЫБОР
				|					КОГДА СправочникВидыКонтактнойИнформации.ПометкаУдаления
				|						ТОГДА 1
				|					КОГДА СправочникВидыКонтактнойИнформации.Предопределенный
				|						ТОГДА 2
				|					ИНАЧЕ 0
				|				КОНЕЦ
				|		КОГДА СправочникВидыКонтактнойИнформации.ПометкаУдаления
				|			ТОГДА 4
				|		КОГДА СправочникВидыКонтактнойИнформации.Предопределенный
				|			ТОГДА ВЫБОР СправочникВидыКонтактнойИнформации.Тип
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
				|						ТОГДА 14
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
				|						ТОГДА 15
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.ВебСтраница)
				|						ТОГДА 16
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Факс)
				|						ТОГДА 17
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Другое)
				|						ТОГДА 18
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
				|						ТОГДА 19
				|					КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Skype)
				|						ТОГДА 21
				|					ИНАЧЕ 3
				|				КОНЕЦ
				|		ИНАЧЕ ВЫБОР СправочникВидыКонтактнойИнформации.Тип
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
				|					ТОГДА 7
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
				|					ТОГДА 8
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.ВебСтраница)
				|					ТОГДА 9
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Факс)
				|					ТОГДА 10
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Другое)
				|					ТОГДА 11
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
				|					ТОГДА 12
				|				КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Skype)
				|					ТОГДА 20
				|				ИНАЧЕ 3
				|			КОНЕЦ
				|	КОНЕЦ КАК ИндексКартинки,
				|	СправочникВидыКонтактнойИнформации.Представление
				|ИЗ
				|	Справочник.ВидыКонтактнойИнформации КАК СправочникВидыКонтактнойИнформации
				|ГДЕ
				|	НЕ СправочникВидыКонтактнойИнформации.ПометкаУдаления
				|	И СправочникВидыКонтактнойИнформации.Используется
				|	И ЕСТЬNULL(СправочникВидыКонтактнойИнформации.Родитель.Используется, ИСТИНА)
				|	И СправочникВидыКонтактнойИнформации.Родитель В(&ГруппаВидовКИ)";
			
			
			РезультатЗапроса = Запрос.Выполнить();
			Если Не РезультатЗапроса.Пустой() Тогда
				СтрокаТипаРеквизита = ЭлементыРеквизитыКонтроля.Добавить();
				
				ТипРеквизита = "ВидКонтактнойИнформацииОбъекта";
				СтруктураРеквизита = Новый Структура;	
				СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
				СтруктураРеквизита.Вставить("СсылкаРеквизита", ТипРеквизита);
				СтруктураРеквизита.Вставить("Представление"  , "Виды контактной информации");
				СтруктураРеквизита.Вставить("Пометка"		 , Ложь);
				СтруктураРеквизита.Вставить("ПометкаПроцесса", Ложь);
				ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита, СтруктураРеквизита);
				
				ТекущиеПараметрыКонтроля 		 = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта		, ТипРеквизита);
				ТекущиеПараметрыКонтроляПроцесса = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляПроцессаОбъекта, ТипРеквизита);
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					СтруктураРеквизита = Новый Структура;	
					СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
					СтруктураРеквизита.Вставить("СсылкаРеквизита", Выборка.ВидКИ);
					СтруктураРеквизита.Вставить("Представление"  , Выборка.Представление);
					СтруктураРеквизита.Вставить("ИндексКартинки" , Выборка.ИндексКартинки);
					СтруктураРеквизита.Вставить("Пометка"		 , ТекущиеПараметрыКонтроля.Найти(Выборка.ВидКИ) <> Неопределено);
					СтруктураРеквизита.Вставить("ПометкаПроцесса", ТекущиеПараметрыКонтроляПроцесса.Найти(Выборка.ВидКИ) <> Неопределено);
					ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита.ПолучитьЭлементы().Добавить(), СтруктураРеквизита);
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Описание
	ОписаниеОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя()).ПустаяСсылка();
	
	// Классификаторы
	Если ИспользоватьКлассификаторы(ОписаниеОбъекта) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИмяТаблицы", ОбъектМетаданных.ПолноеИмя());
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	CRM_КлассификаторыПринадлежность.Ссылка КАК Классификатор,
			|	CRM_КлассификаторыПринадлежность.Ссылка.Представление
			|ИЗ
			|	ПланВидовХарактеристик.CRM_Классификаторы.Принадлежность КАК CRM_КлассификаторыПринадлежность
			|ГДЕ
			|	CRM_КлассификаторыПринадлежность.ИмяТаблицы = &ИмяТаблицы
			|	И CRM_КлассификаторыПринадлежность.Ссылка.ИспользуемаяАналитика = ЗНАЧЕНИЕ(ПланВидовХарактеристик.CRM_АналитикаКлассификаторов.ПустаяСсылка)";
		
		РезультатЗапроса = Запрос.Выполнить();                                      
		Если Не РезультатЗапроса.Пустой() Тогда
			СтрокаТипаРеквизита = ЭлементыРеквизитыКонтроля.Добавить();
			
			ТипРеквизита = "КлассификаторОбъекта";
			СтруктураРеквизита = Новый Структура;	
			СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
			СтруктураРеквизита.Вставить("СсылкаРеквизита", ТипРеквизита);
			СтруктураРеквизита.Вставить("Представление"  , "Классификаторы");
			СтруктураРеквизита.Вставить("Пометка"		 , Ложь);
			СтруктураРеквизита.Вставить("ПометкаПроцесса", Ложь);
			ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита, СтруктураРеквизита);
			
			ТекущиеПараметрыКонтроля		 = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта		, ТипРеквизита);
			ТекущиеПараметрыКонтроляПроцесса = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляПроцессаОбъекта, ТипРеквизита);
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				СтруктураРеквизита = Новый Структура;	
				СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
				СтруктураРеквизита.Вставить("СсылкаРеквизита", Выборка.Классификатор);
				СтруктураРеквизита.Вставить("Представление"  , Выборка.Представление);
				СтруктураРеквизита.Вставить("ИндексКартинки" , 25);
				СтруктураРеквизита.Вставить("Пометка"		 , ТекущиеПараметрыКонтроля.Найти(Выборка.Классификатор) <> Неопределено);
				СтруктураРеквизита.Вставить("ПометкаПроцесса", ТекущиеПараметрыКонтроляПроцесса.Найти(Выборка.Классификатор) <> Неопределено);
				ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита.ПолучитьЭлементы().Добавить(), СтруктураРеквизита);
				
			КонецЦикла;
						
		КонецЕсли;
	
	КонецЕсли;
	
	//// Виды присоединенных файлов
	// Если вогУправлениеПрисоединеннымиФайламиКлиентСерверПовтИсп.ИспользоватьРедактированиеПрисоединенныхФайловПоВидам(ОписаниеОбъекта) Тогда
	//	Запрос = Новый Запрос;
	//	Запрос.УстановитьПараметр("ИмяТаблицы", "Справочник." + РаботаСФайламиСлужебный.ИмяСправочникаХраненияФайлов(ОписаниеОбъекта));
	//		
	//	Запрос.Текст = 
	//		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	//		|	ВидыПрисоединенныхФайлов.Ссылка КАК ВидФайла,
	//		|	ВидыПрисоединенныхФайлов.Представление
	//		|ИЗ
	//		|	Справочник.вогВидыПрисоединенныхФайлов КАК ВидыПрисоединенныхФайлов
	//		|ГДЕ
	//		|	ВидыПрисоединенныхФайлов.Принадлежность = &ИмяТаблицы
	//		|	И НЕ ВидыПрисоединенныхФайлов.ПометкаУдаления";
	//			
	//	РезультатЗапроса = Запрос.Выполнить();
	//	Если Не РезультатЗапроса.Пустой() Тогда
	//		СтрокаТипаРеквизита = ЭлементыРеквизитыКонтроля.Добавить();
	//		
	//		ТипРеквизита = "ВидыПрисоединенныхФайловОбъекта";
	//		СтруктураРеквизита = Новый Структура;	
	//		СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
	//		СтруктураРеквизита.Вставить("СсылкаРеквизита", ТипРеквизита);
	//		СтруктураРеквизита.Вставить("Представление"  , "Виды присоединенных файлов");
	//		СтруктураРеквизита.Вставить("Пометка"		 , Ложь);
	//		СтруктураРеквизита.Вставить("ПометкаПроцесса", Ложь);
	//		ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита, СтруктураРеквизита);
	//		
	//		ТекущиеПараметрыКонтроля 		 = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта		, ТипРеквизита);
	//		ТекущиеПараметрыКонтроляПроцесса = ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляПроцессаОбъекта, ТипРеквизита);
	//		Выборка = РезультатЗапроса.Выбрать();
	//		Пока Выборка.Следующий() Цикл
	//			СтруктураРеквизита = Новый Структура;	
	//			СтруктураРеквизита.Вставить("ТипРеквизита"   , ТипРеквизита);
	//			СтруктураРеквизита.Вставить("СсылкаРеквизита", Выборка.ВидФайла);
	//			СтруктураРеквизита.Вставить("Представление"  , Выборка.Представление);
	//			СтруктураРеквизита.Вставить("ИндексКартинки" , 2);
	//			СтруктураРеквизита.Вставить("Пометка"		 , ТекущиеПараметрыКонтроля.Найти(Выборка.ВидФайла) <> Неопределено);
	//			СтруктураРеквизита.Вставить("ПометкаПроцесса", ТекущиеПараметрыКонтроляПроцесса.Найти(Выборка.ВидФайла) <> Неопределено);
	//			ЗаполнитьЗначенияСвойств(СтрокаТипаРеквизита.ПолучитьЭлементы().Добавить(), СтруктураРеквизита);
	//			
	//		КонецЦикла;
	//					
	//	КонецЕсли;
	//		
	// КонецЕсли;
		
КонецПроцедуры // ЗаполнитьРеквизитыКонтроляОбъекта()

&НаСервереБезКонтекста
Функция ПолучитьТекущиеПараметрыКонтроля(ПараметрыКонтроляОбъекта, ТипРеквизита)
	
	Если ПараметрыКонтроляОбъекта <> Неопределено
	  И ПараметрыКонтроляОбъекта.Свойство(ТипРеквизита) Тогда
		Результат = ПараметрыКонтроляОбъекта[ТипРеквизита];	
		
	Иначе
		Результат = Новый Массив;
		
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции // ПолучитьТекущиеПараметрыКонтроля()

&НаСервере
Процедура СохранитьПараметрыКонтроля()
	
	ПараметрыКонтроляРеквизитов = Новый Соответствие;
	ПараметрыКонтроляПроцесса   = Новый Соответствие;
	
	ЭлементыДерева = ДеревоОбъектов.ПолучитьЭлементы();
	Для каждого СтрокаДерева Из ЭлементыДерева Цикл
		СохранитьСтрокиРеквизитовОбъекта(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, СтрокаДерева.ПолучитьЭлементы());				
	КонецЦикла;
	
	Если ПараметрыКонтроляРеквизитов.Количество() > 0
	  ИЛИ ПараметрыКонтроляПроцесса.Количество() > 0 Тогда
	  
	  	Результат = Новый Структура;
	  	Результат.Вставить("ПараметрыКонтроляРеквизитов", ПараметрыКонтроляРеквизитов);
	  	Результат.Вставить("ПараметрыКонтроляПроцесса"  , ПараметрыКонтроляПроцесса);
		
		Константы.CRM_ПараметрыКонтроляЗаполненностиРеквизитовОбъектов.Установить(
			Новый ХранилищеЗначения(Результат, Новый СжатиеДанных(9))
		);
		
	КонецЕсли;
	
	// Запуск рассчета заполненности
	ФоновыеЗадания.Выполнить("CRM_КонтрольЗаполненностиРеквизитовОбъектов.ВыполнитьРассчет",,, НСтр("ru='Рассчет заполненности реквизитов объектов.';en='Calculate the completeness of the details of objects.'"));
	
КонецПроцедуры // СохранитьПараметрыКонтроля()

&НаСервереБезКонтекста
Процедура СохранитьСтрокиРеквизитовОбъекта(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, ЭлементыДерева)
	
	Для каждого СтрокаОбъекта Из ЭлементыДерева Цикл
		СохранитьРеквизитыКонтроляОбъекта(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, СтрокаОбъекта, СтрокаОбъекта.РеквизитыКонтроля.ПолучитьЭлементы());		
	КонецЦикла;
	
КонецПроцедуры // СохранитьСтрокиРеквизитовОбъекта()

&НаСервереБезКонтекста
Процедура СохранитьРеквизитыКонтроляОбъекта(ПараметрыКонтроляРеквизитов, ПараметрыКонтроляПроцесса, СтрокаОбъекта, ЭлементыРеквизитыКонтроля)
	
	СтруктураТиповРеквизитов		 = Новый Структура;
	СтруктураТиповРеквизитовПроцесса = Новый Структура;
	Для каждого СтрокаТипаРеквизита Из ЭлементыРеквизитыКонтроля Цикл		
		СтруктураТиповРеквизитов.Вставить(СтрокаТипаРеквизита.ТипРеквизита		  , Новый Массив);
		СтруктураТиповРеквизитовПроцесса.Вставить(СтрокаТипаРеквизита.ТипРеквизита, Новый Массив);
		Для каждого СтрокаДерева Из СтрокаТипаРеквизита.ПолучитьЭлементы() Цикл
			Если СтрокаДерева.Пометка Тогда
				СтруктураТиповРеквизитов[СтрокаТипаРеквизита.ТипРеквизита].Добавить(СтрокаДерева.СсылкаРеквизита);
			
			КонецЕсли;	
			
			Если СтрокаДерева.ПометкаПроцесса Тогда
				СтруктураТиповРеквизитовПроцесса[СтрокаТипаРеквизита.ТипРеквизита].Добавить(СтрокаДерева.СсылкаРеквизита);
			
			КонецЕсли;	
			
		КонецЦикла;
				
		Если СтруктураТиповРеквизитов[СтрокаТипаРеквизита.ТипРеквизита].Количество() = 0 Тогда
			СтруктураТиповРеквизитов.Удалить(СтрокаТипаРеквизита.ТипРеквизита);		
		КонецЕсли;		
		
		Если СтруктураТиповРеквизитовПроцесса[СтрокаТипаРеквизита.ТипРеквизита].Количество() = 0 Тогда
			СтруктураТиповРеквизитовПроцесса.Удалить(СтрокаТипаРеквизита.ТипРеквизита);		
		КонецЕсли;			
		
	КонецЦикла;
	
	Если СтруктураТиповРеквизитов.Количество() > 0 Тогда
		СтруктураТиповРеквизитов.Вставить("РассчетПриЗаписи", СтрокаОбъекта.РассчетПриЗаписи);	
		ПараметрыКонтроляРеквизитов.Вставить(СтрокаОбъекта.ПолноеИмяОбъекта, СтруктураТиповРеквизитов);
	
	КонецЕсли;

	Если СтруктураТиповРеквизитовПроцесса.Количество() > 0 Тогда
		СтруктураТиповРеквизитовПроцесса.Вставить("РассчетПриЗаписи", СтрокаОбъекта.РассчетПриЗаписи);	
		ПараметрыКонтроляПроцесса.Вставить(СтрокаОбъекта.ПолноеИмяОбъекта, СтруктураТиповРеквизитовПроцесса);
	
	КонецЕсли;
	
КонецПроцедуры // СохранитьРеквизитыКонтроляОбъекта()

#Область Прочее

&НаСервереБезКонтекста
Функция ИспользоватьДопРеквизиты(МетаданныеВладельца)
	
	Возврат МетаданныеВладельца.ТабличныеЧасти.Найти("ДополнительныеРеквизиты") <> Неопределено
	      И МетаданныеВладельца <> Метаданные.Справочники.НаборыДополнительныхРеквизитовИСведений;
	
	  КонецФункции
	  
&НаСервереБезКонтекста
Функция ИспользоватьКИ(МетаданныеВладельца)
	
	Возврат МетаданныеВладельца.ТабличныеЧасти.Найти("КонтактнаяИнформация") <> Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользоватьКлассификаторы(ОписаниеОбъекта)
	Возврат Метаданные.ПланыВидовХарактеристик.CRM_Классификаторы.Тип.СодержитТип(ТипЗнч(ОписаниеОбъекта));
КонецФункции

&НаКлиенте
Процедура РазвернутьСтрокиДерева(ТекущиеДанные, ИмяРеквизита, Развернуть = Истина)
	
	СтрокиДерева = ТекущиеДанные[ИмяРеквизита].ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если Развернуть Тогда
			Элементы[ИмяРеквизита].Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
		Иначе
			Элементы[ИмяРеквизита].Свернуть(СтрокаДерева.ПолучитьИдентификатор());
		КонецЕсли;
							
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПометкиРекурсивно(ЭлементыДерева, Пометка, ИмяРеквизита)

	Для каждого СтрокаДерева Из ЭлементыДерева Цикл
		СтрокаДерева[ИмяРеквизита] = Пометка;		
		ЗаполнитьПометкиРекурсивно(СтрокаДерева.ПолучитьЭлементы(), Пометка, ИмяРеквизита);
	
	КонецЦикла;	

КонецПроцедуры // ЗаполнитьПометкиРекурсивно()

#КонецОбласти

#КонецОбласти

#КонецОбласти


