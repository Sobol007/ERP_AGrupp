
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоПравам();
	
	// Инициализация констант
	ОснованиеНоменклатура = Параметры.Основание;
	ОтборИспользованиеМатериала = "Используется";
	ОтборИспользованиеВидаРабочегоЦентра = "Используется";
	
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОснованиеНоменклатура, "ИспользованиеХарактеристик,Наименование");
	
	ХарактеристикиИспользуются = (ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры")
		И РеквизитыНоменклатуры.ИспользованиеХарактеристик <> Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать);
	Если НЕ ХарактеристикиИспользуются Тогда
		Элементы.УправлениеСпецификациямиГруппаХарактеристикаСпецификация.ОтображатьВШапке = Ложь;
	КонецЕсли;
	
	СписокПодразделенийНаФорме.Добавить(Справочники.СтруктураПредприятия.ПустаяСсылка(), "1");
	ИспользуетсяНесколькоПодразделенийДиспетчеров = Справочники.СтруктураПредприятия.ИспользуетсяНесколькоПодразделенийДиспетчеров();
	
	Если Параметры.Свойство("РежимСпецификацииИзделия") Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Спецификации изделия %1';
																				|en = 'Product BOM %1'"), РеквизитыНоменклатуры.Наименование);
		Элементы.СтраницаРазрешенияНаЗамену.Видимость = Ложь;
		Элементы.СтраницаМаршрутныеКарты.Видимость    = Ложь;
		Элементы.СтраницаСтруктураИзделия.Видимость   = Ложь;
		Элементы.ГруппаСтраницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Производство (Номенклатура: ""%1"")';
																				|en = 'Production (Products: ""%1"")'"), РеквизитыНоменклатуры.Наименование);
	КонецЕсли;
	
	СписокПользователей = ОбщегоНазначенияУТ.ПолучитьСписокПользователейСПравомДобавления(Метаданные.Документы.РазрешениеНаЗаменуМатериалов);
	ОтборыСписковКлиентСервер.СкопироватьСписокВыбораОтбораПоМенеджеру(Элементы.РазрешенияОтветственный.СписокВыбора, СписокПользователей);
	УстановитьСвойстваДинамическогоСпискаРазрешенийНаЗаменуМатериалов();
	РазрешенияУстановитьОтборПоНоменклатуре();
	
	СписокПользователей = ОбщегоНазначенияУТ.ПолучитьСписокПользователейСПравомДобавления(Метаданные.Справочники.МаршрутныеКарты);
	ОтборыСписковКлиентСервер.СкопироватьСписокВыбораОтбораПоМенеджеру(Элементы.МаршрутныеКартыОтветственный.СписокВыбора, СписокПользователей);
	УстановитьСвойстваДинамическогоСпискаМаршрутныхКарт();
	МаршрутныеКартыУстановитьОтборПоНоменклатуре(ЭтаФорма);

	СписокПользователей = ОбщегоНазначенияУТ.ПолучитьСписокПользователейСПравомДобавления(Метаданные.Документы.ПлановаяКалькуляция);
	ОтборыСписковКлиентСервер.СкопироватьСписокВыбораОтбораПоМенеджеру(Элементы.КалькуляцииОтветственный.СписокВыбора, СписокПользователей);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокПлановыхКалькуляций21, "ОтборИзделие", ОснованиеНоменклатура);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокПлановыхКалькуляций22, "ОтборИзделие", ОснованиеНоменклатура);
	
	Элементы.УправлениеСпецификациямиСпецификацияТип.Видимость = УправлениеДаннымиОбИзделиях.ДоступноОписаниеТипаПроизводственногоПроцесса();
	ИнициализацияДереваСпецификаций();
	ЗаполнитьУправлениеСпецификациями();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокСпецификаций, "ОтборИзделие", ОснованиеНоменклатура);
	
	УстановитьСвойстваДинамическогоСпискаДоступныхСпецификаций();

	Если Параметры.Свойство("АктивизироватьСпецификацию") Тогда
		СтруктураПоиска = Новый Структура("Спецификация", Параметры.АктивизироватьСпецификацию);
		СписокСтрок = УправлениеСпецификациями.НайтиСтроки(СтруктураПоиска);
		Если СписокСтрок.Количество() <> 0 Тогда
			Элементы.УправлениеСпецификациями.ТекущаяСтрока = СписокСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
	ОбновитьСтруктуруИзделия = Истина;
	ДинамическоеСчитывание = Истина;
	
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяТолькоУправлениеПроизводством21") Тогда
		
		Элементы.СтраницаПлановыеКалькуляции.Заголовок = НСтр("ru = 'Плановые калькуляции';
																|en = 'Standard costings'");
		Элементы.СтраницаПлановыеКалькуляции22.Видимость = Ложь;
		
	ИначеЕсли ПолучитьФункциональнуюОпцию("ИспользуетсяТолькоУправлениеПроизводством22") Тогда
		
		Элементы.СтраницаПлановыеКалькуляции22.Заголовок = НСтр("ru = 'Плановые калькуляции';
																|en = 'Standard costings'");
		Элементы.СтраницаПлановыеКалькуляции.Видимость = Ложь;
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РесурсныеСпецификации" 
		ИЛИ ИмяСобытия = "Запись_ОсновныеМаршрутныеКарты" 
		ИЛИ ИмяСобытия = "Запись_ОсновныеСпецификации" Тогда

		ОбновитьДанныеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОчиститьСообщения();
	
	ТекущаяСтрока = Элементы.ДеревоСпецификаций.ТекущаяСтрока;
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ФормаВыбораПоНоменклатуре" Тогда
		
		ИзменитьСпецификациюВТекущейСтроке(ТекущаяСтрока, ВыбранноеЗначение);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпецификацииСтатусПриИзменении(Элемент)
	
	ЗаполнитьУправлениеСпецификациями();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользованиеМатериалаПриИзменении(Элемент)
	
	Если ПустаяСтрока(ОтборИспользованиеМатериала) Тогда
		ОтборИспользованиеМатериала = "Используется";
	КонецЕсли; 
	ЗаполнитьУправлениеСпецификациями();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборМатериалПриИзменении(Элемент)
	
	ЗаполнитьУправлениеСпецификациями();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользованиеВидаРабочегоЦентраПриИзменении(Элемент)
	
	Если ПустаяСтрока(ОтборИспользованиеВидаРабочегоЦентра) Тогда
		ОтборИспользованиеВидаРабочегоЦентра = "Используется";
	КонецЕсли;
	ЗаполнитьУправлениеСпецификациями();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидРабочегоЦентраПриИзменении(Элемент)
	
	ЗаполнитьУправлениеСпецификациями();
	
КонецПроцедуры

&НаКлиенте
Процедура МаршрутныеКартыСтатусПриИзменении(Элемент)
	МаршрутныеКартыУстановитьОтборПоСтатусу(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура МаршрутныеКартыОтветственныйПриИзменении(Элемент)
	МаршрутныеКартыУстановитьОтборПоОтветственному(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура МаршрутныеКартыПодразделениеПриИзменении(Элемент)
	МаршрутныеКартыУстановитьОтборПоПодразделению(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура РазрешенияСтатусПриИзменении(Элемент)
	РазрешенияУстановитьОтборПоСтатусу();
КонецПроцедуры

&НаКлиенте
Процедура РазрешенияСпецификацияПриИзменении(Элемент)
	РазрешенияУстановитьОтборПоСпецификации();
КонецПроцедуры

&НаКлиенте
Процедура РазрешенияОтветственныйПриИзменении(Элемент)
	РазрешенияУстановитьОтборПоОтветственному();
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцииСтатусПриИзменении(Элемент)
	КалькуляцииУстановитьОтборПоСтатусу(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцииВидПриИзменении(Элемент)
	КалькуляцииУстановитьОтборПоОбъекту(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КалькуляцииОтветственныйПриИзменении(Элемент)
	КалькуляцииУстановитьОтборПоОтветственному(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаСтруктураИзделия
		И ОбновитьСтруктуруИзделия Тогда
		
		СформироватьДеревоСпецификацийНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоХарактеристикаНоменклатурыПриИзменении(Элемент)
	
	СписокСтрок = ДеревоСпецификаций.ПолучитьЭлементы();
	Если СписокСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификацийНаИзготовлениеСборку();
	
	ДанныеСпецификации = УправлениеДаннымиОбИзделияхВызовСервера.СпецификацияИзделия(
											ОснованиеНоменклатура,
											ДеревоХарактеристикаНоменклатуры,
											ДеревоДата,
											,
											ПараметрыВыбораСпецификаций,
											СписокСтрок[0].Спецификация);
											
	Если ДанныеСпецификации <> Неопределено Тогда
		СписокСтрок[0].Спецификация = ДанныеСпецификации.Спецификация;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыводитьЭтапыПриИзменении(Элемент)
	ДеревоИзмененыПараметрыОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыводитьВыходныеИзделияПриИзменении(Элемент)
	ДеревоИзмененыПараметрыОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыводитьМатериалыПриИзменении(Элемент)
	ДеревоИзмененыПараметрыОтбора();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыводитьТрудозатратыПриИзменении(Элемент)
	ДеревоИзмененыПараметрыОтбора();
КонецПроцедуры

&НаКлиенте
Процедура МаршрутныеКартыИспользуетсяКакПриИзменении(Элемент)
	
	МаршрутныеКартыУстановитьОтборПоНоменклатуре(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешениеНазначениеНоменклатурыПриИзменении(Элемент)
	
	РазрешенияУстановитьОтборПоНоменклатуре();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУправлениеСпецификациями

&НаКлиенте
Процедура УправлениеСпецификациямиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		КопироватьРесурснуюСпецификациюВСпискеУправлениеСпецификациями();
		Возврат
	КонецЕсли; 
	
	ТекущиеДанные = Элементы.УправлениеСпецификациями.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Основание = Новый Структура;
		Основание.Вставить("Номенклатура", ОснованиеНоменклатура);
		Основание.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	Иначе
		Основание = ОснованиеНоменклатура;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Основание", Основание);
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеСпецификациямиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеСпецификациямиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.УправлениеСпецификациями.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено 
		И ЗначениеЗаполнено(ТекущиеДанные.СпецификацияХарактеристика) 
		И ТекущиеДанные.ТипСтроки = 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,ТекущиеДанные.СпецификацияХарактеристика);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УправлениеСпецификациямиПодразделениеПриИзменении(Элемент)
	
	УстановитьОсновнуюСпецификацию(Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМаршрутныеКарты

&НаКлиенте
Процедура СписокМаршрутныхКартПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Группа Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		Отказ = Истина;
		
		КопироватьМаршрутнуюКарту();
		
	ИначеЕсли НЕ ОснованиеНоменклатура.Пустая() Тогда
		
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Основание", ОснованиеНоменклатура);
		ОткрытьФорму("Справочник.МаршрутныеКарты.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокМаршрутныхКартПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СписокКлючей = Строки.ПолучитьКлючи();
	Если СписокКлючей.ВГраница() = -1 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.МаршрутнаяКарта КАК Ссылка
	|ИЗ
	|	РегистрСведений.ОсновныеМаршрутныеКарты КАК Таблица
	|ГДЕ
	|	Таблица.МаршрутнаяКарта В (&МаршрутныеКарты)
	|	И (Таблица.Номенклатура = &Номенклатура
	|		ИЛИ &Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	) И Таблица.МаршрутнаяКарта.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхКарт.Действует)
	|");
	Запрос.УстановитьПараметр("МаршрутныеКарты", СписокКлючей);
	
	ПараметрДанных = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Номенклатура"));
	Если ПараметрДанных <> Неопределено Тогда
		Запрос.УстановитьПараметр("Номенклатура", ПараметрДанных.Значение);
	Иначе
		Запрос.УстановитьПараметр("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаСписка = Строки[Выборка.Ссылка];
		Для каждого Оформление Из СтрокаСписка.Оформление Цикл
			Оформление.Значение.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,Истина));
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСпецификации

&НаКлиенте
Процедура СписокСпецификацийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Группа Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		
		Отказ = Истина;
		
		КопироватьРесурснуюСпецификациюВСпискеСписокСпецификаций();
		
	ИначеЕсли НЕ ОснованиеНоменклатура.Пустая() Тогда
		
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Основание", ОснованиеНоменклатура);
		ОткрытьФорму("Справочник.РесурсныеСпецификации.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКалькуляции

&НаКлиенте
Процедура СписокПлановыхКалькуляцийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(ОснованиеНоменклатура);
	
	ЗначенияЗаполнения = Новый Структура("МассивОбъектов, ТипОснования", МассивОбъектов, Тип("СправочникСсылка.Номенклатура"));
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ПлановаяКалькуляция.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРазрешения

&НаКлиенте
Процедура СписокРазрешенийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование И НЕ ОснованиеНоменклатура.Пустая() И РазрешенияСпецификация.Пустая() Тогда
		
		Отказ = Истина;
		
		Основание = Новый Структура;
		
		Основание.Вставить("Материал", ОснованиеНоменклатура);
		
		ПараметрыФормы = Новый Структура("Основание", Основание);
		
		ОткрытьФорму("Документ.РазрешениеНаЗаменуМатериалов.ФормаОбъекта", ПараметрыФормы, , Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоСпецификаций

&НаКлиенте
Процедура ДеревоСпецификацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ДеревоСпецификацийНоменклатура" И ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) И 
		ТипЗнч(Элемент.ТекущиеДанные.Номенклатура) <> Тип("Строка") Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,Элемент.ТекущиеДанные.Номенклатура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСпецификацийПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиДерева",0.2,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСпецификацийСпецификацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоСпецификаций.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ВидСтроки = ПредопределенноеЗначение("Перечисление.ВидыСтрокДереваСпецификаций.ВыходноеИзделие") Тогда
		ИмяТЧ = "ВыходныеИзделия";
	Иначе
		ИмяТЧ = "МатериалыИУслуги";
	КонецЕсли;
	
	УправлениеДаннымиОбИзделияхКлиент.ОткрытьФормуВыбораСпецификацийПоНоменклатуре(
		ТекущиеДанные.Номенклатура,
		ТекущиеДанные.Характеристика,
		,
		,
		ПараметрыВыбораСпецификаций[ИмяТЧ],
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСпецификацийСпецификацияОчистка(Элемент, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.ДеревоСпецификаций.ТекущаяСтрока;
	ИзменитьСпецификациюВТекущейСтроке(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСпецификацийПередРазворачиванием(Элемент, Строка, Отказ)
	
	УправлениеДаннымиОбИзделияхКлиентСервер.ДеревоСпецификацийПередРазворачиванием(ЭтаФорма, Строка, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСпецификацийПередСворачиванием(Элемент, Строка, Отказ)
	
	УправлениеДаннымиОбИзделияхКлиентСервер.ДеревоСпецификацийПередСворачиванием(ЭтаФорма, Строка, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДоступныхСпецификаций

&НаКлиенте
Процедура СписокДоступныхСпецификацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СписокДоступныхСпецификаций.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение(,ТекущиеДанные.Спецификация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область КомандаСпискаУправлениеСпецификациями

&НаКлиенте
Процедура КомандыИзменитьСпецификацию(Команда)
	
	ТекущиеДанные = Элементы.УправлениеСпецификациями.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ТипСтроки <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Ключ", ТекущиеДанные.Спецификация);
	ОткрытьФорму("Справочник.РесурсныеСпецификации.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусВРазработке(Команда)
	
	УстановитьСтатусСпецификации("ВРазработке", НСтр("ru = 'В разработке';
													|en = 'Under development'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусДействует(Команда)
	
	УстановитьСтатусСпецификации("Действует", НСтр("ru = 'Действует';
													|en = 'Valid'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусЗакрыта(Команда)
	
	УстановитьСтатусСпецификации("Закрыта", НСтр("ru = 'Закрыта';
												|en = 'Closed'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСравнитьСпецификации(Команда)
	
	СписокИсточников = ПолучитьВыделенныеСпецификацииВСпискеУправлениеСпецификациями();
	Если СписокИсточников.Количество() <> 0 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СписокИсточников", СписокИсточников);
		ОткрытьФорму("Обработка.СравнениеСпецификаций.Форма", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДеревоСпецификации(Команда)
	
	ТекущиеДанные = Элементы.УправлениеСпецификациями.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ТипСтроки <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Спецификация", ТекущиеДанные.Спецификация); 
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина); 
	ПараметрыОткрытия.Вставить("РежимОтображения", "Спецификация");
	ОткрытьФорму("Обработка.ДеревоРесурсныхСпецификаций.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура КомандаОбновитьДанные(Команда)
	ОбновитьДанныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДерево(Команда)
	
	ОчиститьСообщения();
	
	СформироватьДеревоСпецификацийНаСервере();
	
	РазвернутьВсе(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьОсновнойСпецификацией(Команда)
	
	ТекущиеДанныеДерево = Элементы.ДеревоСпецификаций.ТекущиеДанные;
	ТекущиеДанныеСписокДоступных = Элементы.СписокДоступныхСпецификаций.ТекущиеДанные;
	
	Если ТекущиеДанныеДерево = Неопределено ИЛИ ТекущиеДанныеСписокДоступных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанныеСписокДоступных.Спецификация) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОшибки = Новый Структура;
	СвойстваСпецификации = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ТекущиеДанныеСписокДоступных.Спецификация, "Статус,ТипПроизводственногоПроцесса");
	
	Если НЕ УправлениеДаннымиОбИзделияхКлиентСервер.СпецификациюМожноНазначитьОсновной(СвойстваСпецификации, СтруктураОшибки) Тогда
		ПоказатьПредупреждение(,СтруктураОшибки.ТекстОшибки,, НСтр("ru = 'Назначить основной спецификацией';
																	|en = 'Set as the main BOR'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Спецификация, Номенклатура, Характеристика", 
		ТекущиеДанныеСписокДоступных.Спецификация, ТекущиеДанныеДерево.Номенклатура, ТекущиеДанныеДерево.Характеристика);
	
	ОткрытьФорму("РегистрСведений.ОсновныеСпецификации.Форма.НазначитьОсновнойСпецификацией", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.РесурсныеСпецификации.ПустаяСсылка"));
	
	ИменаМакетов = "ДеревоСпецификаций";
	ПараметрыПечати = ПараметрыПечатиДереваСпецификаций();
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Обработка.ДеревоРесурсныхСпецификаций",
		ИменаМакетов,
		ПараметрКоманды,
		ЭтаФорма,
		ПараметрыПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСкрыватьЗаголовкиГруппировок(Команда)
	
	ДеревоВыводитьЗаголовкиГруппировок = НЕ ДеревоВыводитьЗаголовкиГруппировок;
	Элементы.ДеревоСкрыватьЗаголовкиГруппировок.Пометка = НЕ ДеревоВыводитьЗаголовкиГруппировок;
	
	СформироватьДеревоСпецификацийНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Спецификации

&НаКлиенте
Процедура КопироватьРесурснуюСпецификациюВСпискеСписокСпецификаций()

	ТекущиеДанные = Элементы.СписокСпецификаций.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КопироватьРесурснуюСпецификациюВСпискеСписокСпецификацийЗавершение", ЭтотОбъект);
	УправлениеДаннымиОбИзделияхКлиент.КопироватьРесурснуюСпецификацию(ТекущиеДанные.Ссылка, ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура КопироватьРесурснуюСпецификациюВСпискеСписокСпецификацийЗавершение(Ссылка, ДополнительныеПараметры) Экспорт

	Если Ссылка <> Неопределено Тогда
		Элементы.СписокСпецификаций.ТекущаяСтрока = Ссылка;
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУправлениеСпецификациями(ТекущаяСпецификацияХарактеристика = Неопределено)
	
	Если НЕ ПравоПросмотраСпецификаций Тогда
		Возврат
	КонецЕсли;
	
	// Запомним текущую строку, чтобы потом сделать ее активной
	Если ТекущаяСпецификацияХарактеристика = Неопределено Тогда
		ТекущаяСтрока = Элементы.УправлениеСпецификациями.ТекущаяСтрока;
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущиеДанные = УправлениеСпецификациями.НайтиПоИдентификатору(ТекущаяСтрока);
			ТекущаяСпецификацияХарактеристика = ТекущиеДанные.СпецификацияХарактеристика;
		КонецЕсли;
	КонецЕсли;
	
	УправлениеСпецификациями.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	// 0
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТабличнаяЧасть.Ссылка КАК Спецификация,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ СпецификацииИХарактеристикиБезОтбора
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.Номенклатура = &Номенклатура
	|	И (ТабличнаяЧасть.Ссылка.Статус = &СпецификацииСтатус
	|		ИЛИ &СпецификацииСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ПустаяСсылка))
	|	И ТабличнаяЧасть.Ссылка.ТипПроизводственногоПроцесса В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Сборка),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Ремонт))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Ссылка,
	|	ТабличнаяЧасть.ХарактеристикаВходящегоИзделия
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК ТабличнаяЧасть
	|ГДЕ
	|	ТабличнаяЧасть.НоменклатураВходящегоИзделия = &Номенклатура
	|	И (ТабличнаяЧасть.Статус = &СпецификацииСтатус
	|		ИЛИ &СпецификацииСтатус = ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.ПустаяСсылка))
	|	И ТабличнаяЧасть.Ссылка.ТипПроизводственногоПроцесса = ЗНАЧЕНИЕ(Перечисление.ТипыПроизводственныхПроцессов.Разборка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаМатериалыИУслуги.Ссылка КАК Спецификация
	|ПОМЕСТИТЬ СпецификацииСОтборомПоМатериалу
	|ИЗ
	|	Справочник.РесурсныеСпецификации.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|ГДЕ
	|	ТаблицаМатериалыИУслуги.Номенклатура = &Материал
	|	И &Материал <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ТаблицаМатериалыИУслуги.Ссылка В
	|			(ВЫБРАТЬ
	|				СпецификацииИХарактеристикиБезОтбора.Спецификация
	|			ИЗ
	|				СпецификацииИХарактеристикиБезОтбора)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаЭтапыПроизводства.Владелец КАК Спецификация
	|ПОМЕСТИТЬ СпецификацииСОтборомПоВидуРабочегоЦентра
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ТаблицаЭтапыПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК ВидыРабочихЦентровЭтапов
	|		ПО (ВидыРабочихЦентровЭтапов.Ссылка = ТаблицаЭтапыПроизводства.Ссылка)
	|			И (ВидыРабочихЦентровЭтапов.ВидРабочегоЦентра = &ВидРабочегоЦентра)
	|			И (&ВидРабочегоЦентра <> ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка))
	|ГДЕ
	|	ТаблицаЭтапыПроизводства.Владелец В
	|			(ВЫБРАТЬ
	|				СпецификацииИХарактеристикиБезОтбора.Спецификация
	|			ИЗ
	|				СпецификацииИХарактеристикиБезОтбора)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпецификацииИХарактеристикиБезОтбора.Спецификация КАК Спецификация,
	|	СпецификацииИХарактеристикиБезОтбора.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ СпецификацииИХарактеристики
	|ИЗ
	|	СпецификацииИХарактеристикиБезОтбора КАК СпецификацииИХарактеристикиБезОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацииСОтборомПоМатериалу КАК СпецификацииСОтборомПоМатериалу
	|		ПО (СпецификацииСОтборомПоМатериалу.Спецификация = СпецификацииИХарактеристикиБезОтбора.Спецификация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацииСОтборомПоВидуРабочегоЦентра КАК СпецификацииСОтборомПоВидуРабочегоЦентра
	|		ПО (СпецификацииСОтборомПоВидуРабочегоЦентра.Спецификация = СпецификацииИХарактеристикиБезОтбора.Спецификация)
	|ГДЕ
	|	(&Материал = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ИЛИ &ИспользованиеМатериала = ""Используется""
	|				И НЕ СпецификацииСОтборомПоМатериалу.Спецификация ЕСТЬ NULL
	|			ИЛИ &ИспользованиеМатериала = ""НеИспользуется""
	|				И СпецификацииСОтборомПоМатериалу.Спецификация ЕСТЬ NULL)
	|	И (&ВидРабочегоЦентра = ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка)
	|			ИЛИ &ИспользованиеВидаРабочегоЦентра = ""Используется""
	|				И НЕ СпецификацииСОтборомПоВидуРабочегоЦентра.Спецификация ЕСТЬ NULL
	|			ИЛИ &ИспользованиеВидаРабочегоЦентра = ""НеИспользуется""
	|				И СпецификацииСОтборомПоВидуРабочегоЦентра.Спецификация ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникРесурсныеСпецификации.Ссылка КАК Спецификация,
	|	СправочникРесурсныеСпецификации.Статус КАК Статус,
	|	СправочникРесурсныеСпецификации.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	СправочникРесурсныеСпецификации.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Справочник.РесурсныеСпецификации КАК СправочникРесурсныеСпецификации
	|ГДЕ
	|	СправочникРесурсныеСпецификации.Ссылка В
	|			(ВЫБРАТЬ
	|				СпецификацииИХарактеристики.Спецификация
	|			ИЗ
	|				СпецификацииИХарактеристики КАК СпецификацииИХарактеристики)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацииИХарактеристики.Характеристика КАК Характеристика,
	|	СпецификацииИХарактеристики.Спецификация КАК Спецификация,
	|	СпецификацииИХарактеристики.Спецификация.Представление КАК СпецификацияПредставление,
	|	СпецификацииИХарактеристики.Характеристика.Представление КАК ХарактеристикаПредставление
	|ИЗ
	|	СпецификацииИХарактеристики КАК СпецификацииИХарактеристики
	|
	|УПОРЯДОЧИТЬ ПО
	|	СпецификацииИХарактеристики.Характеристика.Наименование,
	|	СпецификацииИХарактеристики.Спецификация.Наименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновныеСпецификации.Характеристика КАК Характеристика,
	|	ОсновныеСпецификации.Подразделение КАК Подразделение,
	|	ОсновныеСпецификации.Подразделение.Представление КАК ПодразделениеПредставление,
	|	ОсновныеСпецификации.Спецификация КАК Спецификация
	|ИЗ
	|	РегистрСведений.ОсновныеСпецификации КАК ОсновныеСпецификации
	|ГДЕ
	|	ОсновныеСпецификации.Номенклатура = &Номенклатура
	|	И ОсновныеСпецификации.Спецификация В
	|			(ВЫБРАТЬ
	|				СпецификацииИХарактеристики.Спецификация
	|			ИЗ
	|				СпецификацииИХарактеристики КАК СпецификацииИХарактеристики)";
	
	Запрос.УстановитьПараметр("Номенклатура", ОснованиеНоменклатура);
	Запрос.УстановитьПараметр("СпецификацииСтатус", СпецификацииСтатус);
	
	Запрос.УстановитьПараметр("ИспользованиеМатериала", ОтборИспользованиеМатериала);
	Запрос.УстановитьПараметр("Материал", ОтборМатериал);
	Запрос.УстановитьПараметр("ИспользованиеВидаРабочегоЦентра", ОтборИспользованиеВидаРабочегоЦентра);
	Запрос.УстановитьПараметр("ВидРабочегоЦентра", ОтборВидРабочегоЦентра);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаСпецификаций               = Результат[4].Выгрузить();
	ТаблицаХарактеристикИСпецификаций = Результат[5].Выгрузить();
	ТаблицаОсновныхСпецификаций       = Результат[6].Выгрузить();
	
	// Добавим подразделения на форму и в таблицу
	СписокПодразделений = Новый СписокЗначений;
	Если ИспользуетсяНесколькоПодразделенийДиспетчеров Тогда
		
		Для каждого ДанныеСтроки Из ТаблицаОсновныхСпецификаций Цикл
			СписокПодразделений.Добавить(ДанныеСтроки.Подразделение, ДанныеСтроки.ПодразделениеПредставление);
		КонецЦикла;
		ДобавитьПодразделенияИзПараметровОбеспечения(СписокПодразделений);
		
		СписокПодразделений.СортироватьПоПредставлению();
		
	КонецЕсли;
	ОбновитьСоставПодразделений(СписокПодразделений);
	
	// Заполним список спецификаций
	ТаблицаХарактеристик = ТаблицаХарактеристикИСпецификаций.Скопировать();
	ТаблицаХарактеристик.Свернуть("Характеристика");
	
	Для каждого ДанныеХарактеристика Из ТаблицаХарактеристик Цикл
		Если ХарактеристикиИспользуются Тогда
			СтрокаХарактеристика = УправлениеСпецификациями.Добавить();
			СтрокаХарактеристика.ТипСтроки = 1;
			СтрокаХарактеристика.СпецификацияХарактеристика = ДанныеХарактеристика.Характеристика;
			СтрокаХарактеристика.Характеристика = ДанныеХарактеристика.Характеристика;
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("Характеристика", ДанныеХарактеристика.Характеристика);
		СписокСтрок = ТаблицаХарактеристикИСпецификаций.НайтиСтроки(СтруктураПоиска);
		Для каждого ДанныеСпецификация Из СписокСтрок Цикл
			СтрокаСпецификация = УправлениеСпецификациями.Добавить();
			СтрокаСпецификация.ТипСтроки = 0;
			СтрокаСпецификация.СпецификацияХарактеристика = ДанныеСпецификация.Спецификация;
			СтрокаСпецификация.Характеристика = ДанныеХарактеристика.Характеристика;
			
			РеквизитыСпецификации = ТаблицаСпецификаций.Найти(ДанныеСпецификация.Спецификация, "Спецификация");
			ЗаполнитьЗначенияСвойств(СтрокаСпецификация, РеквизитыСпецификации);
			
			// Заполним признак основной спецификации
			Для каждого ЭлементСписка Из СписокПодразделенийНаФорме Цикл
				ИмяКолонки = "Подразделение" + ЭлементСписка.Представление;
				ЗаполнитьПризнакОсновнойСпецификации(
							СтрокаСпецификация, 
							ЭлементСписка.Значение, 
							ИмяКолонки,
							ТаблицаОсновныхСпецификаций);
							
				Если СтрокаСпецификация[ИмяКолонки] Тогда
					СтрокаСпецификация.ОсновнаяСпецификация = Истина;
				КонецЕсли; 
			КонецЦикла;
			
		КонецЦикла; 
	КонецЦикла; 
	
	УстановитьСтрокуУправлениеСпецификациями(ТекущаяСпецификацияХарактеристика, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакОсновнойСпецификации(СтрокаСпецификация, Подразделение, ИмяКолонки, ТаблицаОсновныхСпецификаций)

	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Характеристика", СтрокаСпецификация.Характеристика); 
	СтруктураПоиска.Вставить("Спецификация",   СтрокаСпецификация.Спецификация); 
	СтруктураПоиска.Вставить("Подразделение",  Подразделение); 
   	СписокСтрок = ТаблицаОсновныхСпецификаций.НайтиСтроки(СтруктураПоиска);
	Если СписокСтрок.Количество() <> 0 Тогда
		СтрокаСпецификация[ИмяКолонки] = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ДобавитьПодразделенияИзПараметровОбеспечения(СписокПодразделений)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтруктураПредприятия.Ссылка
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия";
	
	РазрешенныеПодразделения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВариантыОбеспеченияТоварами.СпособОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей КАК Подразделение,
	|	ВариантыОбеспеченияТоварами.СпособОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.ВариантыОбеспеченияТоварами КАК ВариантыОбеспеченияТоварами
	|ГДЕ
	|	ВариантыОбеспеченияТоварами.Номенклатура = &Номенклатура
	|	И ВариантыОбеспеченияТоварами.СпособОбеспеченияПотребностей.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)
	|	И ВариантыОбеспеченияТоварами.РеквизитДопУпорядочивания = 1
	|	И ВариантыОбеспеченияТоварами.СпособОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей В(&РазрешенныеПодразделения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпрСпособ.ИсточникОбеспеченияПотребностей,
	|	СпрСпособ.ИсточникОбеспеченияПотребностей.Представление
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СхемыОбеспечения КАК ТаблицаСхемы
	|		ПО ТаблицаСхемы.СхемаОбеспечения = СпрНоменклатура.СхемаОбеспечения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
	|		ПО СпрСпособ.Ссылка = ТаблицаСхемы.СпособОбеспеченияПотребностей
	|		 И СпрСпособ.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)
	|		 И СпрСпособ.ИсточникОбеспеченияПотребностей В(&РазрешенныеПодразделения)
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|	И СпрНоменклатура.ТипНоменклатуры В(
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Набор))
	|	И НЕ СпрСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпрСпособ.ИсточникОбеспеченияПотребностей,
	|	СпрСпособ.ИсточникОбеспеченияПотребностей.Представление
	|ИЗ
	|	Справочник.Номенклатура КАК СпрНоменклатура
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОбеспеченияПотребностей КАК СпрСпособ
	|		ПО СпрСпособ.Ссылка = СпрНоменклатура.СпособОбеспеченияПотребностей
	|		 И СпрСпособ.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)
	|		 И СпрСпособ.ИсточникОбеспеченияПотребностей В(&РазрешенныеПодразделения)
	|ГДЕ
	|	СпрНоменклатура.Ссылка = &Номенклатура
	|	И СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ СпрСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВариантыОбеспеченияРаботами.СпособОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей,
	|	ВариантыОбеспеченияРаботами.СпособОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей.Представление
	|ИЗ
	|	РегистрСведений.ВариантыОбеспеченияРаботами КАК ВариантыОбеспеченияРаботами
	|ГДЕ
	|	ВариантыОбеспеченияРаботами.Номенклатура = &Номенклатура
	|	И ВариантыОбеспеченияРаботами.СпособОбеспеченияПотребностей.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)
	|	И ВариантыОбеспеченияРаботами.РеквизитДопУпорядочивания = 1
	|	И ВариантыОбеспеченияРаботами.СпособОбеспеченияПотребностей.ИсточникОбеспеченияПотребностей В(&РазрешенныеПодразделения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновнойСпособОбеспеченияПотребностей.Значение.ИсточникОбеспеченияПотребностей,
	|	ОсновнойСпособОбеспеченияПотребностей.Значение.ИсточникОбеспеченияПотребностей.Представление
	|ИЗ
	|	Константа.ОсновнойСпособОбеспеченияПотребностей КАК ОсновнойСпособОбеспеченияПотребностей
	|ГДЕ
	|	ОсновнойСпособОбеспеченияПотребностей.Значение.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)
	|	И ОсновнойСпособОбеспеченияПотребностей.Значение.ИсточникОбеспеченияПотребностей В(&РазрешенныеПодразделения)";
	
	Запрос.УстановитьПараметр("Номенклатура", ОснованиеНоменклатура);
	Запрос.УстановитьПараметр("РазрешенныеПодразделения", РазрешенныеПодразделения);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если СписокПодразделений.НайтиПоЗначению(Выборка.Подразделение) = Неопределено Тогда
			СписокПодразделений.Добавить(Выборка.Подразделение, Выборка.Представление);
		КонецЕсли; 
	
	КонецЦикла;

КонецПроцедуры
 
&НаСервере
Процедура ОбновитьСоставПодразделений(СписокПодразделений)

	МассивДобавляемыхРеквизитов   = Новый Массив;
	МассивУдаляемыхРеквизитов     = Новый Массив;
	МассивУдаляемыхЭлементовФормы = Новый Массив;
	
	ИдентификаторПодразделения = 0;
	Для каждого ЭлементСписка Из СписокПодразделенийНаФорме Цикл
		ИдентификаторПодразделения = Макс(Число(ЭлементСписка.Представление));
	КонецЦикла;
	ИдентификаторПодразделения = ИдентификаторПодразделения + 1;
	
	// Добавим новые подразделения как колонки списка
	Для каждого ЭлементСписка Из СписокПодразделений Цикл
		
		ДанныеПодразделенияНаФорме = СписокПодразделенийНаФорме.НайтиПоЗначению(ЭлементСписка.Значение);
		Если ДанныеПодразделенияНаФорме = Неопределено Тогда
			// Этого подразделения еще нет на форме
			
			ТекущаяКолонкаИмя = "Подразделение" + Формат(ИдентификаторПодразделения, "ЧГ=0");
			
			МассивДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы(ТекущаяКолонкаИмя, 
				Новый ОписаниеТипов("Булево"),
				"УправлениеСпецификациями", ЭлементСписка.Представление));
				
			СписокПодразделенийНаФорме.Добавить(ЭлементСписка.Значение, Формат(ИдентификаторПодразделения, "ЧГ=0"));
			ИдентификаторПодразделения = ИдентификаторПодразделения + 1;
			
		КонецЕсли; 
	КонецЦикла;
	
	// Удалим лишние подразделения
	УдаленныеПодразделенияНаФорме = Новый Массив;
	Для каждого ЭлементСписка Из СписокПодразделенийНаФорме Цикл
		Если ЭлементСписка.Значение = Справочники.СтруктураПредприятия.ПустаяСсылка() Тогда
			// Нельзя удалять "в любом подразделении"
			Продолжить;
		КонецЕсли;
		
		Если СписокПодразделений.НайтиПоЗначению(ЭлементСписка.Значение) = Неопределено Тогда
			МассивУдаляемыхРеквизитов.Добавить("УправлениеСпецификациями.Подразделение" + ЭлементСписка.Представление);
			
			ТекущаяКолонкаИмя = "УправлениеСпецификациямиПодразделение" + ЭлементСписка.Представление;
			Элементы.Удалить(Элементы[ТекущаяКолонкаИмя]);
			
			УдаленныеПодразделенияНаФорме.Добавить(ЭлементСписка);
		КонецЕсли; 
	КонецЦикла; 
	Для каждого ЭлементСписка Из УдаленныеПодразделенияНаФорме Цикл
		СписокПодразделенийНаФорме.Удалить(ЭлементСписка);
	КонецЦикла; 
	
	// Удаляем и добавляем реквизиты формы
	Если МассивДобавляемыхРеквизитов.Количество() > 0 ИЛИ МассивУдаляемыхРеквизитов.Количество() > 0 Тогда
		ИзменитьРеквизиты(МассивДобавляемыхРеквизитов, МассивУдаляемыхРеквизитов);
	КонецЕсли;
	
	// Добавим новые подразделения на форму
	ГруппаКолонокПодразделений = Элементы.УправлениеСпецификациямиГруппаНастройкаОсновнойСпецификации;
	Для каждого РеквизитФормы Из МассивДобавляемыхРеквизитов Цикл
		
		ТекущаяКолонкаИмя = "УправлениеСпецификациями" + РеквизитФормы.Имя;
		
		// Определим куда нужно добавить новое подразделение
		ЭлементСледующееПодразделение = Неопределено;
		НашлиТекущий = Ложь;
		Для каждого ЭлементСписка Из СписокПодразделений Цикл
			Если НашлиТекущий Тогда
				ИдентификаторПодразделения = СписокПодразделенийНаФорме.НайтиПоЗначению(ЭлементСписка.Значение).Представление;
				ИмяЭлемента = "УправлениеСпецификациямиПодразделение" + ИдентификаторПодразделения;
				ЭлементСледующееПодразделение = Элементы.Найти(ИмяЭлемента);
				Если ЭлементСледующееПодразделение <> Неопределено Тогда
					Прервать;
				КонецЕсли;
			ИначеЕсли ЭлементСписка.Представление = РеквизитФормы.Заголовок Тогда
				НашлиТекущий = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭлементСледующееПодразделение <> Неопределено Тогда
			НовыйЭлемент = Элементы.Вставить(ТекущаяКолонкаИмя, 
												Тип("ПолеФормы"), 
												ГруппаКолонокПодразделений, 
												ЭлементСледующееПодразделение);
		Иначе
			НовыйЭлемент = Элементы.Добавить(ТекущаяКолонкаИмя, 
												Тип("ПолеФормы"), 
												ГруппаКолонокПодразделений);
		КонецЕсли;
		
		НовыйЭлемент.ПутьКДанным = "УправлениеСпецификациями." + РеквизитФормы.Имя;
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_УправлениеСпецификациямиПодразделениеПриИзменении");
		
		УстановитьУсловноеОформлениеКолонкиПодразделение(ТекущаяКолонкаИмя);
		
	КонецЦикла;
	
	// Настроим внешний вид колонок в зависимости от наличия подразделений
	Если ИспользуетсяНесколькоПодразделенийДиспетчеров Тогда
		Если СписокПодразделенийНаФорме.Количество() = 1 Тогда
			Элементы.УправлениеСпецификациямиГруппаНастройкаОсновнойСпецификации.ОтображатьВШапке = Ложь;
			Элементы.УправлениеСпецификациямиПодразделение1.Заголовок = НСтр("ru = 'Основная во всех подразделениях';
																			|en = 'Main in all departments'");
		Иначе
			Элементы.УправлениеСпецификациямиГруппаНастройкаОсновнойСпецификации.ОтображатьВШапке = Истина;
			Элементы.УправлениеСпецификациямиПодразделение1.Заголовок = НСтр("ru = 'Во всех подразделения';
																			|en = 'In all departments'");
		КонецЕсли;
	Иначе
		Элементы.УправлениеСпецификациямиГруппаНастройкаОсновнойСпецификации.ОтображатьВШапке = Ложь;
		Элементы.УправлениеСпецификациямиПодразделение1.Заголовок = НСтр("ru = 'Основная спецификация';
																		|en = 'Main BOM'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОсновнуюСпецификацию(ТекущаяКолонкаИмя)

	ТекущаяСтрока = Элементы.УправлениеСпецификациями.ТекущаяСтрока;
	ТекущиеДанные = УправлениеСпецификациями.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ИдентификаторПодразделения = Сред(ТекущаяКолонкаИмя, СтрДлина("УправлениеСпецификациямиПодразделение") + 1);
	Если ТекущиеДанные["Подразделение" + ИдентификаторПодразделения] Тогда
		Если ТекущиеДанные.Статус <> ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует") Тогда
			ТекущиеДанные["Подразделение" + ИдентификаторПодразделения] = Ложь;
			ПоказатьПредупреждение(, НСтр("ru = 'Спецификация может быть основной только в статусе ""Действует"".';
											|en = 'Bill of materials can be the main one only in the ""Valid"" status.'"));
			Возврат
		КонецЕсли;
	КонецЕсли;

	УстановитьОсновнуюСпецификациюНаСервере(ИдентификаторПодразделения);
	
КонецПроцедуры
 
&НаСервере
Процедура УстановитьОсновнуюСпецификациюНаСервере(ИдентификаторПодразделения)

	ПодразделениеСпецификации = Неопределено;
	Для каждого ЭлементСписка Из СписокПодразделенийНаФорме Цикл
		Если ЭлементСписка.Представление = ИдентификаторПодразделения Тогда
			ПодразделениеСпецификации = ЭлементСписка.Значение;
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	Если ПодразделениеСпецификации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.УправлениеСпецификациями.ТекущаяСтрока;
	ТекущиеДанные = УправлениеСпецификациями.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если ТекущиеДанные["Подразделение" + ИдентификаторПодразделения] Тогда
		
		СтруктураЗаписи = РегистрыСведений.ОсновныеСпецификации.СтруктураЗаписи();
		СтруктураЗаписи.Номенклатура   = ОснованиеНоменклатура;
		СтруктураЗаписи.Характеристика = ТекущиеДанные.Характеристика;
		СтруктураЗаписи.Спецификация   = ТекущиеДанные.Спецификация;
		СтруктураЗаписи.Подразделение  = ПодразделениеСпецификации;
		
		УправлениеДаннымиОбИзделияхВызовСервера.НазначитьОсновнойСпецификацией(СтруктураЗаписи);
		
	Иначе
		
		СтруктураЗаписи = РегистрыСведений.ОсновныеСпецификации.СтруктураЗаписи();
		СтруктураЗаписи.Номенклатура   = ОснованиеНоменклатура;
		СтруктураЗаписи.Характеристика = ТекущиеДанные.Характеристика;
		СтруктураЗаписи.Подразделение  = ПодразделениеСпецификации;
		
		УправлениеДаннымиОбИзделияхВызовСервера.УбратьПризнакОсновнаяСпецификацияДляИзделия(СтруктураЗаписи);
		
	КонецЕсли; 
	
	ЗаполнитьУправлениеСпецификациями();
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьРесурснуюСпецификациюВСпискеУправлениеСпецификациями()

	ТекущиеДанные = Элементы.УправлениеСпецификациями.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ТипСтроки <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КопироватьРесурснуюСпецификациюВСпискеУправлениеСпецификациямиЗавершение", ЭтотОбъект);
	УправлениеДаннымиОбИзделияхКлиент.КопироватьРесурснуюСпецификацию(ТекущиеДанные.Спецификация, ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура КопироватьРесурснуюСпецификациюВСпискеУправлениеСпецификациямиЗавершение(Ссылка, ДополнительныеПараметры) Экспорт

	ЗаполнитьУправлениеСпецификациями(Ссылка);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтрокуУправлениеСпецификациями(ТекущаяСпецификацияХарактеристика, Форма)

	Если ТекущаяСпецификацияХарактеристика <> Неопределено Тогда
		СтруктураПоиска = Новый Структура("СпецификацияХарактеристика", ТекущаяСпецификацияХарактеристика);
  		СписокСтрок = Форма.УправлениеСпецификациями.НайтиСтроки(СтруктураПоиска);
		Если СписокСтрок.Количество() <> 0 Тогда
			Форма.Элементы.УправлениеСпецификациями.ТекущаяСтрока = СписокСтрок[0].ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусСпецификации(ЗначениеСтатуса, ПредставлениеСтатуса)

	ВыделенныеСсылки = ПолучитьВыделенныеСпецификацииВСпискеУправлениеСпецификациями();
	УправлениеДаннымиОбИзделияхКлиент.УстановитьСтатусСпецификаций(ЗначениеСтатуса, ПредставлениеСтатуса, ВыделенныеСсылки);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьВыделенныеСпецификацииВСпискеУправлениеСпецификациями()

	ВыделенныеСсылки = Новый Массив;
	Для каждого ТекущаяСтрока Из Элементы.УправлениеСпецификациями.ВыделенныеСтроки Цикл
		ТекущиеДанные = УправлениеСпецификациями.НайтиПоИдентификатору(ТекущаяСтрока);
		Если ТекущиеДанные.ТипСтроки = 0 Тогда
			ВыделенныеСсылки.Добавить(ТекущиеДанные.Спецификация);
		КонецЕсли; 
	КонецЦикла; 

	Возврат ВыделенныеСсылки;
	
КонецФункции

#КонецОбласти

#Область МаршрутныеКарты

&НаКлиентеНаСервереБезКонтекста
Процедура МаршрутныеКартыУстановитьОтборПоСтатусу(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокМаршрутныхКарт, 
		"Статус", 
		Форма.МаршрутныеКартыСтатус, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.МаршрутныеКартыСтатус));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МаршрутныеКартыУстановитьОтборПоПодразделению(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокМаршрутныхКарт, 
		"Подразделение", 
		Форма.МаршрутныеКартыПодразделение, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.МаршрутныеКартыПодразделение));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МаршрутныеКартыУстановитьОтборПоОтветственному(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокМаршрутныхКарт, 
		"Ответственный", 
		Форма.МаршрутныеКартыОтветственный, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.МаршрутныеКартыОтветственный));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура МаршрутныеКартыУстановитьОтборПоНоменклатуре(Форма)
	
	УправлениеДаннымиОбИзделияхКлиентСервер.УстановитьОтборПоНоменклатуреВСпискеМаршрутныхКарт(
		Форма.СписокМаршрутныхКарт,
		Форма.ОснованиеНоменклатура,
		Форма.МаршрутныеКартыИспользуетсяКак);
	
КонецПроцедуры

&НаКлиенте
Процедура КопироватьМаршрутнуюКарту()

	ТекстВопроса = НСтр("ru = 'Будет создана и записана копия маршрутной карты (включая техоперации).
								|Скопировать?';
								|en = 'Route sheet copy will be created and written (including manufacturing operations).
								|Copy?'");
	СписокКнопок = Новый СписокЗначений;								
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Скопировать';
														|en = 'Copy'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросКопироватьМаршрутнуюКарту", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросКопироватьМаршрутнуюКарту(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.СписокМаршрутныхКарт.ТекущиеДанные;
	
	Ссылка = КопироватьМаршрутнуюКартуНаСервере(ТекущиеДанные.Ссылка);
	Если Ссылка <> Неопределено Тогда
		Элементы.СписокМаршрутныхКарт.ТекущаяСтрока = Ссылка;
		
		ОповеститьОбИзменении(Тип("СправочникСсылка.МаршрутныеКарты"));
		Оповестить("Запись_ТехнологическиеОперации");
		ОткрытьФорму("Справочник.МаршрутныеКарты.ФормаОбъекта", Новый Структура("Ключ", Ссылка));
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Не удалось скопировать маршрутную карту.';
									|en = 'Cannot copy the operations sheet.'"));
	КонецЕсли; 

КонецПроцедуры

&НаСервереБезКонтекста
Функция КопироватьМаршрутнуюКартуНаСервере(Источник)

	НачатьТранзакцию();
	
	Попытка
		Объект = Источник.Скопировать();
		Объект.Записать();
		
		Если НЕ Справочники.МаршрутныеКарты.ЗаполнитьПоМаршрутнойКарте(Объект, Источник, Объект.Ссылка) Тогда
			ОтменитьТранзакцию();
			Возврат Неопределено;
		КонецЕсли;
		
		Объект.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Объект.Ссылка;
	
КонецФункции

&НаСервере
Процедура УстановитьСвойстваДинамическогоСпискаМаршрутныхКарт()
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	ТекстЗапроса = Справочники.МаршрутныеКарты.ТекстЗапросаДинамическогоСпискаМаршрутныхКарт();
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	
	СвойстваСписка.ОсновнаяТаблица = СписокМаршрутныхКарт.ОсновнаяТаблица;
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокМаршрутныхКарт, СвойстваСписка);
	
КонецПроцедуры

#КонецОбласти

#Область Калькуляции

&НаКлиентеНаСервереБезКонтекста
Процедура КалькуляцииУстановитьОтборПоСтатусу(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокПлановыхКалькуляций,
		"Статус", 
		Форма.КалькуляцииСтатус, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.КалькуляцииСтатус));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КалькуляцииУстановитьОтборПоОбъекту(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокПлановыхКалькуляций,
		"ОбъектКалькуляции", 
		Форма.КалькуляцииОбъект, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.КалькуляцииОбъект));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КалькуляцииУстановитьОтборПоОтветственному(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.СписокПлановыхКалькуляций,
		"Ответственный", 
		Форма.КалькуляцииОтветственный, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		ЗначениеЗаполнено(Форма.КалькуляцииОтветственный));

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокПлановыхКалькуляций22, "Организация", Организация, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокПлановыхКалькуляций22, "ПодразделениеДиспетчер", ПодразделениеДиспетчер, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ПодразделениеДиспетчер));
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокПлановыхКалькуляций22, "Состояние", Состояние, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Состояние));
	
КонецПроцедуры

#КонецОбласти

#Область РазрешенияНаЗаменуМатериалов

&НаСервере
Процедура РазрешенияУстановитьОтборПоСтатусу()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокРазрешений,
		"Статус",
		РазрешенияСтатус,
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(РазрешенияСтатус));
	
КонецПроцедуры

&НаСервере
Процедура РазрешенияУстановитьОтборПоСпецификации()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокРазрешений,
		"Спецификация",
		РазрешенияСпецификация,
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(РазрешенияСпецификация));
	
КонецПроцедуры

&НаСервере
Процедура РазрешенияУстановитьОтборПоОтветственному()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокРазрешений,
		"Ответственный",
		РазрешенияОтветственный,
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеЗаполнено(РазрешенияОтветственный));
	
КонецПроцедуры

&НаСервере
Процедура РазрешенияУстановитьОтборПоНоменклатуре()
	
	Документы.РазрешениеНаЗаменуМатериалов.УстановитьОтборПоНоменклатуреВДинамическомСпискеРазрешенийНаЗаменуМатериалов(
		СписокРазрешений,
		ОснованиеНоменклатура,
		РазрешениеИспользуетсяКак);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваДинамическогоСпискаРазрешенийНаЗаменуМатериалов()
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	ТекстЗапроса = Документы.РазрешениеНаЗаменуМатериалов.ТекстЗапросаДинамическогоСпискаРазрешенийНаЗаменуМатериалов();
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	
	СвойстваСписка.ОсновнаяТаблица = "Документ.РазрешениеНаЗаменуМатериалов";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокРазрешений, СвойстваСписка);
	
КонецПроцедуры

#КонецОбласти

#Область ДеревоСпецификаций

&НаСервере
Процедура ИнициализацияДереваСпецификаций()

	Если НЕ ХарактеристикиИспользуются Тогда
		Элементы.ДеревоХарактеристикаНоменклатуры.Доступность = Ложь;
	КонецЕсли;
	
	ДеревоДата = ТекущаяДатаСеанса();
	
	ПараметрыОтбора = ХранилищеНастроекДанныхФорм.Загрузить("ДеревоРесурсныхСпецификаций", "ПараметрыОтбора");
	Если ЗначениеЗаполнено(ПараметрыОтбора) Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОтбора);
	Иначе
		ДеревоВыводитьЭтапы                = Истина;
		ДеревоВыводитьВыходныеИзделия      = Истина;
		ДеревоВыводитьМатериалы            = Истина;
		ДеревоВыводитьТрудозатраты         = Истина;
		ДеревоВыводитьЗаголовкиГруппировок = Истина;
	КонецЕсли;
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделиях.ПараметрыВыбораСпецификаций(Новый Структура(), Обработки.ДеревоРесурсныхСпецификаций);
	
	УправлениеДаннымиОбИзделиях.НастроитьУсловноеОформлениеДереваСпецификаций(УсловноеОформление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиДерева()
	
	ПроверитьУстановитьПараметрыСпискаДоступныхСпецификаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУстановитьПараметрыСпискаДоступныхСпецификаций()
	
	ТекущиеДанные = Элементы.ДеревоСпецификаций.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ТипЗнч(ТекущиеДанные.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
			НовыйОтборНоменклатура = ТекущиеДанные.Номенклатура;
		Иначе
			НовыйОтборНоменклатура = Неопределено;
		КонецЕсли;
		
		НовыйОтборХарактеристика = ТекущиеДанные.Характеристика;
		
	Иначе
		НовыйОтборНоменклатура = Неопределено;
		НовыйОтборХарактеристика = Неопределено;
	КонецЕсли;
	
	ТекущийОтборНоменклатура = СписокДоступныхСпецификаций.Параметры.Элементы.Найти("Номенклатура").Значение;
	ТекущийОтборХарактеристика = СписокДоступныхСпецификаций.Параметры.Элементы.Найти("Характеристика").Значение;
	
	Если НовыйОтборНоменклатура <> ТекущийОтборНоменклатура ИЛИ НовыйОтборХарактеристика <> ТекущийОтборХарактеристика Тогда
		УстановитьПараметрыСпискаДоступныхСпецификаций(НовыйОтборНоменклатура, НовыйОтборХарактеристика);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваДинамическогоСпискаДоступныхСпецификаций()
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	
	ТекстЗапроса = Справочники.РесурсныеСпецификации.ТекстЗапросаДинамическогоСпискаДоступныхСпецификаций();
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	
	СвойстваСписка.ОсновнаяТаблица = "";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Ложь;
	
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокДоступныхСпецификаций, СвойстваСписка);
	
	СписокДоступныхСпецификаций.Параметры.УстановитьЗначениеПараметра("Номенклатура", ОснованиеНоменклатура);
	СписокДоступныхСпецификаций.Параметры.УстановитьЗначениеПараметра("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыСпискаДоступныхСпецификаций(ТекущаяНоменклатура, ТекущаяХарактеристика = Неопределено)
	
	СписокДоступныхСпецификаций.Параметры.УстановитьЗначениеПараметра("Номенклатура", ТекущаяНоменклатура);
	СписокДоступныхСпецификаций.Параметры.УстановитьЗначениеПараметра("Характеристика", ?(ЗначениеЗаполнено(ТекущаяХарактеристика), ТекущаяХарактеристика, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
	
КонецПроцедуры

&НаСервере
Функция ПараметрыДереваСпецификаций()
	
	ПараметрыДерева = УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыДереваСпецификаций();
	
	ПараметрыДерева.Номенклатура = ОснованиеНоменклатура;
	ПараметрыДерева.ХарактеристикаНоменклатуры = ДеревоХарактеристикаНоменклатуры;
	
	Если ДеревоСпецификаций.ПолучитьЭлементы().Количество() <> 0 Тогда
		ПараметрыДерева.Спецификация = ДеревоСпецификаций.ПолучитьЭлементы()[0].Спецификация;
	Иначе
		ПараметрыДерева.Спецификация = Справочники.РесурсныеСпецификации.ПустаяСсылка();
	КонецЕсли;
	
	ПараметрыДерева.Дата = ДеревоДата;
	
	ПараметрыДерева.ВыводитьЭтапы = ДеревоВыводитьЭтапы;
	ПараметрыДерева.ВыводитьВыходныеИзделия = ДеревоВыводитьВыходныеИзделия;
	ПараметрыДерева.ВыводитьМатериалы = ДеревоВыводитьМатериалы;
	ПараметрыДерева.ВыводитьТрудозатраты = ДеревоВыводитьТрудозатраты;
	ПараметрыДерева.ВыводитьЗаголовкиГруппировок = ДеревоВыводитьЗаголовкиГруппировок;
	
	ПараметрыДерева.ДинамическоеСчитывание = ДинамическоеСчитывание;
	ПараметрыДерева.РазузловыватьПолуфабрикаты = Истина;
	
	Возврат ПараметрыДерева;
	
КонецФункции

&НаСервере
Процедура СформироватьДеревоСпецификацийНаСервере()
	
	ОбновитьСтруктуруИзделия = Ложь;
	
	Если ДеревоИзмененыПараметрыОтбора Тогда
		ДеревоСохранитьПараметрыОтбора();
	КонецЕсли;
	
	ПараметрыДерева = ПараметрыДереваСпецификаций();
	
	УправлениеДаннымиОбИзделиях.ПостроитьДеревоСпецификаций(ЭтаФорма, ПараметрыДерева);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьСпецификациюВСтрокеНаСервере(ТекущаяСтрока)
	
	ТекущиеДанные = ДеревоСпецификаций.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ПараметрыДерева = ПараметрыДереваСпецификаций();
	ПараметрыДерева.Спецификация = Справочники.РесурсныеСпецификации.ПустаяСсылка();
	
	УправлениеДаннымиОбИзделиях.ИзменитьСпецификациюВСтрокеДерева(ЭтаФорма, ПараметрыДерева, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСпецификациюВТекущейСтроке(ТекущаяСтрока, НоваяСпецификация = Неопределено)
	
	ТекущиеДанные = ДеревоСпецификаций.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ТекущиеДанные.ПолучитьЭлементы().Очистить();
	
	ТекущиеДанные.Спецификация = НоваяСпецификация;
	
	ТекущиеДанные.ЕстьСпецификация = ЗначениеЗаполнено(НоваяСпецификация);
	ТекущиеДанные.СпецификацияПрочитана = Ложь;
	
	Если ТекущиеДанные.ЕстьСпецификация Тогда
		ИзменитьСпецификациюВСтрокеНаСервере(ТекущаяСтрока);
	КонецЕсли;
	
	РазвернутьДеревоСпецификаций(ТекущаяСтрока, ТекущиеДанные.Идентификатор);

КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоСпецификаций(ТекущаяСтрока, Идентификатор)
	
	Если Элементы.ДеревоСпецификаций.ТекущаяСтрока <> ТекущаяСтрока Тогда
		
		ТекущиеДанные = НайтиСтрокуПоИдентификатору(ДеревоСпецификаций.ПолучитьЭлементы(), Идентификатор);
		Если ТекущиеДанные <> Неопределено Тогда
			Элементы.ДеревоСпецификаций.ТекущаяСтрока = ТекущиеДанные.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиСтрокуПоИдентификатору(КоллекцияЭлементовДерева, Идентификатор)
	
	Для Каждого Строка Из КоллекцияЭлементовДерева Цикл
		
		Если Строка.Идентификатор = Идентификатор Тогда
			Возврат Строка;
		Иначе
			Результат = НайтиСтрокуПоИдентификатору(Строка.ПолучитьЭлементы(), Идентификатор);
			Если Результат <> Неопределено Тогда
				Возврат Результат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПараметрыПечатиДереваСпецификаций()
	
	Дерево = УправлениеДаннымиОбИзделиях.ПрочитатьДеревоСпецификаций(ЭтаФорма);
	
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("АдресВХранилище", ПоместитьВоВременноеХранилище(Дерево, УникальныйИдентификатор));
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаКлиенте
Процедура РазвернутьВсе(СПодчиненными = Истина)

	СтрокиДерева = ДеревоСпецификаций.ПолучитьЭлементы();
	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		Элементы.ДеревоСпецификаций.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), СПодчиненными);
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ДеревоИзмененыПараметрыОтбора()
	
	ДеревоИзмененыПараметрыОтбора = Истина;
	Элементы.ДеревоСпецификацийПечать.Доступность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ДеревоСохранитьПараметрыОтбора()
	
	ПараметрыОтбора = Новый Структура("
		|ДеревоВыводитьЭтапы,
		|ДеревоВыводитьВыходныеИзделия,
		|ДеревоВыводитьМатериалы,
		|ДеревоВыводитьТрудозатраты,
		|ДеревоВыводитьЗаголовкиГруппировок");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, ЭтаФорма);
	
	ХранилищеНастроекДанныхФорм.Сохранить("ДеревоРесурсныхСпецификаций", "ПараметрыОтбора", ПараметрыОтбора);
	
	Элементы.ДеревоСпецификацийПечать.Доступность = Истина;
	ДеревоИзмененыПараметрыОтбора = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	Справочники.РесурсныеСпецификации.УстановитьУсловноеОформлениеСпискаРесурсныхСпецификаций(УсловноеОформление, Элементы.УправлениеСпецификациями.Имя);
	
	СписокСпецификаций.УсловноеОформление.Элементы.Очистить();
	Справочники.РесурсныеСпецификации.УстановитьУсловноеОформлениеСпискаРесурсныхСпецификаций(СписокСпецификаций.УсловноеОформление);

	СписокМаршрутныхКарт.УсловноеОформление.Элементы.Очистить();
	Справочники.МаршрутныеКарты.УстановитьУсловноеОформлениеСпискаМаршрутныхКарт(СписокМаршрутныхКарт.УсловноеОформление);
	
	СписокДоступныхСпецификаций.УсловноеОформление.Элементы.Очистить();
	Справочники.РесурсныеСпецификации.УстановитьУсловноеОформлениеСпискаДоступныхСпецификаций(СписокДоступныхСпецификаций.УсловноеОформление);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.УправлениеСпецификациями.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("УправлениеСпецификациями.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветФонаГруппировкиОтчета1);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.УправлениеСпецификациямиСпецификацияХарактеристика.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("УправлениеСпецификациями.СпецификацияХарактеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая характеристика>';
																|en = '<any characteristic>'"));
	
	//
	УстановитьУсловноеОформлениеКолонкиПодразделение(Элементы.УправлениеСпецификациямиПодразделение1.Имя);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.УправлениеСпецификациямиСпецификацияХарактеристика.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("УправлениеСпецификациями.СпецификацияХарактеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<любая характеристика>';
																|en = '<any characteristic>'"));
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы.ПлановаяКалькуляция2_2) Тогда
		Возврат;
	КонецЕсли;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСостояние.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокПлановыхКалькуляций22.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "Рассчитана";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Рассчитана';
																|en = 'Calculated'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСостояние.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокПлановыхКалькуляций22.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "НеРассчитана";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не рассчитана';
																|en = 'Not calculated'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСостояние.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокПлановыхКалькуляций22.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "ОшибкаРасчета";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Ошибка расчета';
																|en = 'Calculation error'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСостояние.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокПлановыхКалькуляций22.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "РассчитанаСОшибками";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Рассчитана с ошибками';
																|en = 'Calculated with errors'"));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоПравам()

	ПравоПросмотраСпецификаций		= ПравоДоступа("Просмотр", Метаданные.Справочники.РесурсныеСпецификации);
	ПравоДоступаКалькуляции21		= ПравоДоступа("Просмотр", Метаданные.Документы.ПлановаяКалькуляция);
	ПравоДоступаКалькуляции22		= ПравоДоступа("Просмотр", Метаданные.Документы.ПлановаяКалькуляция2_2);
	ПравоДоступаРазрешения			= ПравоДоступа("Просмотр", Метаданные.Документы.РазрешениеНаЗаменуМатериалов);
	ПравоДоступаМаршрутныеКарты		= ПравоДоступа("Просмотр", Метаданные.Справочники.МаршрутныеКарты);
	ПравоДоступаДеревоСпецификаций	= ПравоДоступа("Использование", Метаданные.Обработки.ДеревоРесурсныхСпецификаций);
	
	Если Не ПравоПросмотраСпецификаций И Не ПравоДоступаМаршрутныеКарты И Не ПравоДоступаРазрешения И Не ПравоДоступаДеревоСпецификаций
			И Не ПравоДоступаКалькуляции21 И Не ПравоДоступаКалькуляции22 Тогда
		Возврат;
	КонецЕсли;
	
	ПравоРедактированияСпецификаций = ПравоДоступа("Редактирование", Метаданные.Справочники.РесурсныеСпецификации);
	Если НЕ ПравоРедактированияСпецификаций Тогда
		Элементы.УправлениеСпецификациями.ТолькоПросмотр = Истина;
		Элементы.УправлениеСпецификациями.ИзменятьСоставСтрок = Ложь;
		Элементы.СписокУстановитьСтатусВРазработке.Видимость = Ложь;
		Элементы.СписокУстановитьСтатусДействует.Видимость = Ложь;
		Элементы.СписокУстановитьСтатусЗакрыта.Видимость = Ложь;
	КонецЕсли; 
	
	Если Не ПравоПросмотраСпецификаций Тогда
		Элементы.СтраницаСпецификацииГдеНоменклатураИзделие.Видимость = Ложь;
		Элементы.СтраницаСпецификацииГдеНоменклатураМатериал.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступаКалькуляции21 Тогда
		Элементы.СтраницаПлановыеКалькуляции.Видимость = Ложь;
		Элементы.ГруппаОтборыКалькуляции.Видимость = Ложь;
		Элементы.СписокПлановыхКалькуляций.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.СтраницаПлановыеКалькуляции22.Видимость = ПравоДоступаКалькуляции22;
	
	Если Не ПравоДоступаРазрешения Тогда
		Элементы.СтраницаРазрешенияНаЗамену.Видимость = Ложь;
		Элементы.ГруппаОтборыРазрешения.Видимость = Ложь;
		Элементы.СписокРазрешений.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступаМаршрутныеКарты Тогда
		Элементы.СтраницаМаршрутныеКарты.Видимость = Ложь;
		Элементы.ГруппаОтборыМаршрутныхКарт.Видимость = Ложь;
		Элементы.СписокМаршрутныхКарт.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступаДеревоСпецификаций Тогда
		Элементы.СтраницаСтруктураИзделия.Видимость = Ложь;
		Элементы.ГруппаСтруктураИзделияШапка.Видимость = Ложь;
		Элементы.ДеревоСпецификаций.Видимость = Ложь;
		Элементы.СписокДоступныхСпецификаций.Видимость = Ложь;
	КонецЕсли;

	Если НЕ ПравоДоступа("Просмотр", Метаданные.Обработки.ДеревоРесурсныхСпецификаций) Тогда
		Элементы.УправлениеСпецификациямиДеревоСпецификации.Видимость = Ложь;
	КонецЕсли; 
		 
	Если НЕ ПравоДоступа("Просмотр", Метаданные.Обработки.СравнениеСпецификаций) Тогда
		Элементы.УправлениеСпецификациямиСравнитьСпецификации.Видимость = Ложь;
	КонецЕсли; 
		 
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеНаСервере()
	
	ЗаполнитьУправлениеСпецификациями();
	Элементы.СписокСпецификаций.Обновить();
	Элементы.СписокРазрешений.Обновить();
	Элементы.СписокМаршрутныхКарт.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеКолонкиПодразделение(ИмяКолонки)
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяКолонки);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("УправлениеСпецификациями.ТипПроизводственногоПроцесса");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;

	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ТипыПроизводственныхПроцессов.Ремонт);
	СписокЗначений.Добавить(Перечисления.ТипыПроизводственныхПроцессов.Разборка);
	
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
