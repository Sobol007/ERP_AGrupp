
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЛогическоеВыражение") Тогда
		ЛогическоеВыражение = Параметры.ЛогическоеВыражение;		
	КонецЕсли;
	
	ЛогическоеВыражение = ?(ЗначениеЗаполнено(ЛогическоеВыражение), ЛогическоеВыражение, "И");
	
	ЗаполнитьДеревоКлассификаторов(Параметры.Принадлежность, Параметры.МассивЗначений, Параметры.МассивЗначенийРеквизитов, Отказ);
	УстановитьВидимостьДоступность(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	СоответствиеЗначений			  = Новый Соответствие;
	МассивВыбранныхЗначений 		  = Новый Массив;
	МассивВыбранныхЗначенийРеквизитов = Новый Массив;
	Для каждого СтрокаКлассификатора Из ДеревоКлассификаторов.ПолучитьЭлементы() Цикл
		Для каждого СтрокаЗначения Из СтрокаКлассификатора.ПолучитьЭлементы() Цикл
			Если СтрокаЗначения.Пометка Тогда
				МассивВыбранныхЗначений.Добавить(СтрокаЗначения.Классификатор_Значение);
				МассивВыбранныхЗначенийРеквизитов.Добавить(СтрокаЗначения.ЗначениеРеквизита);
				
				СоответствиеЗначений.Вставить(СтрокаЗначения.Классификатор_Значение, СтрокаЗначения.ЗначениеРеквизита);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ЛогическоеВыражение"	, ЛогическоеВыражение);
	Результат.Вставить("ЗначенияКлассификаторов", МассивВыбранныхЗначений);
	Результат.Вставить("ЗначенияРеквизитов"     , МассивВыбранныхЗначенийРеквизитов);
	Результат.Вставить("СоответствиеЗначений"   , СоответствиеЗначений);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Развернуть(Команда)
	РазвернутьСтрокиДерева(Элементы.ДеревоКлассификаторов, "ДеревоКлассификаторов");	
КонецПроцедуры

&НаКлиенте
Процедура Свернуть(Команда)
	РазвернутьСтрокиДерева(Элементы.ДеревоКлассификаторов, "ДеревоКлассификаторов", Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура Информация(Команда)

	Элементы.ДеревоКлассификаторовИнформация.Пометка = Не Элементы.ДеревоКлассификаторовИнформация.Пометка;	
	УстановитьВидимостьДоступность(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоКлассификаторов

&НаКлиенте
Процедура ДеревоКлассификаторовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКлассификаторовПередУдалением(Элемент, Отказ)
	Отказ = Истина;	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКлассификаторовПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоКлассификаторов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
		
	КонецЕсли;
	
	ЗаполнитьПометкиРекурсивно(ТекущиеДанные.ПолучитьЭлементы(), ТекущиеДанные.Пометка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКлассификаторовЗначениеРеквизитаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоКлассификаторов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;	
		
	КонецЕсли;
	
	ТекущиеДанные.Пометка = ЗначениеЗаполнено(ТекущиеДанные.ЗначениеРеквизита);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВспомогательныеПроцедурыФункции

&НаСервере
Процедура ЗаполнитьДеревоКлассификаторов(Принадлежность, МассивЗначений, МассивЗначенийРеквизитов, Отказ)
	
	ЭлементыДерева = ДеревоКлассификаторов.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	CRM_ЗначенияКлассификаторов.Владелец КАК Классификатор,
		|	CRM_ЗначенияКлассификаторов.Ссылка КАК Значение,
		|	CRM_ЗначенияКлассификаторов.Владелец.ВидОтображения КАК ВидОтображения,
		|	CRM_ЗначенияКлассификаторов.Владелец.СпособФормирования КАК СпособФормирования,
		|	CRM_ЗначенияКлассификаторов.ДополнительныйРеквизит КАК ДополнительныйРеквизит,
		|	CRM_ЗначенияКлассификаторов.Описание КАК Описание
		|ИЗ
		|	Справочник.CRM_ЗначенияКлассификаторов КАК CRM_ЗначенияКлассификаторов
		|ГДЕ
		|	ИСТИНА
		|	%%УсловиеПринадлежность%%
		|
		|УПОРЯДОЧИТЬ ПО
		|	CRM_ЗначенияКлассификаторов.Владелец.РеквизитДопУпорядочивания,
		|	CRM_ЗначенияКлассификаторов.РеквизитДопУпорядочивания
		|ИТОГИ ПО
		|	Классификатор";
	
	
	ТекстУсловия = "";		
	Если Принадлежность <> Неопределено Тогда
		ТекстУсловия = "И CRM_ЗначенияКлассификаторов.Владелец В
			|(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	CRM_КлассификаторыПринадлежность.Ссылка
			|ИЗ
			|	ПланВидовХарактеристик.CRM_Классификаторы.Принадлежность КАК CRM_КлассификаторыПринадлежность
			|ГДЕ
			|	CRM_КлассификаторыПринадлежность.ИмяТаблицы В (&Принадлежность))";
		
		Запрос.УстановитьПараметр("Принадлежность", Принадлежность);
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%%УсловиеПринадлежность%%", ТекстУсловия);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Список значений классификаторов пуст. '"),,,, Отказ);	
		Возврат;
		
	КонецЕсли;
	
	ВыборкаКлассификатор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКлассификатор.Следующий() Цикл
		
		СтрокаКлассификатор = ЭлементыДерева.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКлассификатор, ВыборкаКлассификатор);
		
		СтрокаКлассификатор.Классификатор_Значение = ВыборкаКлассификатор.Классификатор;
		СтрокаКлассификатор.ИндексКартинки = 1;
		
		ВыборкаЗаписи = ВыборкаКлассификатор.Выбрать();
		Пока ВыборкаЗаписи.Следующий() Цикл
			СтрокаЗначение = СтрокаКлассификатор.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаЗначение, ВыборкаЗаписи);
			
			СтрокаЗначение.Классификатор_Значение = ВыборкаЗаписи.Значение;
			СтрокаЗначение.ИндексКартинки = 2;
			
			Если МассивЗначений <> Неопределено Тогда
				ИндексЭлементаМассиваЗначений = МассивЗначений.Найти(ВыборкаЗаписи.Значение);
				Если ИндексЭлементаМассиваЗначений <> Неопределено Тогда
					СтрокаЗначение.Пометка 			 = Истина;	
					СтрокаЗначение.ЗначениеРеквизита = МассивЗначенийРеквизитов[ИндексЭлементаМассиваЗначений];
					
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
		
КонецПроцедуры // ЗаполнитьДеревоКлассификаторов()

&НаКлиенте
Процедура ЗаполнитьПометкиРекурсивно(ЭлементыДерева, Пометка)

	Для каждого СтрокаДерева Из ЭлементыДерева Цикл
		СтрокаДерева.Пометка = Пометка;		
		ЗаполнитьПометкиРекурсивно(СтрокаДерева.ПолучитьЭлементы(), Пометка);
	
	КонецЦикла;	

КонецПроцедуры // ЗаполнитьПометкиРекурсивно()

&НаКлиенте
Процедура РазвернутьСтрокиДерева(ЭлементДерево, ИмяРеквизитаФормы, Развернуть = Истина)
	
	СтрокиДерева = ЭтотОбъект[ИмяРеквизитаФормы].ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если Развернуть Тогда
			ЭлементДерево.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
		Иначе
			ЭлементДерево.Свернуть(СтрокаДерева.ПолучитьИдентификатор());
		КонецЕсли;
							
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)

	ПометкаВидимости = Форма.Элементы.ДеревоКлассификаторовИнформация.Пометка;
	Форма.Элементы.ПраваяКолонка.Видимость = ПометкаВидимости;	

КонецПроцедуры // УстановитьВидимостьДоступность()

#КонецОбласти

#КонецОбласти

