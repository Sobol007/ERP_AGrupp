

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ВыборФайла.Заголовок = "Выбор файла для сохранения";
	ВыборФайла.Фильтр	= "Таблица Excel (*.xls)|*.xls|Таблица Excel (*.xlsx)|*.xlsx";
	ВыборФайла.Показать(Новый ОписаниеОповещения("ОбработкаВыбораФайла", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораФайла(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если (ВыбранныеФайлы <> Неопределено) И ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКФайлу = ВыбранныеФайлы[0];		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСписокРеквизитов()
	
	ВыгружаемыеРеквизиты.Очистить();
	
	Для Каждого РеквизитДокТендер Из Метаданные.Документы.АГ_Тендер.Реквизиты Цикл
		ВыгружаемыеРеквизиты.Добавить(РеквизитДокТендер.Имя,РеквизитДокТендер.Синоним,Истина);	
	КонецЦикла;	
	
КонецФункции	

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьСписокРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период.ДатаНачала = НачалоПериода;
	Диалог.Период.ДатаОкончания = КонецДня(КонецПериода);
	Диалог.Показать(Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		НачалоПериода = Период.ДатаНачала;
		КонецПериода = Период.ДатаОкончания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ПечатнаяФорма	= ВыгрузитьНаСервере();
	
	Если ВРег(Прав(ПутьКФайлу,4))="XLSX" Тогда
		ТипФайла	= ТипФайлаТабличногоДокумента.XLSX;	
	Иначе
		ТипФайла	= ТипФайлаТабличногоДокумента.XLS	
	КонецЕсли;	
	
	ПечатнаяФорма.Записать(ПутьКФайлу,ТипФайла);
	Сообщить("Файл "+ПутьКФайлу+" сохранен");
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьНаСервере()
	
	ТабВывод	= Новый ТабличныйДокумент;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Макет	= ОбработкаОбъект.ПолучитьМакет("МакетВыгрузки");
	
	ЭтоПервыйСтолбец	= Истина;
	Для Каждого Реквизит ИЗ ВыгружаемыеРеквизиты Цикл
		Если Реквизит.Пометка Тогда
			ОбластьШапка	= Макет.ПолучитьОбласть("Шапка");	
			ОбластьШапка.Параметры.НаименованиеКолонки	= Реквизит.Представление;
			Если ЭтоПервыйСтолбец Тогда
				ТабВывод.Вывести(ОбластьШапка);
				ЭтоПервыйСтолбец	= Ложь;
			Иначе
				ТабВывод.Присоединить(ОбластьШапка);
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	*
		|ИЗ
		|	Документ.АГ_Тендер КАК АГ_Тендер
		|ГДЕ
		|	АГ_Тендер.ПометкаУдаления = ЛОЖЬ
		|	И АГ_Тендер.Дата МЕЖДУ &НачДата И &КонДата
		|	И (АГ_Тендер.Организация = &Организация
		|			ИЛИ &БезОтбораПоОрганизации)
		|	И (АГ_Тендер.ОтветственныйЗаРаспределение = &ОтветственныйЗаРаспределение
		|			ИЛИ &БезОтбораПоОтветственномуЗаРаспределение)
		|	И (АГ_Тендер.ОтветственныйЗаМониторинг = &ОтветственныйЗаМониторинг
		|			ИЛИ &БезОтбораПоОтветственномуЗаМониторинг)
		|	И (АГ_Тендер.Ответственный = &Ответственный
		|			ИЛИ &БезОтбораПоОтветственному)
		|
		|УПОРЯДОЧИТЬ ПО
		|	АГ_Тендер.Дата";
	
	Запрос.УстановитьПараметр("БезОтбораПоОрганизации", НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("БезОтбораПоОтветственному", НЕ ЗначениеЗаполнено(Менеджер));
	Запрос.УстановитьПараметр("БезОтбораПоОтветственномуЗаМониторинг", НЕ ЗначениеЗаполнено(ОтветственныйЗаМониторинг));
	Запрос.УстановитьПараметр("БезОтбораПоОтветственномуЗаРаспределение", НЕ ЗначениеЗаполнено(КураторОЭТ));
	Запрос.УстановитьПараметр("КонДата", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("НачДата", НачалоПериода);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ответственный", Менеджер);
	Запрос.УстановитьПараметр("ОтветственныйЗаМониторинг", ОтветственныйЗаМониторинг);
	Запрос.УстановитьПараметр("ОтветственныйЗаРаспределение", КураторОЭТ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЭтоПервыйСтолбец	= Истина;
		Для Каждого Реквизит ИЗ ВыгружаемыеРеквизиты Цикл
			Если Реквизит.Пометка Тогда
				ОбластьСтрока	= Макет.ПолучитьОбласть("Строка");	
				ОбластьСтрока.Параметры.ЗначениеРеквизита	= ВыборкаДетальныеЗаписи[Реквизит.Значение];
				Если ЭтоПервыйСтолбец Тогда
					ТабВывод.Вывести(ОбластьСтрока);
					ЭтоПервыйСтолбец	= Ложь;
				Иначе
					ТабВывод.Присоединить(ОбластьСтрока);
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТабВывод;
	
КонецФункции	

	

