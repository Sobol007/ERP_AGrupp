
&НаСервере
Процедура ВыполнитьКонвертациюНаСервере()
	
	// Выборка Встреч и Запланированных взаимодействий 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Встреча.Ссылка КАК Ссылка,
	               |	Встреча.Дата КАК Дата,
	               |	Встреча.Автор КАК Автор,
	               |	Встреча.ДатаНачала КАК ДатаНачала,
	               |	Встреча.ДатаОкончания КАК ДатаОкончания,
	               |	Встреча.МестоПроведенияВстречи КАК МестоПроведенияВстречи,
	               |	Встреча.Описание КАК Описание,
	               |	Встреча.СписокУчастников КАК СписокУчастников,
	               |	Встреча.Тема КАК Тема,
	               |	Встреча.Комментарий КАК Комментарий,
	               |	Встреча.Участники.(
	               |		Контакт КАК Контакт,
	               |		ПредставлениеКонтакта КАК ПредставлениеКонтакта,
	               |		КакСвязаться КАК КакСвязаться
	               |	) КАК Участники,
	               |	Встреча.Ответственный КАК Ответственный,
	               |	ПредметыПапкиВзаимодействий.Рассмотрено КАК Рассмотрено,
	               |	NULL КАК АбонентКакСвязаться,
	               |	NULL КАК АбонентКонтакт,
	               |	Встреча.Представление КАК Представление
	               |ИЗ
	               |	Документ.Встреча КАК Встреча
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
	               |		ПО Встреча.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
	               |ГДЕ
	               |	НЕ Встреча.ПометкаУдаления
	               |	И Встреча.ДатаНачала >= &ДатаОбработки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ЗапланированноеВзаимодействие.Ссылка,
	               |	ЗапланированноеВзаимодействие.Дата,
	               |	ЗапланированноеВзаимодействие.Автор,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	ЗапланированноеВзаимодействие.Описание,
	               |	ЗапланированноеВзаимодействие.СписокУчастников,
	               |	ЗапланированноеВзаимодействие.Тема,
	               |	ЗапланированноеВзаимодействие.Комментарий,
	               |	ЗапланированноеВзаимодействие.Участники.(
	               |		Контакт,
	               |		ПредставлениеКонтакта,
	               |		КакСвязаться
	               |	),
	               |	ЗапланированноеВзаимодействие.Ответственный,
	               |	ПредметыПапкиВзаимодействий.Рассмотрено,
	               |	NULL,
	               |	NULL,
	               |	ЗапланированноеВзаимодействие.Представление
	               |ИЗ
	               |	Документ.ЗапланированноеВзаимодействие КАК ЗапланированноеВзаимодействие
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
	               |		ПО ЗапланированноеВзаимодействие.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
	               |ГДЕ
	               |	НЕ ЗапланированноеВзаимодействие.ПометкаУдаления
	               |	И ЗапланированноеВзаимодействие.Дата >= &ДатаОбработки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ТелефонныйЗвонок.Ссылка,
	               |	ТелефонныйЗвонок.Дата,
	               |	ТелефонныйЗвонок.Автор,
	               |	NULL,
	               |	NULL,
	               |	NULL,
	               |	ТелефонныйЗвонок.Описание,
	               |	NULL,
	               |	ТелефонныйЗвонок.Тема,
	               |	ТелефонныйЗвонок.Комментарий,
	               |	ПУСТАЯТАБЛИЦА.(, , ),
	               |	ТелефонныйЗвонок.Ответственный,
	               |	ПредметыПапкиВзаимодействий.Рассмотрено,
	               |	ТелефонныйЗвонок.АбонентКакСвязаться,
	               |	ТелефонныйЗвонок.АбонентКонтакт,
	               |	ТелефонныйЗвонок.Представление
	               |ИЗ
	               |	Документ.ТелефонныйЗвонок КАК ТелефонныйЗвонок
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПредметыПапкиВзаимодействий КАК ПредметыПапкиВзаимодействий
	               |		ПО ТелефонныйЗвонок.Ссылка = ПредметыПапкиВзаимодействий.Взаимодействие
	               |ГДЕ
	               |	НЕ ТелефонныйЗвонок.ПометкаУдаления
	               |	И ТелефонныйЗвонок.Дата >= &ДатаОбработки
	               |	И ТелефонныйЗвонок.сфпИдентификаторЗвонка = """"";
	
	Запрос.УстановитьПараметр("ДатаОбработки", Объект.ДатаНачала);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
	
		ВыбранныйДокумент = РезультатЗапроса.Ссылка;
		
		ЭтоВстреча = (ТипЗнч(ВыбранныйДокумент) = Тип("ДокументСсылка.Встреча"));
		ЭтоЗвонок  = (ТипЗнч(ВыбранныйДокумент) = Тип("ДокументСсылка.ТелефонныйЗвонок"));
		ЭтоПрочее  = (ТипЗнч(ВыбранныйДокумент) = Тип("ДокументСсылка.ЗапланированноеВзаимодействие"));
		
		
		ВзаимодействиеОбъект = Документы.CRM_Взаимодействие.СоздатьДокумент();
		ВзаимодействиеОбъект.Тема = РезультатЗапроса.Тема;
		ВзаимодействиеОбъект.Дата = РезультатЗапроса.Дата;
		ВзаимодействиеОбъект.Автор = РезультатЗапроса.Автор;
		ВзаимодействиеОбъект.Ответственный = РезультатЗапроса.Ответственный;
		ВзаимодействиеОбъект.Подразделение = РезультатЗапроса.Ответственный.Подразделение;
		ВзаимодействиеОбъект.Содержание = РезультатЗапроса.Описание;
		ВзаимодействиеОбъект.Результат = РезультатЗапроса.Комментарий;
		
		Если ЭтоВстреча Тогда
			ВзаимодействиеОбъект.ПлановаяДата = РезультатЗапроса.ДатаНачала;
			ВзаимодействиеОбъект.ПлановаяДатаЗавершение = РезультатЗапроса.ДатаОкончания;
			ВзаимодействиеОбъект.Место = РезультатЗапроса.МестоПроведенияВстречи;
			ВзаимодействиеОбъект.ВидВзаимодействия = Объект.ВидВзаимодействияВстречи;
			
		ИначеЕсли ЭтоЗвонок тогда
			ВзаимодействиеОбъект.ПлановаяДата = РезультатЗапроса.Дата;
			ВзаимодействиеОбъект.ВидВзаимодействия = Объект.ВидВзаимодействияЗвонок;
			
		Иначе
			ВзаимодействиеОбъект.ПлановаяДата = РезультатЗапроса.Дата;
			ВзаимодействиеОбъект.ВидВзаимодействия = Объект.ВидВзаимодействияПрочееВзаимодействие;
		КонецЕсли;
		
		Если РезультатЗапроса.Рассмотрено = Истина Тогда
			ВзаимодействиеОбъект.СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Завершено;
		Иначе
			ВзаимодействиеОбъект.СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Запланировано;
		КонецЕсли;
		
		
		Если ЗначениеЗаполнено(РезультатЗапроса.Автор) Тогда
			ВзаимодействиеОбъект.Организация = CRM_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(РезультатЗапроса.Автор, "ОсновнаяОрганизация");
		КонецЕсли;
		
		Если ЭтоЗвонок Тогда
			
			Если ТипЗнч(РезультатЗапроса.АбонентКонтакт) = Тип("СправочникСсылка.Партнеры") Тогда
				ВзаимодействиеОбъект.Партнер = РезультатЗапроса.АбонентКонтакт;
				ВзаимодействиеОбъект.СписокКлиентов = РезультатЗапроса.АбонентКонтакт;
				
			ИначеЕсли ТипЗнч(РезультатЗапроса.АбонентКонтакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
				ВзаимодействиеОбъект.Партнер = РезультатЗапроса.АбонентКонтакт.Владелец;
				ВзаимодействиеОбъект.КонтактноеЛицо = РезультатЗапроса.АбонентКонтакт;
				ВзаимодействиеОбъект.СписокКлиентов = РезультатЗапроса.АбонентКонтакт.Владелец;
			КонецЕсли;
			
			ВзаимодействиеОбъект.Содержание = НСтр("ru='Для связи:'") + РезультатЗапроса.АбонентКакСвязаться + Символы.ПС + Символы.ПС+ ВзаимодействиеОбъект.Содержание;
			
		Иначе
			// Участики
			ВыборкаУчастники = РезультатЗапроса.Участники.Выбрать();
			Пока ВыборкаУчастники.Следующий() Цикл
				Контакт = ВыборкаУчастники.Контакт;
				КакСвязаться = ВыборкаУчастники.КакСвязаться;
				
				Если ТипЗнч(Контакт) = Тип("СправочникСсылка.Партнеры") Тогда
					НоваяСтрокаТЧ = ВзаимодействиеОбъект.СторонниеЛица.Добавить();
					НоваяСтрокаТЧ.Партнер = Контакт;
					НоваяСтрокаТЧ.Адрес = КакСвязаться;
					НоваяСтрокаТЧ.ПредставлениеАдреса = КакСвязаться;
					
				ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.КонтактныеЛицаПартнеров") Тогда
					НоваяСтрокаТЧ = ВзаимодействиеОбъект.СторонниеЛица.Добавить();
					НоваяСтрокаТЧ.Партнер = Контакт.Владелец;
					НоваяСтрокаТЧ.КонтактноеЛицо = Контакт;
					НоваяСтрокаТЧ.Адрес = КакСвязаться;
					НоваяСтрокаТЧ.ПредставлениеАдреса = КакСвязаться;
				
				ИначеЕсли ТипЗнч(Контакт) = Тип("СправочникСсылка.Пользователи") ИЛИ ТипЗнч(Контакт) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
					НоваяСтрокаТЧ = ВзаимодействиеОбъект.СвоиЛица.Добавить();
					НоваяСтрокаТЧ.Лицо = Контакт;
					НоваяСтрокаТЧ.Адрес = КакСвязаться;
					НоваяСтрокаТЧ.ПредставлениеАдреса = КакСвязаться;
				КонецЕсли;
				
			КонецЦикла;
			
			// Партнер и его КЛ
			Если ВзаимодействиеОбъект.СторонниеЛица.Количество() = 1 Тогда
				ВзаимодействиеОбъект.Партнер = ВзаимодействиеОбъект.СторонниеЛица[0].Партнер;
				ВзаимодействиеОбъект.КонтактноеЛицо = ВзаимодействиеОбъект.СторонниеЛица[0].КонтактноеЛицо;
			КонецЕсли;
			
			// Списки участников и клиентов
			СписокКлиентов = "";
			СписокУчастников = "";
			Для Каждого Участник Из ВзаимодействиеОбъект.СторонниеЛица Цикл
				Если ЗначениеЗаполнено(Участник.Партнер) Тогда
					СписокКлиентов = СписокКлиентов + ?(СписокКлиентов = "","","; ") + Участник.Партнер;
				КонецЕсли;
				Если ЗначениеЗаполнено(Участник.КонтактноеЛицо) Тогда
					СписокУчастников = СписокУчастников + ?(СписокУчастников = "","","; ") + Участник.КонтактноеЛицо;
				КонецЕсли;
			КонецЦикла;
			
			ВзаимодействиеОбъект.СписокКлиентов = СписокКлиентов;
			ВзаимодействиеОбъект.СписокУчастников = СписокУчастников;
		КонецЕсли; 
		
		ВзаимодействиеОбъект.Содержание = НСтр("ru='Перенесено из документа: '") + РезультатЗапроса.Представление + Символы.ПС + Символы.ПС+ ВзаимодействиеОбъект.Содержание;
		
		Попытка
		
			ВзаимодействиеОбъект.Записать();
		
		Исключение
			Сообщить(НСтр("ru='Не удалось записать документ '") + ВзаимодействиеОбъект);
			Сообщить(ОписаниеОшибки())
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКонвертацию(Команда)
	
	Сообщить(НСтр("ru='Выполняется конвертация взаимодействий...'"));
	
	ВыполнитьКонвертациюНаСервере();
	
	Сообщить(НСтр("ru='Конвертация завершена'"));
	
КонецПроцедуры

