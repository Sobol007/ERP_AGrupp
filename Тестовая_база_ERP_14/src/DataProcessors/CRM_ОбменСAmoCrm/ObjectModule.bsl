#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ОписаниеПеременных
Перем Сервер Экспорт; // Сервер = домен + ".amocrm.ru"
Перем ПараметрыАвторизации Экспорт; // "?USER_LOGIN=" + %адрес эл. почты + "&USER_HASH=" + в настройках amoCRM ключ API
Перем cookieДанные Экспорт;
Перем Соединение Экспорт;
Перем ОбсуждениеРазрешены Экспорт;
Перем КофигурацияУТ Экспорт;
#КонецОбласти

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	ПараметрыРегистрации.Вид=ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Обмен с amo crm";
	НоваяКоманда.Идентификатор = "ОбменСAmoCrm";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	Возврат ПараметрыРегистрации;
	
КонецФункции

#Область Подключение
Функция ВыполнитьПроверкуПодключения() Экспорт
	
	ТекстСообщения = "Начало загрузки данных" + Символы.ПС + "Попытка авторизации:";
	ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	
	ИнициализироватьПараметрыСервера();
	ИнициализироватьПараметрыАвторизации(Истина);
	Ресурс = "/private/api/auth.php" + ПараметрыАвторизации;
	
	имяВыходногоФайла = ПолучитьИмяВременногоФайла();
	
	ССЛ = Новый ЗащищенноеСоединениеOpenSSL();
	Соединение = Новый HTTPСоединение(Сервер,,Логин,Пароль,,,ССЛ,Ложь);
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = Ресурс;
	ОтветHTTP = Соединение.ОтправитьДляОбработки(HTTPЗапрос, имяВыходногоФайла);
	// В ответ на запрос, при успешной авторизации, кроме тела ответа возвращается cookie файл,
	// содержащий ключ сессии, аналогично работе с WEB-браузером. 
	// При дальнейших запросах к API-методам нужно обратно передавать полученные cookie. 
	// Время жизни сессии - 15 минут.
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		ТекстСообщения = "success";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	Иначе
		ТекстСообщения = ПолучитьИнформациюПоОшибкам(ОтветHTTP.КодСостояния);
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Истина,Ложь,Ложь);
		Возврат Ложь;
	КонецЕсли;
	cookieДанные = ОтветHTTP.Заголовки.Получить("Set-Cookie");
	cookieДанные = СтрЗаменить(cookieДанные, ";", Символы.ПС);
	cookieДанные = СтрПолучитьСтроку(cookieДанные,1);
	
	текстовыйДокумент = Новый ТекстовыйДокумент;
	текстовыйДокумент.Прочитать(имяВыходногоФайла);
	ОтветЗначение = текстовыйДокумент.ПолучитьТекст();
	
	Результат = jsonПрочитать(ОтветЗначение);
	
	Возврат Результат.Получить("response").Получить("auth");
	
КонецФункции

Процедура СформироватьДерево(ЧтениеJSON, Дерево)
	
	ИмяСвойства = Неопределено;
	
	Пока ЧтениеJSON.Прочитать() Цикл
		TипJSON = ЧтениеJSON.ТипТекущегоЗначения;
		
		Если TипJSON = ТипЗначенияJSON.НачалоОбъекта 
			ИЛИ TипJSON = ТипЗначенияJSON.НачалоМассива Тогда
			НовыйОбъект = ?(TипJSON = ТипЗначенияJSON.НачалоОбъекта, Новый Соответствие, Новый Массив);
			
			Если ТипЗнч(Дерево) = Тип("Массив") Тогда
				Дерево.Добавить(НовыйОбъект);
			ИначеЕсли ТипЗнч(Дерево) = Тип("Соответствие") И ЗначениеЗаполнено(ИмяСвойства) Тогда
				Дерево.Вставить(ИмяСвойства, НовыйОбъект);
			КонецЕсли;
			
			СформироватьДерево(ЧтениеJSON, НовыйОбъект);
			
			Если Дерево = Неопределено Тогда
				Дерево = НовыйОбъект;
			КонецЕсли;
		ИначеЕсли TипJSON = ТипЗначенияJSON.ИмяСвойства Тогда
			ИмяСвойства = ЧтениеJSON.ТекущееЗначение;
		ИначеЕсли TипJSON = ТипЗначенияJSON.Число 
			ИЛИ TипJSON = ТипЗначенияJSON.Строка 
			ИЛИ TипJSON = ТипЗначенияJSON.Булево 
			ИЛИ TипJSON = ТипЗначенияJSON.Null Тогда
			Если ТипЗнч(Дерево) = Тип("Массив") Тогда
				Дерево.Добавить(ЧтениеJSON.ТекущееЗначение);
			ИначеЕсли ТипЗнч(Дерево) = Тип("Соответствие") Тогда
				Дерево.Вставить(ИмяСвойства, ЧтениеJSON.ТекущееЗначение);
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция jsonПрочитать(Значение)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(Значение);
	
	Результат = Неопределено;
	СформироватьДерево(ЧтениеJSON, Результат);
	
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьПараметрыАвторизации(ИспользоватьJson)
	
	ПараметрыАвторизации = "?USER_LOGIN=" + Логин + "&USER_HASH=" + Хеш + ?(ИспользоватьJson,"&type=json","");
	
КонецПроцедуры

Процедура ИнициализироватьПараметрыСервера()
	
	Если СтрНайти(Домен,".amocrm.ru") > 0 Тогда
		Сервер = Домен;
	Иначе
		Сервер = Домен + ".amocrm.ru";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура СохранитьНастройки() Экспорт
	
	КлючНастроек = "Обработка.CRM_ОбменСAmoCrm";
	Настройки = Новый Соответствие;
	Настройки.Вставить("ПартнерыСопоставление", 		ПартнерыСопоставление.Выгрузить());
	Настройки.Вставить("КонтактныеЛицаСопоставление", 	КонтактныеЛицаСопоставление.Выгрузить());
	Настройки.Вставить("СделкиСопоставление", 			СделкиСопоставление.Выгрузить());
	Настройки.Вставить("ПользователиСопоставление", 	ПользователиСопоставление.Выгрузить());
	Настройки.Вставить("ВидыВзаимодействийСопоставление", 	ВидыВзаимодействийСопоставление.Выгрузить());
	
	Настройки.Вставить("ПользователиСопоставлены", 	ПользователиСопоставлены);
	Настройки.Вставить("Логин", 					Логин);
	Настройки.Вставить("Пароль", 					Пароль);
	Настройки.Вставить("Хеш", 						Хеш);
	Настройки.Вставить("Домен", 					Домен);
	Настройки.Вставить("Организация", 				Организация);
	Настройки.Вставить("ДопРеквизитыСозданы", 		ДопРеквизитыСозданы);
	
	Настройки.Вставить("УчетнаяЗапись",		 		УчетнаяЗапись);
	Настройки.Вставить("РежимЗагрузкиДанных", 		РежимЗагрузкиДанных);
	Настройки.Вставить("ПерезаписыватьДанные", 		ПерезаписыватьДанные);
	Настройки.Вставить("ДатаЗагрузки", 				ДатаЗагрузки);
	
	Настройки.Вставить("Компании", 					Компании);
	Настройки.Вставить("Контакты", 					Контакты);
	Настройки.Вставить("Задачи", 					Задачи);
	Настройки.Вставить("Сделки", 					Сделки);
	Настройки.Вставить("События", 					События);
	Настройки.Вставить("СоздаватьФизЛиц", 			СоздаватьФизЛиц);
	
	Настройки.Вставить("ПомощникПройден", 			ПомощникПройден);
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Обработка.ВыгрузкаНоменклатуры", КлючНастроек, Настройки,,Строка(ПользователиКлиентСервер.ТекущийПользователь()));
	
КонецПроцедуры

Процедура ПередатьДанныеДляЖурнала() Экспорт
	
	КлючНастроек = "Обработка.CRM_ОбменСAmoCrm.ЖурналДанных";
	Настройки = Новый Соответствие;
	Настройки.Вставить("ЖурналСобытий", 					ЖурналСобытий);
	Настройки.Вставить("ЖурналСозданныхОбъектов", 			ЖурналСозданныхОбъектов.Выгрузить());
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Обработка.ВыгрузкаНоменклатуры", КлючНастроек, Настройки,,Строка(ПользователиКлиентСервер.ТекущийПользователь()));
	
КонецПроцедуры

Процедура ВосстановитьНастройки() Экспорт
	
	КлючНастроек = "Обработка.CRM_ОбменСAmoCrm";
	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Обработка.ВыгрузкаНоменклатуры", КлючНастроек,,,Строка(ПользователиКлиентСервер.ТекущийПользователь()));
	Если ТипЗнч(ЗначениеНастроек) = Тип("Соответствие") Тогда
		
		ЗначениеИзНастройкиПартнеры = ЗначениеНастроек.Получить("ПартнерыСопоставление");
		ЗначениеИзНастройкиКонтЛица = ЗначениеНастроек.Получить("КонтактныеЛицаСопоставление");
		ЗначениеИзНастройкиСделки	= ЗначениеНастроек.Получить("СделкиСопоставление");
		ЗначениеИзНастройкиПользов	= ЗначениеНастроек.Получить("ПользователиСопоставление");
		ЗначениеИзНастройкиВидыВзаимодействий	= ЗначениеНастроек.Получить("ВидыВзаимодействийСопоставление");
		
		ПользователиСопоставлены = ЗначениеНастроек.Получить("ПользователиСопоставлены");
		Логин        			= ЗначениеНастроек.Получить("Логин");
		Хеш 					= ЗначениеНастроек.Получить("Хеш");
		Домен     				= ЗначениеНастроек.Получить("Домен");
		Организация				= ЗначениеНастроек.Получить("Организация");
		ДопРеквизитыСозданы		= ЗначениеНастроек.Получить("ДопРеквизитыСозданы");
		Пароль                  = ЗначениеНастроек.Получить("Пароль");
		
		УчетнаяЗапись			 = ЗначениеНастроек.Получить("УчетнаяЗапись");
		РежимЗагрузкиДанных      = ЗначениеНастроек.Получить("РежимЗагрузкиДанных");
		ПерезаписыватьДанные     = ЗначениеНастроек.Получить("ПерезаписыватьДанные");
		ДатаЗагрузки             = ЗначениеНастроек.Получить("ДатаЗагрузки");
		
		Компании 				=  ЗначениеНастроек.Получить("Компании");
		Контакты 				=  ЗначениеНастроек.Получить("Контакты");
		Задачи 					=  ЗначениеНастроек.Получить("Задачи");
		Сделки 					=  ЗначениеНастроек.Получить("Сделки");
		События 				=  ЗначениеНастроек.Получить("События");
		СоздаватьФизЛиц         =  ЗначениеНастроек.Получить("СоздаватьФизЛиц");

		ПомощникПройден			=  ЗначениеНастроек.Получить("ПомощникПройден");
		
		Если ТипЗнч(ЗначениеИзНастройкиПартнеры) = Тип("ТаблицаЗначений") Тогда
			ПартнерыСопоставление.Загрузить(ЗначениеИзНастройкиПартнеры);
		КонецЕсли;
		Если ТипЗнч(ЗначениеИзНастройкиКонтЛица) = Тип("ТаблицаЗначений") Тогда
			КонтактныеЛицаСопоставление.Загрузить(ЗначениеИзНастройкиКонтЛица);
		КонецЕсли;
		Если ТипЗнч(ЗначениеИзНастройкиСделки) = Тип("ТаблицаЗначений") Тогда
			СделкиСопоставление.Загрузить(ЗначениеИзНастройкиСделки);
		КонецЕсли;
		Если ТипЗнч(ЗначениеИзНастройкиПользов) = Тип("ТаблицаЗначений") Тогда
			ПользователиСопоставление.Загрузить(ЗначениеИзНастройкиПользов);
		КонецЕсли;
		Если ТипЗнч(ЗначениеИзНастройкиВидыВзаимодействий) = Тип("ТаблицаЗначений") Тогда
			ВидыВзаимодействийСопоставление.Загрузить(ЗначениеИзНастройкиВидыВзаимодействий);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ЗагрузкаДанных
Функция ЗапуститьВФонеЗагрузкуДанных(ПараметрыЗаполнения,АдресОбработки) Экспорт
	
	ВосстановитьНастройки();
	
	ЖурналСобытий = "";
	ЖурналСозданныхОбъектов.Очистить();
	
	// Компании - Партнеры
	Если Компании Тогда
		
		Ресурс = "/api/v2/companies";
		
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,0,Ложь);
		
	КонецЕсли;
	
	// Контакты - Партнеры - контактные лица
	Если Контакты Тогда
		
		Ресурс = "/api/v2/contacts";
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,1,Ложь);
		
	КонецЕсли;
	
	Если Сделки Тогда
		
		Ресурс = "/api/v2/leads";
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,2,Ложь);
		
	КонецЕсли;
	
	Если Задачи Тогда
		
		Ресурс = "/api/v2/tasks";
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,3,Ложь);
		
	КонецЕсли;
	
	Если События Тогда
		
		Ресурс = "/api/v2/notes";
		// примечания к сделкам
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,4,Ложь);
		// примечания к компаниям
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,5,Ложь);
		// примечания к контактам
		ОтветСервера = ВыполнитьЗагрузкуДанных(Ресурс,6,Ложь);
		
	КонецЕсли;	
	
	ПередатьДанныеДляЖурнала();
	
КонецФункции

// Функция - Выполнить загрузку данных
//
// Параметры:
//  Ресурс							Ресурс - строка подключения к амо 
//  ИндексЗагрузки					ДанныеДляЗагрузки - цифра, обозначающая какие данные мы загружаются	  
//  ЗагрузкаДанныхДлясопоставления	ЗагрузкаДанных - Булево, загружаем данные в базу; ложь - данные для сопоставления	  
// 
// Возвращаемое значение:
//   - 
//
Функция ВыполнитьЗагрузкуДанных(Ресурс,ИндексЗагрузки,ЗагрузкаДанныхДлясопоставления,НачальныйЭлемент = Неопределено) Экспорт
	
	Если cookieДанные = Неопределено Тогда
		Отказ = НЕ ВыполнитьПроверкуПодключения();
		Если Отказ Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ИнициализироватьПараметрыАвторизации(Ложь);
	
	имяВыходногоФайла = ПолучитьИмяВременногоФайла();
	
	Если ИндексЗагрузки = 10 Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&with=users&free_users=Y";
	ИначеЕсли ИндексЗагрузки = 20 Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&with=task_types";
	ИначеЕсли ИндексЗагрузки = 0 ИЛИ ИндексЗагрузки = 1 И ЗагрузкаДанныхДлясопоставления Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&with=custom_fields";
	ИначеЕсли ИндексЗагрузки = 4 Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&type=lead";
		Если ЗначениеЗаполнено(ДатаЗагрузки) Тогда
			ПараметрыАвторизации = ПараметрыАвторизации + "&&filter[date_create][from]=" + ДатуВTimestamp(ДатаЗагрузки);
			ПараметрыАвторизации = ПараметрыАвторизации + "&&filter[date_create][to]=" + ДатуВTimestamp(ТекущаяДата());
		КонецЕсли;
	ИначеЕсли ИндексЗагрузки = 3 ИЛИ ИндексЗагрузки = 2 Тогда
		Если ЗначениеЗаполнено(ДатаЗагрузки) Тогда
			ПараметрыАвторизации = ПараметрыАвторизации + "&&filter[date_create][from]=" + ДатуВTimestamp(ДатаЗагрузки);
			ПараметрыАвторизации = ПараметрыАвторизации + "&&filter[date_create][to]=" + ДатуВTimestamp(ТекущаяДата());
		КонецЕсли;
	ИначеЕсли ИндексЗагрузки = 5 Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&type=company";
	ИначеЕсли ИндексЗагрузки = 6 Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&type=contact";
	КонецЕсли;
	
	// Ограничение 500 элементов.
	ПараметрыАвторизации = ПараметрыАвторизации + "&limit_rows=500";
	Если НЕ НачальныйЭлемент = Неопределено Тогда
		ПараметрыАвторизации = ПараметрыАвторизации + "&limit_offset=" + СтрЗаменить(Формат(НачальныйЭлемент,"ЧЦ=10"),Символы.НПП,"");
	КонецЕсли;

	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = Ресурс + ПараметрыАвторизации;
	
	HTTPЗапрос.Заголовки.Вставить("Set-cookie", cookieДанные);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
	
	Если РежимЗагрузкиДанных = 1 И ЗначениеЗаполнено(ДатаЗагрузки) Тогда
		HTTPЗапрос.Заголовки.Вставить("IF-MODIFIED-SINCE", ДатуВTimestamp(ДатаЗагрузки));
	КонецЕсли;
	ТекстСообщения = "Получение данных из amoCRM";
	ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	
	
	ОтветHTTP = Соединение.Получить(HTTPЗапрос, имяВыходногоФайла);
	
	Если ОтветHTTP.КодСостояния = 200 Тогда
		ТекстСообщения = "success";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	Иначе
		ТекстСообщения = ПолучитьИнформациюПоОшибкам(ОтветHTTP.КодСостояния);
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		Возврат Ложь;
	КонецЕсли;
	
	ТекстСообщения = "Обработка ответа...";
	ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	текстовыйДокумент = Новый ТекстовыйДокумент;
	текстовыйДокумент.Прочитать(имяВыходногоФайла);
	ОтветЗначение = текстовыйДокумент.ПолучитьТекст();
	
	Результат = jsonПрочитать(ОтветЗначение);
	
	// переходим к загрузке данных
	// данные собираются в _embedded в массив items 
	Если ИндексЗагрузки = 10 Тогда
		МассивДанных = Результат.Получить("_embedded").Получить("users");
	ИначеЕсли ИндексЗагрузки = 0 И ЗагрузкаДанныхДлясопоставления Тогда
		МассивДанных = Результат.Получить("_embedded").Получить("custom_fields").Получить("companies");
	ИначеЕсли ИндексЗагрузки = 1 И ЗагрузкаДанныхДлясопоставления Тогда
		МассивДанных = Результат.Получить("_embedded").Получить("custom_fields").Получить("contacts");
	ИначеЕсли ИндексЗагрузки = 20 Тогда
		МассивДанных = Результат.Получить("_embedded").Получить("task_types");
	Иначе
		МассивДанных = Результат.Получить("_embedded").Получить("items");
	КонецЕсли;
	
	Если МассивДанных.Количество() = 0 Тогда
		ТекстСообщения = "Данные для загрузки отсутсвуют";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗагрузкаДанныхДлясопоставления Тогда
		Если ИндексЗагрузки = 10 Тогда
			ЗагрузитьПользователейДляСопоставления(МассивДанных);
		ИначеЕсли ИндексЗагрузки = 0 Тогда
			ЗагрузитьПоляКомпанийДляСопоставления(МассивДанных);
		ИначеЕсли ИндексЗагрузки = 1 Тогда
			ЗагрузитьПоляКонтактныхЛицДляСопоставления(МассивДанных);
		ИначеЕсли ИндексЗагрузки = 20 Тогда
			ЗагрузитьВидыВзаимодействий(МассивДанных);
		Иначе
			ЗагрузкаДанныхДляСопоставления(МассивДанных,ИндексЗагрузки);
		КонецЕсли;
		
	Иначе
		ТекстСообщения = "Загрузка данных " + ТекущаяДата();
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
		ЗагрузкаДанныхВБазуДанных(МассивДанных,ИндексЗагрузки);
		
		ТекстСообщения = "Загрузка данных завершена " + ТекущаяДата();
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	КонецЕсли;
	
	ПередатьДанныеДляЖурнала();
	
	Если НЕ ЗагрузкаДанныхДлясопоставления Тогда
		
		Если НачальныйЭлемент = Неопределено Тогда
			НачальныйЭлемент = 501;
		Иначе
			НачальныйЭлемент = НачальныйЭлемент + 500;
		КонецЕсли;
		
		ВыполнитьЗагрузкуДанных(Ресурс,ИндексЗагрузки,ЗагрузкаДанныхДлясопоставления,НачальныйЭлемент);
		
	КонецЕсли;

КонецФункции

Процедура ЗагрузитьПользователейДляСопоставления(Массив)
	
	Для Каждого СтокаДанных ИЗ Массив Цикл
		
		СтрокаИнформация = СтокаДанных.Значение;
		
		Структура = Новый Структура;
		Структура.Вставить("ИДАмо",ВернутьПредставлениеИДАмо(СтрокаИнформация.Получить("id")));
		Структура.Вставить("ИмяАмо",СтрокаИнформация.Получить("name"));
		Структура.Вставить("ЭлАдресПользователя", СтрокаИнформация.Получить("login"));
		Структура.Вставить("Выбрано",Истина);
		
		НоваяСтрока = ПользователиСопоставление.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Структура);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьПоляКонтактныхЛицДляСопоставления(Массив)
	
	// дополнительные поля находятся в custom_fields - массив
	Для Каждого СтокаДанных ИЗ Массив Цикл
		Структура = Новый Структура;
		Структура.Вставить("ИДАмо",ВернутьПредставлениеИДАмо(СтокаДанных.Ключ));
		Структура.Вставить("ИмяАмо",СтокаДанных.Значение.Получить("name"));
		СписокЗначений = СтокаДанных.Значение.Получить("enums");
		Если НЕ СписокЗначений = Неопределено Тогда
			Структура.Вставить("ТИпЗначений","СписокЗначений");
			Структура.Вставить("ТипЗначенийФизЛицо", "СписокЗначений");
		КонецЕсли;
		
		ТипЗначенияРеквизита = СтокаДанных.Значение.Получить("field_type");
		Если ТипЗначенияРеквизита = 3 Тогда
			// Булево
			Структура.Вставить("ТИпЗначений","Булево");
			Структура.Вставить("ТипЗначенийФизЛицо","Булево");
			
		ИначеЕсли ТипЗначенияРеквизита = 1 Тогда
			// Строка
			Структура.Вставить("ТИпЗначений","Строка");
			Структура.Вставить("ТипЗначенийФизЛицо","Строка");
			
		ИначеЕсли ТипЗначенияРеквизита = 2 Тогда
			// Число
			Структура.Вставить("ТИпЗначений","Число");
			Структура.Вставить("ТипЗначенийФизЛицо","Число");
			
		ИначеЕсли ТипЗначенияРеквизита = 6 Тогда
			// Дата
			Структура.Вставить("ТИпЗначений","Дата");
			Структура.Вставить("ТипЗначенийФизЛицо","Дата");
			
		КонецЕсли;
		
		НоваяСтрока = КонтактныеЛицаСопоставление.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Структура);
	КонецЦикла;
	
	КонтактныеЛицаСопоставление.Свернуть("ИдАмо,ИмяАмо,ТИпЗначений,ТипЗначенийФизЛицо");
	КонтактныеЛицаСопоставление.Сортировать("ИмяАмо");
	
КонецПроцедуры

Процедура ЗагрузитьПоляКомпанийДляСопоставления(Массив)
	
	// дополнительные поля находятся в custom_fields - массив
	Для Каждого СтокаДанных ИЗ Массив Цикл
		Структура = Новый Структура;
		Структура.Вставить("ИДАмо",ВернутьПредставлениеИДАмо(СтокаДанных.Ключ));
		Структура.Вставить("ИмяАмо",СтокаДанных.Значение.Получить("name"));
		СписокЗначений = СтокаДанных.Значение.Получить("enums");
		Если НЕ СписокЗначений = Неопределено Тогда
			Структура.Вставить("ТИпЗначений","СписокЗначений");
		КонецЕсли;
		
		ТипЗначенияРеквизита = СтокаДанных.Значение.Получить("field_type");
		Если ТипЗначенияРеквизита = 3 Тогда
			Структура.Вставить("ТИпЗначений","Булево");
		КонецЕсли;
		
		НоваяСтрока = ПартнерыСопоставление.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Структура);
	КонецЦикла;
	
	ПартнерыСопоставление.Свернуть("ИдАмо,ИмяАмо,ТИпЗначений");
	ПартнерыСопоставление.Сортировать("ИмяАмо");
	
КонецПроцедуры

Процедура ЗагрузкаДанныхДляСопоставления(Массив,ИндексЗагрузки)
	
	// дополнительные поля находятся в custom_fields - массив
	Для Каждого СтокаДанных ИЗ Массив Цикл
		
		Если ИндексЗагрузки = 2 Тогда
			МассивДопПолей = СтокаДанных.Значение.Получить("statuses");
			СтрокаВерхнегоУровня = СделкиСопоставление.Добавить();
			СтрокаВерхнегоУровня.ИмяАмо = СтокаДанных.Значение.Получить("name");
			СтрокаВерхнегоУровня.ИдАмо = СтокаДанных.Ключ;
			СтрокаВерхнегоУровня.Воронка = Истина;
			СтрокаВерхнегоУровня.Владелец = СтокаДанных.Значение.Получить("name");
			СтрокаВерхнегоУровня.Выбрано = Истина;
		Иначе
			МассивДопПолей = СтокаДанных.Получить("custom_fields");
		КонецЕсли;
		Для Каждого ДопПоля Из МассивДопПолей Цикл
			
			Структура = Новый Структура;
			Если ИндексЗагрузки = 2 Тогда
				Структура.Вставить("ИДАмо",ВернутьПредставлениеИДАмо(ДопПоля.Значение.Получить("id")));
				Структура.Вставить("ИмяАмо",ДопПоля.Значение.Получить("name"));
				Структура.Вставить("Владелец",СтрокаВерхнегоУровня.ИмяАмо);
				Структура.Вставить("Выбрано",Истина);
			Иначе
				Структура.Вставить("ИДАмо",ВернутьПредставлениеИДАмо(ДопПоля.Получить("id")));
				Структура.Вставить("ИмяАмо",ДопПоля.Получить("name"));
			КонецЕсли;
			
			Если ИндексЗагрузки = 0 Тогда
				НоваяСтрока = ПартнерыСопоставление.Добавить();
			ИначеЕсли ИндексЗагрузки = 1 Тогда
				НоваяСтрока = КонтактныеЛицаСопоставление.Добавить();
			ИначеЕсли ИндексЗагрузки = 2 Тогда
				НоваяСтрока = СделкиСопоставление.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(НоваяСтрока,Структура);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ИндексЗагрузки = 0 Тогда
		ЭтотОбъект.ПартнерыСопоставление.Свернуть("ИдАмо,ИмяАмо");
	ИначеЕсли ИндексЗагрузки = 1 Тогда
		ЭтотОбъект.КонтактныеЛицаСопоставление.Свернуть("ИдАмо,ИмяАмо");
	ИначеЕсли ИндексЗагрузки = 2 Тогда
		ЭтотОбъект.СделкиСопоставление.Свернуть("ИдАмо,ИмяАмо,Воронка,Владелец,Выбрано");
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузкаДанныхВБазуДанных(Массив,ДанныеДляЗагруки) Экспорт
	
	Если КофигурацияУТ = Неопределено Тогда
		ОпределитьКонфигурацию();
	КонецЕсли;
	
	
	Если ДанныеДляЗагруки = 0 Тогда
		
		ТекстСообщения = "------------------------------------------------" + Символы.ПС + "Выполнение операции : создание и обновление партнеров...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
		ЗагрузкаПартнеровВБазуДанных(Массив);
		
		ТекстСообщения = "Загрузка партнеров завершена";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
	ИначеЕсли ДанныеДляЗагруки = 1 Тогда
		
		ТекстСообщения =  "------------------------------------------------" + Символы.ПС + "Выполнение операции : создание и обновление контактных лиц партнеров...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
		ЗагрузкаКонтактныхЛицВБазуДанных(Массив);
		
		ТекстСообщения = "Загрузка контактных лиц партнеров завершена";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
	ИначеЕсли ДанныеДляЗагруки = 2 Тогда
		
		ТекстСообщения =  "------------------------------------------------" + Символы.ПС + "Выполнение операции : создание и обновление сделок(интересов) партнеров...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
		ЗагрузкаСделокВБазуДанных(Массив);
		
		ТекстСообщения = "Загрузка Сделок(интересов) завершена";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
	ИначеЕсли ДанныеДляЗагруки = 3 Тогда
		
		ТекстСообщения =  "------------------------------------------------" + Символы.ПС + "Выполнение операции : создание и обновление взаимодействий с клиентами...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
		ЗагрузкаЗадачВБазуДанных(Массив);
		
		ТекстСообщения = "Загрузка взаимодействий завершена";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
	ИначеЕсли ДанныеДляЗагруки = 4 ИЛИ ДанныеДляЗагруки = 5 ИЛИ ДанныеДляЗагруки = 6 Тогда
		
		ТекстСообщения =  "------------------------------------------------" + Символы.ПС + "Выполнение операции : создание и обновление партнеров...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
		ЗагрузитьПримечанияДляОбъектовБазыДанных(Массив);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьПримечанияДляОбъектовБазыДанных(Массив)
	
	ТекстСообщения = "Загрузка дополнительных сведений...";
	ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	
	Для Каждого Стр Из Массив Цикл
		
		Структура = Новый Структура;
		ПоказатьХодВыполнения(Массив.Найти(Стр)/Массив.Количество() * 10,"Загрузка дополнительных сведений...");
		
		ТипСобытия = Стр.Получить("note_type");
		// Код	Тип	Описание
		// 1	DEAL_CREATED	Сделка создана
		// 2	CONTACT_CREATED	Контакт создан
		// 3	DEAL_STATUS_CHANGED	Статус сделки изменен
		// 4	COMMON	Обычное примечание
		// 5 - файл  - в описании к амо нет информации об этом
		// 10	CALL_IN	Входящий звонок
		// 11	CALL_OUT	Исходящий звонок
		// 12	COMPANY_CREATED	Компания создана
		// 13	TASK_RESULT	Результат по задаче
		// 25	SYSTEM	Системное сообщение
		// 102	SMS_IN	Входящее смс
		// 103	SMS_OUT	Исходящее смс
		ТипСущности 	= Стр.Получить("element_type");
		ИДСущности  	= ВернутьПредставлениеИДАмо(Стр.Получить("element_id"));
		Автор			= ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("created_by")));
		Ответственный 	= ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("responsible_user_id")));
		
		Структура.Вставить("Автор",Автор);
		Структура.Вставить("Ответственный",Ответственный);
		
		Если ТипСущности = 1 Тогда
			ОбъектДанных = ПолучитьКонтактныеЛицаПартнераПоИД(ИДСущности,Неопределено);
		ИначеЕсли ТипСущности = 2 Тогда
			ОбъектДанных = ПолучитьСделкуПоИД(ИДСущности);
		ИначеЕсли ТипСущности = 3 Тогда
			ОбъектДанных = ПолучитьПартнераПоИД(ИДСущности);
		КонецЕсли;
		
		Если  ТипСобытия = 1 ИЛИ ТипСобытия = 25 Тогда
			
			ИдПримечания = ВернутьПредставлениеИДАмо(Стр.Получить("id"));
			ИдентификаторОбсужденияСистемыВзаимодействия = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ИдПримечания + "тест");
			Примечание = Стр.Получить("text");
			Структура.Вставить("ИдентификаторСообщения",ИДСущности);
			Структура.Вставить("ИдентификаторОбсуждения",ИдентификаторОбсужденияСистемыВзаимодействия);
			Структура.Вставить("Текст",Примечание);
			Структура.Вставить("Объект",ОбъектДанных);
			
		ИначеЕсли  ТипСобытия = 3  Тогда                                                                    
			
			ИдПримечания = ВернутьПредставлениеИДАмо(Стр.Получить("id"));
			ИдентификаторОбсужденияСистемыВзаимодействия = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ИдПримечания + "тест");
			Попытка
				Примечание = Стр.Получить("params").Получить("TEXT");
				СтатусНовый =  СделкиСопоставление.Найти(Стр.Получить("params").Получить("STATUS_NEW"),"ИДАмо").СостояниеИнтереса;
				СтатусСтарый = СделкиСопоставление.Найти(Стр.Получить("params").Получить("STATUS_OLD"),"ИДАмо").СостояниеИнтереса;
				ТекстЗаполнения = Примечание + " " + СтатусНовый;
				Структура.Вставить("ИдентификаторСообщения",ИДСущности);
				Структура.Вставить("ИдентификаторОбсуждения",ИдентификаторОбсужденияСистемыВзаимодействия);
				Структура.Вставить("Текст",ТекстЗаполнения);
				Структура.Вставить("Объект",ОбъектДанных);
			Исключение
			КонецПопытки;
			
		ИначеЕсли  ТипСобытия = 4  Тогда                                                                    
			
			ИдПримечания = ВернутьПредставлениеИДАмо(Стр.Получить("id"));
			ИдентификаторОбсужденияСистемыВзаимодействия = Новый ИдентификаторОбсужденияСистемыВзаимодействия(ИдПримечания + "тест");
			Примечание = Стр.Получить("text");
			Структура.Вставить("ИдентификаторСообщения",ИДСущности);
			Структура.Вставить("ИдентификаторОбсуждения",ИдентификаторОбсужденияСистемыВзаимодействия);
			Структура.Вставить("Текст",Примечание);
			Структура.Вставить("Объект",ОбъектДанных);
			
			Попытка
				ВыполнитьРаботуСОповещениями(Структура);
			Исключение
				ТекстСообщения = "Данные по примечаниям не загружены в базу. Убедитесь, что все пользователи зарегестрированы в системе взаимодействия";
				ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
			КонецПопытки;
			
		ИначеЕсли ТипСобытия = 5 Тогда
			
			ПутьДляЗагрузки = "/download/";
			
			ИмяФайлаДляЗагрузки = Стр.Получить("attachment");
			
			ИмяФайлаДляСохранения = Стр.Получить("params").Получить("TEXT");
			
			ИмяФайлаДляЗагрузки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайлаДляЗагрузки);
			
			Попытка
				ПрисоединитьФайлКобъекту(ОбъектДанных,ИмяФайлаДляЗагрузки,ИмяФайлаДляСохранения,ПутьДляЗагрузки);
				Текстсообщения = "Дополнительные файл(ы): " + ИмяФайлаДляСохранения + " добавлены в объект " + ОбъектДанных;
				ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
			Исключение
				Текстсообщения = "Возникли ошибки при записи файла к объекту: " + ОбъектДанных + Символы.ПС + ОписаниеОшибки();
				ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗагрузитьВидыВзаимодействий(Массив)
	
	для Каждого Стр Из Массив Цикл
		
		ИД = ВернутьПредставлениеИДАмо(Стр.Значение.Получить("id"));
		
		Наименование = Стр.Значение.Получить("name");
		
		СтрТабЧасти = ВидыВзаимодействийСопоставление.Найти("ИД","ИдАмо");
		                                   
		Если НЕ СтрТабЧасти = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ВидыВзаимодействийСопоставление.Добавить();
		
		НоваяСтрока.ИдАмо = ИД;
		НоваяСтрока.ИмяАмо = Наименование;
		НоваяСтрока.ВидыВзаимодействий = ПолучитьВидыВзаимодействийПоИнтересу(Наименование);
		Справочники.CRM_ВидыВзаимодействий.НайтиПоНаименованию(Наименование);
		
	КонецЦикла;
		
КонецПроцедуры

Функция ПолучитьВидыВзаимодействийПоИнтересу(Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	CRM_ВидыВзаимодействий.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.CRM_ВидыВзаимодействий КАК CRM_ВидыВзаимодействий
	|ГДЕ
	|	CRM_ВидыВзаимодействий.Наименование = &Наименование
	|	И CRM_ВидыВзаимодействий.ВидДела = &ВидДела";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("ВидДела", Справочники.CRM_ВидыДелВзаимодействий.Документ_CRM_Интерес);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	Иначе
		Возврат Справочники.CRM_ВидыВзаимодействий.ПустаяСсылка();
	КонецЕсли;

КонецФункции

Процедура ЗагрузкаЗадачВБазуДанных(Массив)
	
	Для Каждого Стр Из Массив Цикл
		
		ПоказатьХодВыполнения(Массив.Найти(Стр)/Массив.Количество() * 10,"Загрузка взаимодействий по сделкам...");
		
		ТипЭлемента = Стр.Получить("element_type");
		ТипСобытия  = ВернутьПредставлениеИДАмо(Стр.Получить("task_type"));
		СтруктураДанных = Новый Структура;
		
		Попытка
			СтрокаТЗ = ВидыВзаимодействийСопоставление.Найти(ТипСобытия,"ИдАмо");
			СтруктураДанных.Вставить("ВидВзаимодействия",СтрокаТЗ.ВидыВзаимодействий);
		Исключение
			СтруктураДанных.Вставить("ВидВзаимодействия",Справочники.CRM_ВидыВзаимодействий.ПустаяСсылка());
		КонецПопытки;
		
		СтруктураДанных.Вставить("ИДЗадачи",ВернутьПредставлениеИДАмо(Стр.Получить("id")));
		
		СтруктураДанных.Вставить("ИДОбъекта",ВернутьПредставлениеИДАмо(Стр.Получить("element_id")));
		
		СтруктураДанных.Вставить("Завершено",Стр.Получить("is_completed"));
		
		Если НЕ Стр.Получить("result").Получить("text") = Неопределено Тогда
			 ТекстРезультата = Стр.Получить("result").Получить("text");
		 Иначе
			 ТекстРезультата = "";
		КонецЕсли;
		СтруктураДанных.Вставить("Результат",ТекстРезультата);
		
		СтруктураДанных.Вставить("Содержание",Стр.Получить("text"));
		
		СтруктураДанных.Вставить("Автор",ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("created_by"))));
		
		СтруктураДанных.Вставить("Ответственный",ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("responsible_user_id"))));
		
		СтруктураДанных.Вставить("Дата",TimestampВДату(Стр.Получить("created_at")));
		СтруктураДанных.Вставить("ДатаЗавершенияВзаимодействия",TimestampВДату(Стр.Получить("complete_till_at")));
		СтруктураДанных.Вставить("ПлановаяДата",TimestampВДату(Стр.Получить("created_at")));
		СтруктураДанных.Вставить("ПлановаяДатаЗавершение",TimestampВДату(Стр.Получить("complete_till_at")));
		
		СоздатьВзаимодействиеДляОбъекта(ТипЭлемента,СтруктураДанных, ТипСобытия);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьВзаимодействиеДляОбъекта(ТипДанных,Структура,Типсобытия)
	
	ВидВзаимодействия = Структура.ВидВзаимодействия;
	
	Если Структура.Завершено Тогда
		СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Завершено;
	Иначе
		СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Запланировано;
	КонецЕсли;
	
	ИДОбъекта = Структура.ИдОбъекта;
	
	Структура.Вставить("Организация",Организация);
	Структура.Вставить("СтатусВзаимодействия",СтатусВзаимодействия);
	
	Если ЗначениеЗаполнено(Структура.Содержание) Тогда
		Структура.Вставить("Тема", Структура.Содержание);
	Иначе
		Структура.Вставить("Тема",Строка(ВидВзаимодействия));
	КонецЕсли;
	
	Если ТипДанных = 1 Тогда
		ОбъектДанных = ПолучитьКонтактныеЛицаПартнераПоИД(ИдОбъекта,Неопределено);
	ИначеЕсли ТипДанных = 2 Тогда
		ОбъектДанных = ПолучитьСделкуПоИД(ИДОбъекта);
		Если НЕ ОбъектДанных = Неопределено Тогда
			Структура.Вставить("ДокументОснование",ОбъектДанных);
			Структура.Вставить("Партнер",ОбъектДанных.Партнер);
			Структура.Вставить("КонтактноеЛицо",ОбъектДанных.КонтактноеЛицо);
		КонецЕсли;
		
	ИначеЕсли ТипДанных = 3 ТОгда
		ОбъектДанных = ПолучитьПартнераПоИД(ИДОбъекта);
		Если ОбъектДанных = Неопределено Тогда
		Структура.Вставить("Партнер",Справочники.Партнеры.ПустаяСсылка());
		Иначе
		Структура.Вставить("Партнер",ОбъектДанных);
		КонецЕсли;
	КонецЕсли;
	
	ДокументНайден = Истина;
	
	ДокСсылка = ПолучитьВзаимодействиеПоИд(Структура.ИдЗадачи);
	Если ДокСсылка = Неопределено Тогда
		ДокументНайден = Ложь;
	КонецЕсли;
	
	Если НЕ ПерезаписыватьДанные и ДокументНайден Тогда
		ДокОбъект = ДокСсылка.ПолучитьОбъект();
		ДокОбъект.CRM_Теги.Очистить();
	ИначеЕсли НЕ ПерезаписыватьДанные и ДокументНайден Тогда
		ТекстСообщения = "Взаимодействие по сделке с ид " + Структура.ИдЗадачи + " уже существует в базе данных, обновление производиться не будет. Продолжение загрузки...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		возврат;
	ИначеЕсли  НЕ ПерезаписыватьДанные И НЕ ДокументНайден Тогда
		ДокОбъект = Документы.CRM_Взаимодействие.СоздатьДокумент();
	Иначе
		ДокОбъект = Документы.CRM_Взаимодействие.СоздатьДокумент();
	КонецЕсли;
	
	ДокОбъект.Заполнить(Структура);
	
	Попытка
		ДокОбъект.Записать();
		ТекстСообщения = "Создано взаимодействие " + ДокОбъект;
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,ДокОбъект);
	Исключение
		Текстсообщения = ОписаниеОшибки();
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	КонецПопытки;
	
	Если Не ДокОбъект = Неопределено Тогда
		
		ПВХИД = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИДВзаимодействиеАмо");
		
		ДопРеквизитОтсутствует = УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ДокОбъект.Ссылка,ПВХИД);
		
		ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
		ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство",Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
		ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
		
		НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
		НоваястрТаблицы.Свойство = ПВХИД;
		НоваястрТаблицы.Значение = Структура.ИДЗадачи;
		
		УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ДокОбъект.Ссылка,ТаблицаСвойствИЗначений);
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗагрузкаКонтактныхЛицВБазуДанных(Массив)
	
	ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
	ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство",Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
	
	Для Каждого Стр Из Массив Цикл
		
		ПоказатьХодВыполнения(Массив.Найти(Стр)/Массив.Количество() * 10,"Загрузка данных по контактным лицам партнеров...");
		
		ТаблицаСвойствИЗначений.Очистить();
		
		ИДКонтактногоЛица = ВернутьПредставлениеИДАмо(Стр.Получить("id"));
		ИДКомпании		  = ВернутьПредставлениеИДАмо(Стр.Получить("company").Получить("id"));
		
		Если ИДКомпании = Неопределено ИЛИ ИДКомпании = "" Тогда
			ПартнерСсылка = Неопределено;
		Иначе
			ПартнерСсылка = ПолучитьПартнераПоИД(ИДКомпании);
		КонецЕсли;
		
		Если ПартнерСсылка = Неопределено И СоздаватьФизЛиц Тогда
			// создаем физических лиц
			Если НЕ ИДКонтактногоЛица = Неопределено ИЛИ НЕ ИДКонтактногоЛица = "" Тогда
				ПартнерСсылка = ПолучитьПартнераПоИД(ИДКонтактногоЛица);
			КонецЕсли;
			СтруктураЗаполнения = Новый Структура;
			СтруктураЗаполнения.Вставить("ПартнерСсылка",ПартнерСсылка);
			СтруктураЗаполнения.Вставить("ИДПартнер",ИДКонтактногоЛица);
			ОтветственныйПользователь = ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("responsible_user_id")));
			СтруктураЗаполнения.Вставить("Наименование",Стр.Получить("name"));
			СтруктураЗаполнения.Вставить("ОсновнойМенеджер",ОтветственныйПользователь);
			СтруктураЗаполнения.Вставить("CRM_ДатаРегистрацииКомпании",ДатуВTimestamp(Стр.Получить("created_at")));
			СтруктураЗаполнения.Вставить("ЮрФизЛицо",Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);
			
			
			ПартнерОбъект = ЗаполнитьПартнераВБазеДанных(СтруктураЗаполнения);
			Если КофигурацияУТ Тогда
				МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("ПартнерыИКонтрагенты");
				
				Контрагент = МодульУправлениеДоступом.ПолучитьКонтрагентаПартнераПоУмолчанию(ПартнерОбъект.Ссылка);
				Если Контрагент.ПУстая() Тогда
					КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
				Иначе
					КонтрагентОбъект = Контрагент.ПолучитьОбъект();
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(КонтрагентОбъект,СтруктураЗаполнения);
				КонтрагентОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель;
				КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
			КонецЕсли;

			МассивРеквизитов = Стр.Получить("custom_fields");
			
			Для Каждого СтрокаРеквизит Из МассивРеквизитов Цикл
				
				// Нужна проверка на контактную информацию
				ИДРеквизита = ВернутьПредставлениеИДАмо(СтрокаРеквизит.Получить("id"));
				
				ЗначениеРеквизита = СтрокаРеквизит.получить("values")[0].Получить("value");
				СтруктураПоиска = Новый Структура;
				СтруктураПоиска.Вставить("ИдАмо",ИДРеквизита);
				
				Если СоздаватьФизЛиц Тогда
					НайденныеСтроки = КонтактныеЛицаСопоставление.НайтиСтроки(СтруктураПоиска);
				Иначе
					НайденныеСтроки = ПартнерыСопоставление.НайтиСтроки(СтруктураПоиска);
				КонецЕсли;
				
				Если НайденныеСтроки.Количество() = 0 Тогда
					Продолжить;
				Иначе
					СтрокаДанные = НайденныеСтроки[0];
					РеквизитДляПоиска = СтрокаДанные.ИмяРеквизита;
					Если НЕ ЗначениеЗаполнено(РеквизитДляПоиска) Тогда
						Продолжить;
					КонецЕсли;
					
					// Если КонтактнаяИнформация, то добавляем данные в ТЧ.КонтактнаяИнформация
					Если СтрокаДанные.КонтактнаяИнформация Тогда
						
						ОбъектXDTOКИ = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOПоПредставлению(ЗначениеРеквизита, СтрокаДанные.ТипКонтактнойИнформации);
						ЗначенияПолей = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOВXML(ОбъектXDTOКИ);
						CRM_УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(ПартнерОбъект, ЗначенияПолей, Справочники.ВидыКонтактнойИнформации[СтрокаДанные.ИмяРеквизита],СтрокаДанные.ТипКонтактнойИнформации,,ПартнерОбъект.CRM_ДатаРегистрацииКомпании,Истина);
					иначеЕсли СтрокаДанные.ДопРеквизит Тогда
						
						ПВХДоп = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя",РеквизитДляПоиска);
						НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
						НоваястрТаблицы.Свойство = ПВХДоп;
						Если СтрокаДанные.ТипЗначений = "Булево" Тогда
							Если ЗначениеРеквизита = "1" Тогда
								ЗначениеРеквизита = Истина;
							иначе
								ЗначениеРеквизита = Ложь;
							КонецЕсли;
						ИначеЕсли СтрокаДанные.ТипЗначений = "СписокЗначений" Тогда
							ДопЗначениеРеквизита = ПолучитьЗначениеСвойствПВХ(ПВХДоп,ЗначениеРеквизита);
							ЗначениеРеквизита = ДопЗначениеРеквизита;
						КонецЕсли;
						
						НоваястрТаблицы.Значение = ЗначениеРеквизита;
						
					Иначе
						
					Если КофигурацияУТ Тогда
						Попытка
							ЗаписываемРеквизитОбъектаПоТипу(ПартнерОбъект,РеквизитДляПоиска,ЗначениеРеквизита);
						Исключение
						КонецПопытки;
						Попытка
							ЗаписываемРеквизитОбъектаПоТипу(КонтрагентОбъект,РеквизитДляПоиска,ЗначениеРеквизита);
						Исключение
						КонецПопытки;
					Иначе 
						Попытка
							ЗаписываемРеквизитОбъектаПоТипу(ПартнерОбъект,РеквизитДляПоиска,ЗначениеРеквизита);
						Исключение
						КонецПопытки;
					КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Попытка
				ПартнерОбъект.Записать();
				Текстсообщения = "Партнер: " + ПартнерОбъект + " записан в базу данных. ИД партнера: " + ИДКомпании;
				ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,ПартнерОбъект);
				Если КофигурацияУТ Тогда
					КонтрагентОбъект.Партнер = ПартнерОбъект.Ссылка;
					КонтрагентОбъект.записать();
					Текстсообщения = "Контрагент: " + ПартнерОбъект + " записан в базу данных. ИД Контрагента: " + ИДКомпании;
					ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,КонтрагентОбъект);
				КонецЕсли;

			Исключение
				Текстсообщения = "Не удалось записать партнера по причине " + ОписаниеОшибки();
				ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);			
			КонецПопытки;
			
			Если НЕ ПартнерОбъект = Неопределено Тогда
				
				ПВХИД = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИдПартнерыАмо");
				
				ДопРеквизитОтсутствует = УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ПартнерОбъект.Ссылка,ПВХИД);
				
				Если Не ДопРеквизитОтсутствует Тогда
					
					Продолжить;
					
				КонецЕсли;
				
				НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
				НоваястрТаблицы.Свойство = ПВХИД;
				Если ИДКомпании = Неопределено ИЛИ ИДКомпании = "" Тогда
					Если СоздаватьФизЛиц И ЗначениеЗаполнено(ИДКонтактногоЛица) Тогда
						НоваястрТаблицы.Значение = ИДКонтактногоЛица;
					КонецЕсли;
				Иначе
					НоваястрТаблицы.Значение = ИДКомпании;
				КонецЕсли;
				
				УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ПартнерОбъект.Ссылка,ТаблицаСвойствИЗначений);
				
				Продолжить;
				
			КонецЕсли;
			
		ИначеЕсли ПартнерСсылка = Неопределено Тогда
			ТекстСообщения = "Загрузка контактных лиц. Партнер с ид " + ИДКомпании + " не найден в базе данных. Продолжение загрузки...";
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
			Продолжить;
		КонецЕсли;
		
		КЛНайден = Истина;
		
		КЛСсылка = ПолучитьКонтактныеЛицаПартнераПоИД(ИДКонтактногоЛица,ПартнерСсылка);
		Если НЕ ЗначениеЗаполнено(КЛСсылка) Тогда
			// записываем в журнал для дальнейшего вывода
			КЛНайден = Ложь;
		КонецЕсли;
		
		Если НЕ ПерезаписыватьДанные и КЛНайден Тогда
			КЛОбъект = КЛСсылка.ПолучитьОбъект();
			КЛОбъект.КонтактнаяИнформация.Очистить();
		ИначеЕсли ПерезаписыватьДанные и КЛНайден Тогда
			ТекстСообщения = "Контактное лицо с ид " + ИДКонтактногоЛица + " уже существует в базе данных, обновление производиться не будет. Продолжение загрузки...";
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
			Продолжить;
		ИначеЕсли НЕ ПерезаписыватьДанные И НЕ КЛНайден Тогда
			КЛОбъект = Справочники.КонтактныеЛицаПартнеров.СоздатьЭлемент();
		Иначе
			КЛОбъект = Справочники.КонтактныеЛицаПартнеров.СоздатьЭлемент();
		КонецЕсли;
		
		КЛОбъект.Владелец = ПартнерСсылка;
		ФИООбщее = Стр.Получить("name");
		
			СписокФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИООбщее);
		
		КЛОбъект.Наименование = ФИООбщее;
		КЛОбъект.CRM_Имя = СписокФИО.Имя;
		КЛОбъект.CRM_Отчество = СписокФИО.Отчество;
		КЛОбъект.CRM_Фамилия = СписокФИО.Фамилия;
		
		МассивРеквизитов = Стр.Получить("custom_fields");
		
		Для Каждого СтрокаРеквизит Из МассивРеквизитов Цикл
			
			ИДРеквизита = ВернутьПредставлениеИДАмо(СтрокаРеквизит.Получить("id"));
			
			ЗначениеРеквизита = СтрокаРеквизит.получить("values")[0].Получить("value");
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ИдАмо",ИДРеквизита);
			
			НайденныеСтроки = КонтактныеЛицаСопоставление.НайтиСтроки(СтруктураПоиска);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			Иначе
				СтрокаДанные = НайденныеСтроки[0];
				РеквизитДляПоиска = СтрокаДанные.ИмяРеквизита;
				Если НЕ ЗначениеЗаполнено(РеквизитДляПоиска) Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрокаДанные.КонтактнаяИнформация Тогда
					
					ОбъектXDTOКИ = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOПоПредставлению(ЗначениеРеквизита, СтрокаДанные.ТипКонтактнойИнформации);
					ЗначенияПолей = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOВXML(ОбъектXDTOКИ);
					CRM_УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(КЛОбъект, ЗначенияПолей, Справочники.ВидыКонтактнойИнформации[СтрокаДанные.ИмяРеквизита],СтрокаДанные.ТипКонтактнойИнформации,,,Истина);
					
				иначеЕсли СтрокаДанные.ДопРеквизит Тогда
					
					ПВХДоп = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя",РеквизитДляПоиска);
					НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
					НоваястрТаблицы.Свойство = ПВХДоп;
					
					Если СтрокаДанные.ТипЗначений = "Булево" Тогда
						Если ЗначениеРеквизита = "1" Тогда
							ЗначениеРеквизита = Истина;
						иначе
							ЗначениеРеквизита = Ложь;
						КонецЕсли;
					ИначеЕсли СтрокаДанные.ТипЗначений = "СписокЗначений" Тогда
						ДопЗначениеРеквизита = ПолучитьЗначениеСвойствПВХ(ПВХДоп,ЗначениеРеквизита);
						ЗначениеРеквизита = ДопЗначениеРеквизита;
					КонецЕсли;
					
					НоваястрТаблицы.Значение = ЗначениеРеквизита;
					
				иначе
					
					КЛОбъект[РеквизитДляПоиска] = ЗначениеРеквизита;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			КЛОбъект.Записать();
			Текстсообщения = "Контактное лицо " + КЛОбъект + " для партнера " + КЛОбъект.Владелец + " записано в базу";
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,КЛОбъект);
		Исключение
			Текстсообщения = "Не удалось записать партнера по причине " + ОписаниеОшибки();
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		КонецПопытки;
		
		Если НЕ КЛОбъект = Неопределено Тогда
			
			ПВХИД = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИДКонтактныеЛицаПартнеровАмо");
			
			ДопРеквизитОтсутствует = УправлениеСвойствами.ПроверитьСвойствоУОбъекта(КЛОбъект.Ссылка,ПВХИД);
			
			Если Не ДопРеквизитОтсутствует Тогда
				Продолжить;
			КонецЕсли;
			
			НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
			НоваястрТаблицы.Свойство = ПВХИД;
			НоваястрТаблицы.Значение = ИДКонтактногоЛица;
			
			УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(КЛОбъект.Ссылка,ТаблицаСвойствИЗначений);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаполнитьПартнераВБазеДанных(Структура)
	
	ПартнерНайден = Истина;
	
	Если Структура.ПартнерСсылка = Неопределено Тогда
		ПартнерНайден = Ложь;
	КонецЕсли;
	
	Если НЕ ПерезаписыватьДанные и ПартнерНайден Тогда
		ПартнерОбъект = Структура.ПартнерСсылка.ПолучитьОбъект();
		ПартнерОбъект.КонтактнаяИнформация.Очистить();
	ИначеЕсли ПерезаписыватьДанные и ПартнерНайден Тогда
		ТекстСообщения = "Партнер с ид " + Структура.ИДПартнер + " уже существует в базе данных, обновление производиться не будет. Продолжение загрузки...";
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		Возврат Неопределено;
	ИначеЕсли НЕ ПерезаписыватьДанные И НЕ ПартнерНайден Тогда
		ПартнерОбъект = Справочники.Партнеры.СоздатьЭлемент();
	Иначе
		ПартнерОбъект = Справочники.Партнеры.СоздатьЭлемент();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПартнерОбъект,Структура);
	
	Возврат ПартнерОбъект;
	
КонецФункции

// Все дополнительныеРеквизиты строятся по принципу :
// наименование реквизита в Амо + "Амо"
// например "ИДАмо"
Процедура ЗагрузкаПартнеровВБазуДанных(Массив)
	
	ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
	ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство",Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
	
	// Если в последующем будет дозагрузка , будет поиск по доп. реквизиту ИД
	Для Каждого Стр Из Массив Цикл
		
		ТаблицаСвойствИЗначений.Очистить();
		
		ПоказатьХодВыполнения(Массив.Найти(Стр)/Массив.Количество() * 10,"Загрузка данных по партнерам...");
		
		ИДПартнер = ВернутьПредставлениеИДАмо(Стр.Получить("id"));
		
		ПартнерНайден = Истина;
		
		ПартнерСсылка = ПолучитьПартнераПоИД(ИДПартнер);
		
		СтруктураЗаполнения = Новый Структура;
		СтруктураЗаполнения.Вставить("ПартнерСсылка",ПартнерСсылка);
		СтруктураЗаполнения.Вставить("ИДПартнер",ИДПартнер);
		ОтветственныйПользователь = ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("responsible_user_id")));
		СтруктураЗаполнения.Вставить("Наименование",Стр.Получить("name"));
		СтруктураЗаполнения.Вставить("ОсновнойМенеджер",ОтветственныйПользователь);
		СтруктураЗаполнения.Вставить("CRM_ДатаРегистрацииКомпании",ДатуВTimestamp(Стр.Получить("created_at")));
		
		ПартнерОбъект = ЗаполнитьПартнераВБазеДанных(СтруктураЗаполнения);
		
		Если КофигурацияУТ Тогда
			МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("ПартнерыИКонтрагенты");
			
			Контрагент = МодульУправлениеДоступом.ПолучитьКонтрагентаПартнераПоУмолчанию(ПартнерОбъект.Ссылка);
			Если Контрагент.ПУстая() Тогда
				КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
			Иначе
				КонтрагентОбъект = Контрагент.ПолучитьОбъект();
			КонецЕсли;
			СтруктураЗаполнения.Вставить("Клиент", Истина);
			ЗаполнитьЗначенияСвойств(КонтрагентОбъект,СтруктураЗаполнения);
			КонтрагентОбъект.ЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо;
			КонтрагентОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
		Иначе
			СтруктураЗаполнения.Вставить("ЮрФизЛицо",Перечисления.КомпанияЧастноеЛицо.Компания);
		КонецЕсли;

		Если ПартнерОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Теги = Стр.Получить("tags");
		
		МассивРеквизитов = Стр.Получить("custom_fields");
		
		Для Каждого СтрокаРеквизит Из МассивРеквизитов Цикл
			
			ИДРеквизита = ВернутьПредставлениеИДАмо(СтрокаРеквизит.Получить("id"));
			
			ЗначениеРеквизита = СтрокаРеквизит.получить("values")[0].Получить("value");
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("ИдАмо",ИДРеквизита);
			
			НайденныеСтроки = ПартнерыСопоставление.НайтиСтроки(СтруктураПоиска);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			Иначе
				СтрокаДанные = НайденныеСтроки[0];
				РеквизитДляПоиска = СтрокаДанные.ИмяРеквизита;
				Если НЕ ЗначениеЗаполнено(РеквизитДляПоиска) Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрокаДанные.КонтактнаяИнформация Тогда
					
					ОбъектXDTOКИ = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOПоПредставлению(ЗначениеРеквизита, СтрокаДанные.ТипКонтактнойИнформации);
					ЗначенияПолей = УправлениеКонтактнойИнформациейСлужебный.КонтактнаяИнформацияXDTOВXML(ОбъектXDTOКИ);
					CRM_УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(ПартнерОбъект, ЗначенияПолей, Справочники.ВидыКонтактнойИнформации[СтрокаДанные.ИмяРеквизита],СтрокаДанные.ТипКонтактнойИнформации,,ПартнерОбъект.CRM_ДатаРегистрацииКомпании,Истина);
				иначеЕсли СтрокаДанные.ДопРеквизит Тогда
					
					ПВХДоп = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя",РеквизитДляПоиска);
					НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
					НоваястрТаблицы.Свойство = ПВХДоп;
					Если СтрокаДанные.ТипЗначений = "Булево" Тогда
						Если ЗначениеРеквизита = "1" Тогда
							ЗначениеРеквизита = Истина;
						иначе
							ЗначениеРеквизита = Ложь;
						КонецЕсли;
					ИначеЕсли СтрокаДанные.ТипЗначений = "СписокЗначений" Тогда
						ДопЗначениеРеквизита = ПолучитьЗначениеСвойствПВХ(ПВХДоп,ЗначениеРеквизита);
						ЗначениеРеквизита = ДопЗначениеРеквизита;
					КонецЕсли;
					
					НоваястрТаблицы.Значение = ЗначениеРеквизита;
					
				Иначе
					
					Если КофигурацияУТ Тогда
						Попытка
							ЗаписываемРеквизитОбъектаПоТипу(ПартнерОбъект,РеквизитДляПоиска,ЗначениеРеквизита);
						Исключение
						КонецПопытки;
						Попытка
							ЗаписываемРеквизитОбъектаПоТипу(КонтрагентОбъект,РеквизитДляПоиска,ЗначениеРеквизита);
						Исключение
						КонецПопытки;
					Иначе 
						Попытка
							ЗаписываемРеквизитОбъектаПоТипу(ПартнерОбъект,РеквизитДляПоиска,ЗначениеРеквизита);
						Исключение
						КонецПопытки;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Попытка
			ПартнерОбъект.Записать();
			Текстсообщения = "Партнер: " + ПартнерОбъект + " записан в базу данных. ИД партнера: " + ИДПартнер;
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,ПартнерОбъект);
			Если КофигурацияУТ Тогда
				КонтрагентОбъект.Партнер = ПартнерОбъект.Ссылка;
				КонтрагентОбъект.записать();
				Текстсообщения = "Контрагент: " + ПартнерОбъект + " записан в базу данных. ИД Контрагента: " + ИДПартнер;
				ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,КонтрагентОбъект);
				
			КонецЕсли;	
		Исключение
			Текстсообщения = "Не удалось записать партнера по причине " + ОписаниеОшибки();
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);			
		КонецПопытки;
		
		Если ЗначениеЗаполнено(ПартнерОбъект.ОсновнойМенеджер.Подразделение) Тогда
			
			ЗаписатьОбновитьТегиВОбъекте(ПартнерОбъект,Теги,ПартнерОбъект.ОсновнойМенеджер.Подразделение);
			
		КонецЕсли;
		
		Если НЕ ПартнерОбъект = Неопределено Тогда
			
			ПВХИД = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИдПартнерыАмо");
			
			ДопРеквизитОтсутствует = УправлениеСвойствами.ПроверитьСвойствоУОбъекта(ПартнерОбъект.Ссылка,ПВХИД);
			
			Если Не ДопРеквизитОтсутствует Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
			НоваястрТаблицы.Свойство = ПВХИД;
			НоваястрТаблицы.Значение = ИДПартнер;
			
			УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ПартнерОбъект.Ссылка,ТаблицаСвойствИЗначений);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСоздатьПотенциальногоКлиента(Структура)
	
	Наименование ="НЕТ КОНТАКТА: " +Формат(Структура.Дата,"ДФ=dd.MM.yyyy") + " " + Структура.Тема;
	
	Потенциальный = Справочники.CRM_ПотенциальныеКлиенты.НайтиПоНаименованию(Наименование);
	
	Если Потенциальный.Пустая() Тогда
		
		ПотенциальныйОбъект = Справочники.CRM_ПотенциальныеКлиенты.СоздатьЭлемент();
		
		ПотенциальныйОбъект.Наименование = Наименование;
		
		ПотенциальныйОбъект.Записать();
		
		Потенциальный = ПотенциальныйОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат Потенциальный;
	
КонецФункции

Процедура ЗагрузкаСделокВБазуДанных(Массив)
	
	Для Каждого Стр Из Массив Цикл
		
		ПоказатьХодВыполнения(Массив.Найти(Стр)/Массив.Количество() * 10,"Загрузка данных по сделкам...");
		
		СтруктураЗагрузки = Новый Структура;
		
		ИДСделка = ВернутьПредставлениеИДАмо(Стр.Получить("id"));
		
		ДокСсылка = ПолучитьСделкуПоИД(ИДСделка);
		
		ДокументНайден = Истина;
		
		Если ДокСсылка = Неопределено Тогда
			ДокументНайден = Ложь;
		КонецЕсли;
		
		Если НЕ ПерезаписыватьДанные и ДокументНайден Тогда
			ДокОбъект = ДокСсылка.ПолучитьОбъект();
			ДокОбъект.CRM_Теги.Очистить();
		ИначеЕсли ПерезаписыватьДанные и ДокументНайден Тогда
			ТекстСообщения = "Сделка с ид " + ИДСделка + " уже существует в базе данных, обновление производиться не будет. Продолжение загрузки...";
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
			Продолжить;
		ИначеЕсли НЕ ПерезаписыватьДанные И НЕ ДокументНайден Тогда
			ДокОбъект = Документы.CRM_Интерес.СоздатьДокумент();
		Иначе
			ДокОбъект = Документы.CRM_Интерес.СоздатьДокумент();
		КонецЕсли;
		
		ИДПартнера = ВернутьПредставлениеИДАмо(Стр.Получить("company").Получить("id"));
		ИнформацияОКонтЛице = Стр.Получить("contacts").Получить("id");
		
		СтруктураЗагрузки.Вставить("Организация",Организация);
		СтруктураЗагрузки.Вставить("Тема",Стр.Получить("name"));
		
		СтруктураЗагрузки.Вставить("Дата",TimestampВДату(Стр.Получить("created_at")));
		СтруктураЗагрузки.Вставить("Ответственный",	ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("responsible_user_id"))));
		
		Автор = ПолучитьОтветственногоПользователя(ВернутьПредставлениеИДАмо(Стр.Получить("created_by")));

		ЕстьПотенциальный = Ложь;
		
		Если ИнформацияОКонтЛице = Неопределено Тогда
			
		ИначеЕсли ТипЗнч(ИнформацияОКонтЛице) = Тип("Массив") Тогда
			ИДКонтЛица = ИнформацияОКонтЛице[0];
		Иначе
			ИДКонтЛица = ИнформацияОКонтЛице;
		КонецЕсли;
		
		ИДКонтЛица = ВернутьПредставлениеИДАмо(ИДКонтЛица);
		
		Если Не ИДПартнера = "" Тогда
			ПартнерСсылка = ПолучитьПартнераПоИД(ИДПартнера);
		Иначе
			ПартнерСсылка = ПолучитьПартнераПоИД(ИДКонтЛица);
		КонецЕсли;

		Если Не ПартнерСсылка = Неопределено Тогда
			
			СтруктураЗагрузки.Вставить("Партнер",ПартнерСсылка);
			КЛСсылка = ПолучитьКонтактныеЛицаПартнераПоИД(ИДКонтЛица,ПартнерСсылка);
			
			Если не КЛСсылка = Неопределено Тогда
				
				СтруктураЗагрузки.Вставить("КонтактноеЛицо",КЛСсылка);
				
			Иначе
				Продолжить;
			КонецЕсли;
			
		Иначе
			ЕстьПотенциальный = Истина;
			ПотенциальныйКлиент = ПолучитьСоздатьПотенциальногоКлиента(СтруктураЗагрузки);
			СтруктураЗагрузки.Вставить("ПотенциальныйКлиент",ПотенциальныйКлиент);
			
		КонецЕсли;
		
		Теги = Стр.Получить("tags");
		Если ЗначениеЗаполнено(Автор.Подразделение) Тогда
			ЗаписатьОбновитьТегиВОбъекте(ДокОбъект,Теги,Автор.Подразделение);
		КонецЕсли;
		СтруктураЗагрузки.Вставить("Автор",			Автор);
		СтруктураЗагрузки.Вставить("Подразделение",	ДокОбъект.Ответственный.Подразделение);
		СтруктураЗагрузки.Вставить("Офис",			ДокОбъект.Подразделение.CRM_офис);
		СтруктураЗагрузки.Вставить("ТипУслуги",		ПредопределенноеЗначение("Справочник.CRM_ТипУслуги.ПоставкаТоварыУслуги"));
		
		Попытка
			СтруктураЗагрузки.Вставить("ОжидаемаяВыручка",Стр.Получить("sale"));
		Исключение
		КонецПопытки;
		
		ИДСостояниеСделки = ВернутьПредставлениеИДАмо(Стр.Получить("status_id"));
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ИдАмо",ИДСостояниеСделки);
		НайденныеСтроки = СделкиСопоставление.НайтиСтроки(СтруктураПоиска);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		Иначе
			состояниеИнтереса = НайденныеСтроки[0].СостояниеИнтереса;
		КонецЕсли;
		
		СтруктураЗагрузки.Вставить("СостояниеИнтереса",состояниеИнтереса);
		
		ДокОбъект.Заполнить(СтруктураЗагрузки);
		
		ДокОбъект.Дата = СтруктураЗагрузки.Дата;
		
		Попытка
			ДокОбъект.Записать();
			Текстсообщения = "Сделка записана в базу данных " + ДокОбъект;
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,ДокОбъект);
		Исключение
			Текстсообщения = "Не удалось записать сделку по причине " + ОписаниеОшибки();
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		КонецПопытки;
		
		ПВХИД = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИДСделкаАмо");
		
		ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
		ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство",Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
		ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
		
		НоваястрТаблицы = ТаблицаСвойствИЗначений.Добавить();
		НоваястрТаблицы.Свойство = ПВХИД;
		НоваястрТаблицы.Значение = ИДСделка;
		
		УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ДокОбъект.Ссылка,ТаблицаСвойствИЗначений);
		
		ПолучитьСоздатьЗадачуНаОбследованиеЗагрузкиИзАмо(ИДСделка,ДокОбъект.Ссылка);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПолучитьСоздатьЗадачуНаОбследованиеЗагрузкиИзАмо(Ид,ДокументОснование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	CRM_Взаимодействие.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.CRM_Взаимодействие КАК CRM_Взаимодействие
	|ГДЕ
	|	CRM_Взаимодействие.ДокументОснование = &ДокументОснование";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить().Пустой();
	
	Если РезультатЗапроса Тогда
		
		// проверяем в амо, есть ли взамодействия по нему
		HTTPЗапрос = Новый HTTPЗапрос;
		HTTPЗапрос.АдресРесурса = "/api/v2/tasks" + ПараметрыАвторизации + "&element_id=" + Ид;
		
		HTTPЗапрос.Заголовки.Вставить("Set-cookie", cookieДанные);
		HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");
		имяВыходногоФайла = ПолучитьИмяВременногоФайла();
		ОтветHTTP = Соединение.Получить(HTTPЗапрос, имяВыходногоФайла);
		текстовыйДокумент = Новый ТекстовыйДокумент;
		текстовыйДокумент.Прочитать(имяВыходногоФайла);
		ОтветЗначение = текстовыйДокумент.ПолучитьТекст();
		
		Если ОтветЗначение = "" Тогда
			
			СоздатьВзаимодействиеНаПросмотрДанных(ДокументОснование);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьВзаимодействиеНаПросмотрДанных(ДокументОснование)
	
	Структура = новый Структура;
	
	ВидВзаимодействия = Справочники.CRM_ВидыВзаимодействий.ЛичнаяЗадача;
	СтатусВзаимодействия = Справочники.CRM_СостоянияСобытий.Запланировано;
	
	Структура.Вставить("Организация",Организация);
	Структура.Вставить("СтатусВзаимодействия",СтатусВзаимодействия);
	Структура.Вставить("ВидВзаимодействия",ВидВзаимодействия);
	
	Структура.Вставить("ДокументОснование",ДокументОснование);
	Структура.Вставить("Партнер",ДокументОснование.Партнер);
	Структура.Вставить("КонтактноеЛицо",ДокументОснование.КонтактноеЛицо);
	
	Структура.Вставить("Содержание","Проверить загрузку данных из amoCRM");
	
	Структура.Вставить("Автор",ДокументОснование.Автор);
	
	Структура.Вставить("Ответственный",ДокументОснование.Ответственный);
	
	Структура.Вставить("Дата",ДокументОснование.Дата + 86400);
	Структура.Вставить("НаВесьДень",Истина);
	
	Структура.Вставить("Тема", Строка(ВидВзаимодействия) +" " + Структура.Содержание);
	
	ДокОбъект = Документы.CRM_Взаимодействие.СоздатьДокумент();
	
	ДокОбъект.Заполнить(Структура);
	
	Попытка
		ДокОбъект.Записать();
		ТекстСообщения = "Создано взаимодействие на проверку данных для ответственного " + ДокументОснование.Ответственный;
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь,ДокОбъект);
	Исключение
		Текстсообщения = ОписаниеОшибки();
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСоВзаимодействиями

Процедура ВыполнитьРаботуСОповещениями(СтруктураДанных)
	
	Если СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		
		ОбсуждениеРазрешены = Истина;
		
		УстановитьПривилегированныйРежим(Истина);
		
		УидСделки = Новый ИдентификаторОбсужденияСистемыВзаимодействия(Строка(СтруктураДанных.Объект.УникальныйИдентификатор()));
		
		Отбор =  Новый ОтборОбсужденийСистемыВзаимодействия;
		Отбор.КонтекстноеОбсуждение = Истина;
		Отбор.Ключ  = Строка(УидСделки);
		МассивОбсуждений = СистемаВзаимодействия.ПолучитьОбсуждения(Отбор);
		
		Если МассивОбсуждений.Количество() = 0 Тогда
			
			НовоеОбсуждение = СистемаВзаимодействия.СоздатьОбсуждение();
			НовоеОбсуждение.Отображаемое = истина;
			НовоеОбсуждение.Ключ = Строка(УидСделки);
			НовоеОбсуждение.Заголовок = "Обсуждение по сделке " + СтруктураДанных.Объект;
			НовоеОбсуждение.КонтекстОбсуждения = Новый КонтекстОбсужденияСистемыВзаимодействия(ПолучитьНавигационнуюСсылку(СтруктураДанных.Объект));
			НовоеОбсуждение.Записать();
			
		Иначе
			
			НовоеОбсуждение = МассивОбсуждений[0];
			
		КонецЕсли;
		
		ОтборСообщений = Новый ОтборСообщенийСистемыВзаимодействия;
		ОтборСообщений.Обсуждение = НовоеОбсуждение.Идентификатор;
		МассивСообщений = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений);
		
		ЗаписыватьСообщение = Истина;
		Для Каждого Стр Из МассивСообщений Цикл
			Если Строка(Стр.Текст) = СтруктураДанных.Текст Тогда
				ЗаписыватьСообщение = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаписыватьСообщение Тогда
			
			ОтветственныйПользовательИБ = СтруктураДанных.Ответственный;
			ПользовательСуществует = СистемаВзаимодействия.ПолучитьПользователя(Новый ИдентификаторПользователяСистемыВзаимодействия(Строка(ОтветственныйПользовательИБ.ИдентификаторПользователяИБ)));
			ИдентификаторПользователяСВ = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ОтветственныйПользовательИБ.ИдентификаторПользователяИБ);
			
			НовоеСообщение = СистемаВзаимодействия.СоздатьСообщение(НовоеОбсуждение.Идентификатор);
			
			НовоеСообщение.Данные = СтруктураДанных.Объект;
			
			НовоеСообщение.Получатели.Добавить(ИдентификаторПользователяСВ);
			НовоеСообщение.Дата = ТекущаяДата();
			НовоеСообщение.Текст = СтруктураДанных.Текст;
			Попытка
				НовоеСообщение.Записать();
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ТекстСообщения = "Загрузка примечаний для объекта: " + СтруктураДанных.Объект;
		ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		
	Иначе
		
		Если ОбсуждениеРазрешены = Неопределено Тогда
			ТекстСообщения = "Информационная база не зарегистрирована в системе взаимодействия";
			ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,Ложь,Истина,Ложь);
		КонецЕсли;
		ОбсуждениеРазрешены = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительныеФункции
Процедура ПрисоединитьФайлКобъекту(Владелец,ИмяФайла,ИмяФайлаСохранение,ПутьЗагрузки)
	
	Если cookieДанные = Неопределено Тогда
		Отказ = НЕ ВыполнитьПроверкуПодключения();
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ИмяФайлаБезРасширения = "";
	РасширениеФайла = "";
	МассивСтрок = СтрРазделить(ИмяФайлаСохранение, ".", Ложь);
	Если МассивСтрок.Количество() > 1 Тогда
		ИмяФайлаБезРасширения = МассивСтрок[0];
		РасширениеФайла = МассивСтрок[МассивСтрок.Количество() - 1];
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	#Если ВебКлиент Тогда
		ПодключитьРасширениеРаботыСФайлами();
	#КонецЕсли
	
	АдресРесурса = "/download/" + ИмяФайла;
	HTTPЗапрос = Новый HTTPЗапрос;
	HTTPЗапрос.АдресРесурса = АдресРесурса;
	
	HTTPЗапрос.Заголовки.Вставить("Content-Type","application/json");
	HTTPЗапрос.Заголовки.Вставить("Set-Cookie",cookieДанные);
	
	Ответ = Соединение.Получить(HTTPЗапрос,ИмяВременногоФайла);
	
	Если Ответ.КодСостояния = 302 Тогда
		
		НовоеРасположение = Ответ.ЗАголовки.Получить("Location");
		УдалитьФайлы(ИмяВременногоФайла);
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
		МассивСтрок = СтрРазделить(НовоеРасположение,"/");
		СерверФайла = МассивСтрок[2];
		ПУтьРесурс = "/" + МассивСтрок[3] + "/" + МассивСтрок[4] + "/" + МассивСтрок[5];
		ССЛ = Новый ЗащищенноеСоединениеOpenSSL();
		СоединениеФайл = Новый HTTPСоединение(СерверФайла,,,,,,ССЛ,Ложь);
		HTTPЗапросФайл = Новый HTTPЗапрос;
		HTTPЗапросФайл.АдресРесурса = ПУтьРесурс;
		
		ОтветФайл = СоединениеФайл.Получить(HTTPЗапросФайл,ИмяВременногоФайла);
		
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
		адресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		
		ВремяИзменения = CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса();
		
		ПрисоединенныеФайлы.ДобавитьФайл(Владелец,ИмяФайлаБезРасширения,РасширениеФайла, ВремяИзменения ,ВремяИзменения , адресВХранилище);
		
		УдалитьФайлы(ИмяВременногоФайла);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОтветственногоПользователя(Ид)
	
	СтрокаТаблицы = ПользователиСопоставление.Найти(ИД,"ИдАмо");
	
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат Справочники.Пользователи.ПустаяСсылка();
	Иначе
		Возврат СтрокаТаблицы.Пользователь;
	КонецЕсли;
	
КонецФункции

Функция ВернутьПредставлениеИДАмо(ИдПредставление)
	
	Ид = СокрЛП(Строка(ИдПредставление));
	Ид = СтрЗаменить(Ид,Символы.НПП,"");
	
	Возврат Ид;
	
КонецФункции

Функция ПолучитьПартнераПоИД(Ид)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПартнерыДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Партнеры.ДополнительныеРеквизиты КАК ПартнерыДополнительныеРеквизиты
	|ГДЕ
	|	ПартнерыДополнительныеРеквизиты.Свойство = &Свойство
	|	И ПартнерыДополнительныеРеквизиты.Значение = &Значение";
	
	Запрос.УстановитьПараметр("Значение", Ид);
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИдПартнерыАмо"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСделкуПоИД(ИД)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	CRM_ИнтересДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.CRM_Интерес.ДополнительныеРеквизиты КАК CRM_ИнтересДополнительныеРеквизиты
	|ГДЕ
	|	CRM_ИнтересДополнительныеРеквизиты.Свойство = &Свойство
	|	И CRM_ИнтересДополнительныеРеквизиты.Значение = &Значение";
	
	Запрос.УстановитьПараметр("Значение", ИД);
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИдСделкаАмо"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИнформациюПоОшибкам(КодОшибки)
	
	ТекстСообщения = "";
	
	Если КодОшибки = 401 Тогда
		ТекстСообщения = "Общая ошибка авторизации. Неправильный логин или пароль.";
	ИначеЕсли КодОшибки = 403 Тогда
		ТекстСообщения = "Доступ к данному аккаунту запрещён с Вашего IP адреса. Возникает, когда в настройках безопасности аккаунта включена фильтрация доступа к API по ""белому списку IP адресов"".";
	ИначеЕсли КодОшибки = 429 Тогда
		ТекстСообщения = "Превышено допустимое количество запросов в секунду";
	КонецЕсли;
	
	Возврат ТекстСообщения;
	
КонецФункции

Функция ПолучитьВзаимодействиеПоИд(Ид)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	CRM_ВзаимодействиеДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.CRM_Взаимодействие.ДополнительныеРеквизиты КАК CRM_ВзаимодействиеДополнительныеРеквизиты
	|ГДЕ
	|	CRM_ВзаимодействиеДополнительныеРеквизиты.Свойство = &Свойство
	|	И CRM_ВзаимодействиеДополнительныеРеквизиты.Значение = &Значение";
	
	Запрос.УстановитьПараметр("Значение", ИД);
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИДВзаимодействиеАмо"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьКонтактныеЛицаПартнераПоИД(ИД,Владелец)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтактныеЛицаПартнеровДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КонтактныеЛицаПартнеров.ДополнительныеРеквизиты КАК КонтактныеЛицаПартнеровДополнительныеРеквизиты
	|ГДЕ
	|	КонтактныеЛицаПартнеровДополнительныеРеквизиты.Свойство = &Свойство
	|	И КонтактныеЛицаПартнеровДополнительныеРеквизиты.Значение = &Значение";
	Если НЕ Владелец = Неопределено Тогда
		Запрос.Текст = запрос.Текст + "	И КонтактныеЛицаПартнеровДополнительныеРеквизиты.Ссылка.Владелец = &Владелец";
		Запрос.УстановитьПараметр("Владелец", Владелец);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Значение", ИД);
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ИДКонтактныеЛицаПартнеровАмо"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат Справочники.КонтактныеЛицаПартнеров.ПустаяСсылка();
		
	Иначе
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВыборкаДетальныеЗаписи.Следующий();
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
		
	КонецЕсли;
	
КонецФункции

Процедура ЗаписываемРеквизитОбъектаПоТипу(ОбъектДанных,Реквизит,ЗначениеРеквизита)
	
	Если ТипЗнч(ОбъектДанных[Реквизит]) = Тип("Строка") Тогда
		ОбъектДанных[Реквизит] = ЗначениеРеквизита;
	Иначе
		ТипЗначения = ТипЗнч(ОбъектДанных[Реквизит]);
		ОбъектДанных[Реквизит] = Справочники[ТипЗначения].НайтиПоНаименованию(ЗначениеРеквизита);
	КонецЕсли;
	
КонецПроцедуры

Функция TimestampВДату(пДатаТС)
	Попытка
		Возврат Дата("19700101")+?(ТипЗнч(пДатаТС) = Тип("Строка"), Число(пДатаТС), пДатаТС);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
КонецФункции

Функция ДатуВTimestamp(пДата = Неопределено)
	Возврат Формат(Число(?(ТипЗнч(пДата) = Тип("Дата"), пДата, ТекущаяДата())-Дата("19700101")),"ЧН=0; ЧГ=0");
КонецФункции

Процедура ЗаписываемДанныеВЖурналыБазДанных(ТекстСообщения,ПоказатьПользователю,ВнутреннийЖурнал,ЖурналРегистрации,Объект = Неопределено)
	
	Если ПоказатьПользователю Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если ВнутреннийЖурнал Тогда
		ЖурналСобытий = ЖурналСобытий + Символы.ПС + ТекстСообщения;
	КонецЕсли;
	
	Если Не объект = Неопределено Тогда
		
		МассивНайденных = ЖурналСозданныхОбъектов.НайтиСтроки(Новый Структура("ОбъектДанных",Объект.Ссылка));
		
		Если НЕ МассивНайденных.Количество() > 0 Тогда
			НоваяСтр = ЖурналСозданныхОбъектов.Добавить();
			НоваяСтр.ДатаЗагрузки = НачалоДня(ТекущаяДата());
			НоваяСтр.ОбъектДанных = Объект.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеСвойствПВХ(ПВХ,Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец = &Владелец
	|	И ЗначенияСвойствОбъектов.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("Владелец", ПВХ);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Возврат ВыборкаДетальныеЗаписи.Ссылка;
	Иначе
		
		ЗначениеСсылка = Справочники.ЗначенияСвойствОбъектов.СоздатьЭлемент();
		ЗначениеСсылка.Владелец = ПВХ;
		ЗначениеСсылка.Наименование = Наименование;
		ЗначениеСсылка.ПолноеНаименование = Наименование;
		
		Попытка
			ЗначениеСсылка.Записать();
		Исключение
			Сообщить(ОписаниеОшибки());	
		КонецПопытки;
		
		Возврат ЗначениеСсылка.Ссылка;
		
	КонецЕсли;
	
КонецФункции

Процедура ПоказатьХодВыполнения(Процент,Текст)
	
	ДлительныеОперации.СообщитьПрогресс(Окр(Процент*10,0),Текст);
	
КонецПроцедуры

Процедура ЗаписатьОбновитьТегиВОбъекте(Объект,Теги,Подразделение)
	
	Если Теги.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если типЗнч(Теги) = Тип("Массив") Тогда
		Для Каждого Стр Из Теги Цикл
			Наименование = СокрЛП(Стр.Получить("name"));
			
			ЗаписатьТегиВОбъект(Объект,Наименование,Подразделение);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСоздатьТег(Наименование,Подразделение)
	
	ТегССылка = Справочники.CRM_Теги.НайтиПоНаименованию(Наименование,Истина,Подразделение);
	Если ТегССылка.Пустая() Тогда
		Тегобъект = Справочники.CRM_Теги.СоздатьЭлемент();
		Тегобъект.Наименование = Наименование;
		Тегобъект.Владелец = Подразделение;
		Тегобъект.Записать();
		ТегССылка = Тегобъект.Ссылка;
	КонецЕсли;
	
	Возврат ТегССылка;
	
КонецФункции

Процедура ЗаписатьТегиВОбъект(объект,Наименование,Подразделение)
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.CRM_Взаимодействие") ИЛИ ТипЗнч(Объект) = Тип("ДокументОбъект.CRM_Интерес") Тогда
		
		Тег = ПолучитьСоздатьТег(Наименование,Подразделение);
		
		НоваяСтрокаТЧ = объект.CRM_Теги.Добавить();
		НоваяСтрокаТЧ.Тег = Тег;
		
	Иначе
		
		Тег = ПолучитьСоздатьТегДляСправочника(Наименование,Подразделение);
		
		новШк = РегистрыСведений.CRM_ОбъектыЗначенийКлассификаторов.СоздатьМенеджерЗаписи();
		новШк.Объект         				= Объект.Ссылка;
		новШк.Аналитика         			= Подразделение;
		новШк.ЗначениеКлассификатора        = Тег;
		
		новШк.Прочитать();
		
		Если Не новШк.Выбран() Тогда
			новШк.Объект         				= Объект.Ссылка;
			новШк.Аналитика         			= Подразделение;
			новШк.ЗначениеКлассификатора        = Тег;
			новШк.Записать();
		КонецЕсли;
		
		Объект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьМакетОформленияПолей(ИмяМакета) Экспорт
	
	Возврат ПолучитьМакет(ИмяМакета);
	
КонецФункции

Функция ПолучитьСоздатьТегДляСправочника(Наименование,Подразделение)
	
	ТегССылка = Справочники.CRM_ЗначенияКлассификаторов.НайтиПоНаименованию(Наименование,Истина);
	Если ТегССылка.Пустая() Тогда
		Тегобъект = Справочники.CRM_ЗначенияКлассификаторов.СоздатьЭлемент();
		Структура = Новый Структура;
		Структура.Вставить("Наименование",Наименование);
		Структура.Вставить("Владелец",ПланыВидовХарактеристик.CRM_Классификаторы.НайтиПоНаименованию("Теги"));
		Тегобъект.Заполнить(Структура);
		Тегобъект.Наименование = Наименование;
		Тегобъект.Записать();
		ТегССылка = Тегобъект.Ссылка;
	КонецЕсли;
	
	Возврат ТегССылка;
	
КонецФункции

Функция ОпределитьКонфигурацию() Экспорт
	
	Если НЕ CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		КофигурацияУТ = Истина;
	Иначе
		КофигурацияУТ = Ложь;
	КонецЕсли;
	
	Возврат КофигурацияУТ;
	
КонецФункции

#КонецОбласти

#КонецЕсли