
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ИмяМакета") Тогда
		Отбор = Неопределено;
		Параметры.Свойство("Отбор", Отбор);
		Дерево = СформироватьДеревоРеквизитов(Параметры.ИмяМакета, Отбор);
		Если Параметры.Свойство("ИсключитьРеквизиты") Тогда
			Для каждого ИсключаемыйРеквизит из Параметры.ИсключитьРеквизиты Цикл
				Строки = Дерево.Строки.НайтиСтроки(Новый Структура("ПредставлениеРеквизита", ИсключаемыйРеквизит), Истина);
				Для каждого Строка из Строки Цикл
					Строка.Родитель.Строки.Удалить(Строка);
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		Для каждого Строка из Дерево.Строки Цикл
			Идентификатор = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Строка.Объект);
			МетаданныеОбъекта = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Идентификатор);
			КонтактнаяИнформация = (ПравоДоступа("Добавление", Метаданные.Справочники.ВидыКонтактнойИнформации)
										И МетаданныеОбъекта.ТабличныеЧасти.Найти("КонтактнаяИнформация")<>Неопределено);
			ДополнительныеРеквизиты = (ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения")
										И ПравоДоступа("Добавление", Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения)
										И МетаданныеОбъекта.ТабличныеЧасти.Найти("ДополнительныеРеквизиты")<>Неопределено);
			Если КонтактнаяИнформация ИЛИ ДополнительныеРеквизиты Тогда
				НовОбъект = ОбъектыСДопРекИКИ.Добавить();
				НовОбъект.Объект = Идентификатор;
				НовОбъект.КонтактнаяИнформация = КонтактнаяИнформация;
				НовОбъект.ДополнительныеРеквизиты = ДополнительныеРеквизиты;
				СписокОбъектов.Добавить(Идентификатор);
			КонецЕсли;
		КонецЦикла;
		Если (ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") И ПравоДоступа("Добавление", Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения))
			ИЛИ ПравоДоступа("Добавление", Метаданные.Справочники.ВидыКонтактнойИнформации) Тогда
		//	Элементы.ДекорацияНовоеПоле.Видимость = Истина;
		КонецЕсли;
		ЗначениеВРеквизитФормы(Дерево, "ДеревоРеквизитов");
	КонецЕсли;
	ФизЛицо = Параметры.ФизЛицо;
	
КонецПроцедуры

&НаСервере
Функция СформироватьДеревоРеквизитов(ИмяМакета, Отбор)
	ТабДок = РеквизитФормыВЗначение("Объект").ПолучитьМакетОформленияПолей(ИмяМакета);
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область(1, 1, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы));
	Если ЗначениеЗаполнено(Отбор) Тогда
		Построитель.Отбор.Добавить("Отбор");
		Построитель.Отбор["Отбор"].Использование = Истина;
		Построитель.Отбор["Отбор"].Значение = "";
		Построитель.Отбор["Отбор"].ВидСравнения = ВидСравнения.Равно;
		Построитель.Выполнить();
		ТаблицаРеквизитов = Построитель.Результат.Выгрузить();
		Построитель.Отбор["Отбор"].Значение = СтрЗаменить(Строка(Отбор), " ", "");
		Построитель.Выполнить();
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Построитель.Результат.Выгрузить(), ТаблицаРеквизитов);
	Иначе	
		Построитель.Выполнить();
		ТаблицаРеквизитов = Построитель.Результат.Выгрузить();
	КонецЕсли;
	ТаблицаОбъектов = ТаблицаРеквизитов.Скопировать(,"Объект");
	ТаблицаОбъектов.Свернуть("Объект");
	ПустаяСтрока = ТаблицаОбъектов.Найти("", "Объект");
	Если ПустаяСтрока <> Неопределено Тогда
		ТаблицаОбъектов.Удалить(ПустаяСтрока);
	КонецЕсли;
	
	ДеревоРеквизитов1 = Новый ДеревоЗначений;
	ДеревоРеквизитов1.Колонки.Добавить("Объект");
	ДеревоРеквизитов1.Колонки.Добавить("ИмяРеквизита");
	ДеревоРеквизитов1.Колонки.Добавить("ПредставлениеРеквизита",, "Реквизит");
	ДеревоРеквизитов1.Колонки.Добавить("ТипРеквизита");
	ДеревоРеквизитов1.Колонки.Добавить("Обязательный");
	ДеревоРеквизитов1.Колонки.Добавить("Создавать");
	ДеревоРеквизитов1.Колонки.Добавить("ДопРеквизит");
	ДеревоРеквизитов1.Колонки.Добавить("ТипЗначений");
	ДеревоРеквизитов1.Колонки.Добавить("КонтактнаяИнформация");
	ДеревоРеквизитов1.Колонки.Добавить("ТипКонтактнойИнформации");
	
	Для каждого ТекОбъект из ТаблицаОбъектов цикл
		МетаданныеОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ТекОбъект.Объект).ПустаяСсылка().Метаданные();
		НовСтрОбъект = ДеревоРеквизитов1.Строки.Добавить();
		НовСтрОбъект.ПредставлениеРеквизита = МетаданныеОбъекта.Синоним;
		НовСтрОбъект.Объект = ТекОбъект.Объект;
		РеквизитыОбъекта = ТаблицаРеквизитов.НайтиСтроки(Новый Структура("Объект", ТекОбъект.Объект));
		Для каждого Реквизит из РеквизитыОбъекта Цикл
			НовСтрРеквизит = НовСтрОбъект.Строки.Добавить();
			НовСтрРеквизит.Объект = Реквизит.Объект;
			НовСтрРеквизит.ИмяРеквизита = Реквизит.ИмяРеквизита;
			НовСтрРеквизит.ПредставлениеРеквизита = Реквизит.ПредставлениеРеквизита;
			НовСтрРеквизит.Создавать = (ЗначениеЗаполнено(Реквизит.Создавать) И Вычислить(Реквизит.Создавать));
			Если ЗначениеЗаполнено(Реквизит.ТипЗначения) Тогда
				НовСтрРеквизит.ТипРеквизита = Новый ОписаниеТипов(Реквизит.ТипЗначения);
			Иначе
				МетаданныеРеквизит = МетаданныеОбъекта.Реквизиты.Найти(Реквизит.ИмяРеквизита);
				Если МетаданныеРеквизит <> Неопределено Тогда
					НовСтрРеквизит.ТипРеквизита = МетаданныеРеквизит.Тип;
					НовСтрРеквизит.Обязательный = (МетаданныеРеквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку);
					Если НовСтрРеквизит.Обязательный Тогда
						НовСтрРеквизит.ПредставлениеРеквизита = НовСтрРеквизит.ПредставлениеРеквизита;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Для каждого СтандартныйРеквизит из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
			Строка = НовСтрОбъект.Строки.Найти(СтандартныйРеквизит.Имя, "ИмяРеквизита");
			Если Строка <> Неопределено Тогда
				Строка.ТипРеквизита = СтандартныйРеквизит.Тип;
				Строка.Обязательный = (СтандартныйРеквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку);
				Если Строка.Обязательный Тогда
					Строка.ПредставлениеРеквизита = Строка.ПредставлениеРеквизита;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ДополнитьДеревоРеквизитовКонтактнойИнформацией(МетаданныеОбъекта, НовСтрОбъект, Отбор);
		//Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		//	ДополнитьДеревоРеквизитовДополнительнымиРеквизитами(МетаданныеОбъекта, НовСтрОбъект, Отбор);
		//КонецЕсли;
	КонецЦикла;
	Возврат ДеревоРеквизитов1;
КонецФункции

&НаСервере
Процедура ДополнитьДеревоРеквизитовКонтактнойИнформацией(МетаданныеОбъекта, НовСтрОбъект, Отбор)
	МассивВидов = Новый Массив;
	Если МетаданныеОбъекта = Метаданные.Справочники.Партнеры Тогда
		МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.СправочникПартнеры);
		Если Отбор = Перечисления.КомпанияЧастноеЛицо.Компания Тогда
			МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.CRM_СправочникПартнерыКомпания);
		ИначеЕсли Отбор = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо Тогда
			МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.CRM_СправочникПартнерыЧастноеЛицо);
		КонецЕсли;
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.CRM_ПотенциальныеКлиенты Тогда
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.КонтактныеЛицаПартнеров Тогда
		МассивВидов.Добавить(Справочники.ВидыКонтактнойИнформации.СправочникКонтактныеЛицаПартнеров);
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.Организации Тогда
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.ФизическиеЛица Тогда
		
	КонецЕсли;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВидыКонтактнойИнформации.Ссылка КАК Ссылка,
	                      |	ВидыКонтактнойИнформации.Тип КАК Тип,
	                      |	ВидыКонтактнойИнформации.Наименование КАК Наименование
	                      |ИЗ
	                      |	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	                      |ГДЕ
	                      |	ВидыКонтактнойИнформации.Родитель В(&Родитель)");
	Запрос.УстановитьПараметр("Родитель", МассивВидов);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НовСтрРеквизит = НовСтрОбъект.Строки.Добавить();
		НовСтрРеквизит.Объект = НовСтрОбъект.Объект;
		НовСтрРеквизит.ИмяРеквизита = Выборка.Ссылка;
		НовСтрРеквизит.ПредставлениеРеквизита = Выборка.Наименование + " ("+НРег(МетаданныеОбъекта.ПредставлениеОбъекта) + ?(ФизЛицо,"/физ.лицо","")+")";
		НовСтрРеквизит.ТипРеквизита = Выборка.Тип;
		НовСтрРеквизит.ТипКонтактнойИнформации = Выборка.Тип;
		НовСтрРеквизит.КонтактнаяИнформация = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ФизЛицо Тогда
		СтрокаДополнения = "ФизЛицо";
	Иначе
		СтрокаДополнения = "";
	КонецЕсли;
	
	ТекДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекДанные.ИмяРеквизита) Тогда
			СтруктураРеквизита = Новый Структура;
			СтруктураРеквизита.Вставить("Объект", ТекДанные.Объект);
			СтруктураРеквизита.Вставить("ИмяРеквизита"+СтрокаДополнения, ТекДанные.ИмяРеквизита);
			СтруктураРеквизита.Вставить("ПредставлениеРеквизита"+СтрокаДополнения, ТекДанные.ПредставлениеРеквизита);
			СтруктураРеквизита.Вставить("ТипРеквизита"+СтрокаДополнения, ТекДанные.ТипРеквизита);
			СтруктураРеквизита.Вставить("ТипКонтактнойИнформации", ТекДанные.ТипКонтактнойИнформации);
			СтруктураРеквизита.Вставить("КонтактнаяИнформация", ТекДанные.КонтактнаяИнформация);
			
			Если ТекДанные.ИмяРеквизита = "СоздатьДопРеквизит" Тогда
				СтруктураРеквизита.Вставить("ДопРеквизит", Истина);
			Иначе
				СтруктураРеквизита.Вставить("ДопРеквизит", ЛОжь);
			КонецЕсли;
			
			СтруктураРеквизита.Вставить("Обязательный", ТекДанные.Обязательный);
			СтруктураРеквизита.Вставить("Создавать", ТекДанные.Создавать);
			ОповеститьОВыборе(СтруктураРеквизита);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ПриАктивизацииСтроки", 0.2, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииСтроки()
	ТекДанные = Элементы.ДеревоРеквизитов.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Элементы.Выбрать.Доступность = ЗначениеЗаполнено(ТекДанные.ИмяРеквизита);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Выбрать(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНовоеПолеНажатие(Элемент)
	СозданиеПоляНачать();
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПоляНачать()
	ОписаниеОпопвещения = Новый ОписаниеОповещения("СозданиеПоляПродолжить", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	СтруктураОбъектыСДопРекИКИ = Новый Соответствие;
	Для каждого ОбъектСДопРекИКИ из ОбъектыСДопРекИКИ Цикл
		СтруктураОбъектыСДопРекИКИ.Вставить(ОбъектСДопРекИКИ.Объект, Новый Структура("ДополнительныеРеквизиты, КонтактнаяИнформация", ОбъектСДопРекИКИ.ДополнительныеРеквизиты, ОбъектСДопРекИКИ.КонтактнаяИнформация));
	КонецЦикла;
	ПараметрыОткрытия.Вставить("ОбъектыСДопРекИКИ", СтруктураОбъектыСДопРекИКИ);
	ОткрытьФорму("Обработка.CRM_ЗагрузкаДанныхИзФайла.Форма.ФормаСозданияПоля", ПараметрыОткрытия, ЭтотОбъект,,,, ОписаниеОпопвещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СозданиеПоляПродолжить(СтруктураРеквизита, ДопРеквизиты) Экспорт
	
	Если СтруктураРеквизита <> Неопределено Тогда
		ОповеститьОВыборе(СтруктураРеквизита);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ДекорацияОтменитьНажатие(Элемент)
	Закрыть();
КонецПроцедуры

