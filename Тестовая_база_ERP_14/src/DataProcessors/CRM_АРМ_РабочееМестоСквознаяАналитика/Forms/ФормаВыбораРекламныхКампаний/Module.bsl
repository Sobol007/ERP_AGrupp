
&НаКлиенте
Процедура ТаблицаРекламныхКампанийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ИспользоватьОдну Тогда
		Выбрать();
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтображатьАктивные = Истина;
	
	ИмяПустойКампании = "<Пустое значение>";
	
	НовСтр = ТаблицаРекламныхКампаний.Добавить();
	НовСтр.РекламныеКампании = ИмяПустойКампании;
	
	Для Каждого Эл Из Параметры.МассивДанных Цикл
		Если ТипЗнч(Эл) = Тип("СправочникСсылка.МаркетинговыеМероприятия") Тогда
			СтруктураПоиска = Новый Структура("РекламныеКампании", Эл);
			НайденныеСтроки = ТаблицаРекламныхКампаний.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НовСтр = ТаблицаРекламныхКампаний.Добавить();
				НовСтр.РекламныеКампании = Эл;
				НовСтр.Активная = НЕ Эл.Завершено;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ИспользоватьОдну = Параметры.ВыбратьТолькоОдну;
	Если ИспользоватьОдну Тогда
		Элементы.ТаблицаРекламныхКампанийВыбрано.Видимость = Ложь;
		Элементы.ТаблицаРекламныхКампаний.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПоказатьНайденныеКампании();
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда = Неопределено)
	
	ВыбранныеКампании = Новый Массив();
	
	Если ИспользоватьОдну Тогда
		ВыбранныеКампании.Добавить(Элементы.ТаблицаРекламныхКампаний.ТекущиеДанные.РекламныеКампании);
		
	Иначе
		Для Каждого ТекСтрока Из ТаблицаРекламныхКампаний.НайтиСтроки(Новый Структура("Выбрано", Истина)) Цикл
			ВыбранныеКампании.Добавить(ТекСтрока.РекламныеКампании);
		КонецЦикла;
	КонецЕсли;
	
	СтруктураВыбора = Новый Структура("ВключатьДанныеБезКампаний,ВыбранныеКампании", ВключатьДанныеБезКампаний, ВыбранныеКампании);
	ОповеститьОВыборе(СтруктураВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНайденныеКампании() Экспорт
	
	Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Для Каждого ТекСтрока Из ТаблицаРекламныхКампаний Цикл
			Если ТекСтрока.Выбрано Тогда
				ТекСтрока.УдовлетворяетУсловиюПоиска = Истина;
				
			Иначе
				ТекСтрока.УдовлетворяетУсловиюПоиска = (Найти(ВРЕГ(ТекСтрока.РекламныеКампании), ВРЕГ(СтрокаПоиска)) > 0);
				
				Если ОтображатьАктивные И ТекСтрока.УдовлетворяетУсловиюПоиска Тогда
					ТекСтрока.УдовлетворяетУсловиюПоиска = ТекСтрока.Активная;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
	
		Элементы.ТаблицаРекламныхКампаний.ОтборСтрок = Новый ФиксированнаяСтруктура("УдовлетворяетУсловиюПоиска", Истина);
		
	ИначеЕсли ОтображатьАктивные Тогда
		Для Каждого ТекСтрока Из ТаблицаРекламныхКампаний Цикл
			Если ТекСтрока.Выбрано Тогда
				ТекСтрока.УдовлетворяетУсловиюПоиска = Истина;
				
			Иначе
				Если ОтображатьАктивные Тогда
					ТекСтрока.УдовлетворяетУсловиюПоиска = ТекСтрока.Активная;
				КонецЕсли;	
			КонецЕсли;
		КонецЦикла;
		
		Элементы.ТаблицаРекламныхКампаний.ОтборСтрок = Новый ФиксированнаяСтруктура("УдовлетворяетУсловиюПоиска", Истина);

	Иначе	
		Элементы.ТаблицаРекламныхКампаний.ОтборСтрок = Неопределено;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент = Неопределено)
	ПоказатьНайденныеКампании();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРекламныхКампанийВыбраноПриИзменении(Элемент)
	
	ТекДанные = Элементы.ТаблицаРекламныхКампаний.ТекущиеДанные;
	Если ТипЗнч(ТекДанные.РекламныеКампании) = Тип("Строка") Тогда
		ВключатьДанныеБезКампаний = ТекДанные.Выбрано;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьАктивныеПриИзменении(Элемент)
	ПоказатьНайденныеКампании();
КонецПроцедуры


