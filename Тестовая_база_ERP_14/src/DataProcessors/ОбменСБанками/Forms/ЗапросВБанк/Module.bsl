#Область ОписаниеПеременных

&НаКлиенте
Перем МассивТикетовСбербанк; // хранит массив тикетов отправленных запросов

&НаКлиенте
Перем ТикетСбербанк; // тикет на запрос статусов запросов выписки

&НаКлиенте
Перем ОповещениеПослеПолученияОтветаПоТикетуСбербанк; // хранит оповещение, которое должно быть вызвано после получения ответа по тикету

&НаКлиенте
Перем СертификатПодписиСбербанк; // сертификат, с использованием которого подписываются запросы новых документов

&НаКлиенте
Перем ИдентификаторСертификатаСбербанк; // идентификатор сертификата подписи

&НаКлиенте
Перем ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк; // параметры подключаемой процедуры при ожидании конечного статуса запросов выписки

&НаКлиенте
Перем ПараметрыОбработчикаОжидания; // Параметры обработчика ожидания для длительных операций

&НаКлиенте
Перем ПараметрыОжиданияПолученияВыписки; // Параметры обработчика ожидания при получении выписки

&НаКлиенте
Перем КоличествоОтправленных; // Количество отправленных документов

&НаКлиенте
Перем КоличествоПолученных; // Количество полученных документов

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		ЭтотОбъект, Параметры, , "ЗакрыватьПриВыборе, ЗакрыватьПриЗакрытииВладельца, ТолькоПросмотр");
	
	УстановитьПривилегированныйРежим(Истина);
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОбмена, "Организация, Банк,
		|ПрограммаБанка, ИдентификаторОрганизации, ИмяВнешнегоМодуля, ИспользуетсяКриптография, ДополнительнаяОбработка,
		|АдресСервера, ЛогинДляПолученияВыписки, ВерсияФормата");
	
	ПрограммаБанка = РеквизитыНастройкиОбмена.ПрограммаБанка;
	БИК = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыНастройкиОбмена.Банк, "Код");
	ИмяВнешнегоМодуля = РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля;
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	ИспользуетсяКриптография = РеквизитыНастройкиОбмена.ИспользуетсяКриптография;
	ДополнительнаяОбработка = РеквизитыНастройкиОбмена.ДополнительнаяОбработка;
	
	Если Параметры.ВидОперации = "ПолучениеВыписки" Тогда
		ГотовыеВыпискиСсылка =  ПоместитьВоВременноеХранилище(Параметры.ГотовыеВыписки, УникальныйИдентификатор);
		МассивСообщенийОбменаСсылка = ПоместитьВоВременноеХранилище(Параметры.МассивСообщенийОбмена, УникальныйИдентификатор);
		
		Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
			ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку Тогда
			МассивБанковскихСчетов = Новый Массив;
			Если ЗначениеЗаполнено(Параметры.НомерСчета) Тогда
				МассивБанковскихСчетов.Добавить(Параметры.НомерСчета);
			Иначе
				ОбменСБанкамиПереопределяемый.ПолучитьНомераБанковскихСчетов(
					РеквизитыНастройкиОбмена.Организация, РеквизитыНастройкиОбмена.Банк, МассивБанковскихСчетов);
			КонецЕсли;
			МассивБанковскихСчетовСсылка = ПоместитьВоВременноеХранилище(МассивБанковскихСчетов, УникальныйИдентификатор);
		КонецЕсли;
		
		Заголовок = НСтр("ru = 'Получение выписки';
						|en = 'Statement receiving'");
		
		// Если пришли запросы выписок Сбербанка, то необходимо получить тексты запросов на сервере.
		Если ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн И Параметры.ГотовыеВыписки.Количество() = 0 Тогда
			Если РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
				ДанныеЗапросовВыписокСбербанк = ДанныеЗапросовВыписокСбербанк(Параметры.МассивСообщенийОбмена);
				ТекстыЗапросовВыписокСбербанк = ПоместитьВоВременноеХранилище(ДанныеЗапросовВыписокСбербанк, УникальныйИдентификатор);
			ИначеЕсли Параметры.МассивТикетов.Количество() Тогда
				МассивТикетовСсылкаСбербанк = ПоместитьВоВременноеХранилище(Параметры.МассивТикетов, УникальныйИдентификатор);
				ЗапросыВыписокОтправленыСбербанк = Истина;
			КонецЕсли;
			ДатаНачалаОперацииСбербанк = ТекущаяДатаСеанса();
		ИначеЕсли ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку Тогда
			ДанныеСертификатаСсылка = ПоместитьВоВременноеХранилище(Параметры.ДанныеСертификата, УникальныйИдентификатор);
		КонецЕсли;
	ИначеЕсли Параметры.ВидОперации = "ВыполнениеСинхронизации" Тогда
		Заголовок = НСтр("ru = 'Выполнение синхронизации';
						|en = 'Synchronization in progress'");
	ИначеЕсли Параметры.ВидОперации = "ПолучениеСостоянияДокумента" Тогда
		Заголовок = НСтр("ru = 'Получение состояния электронного документа';
						|en = 'Receiving electronic document state'");
	ИначеЕсли Параметры.ВидОперации = "Тестирование" Тогда
		Заголовок = НСтр("ru = 'Тестирование обмена данными';
						|en = 'Data exchange testing'")
	ИначеЕсли Параметры.ВидОперации = "ИнициализацияПодтвержденияДокументаСбербанк" Тогда
		Заголовок = НСтр("ru = 'Получение SMS сообщения';
						|en = 'Receive SMS message'")
	ИначеЕсли Параметры.ВидОперации = "ПолучениеРезультатаПодтверждения" Тогда
		Заголовок = НСтр("ru = 'Проверка SMS пароля в банке';
						|en = 'Check SMS password in the bank'")
	ИначеЕсли Параметры.ВидОперации = "ПробноеПолучениеВыписки" Тогда
		ПараметрыЗапроса = ОбменСБанкамиКлиентСервер.ПараметрыПолученияВыпискиБанка();
		ПараметрыЗапроса.ДатаНачала = ОбменСБанкамиСлужебный.ДатаНачалаЗапросаВыписки(НастройкаОбмена);
		ПараметрыЗапроса.ДатаОкончания = ТекущаяДатаСеанса();
		МассивСообщенийОбмена = ОбменСБанкамиСлужебныйВызовСервера.ЗапросыВыписок(
			НастройкаОбмена, ПараметрыЗапроса, , , Истина);
		МассивСообщенийОбменаСсылка = ПоместитьВоВременноеХранилище(МассивСообщенийОбмена, УникальныйИдентификатор);
		Заголовок = НСтр("ru = 'Проверка получения выписки';
						|en = 'Checking statement receiving'");
		ДанныеАутентификации = Новый Структура;
		ДанныеАутентификации.Вставить("Логин", РеквизитыНастройкиОбмена.ЛогинДляПолученияВыписки);
		ДанныеАутентификации.Вставить("Пароль", ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(НастройкаОбмена));
		Результат = ОбменСБанкамиСлужебныйВызовСервера.БазоваяАутентификация(
			РеквизитыНастройкиОбмена.АдресСервера, РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации,
			РеквизитыНастройкиОбмена.ВерсияФормата, НастройкаОбмена);
		Если ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки, НастройкаОбмена);
		КонецЕсли;
		ИдентификаторСессии = Результат.ИдентификаторСессии;
	КонецЕсли;
	
	Если ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК
		ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн Тогда
		ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(НастройкаОбмена);
		ИспользоватьЖурналирование = ПараметрыЖурналирования.ИспользоватьЖурналирование;
		КаталогДляЖурналирования = ПараметрыЖурналирования.КаталогДляЖурналирования;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Перем ЗапросОтправлен, ВыпискаБанка;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		Если ВидОперации = "ПолучениеВыписки" Тогда
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаВыпискиВБанк", 0.1, Истина);
		ИначеЕсли ВидОперации = "ПолучениеСостоянияДокумента" Тогда
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаСтатусаЭД", 0.1, Истина);
		ИначеЕсли ВидОперации = "ПробноеПолучениеВыписки" Тогда
			Если ЗначениеЗаполнено(ИдентификаторСессии) Тогда
				ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаВыпискиВБанк", 0.1, Истина);
			Иначе
				ВыполнитьОбработкуОповещения(ЭтотОбъект.ОписаниеОповещенияОЗакрытии);
				Отказ = Истина;
			КонецЕсли;
		Иначе
			ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаЗонда", 0.1, Истина);
		КонецЕсли
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		Если ВидОперации = "ПолучениеСостоянияДокумента" Тогда
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("ПолучитьИзвещениеОСостоянииДокументаСбербанк", 0.1, Истина);
		ИначеЕсли ВидОперации = "ВыполнениеСинхронизации" Тогда
			Если ИспользуетсяКриптография Тогда
				ПодключитьОбработчикОжидания("ОтправитьАвтоматическиеЗапросыВыписокСбербанк", 0.1, Истина);
			Иначе
				ПодключитьОбработчикОжидания("ПолучитьНовыеДокументыСбербанк", 0.1, Истина);
			КонецЕсли;
			
		ИначеЕсли ВидОперации = "ИнициализацияПодтвержденияДокументаСбербанк" Тогда
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьИдентификаторКриптопрофиляСбербанк", 0.1, Истина);
		ИначеЕсли ВидОперации = "ПолучениеРезультатаПодтверждения" Тогда
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьРезультатПроверкиSMSСбербанк", 0.1, Истина);
		Иначе // получение выписки
			ГотовыеВыписки = ПолучитьИзВременногоХранилища(ГотовыеВыпискиСсылка);
			Если ГотовыеВыписки.Количество() Тогда
				ОповеститьОВыборе(ГотовыеВыписки);
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			Если ЗапросыВыписокОтправленыСбербанк Тогда // возможно только при обмене через логин-пароль
				ПодключитьОбработчикОжидания(
					"Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк", 0.1, Истина);
			Иначе
				ПодключитьОбработчикОжидания("НачатьОтправкуЗапросовВыписокСбербанк", 0.1, Истина);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		ОбменСБанкамиСлужебныйКлиент.ПроцессПрерван(НастройкаОбмена);
		Если ВидОперации = "ПолучениеСостоянияДокумента" Тогда
			ПолучитьСостояниеЭДЧерезВК();
		Иначе
			ПолучитьВыпискуЧерезВК();
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ПолучитьВыпискуЧерезДополнительнуюОбработку();
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
		ПодключитьОбработчикОжидания("НачатьОтправкуЗапросаВыпискиВБанк", 0.1, Истина);
	КонецЕсли;
	
	Если ЗакрытьФорму Тогда
		Отказ = Истина;
	Иначе
		ФормаОткрыта = Истина;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПроизошлаОшибка Тогда
		Отказ = Истина;
		ПроизошлаОшибка = Ложь;
		Элементы.Страницы.ТекущаяСтраница = Элементы.Ошибка;
		Элементы.ФормаОтмена.Заголовок = "Закрыть";
	Иначе
		ФормаОткрыта = Ложь;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ПрерватьПроцессыНаКлиенте(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		ПриЗакрытииНаСервере(ИдентификаторЗадания);
	КонецЕсли;
	
	ФормаОткрыта = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьДатуСинхронизации(Знач НастройкаОбмена)

	ОбменСБанкамиСлужебный.СохранитьДатуСинхронизации(НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтправкуЗапросаВыпискиВБанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыЗапроса.Вставить("ВидОперации", ВидОперации);
	ПараметрыЗапроса.Вставить("ПрограммаБанка", ПрограммаБанка);
	
	РезультатЗадания = ЗапускЗаданияОтправкиЗапросовВыпискиВБанк(
		ПараметрыЗапроса, УникальныйИдентификатор, МассивСообщенийОбменаСсылка);
	
	Если РезультатЗадания.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатЗапросаВыписки", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатЗадания, Оповещение, ПараметрыОжидания);
	Иначе
		ОбработатьРезультатЗапросаВыписки(РезультатЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтправкуЗапросаСтатусаЭД()
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	Результат = ОтправитьЗапросСостоянияЭДНаСервере(ПараметрыЗапроса, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатЗапросаСостоянияЭД", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ОбработатьРезультатЗапросаСостоянияЭД(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтправкуЗапросаЗонда()
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры = Новый Структура("ПараметрыЗапроса", ПараметрыЗапроса);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияОтправкиЗапросЗондаНаСервере(
		ПараметрыЗапроса, УникальныйИдентификатор);
		
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросЗонда", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеОтправкиЗапросЗонда(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияОтправкиЗапросовВыпискиВБанк(Знач ПараметрыЗапроса, Знач УникальныйИдентификатор, Знач МассивЗапросовСсылка)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыЗапроса.Вставить("МассивСообщенийОбмена", ПолучитьИзВременногоХранилища(МассивЗапросовСсылка));
	
	Если ПараметрыЗапроса.ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен Тогда
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка запроса выписки в банк.';
																|en = 'Sending a statement request to the bank.'");
		Результат = ДлительныеОперации.ВыполнитьВФоне(
			"ОбменСБанкамиСлужебный.ОтправитьЗапросВыпискиАсинхронныйОбмен", ПараметрыЗапроса, ПараметрыВыполнения);
	Иначе
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение выписки из банка.';
																|en = 'Receiving statement from the bank.'");
		Результат = ДлительныеОперации.ВыполнитьВФоне(
			"ОбменСБанкамиСлужебный.ПолучитьВыпискуСинхронныйОбмен", ПараметрыЗапроса, ПараметрыВыполнения);
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции

&НаСервереБезКонтекста
Функция ОтправитьЗапросСостоянияЭДНаСервере(Знач ПараметрыЗапроса, Знач УникальныйИдентификатор)
	
	НаименованиеЗадания = НСтр("ru = 'Отправка запроса состояния электронного документа в банк';
								|en = 'Sending an electronic document status request to the bank'");
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ОтправитьЗапросСостоянияЭДВБанк", ПараметрыЗапроса, ПараметрыВыполнения);

	Возврат Результат
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПриЗакрытииНаСервере(Знач ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаВыписки(РезультатЗадания, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Отправка запроса выписки в банк.';
							|en = 'Sending a statement request to the bank.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
			РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки, "ОбменСБанками",
			НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн") Тогда
			ОбработатьРезультатПолученияВыпискиСинхронныйОбмен(РезультатЗадания);
		Иначе
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОжиданияПолученияВыписки);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьВыпискуАсинхронныйОбмен",ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолученияВыпискиСинхронныйОбмен(РезультатЗадания)
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
	
	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеСохраненияПолученныхПодписейВыпискиСинхронныйОбмен", ЭтотОбъект, СтруктураВозврата);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		ДополнительныеПараметры.Вставить("ОповещениеПослеСохраненияПодписей", Оповещение);
		
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;
		
	КонецЕсли;
	
	ПослеСохраненияПолученныхПодписейВыпискиСинхронныйОбмен(Ложь, СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПолученныхПодписейВыпискиСинхронныйОбмен(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибка Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	ВыпискаБанка = ДополнительныеПараметры.ВыпискаБанка;
	
	Если ВыпискаБанка = Неопределено Тогда // Произошла ошибка
		ПроизошлаОшибка = Истина;
		ОповеститьОВыборе(Неопределено);
	Иначе
		ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк';
									|en = '1C:DirectBank'");
		ТекстОповещения = НСтр("ru = 'Получено документов: (1).';
								|en = 'Documents received: (1).'");
		Оповестить("ОбновитьСостояниеОбменСБанками");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
		ОповеститьОВыборе(ВыпискаБанка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьВыпискуАсинхронныйОбмен()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("ВидОперации", ВидОперации);
	
	РезультатЗадания = ЗапускЗаданияПолученияВыпискиАсинхронныйОбмен(ПараметрыЗапроса, УникальныйИдентификатор);
	
	Если РезультатЗадания.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияВыпискиАсинхронныйОбмен", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатЗадания, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияВыпискиАсинхронныйОбмен(РезультатЗадания)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияВыпискиАсинхронныйОбмен(РезультатЗадания, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение выписки банка.';
							|en = 'Receive bank statement.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
			РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки, "ОбменСБанками",
			НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		Результат = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если Результат = Неопределено Тогда
			ЗакрытьФормуНаКлиенте();
		Иначе
			ОбменСБанкамиСлужебныйКлиент.ВызватьОповещения(Результат);
			Оповестить("ОбновитьСостояниеОбменСБанками", Результат.ПараметрОповещения);
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Результат", Результат);
			
			Если Результат.Свойство("ДанныеЭП") И Результат.ДанныеЭП.Количество() Тогда
				ДополнительныеПараметры.Вставить("ДанныеЭП", Результат.ДанныеЭП);
				ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
				Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПолученныхПодписейВыписки", ЭтотОбъект, ДополнительныеПараметры);
				ДополнительныеПараметры.Вставить("ОповещениеПослеСохраненияПодписей", Оповещение);
				СохранитьОчереднуюПодпись(ДополнительныеПараметры);
			
			Иначе
				ПослеСохраненияПолученныхПодписейВыписки(Ложь, ДополнительныеПараметры);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатЗапросаСостоянияЭД(РезультатЗадания, ДополнительныеПараметры = Неопределено) Экспорт

	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;

	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение состояния электронного документа.';
							|en = 'Receiving an electronic document status.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, 
			РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки, "ОбменСБанками",
			НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		Результат = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Оповестить("ОбновитьСостояниеОбменСБанками");
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
		ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
		ПараметрыЗапроса.Вставить("СообщениеОбмена", Результат.СообщениеОбменаЗапросСостояния);
	
		Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияИзвещенияОСостоянииЭДНаСервере(
			ПараметрыЗапроса, УникальныйИдентификатор);
		
		ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		
		Если Результат.Статус = "Выполняется" Тогда
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
			Оповещение = Новый ОписаниеОповещения("ПолучитьИзвещениеОСостоянииЭД", ЭтотОбъект);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
		Иначе
			ПолучитьИзвещениеОСостоянииЭД(Результат, Неопределено);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросЗонда(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Отправка тестового запроса в банк';
							|en = 'Sending a test request to the bank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	КолОтправленных = 0;
	КолПолученных = 0;
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияИзвещенияОСостоянииЭДНаСервере(
		ДополнительныеПараметры.ПараметрыЗапроса, УникальныйИдентификатор);
		
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПолучитьИзвещениеОСостоянииЭД", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПолучитьИзвещениеОСостоянииЭД(Результат, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияПолученияВыпискиАсинхронныйОбмен(Знач ПараметрыЗапроса, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение выписки из банка';
															|en = 'Getting statement from the bank'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ПолучитьНовыеДокументыИзБанка", ПараметрыЗапроса, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеЭП.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеСохраненияПодписей, ДополнительныеПараметры.ЕстьОшибка);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеЭП Цикл
		Прервать;
	КонецЦикла;
	
	ДополнительныеПараметры.ДанныеЭП.Удалить(КлючЗначение.Ключ);
	
	Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПодписейСообщенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПодписейСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Истина);
	КонецЕсли;
	
	СохранитьОчереднуюПодпись(ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПолученныхПодписейВыписки(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибка Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ДополнительныеПараметры.Результат;
	
	Если СтруктураВозврата.ТребуетсяПовторнаяАутентификация Тогда
		ПроизошлаОшибка = Ложь;
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("НастройкаОбмена", НастройкаОбмена);
		ПараметрыОбработки.Вставить("ПроцедураОбработчик", "ПродолжитьПолучениеВыпискиПослеПолученияМаркераБанка");
		ПараметрыАвторизации = Новый Структура;
		Если ОбменСБанкамиСлужебныйКлиент.ПолученыДанныеАвторизации(НастройкаОбмена, ПараметрыАвторизации) Тогда
			ОбработатьПолучениеДанныхАутентификации(ПараметрыАвторизации, ПараметрыОбработки);
		Иначе
			Оповещение = Новый ОписаниеОповещения("ОбработатьПолучениеДанныхАутентификации", ЭтотОбъект, ПараметрыОбработки);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ВыпискиБанка = ВыпискиПоЗапросам(НастройкаОбмена, МассивСообщенийОбменаСсылка);
	
	Если ВыпискиБанка = Неопределено Тогда // Произошла ошибка
		ПроизошлаОшибка = Истина;
		ОповеститьОВыборе(Неопределено);
	ИначеЕсли ВыпискиБанка.Количество() Тогда
		ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк';
									|en = '1C:DirectBank'");
		ТекстОповещения = НСтр("ru = 'Получено документов: (%1).';
								|en = 'Documents received: (%1).'");
		ТекстОповещения = СтрШаблон(ТекстОповещения, ВыпискиБанка.Количество());
		Оповестить("ОбновитьСостояниеОбменСБанками");
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
		Если ВидОперации = "ПробноеПолучениеВыписки" Тогда
			Закрыть(Истина);
		Иначе
			ОповеститьОВыборе(ВыпискиБанка);
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОжиданияПолученияВыписки);
		
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьВыпискуАсинхронныйОбмен", ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыпискиПоЗапросам(Знач НастройкаОбмена, Знач МассивЗапросовСсылка)
	
	МассивЗапросов = ПолучитьИзВременногоХранилища(МассивЗапросовСсылка);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СообщениеОбменСБанками.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	|ГДЕ
	|	СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
	|	И СообщениеОбменСБанками.СообщениеРодитель В(&МассивЗапросов)
	|	И СообщениеОбменСБанками.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ВыпискаБанка)";
	Запрос.УстановитьПараметр("МассивЗапросов", МассивЗапросов);
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);

	МассивВозврата = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() >= МассивЗапросов.Количество() Тогда
		Пока Выборка.Следующий() Цикл
			МассивВозврата.Добавить(Выборка.Ссылка);
		КонецЦикла;
	Иначе
		ЗапросОшибки = Новый Запрос;
		ЗапросОшибки.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	СообщениеОбменСБанками.Ссылка КАК Ссылка,
			|	СообщениеОбменСБанками.ПричинаОтклонения КАК ПричинаОтклонения
			|ИЗ
			|	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
			|ГДЕ
			|	СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
			|	И СообщениеОбменСБанками.Ссылка В(&МассивЗапросов)
			|	И СообщениеОбменСБанками.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.ОтклоненБанком)";
		ЗапросОшибки.УстановитьПараметр("МассивЗапросов", МассивЗапросов);
		ЗапросОшибки.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
		Результат = ЗапросОшибки.Выполнить();
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			ТекстСообщения = НСтр("ru = 'Запрос отклонен.
										|Причина отклонения: %1';
										|en = 'Request is rejected.
										|Rejection reason: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.ПричинаОтклонения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьИзвещениеОСостоянииЭД(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение извещения о состоянии документа из банка';
							|en = 'Receiving notification of a document status from the bank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	ОбменСБанкамиСлужебныйКлиент.ВызватьОповещения(СтруктураВозврата);

	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПослеСохраненияПолученныхПодписейИзвещения", ЭтотОбъект, СтруктураВозврата);
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ДанныеЭП", СтруктураВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("ЕстьОшибка", Ложь);
		ДополнительныеПараметры.Вставить("ОповещениеПослеСохраненияПодписей", Оповещение);
		
		СохранитьОчереднуюПодпись(ДополнительныеПараметры);
		
		Возврат;

	КонецЕсли;
	
	ПослеСохраненияПолученныхПодписейИзвещения(Ложь, СтруктураВозврата)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияПолученныхПодписейИзвещения(ЕстьОшибка, ДополнительныеПараметры) Экспорт
	
	Если ЕстьОшибка Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк';
								|en = '1C:DirectBank'");
	ТекстОповещения = НСтр("ru = 'Отправлено документов: (1).
								|Получено документов: (%1).';
								|en = 'Documents sent: (1).
								|Documents received: (%1).'");
	ТекстОповещения = СтрШаблон(ТекстОповещения, ДополнительныеПараметры.КоличествоПолученныхПакетов);
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
	Если ДополнительныеПараметры.ТребуетсяПовторнаяАутентификация Тогда
		ПроизошлаОшибка = Ложь;
		ПараметрыОбработки = Новый Структура;
		ПараметрыОбработки.Вставить("НастройкаОбмена", НастройкаОбмена);
		ПараметрыОбработки.Вставить("ПроцедураОбработчик", "ПродолжитьПолучениеИзвещенияПослеПолученияМаркераБанка");
		ПараметрыАвторизации = Новый Структура;
		Если ОбменСБанкамиСлужебныйКлиент.ПолученыДанныеАвторизации(НастройкаОбмена, ПараметрыАвторизации) Тогда
			ОбработатьПолучениеДанныхАутентификации(ПараметрыАвторизации, ПараметрыОбработки);
		Иначе
			Оповещение = Новый ОписаниеОповещения("ОбработатьПолучениеДанныхАутентификации", ЭтотОбъект, ПараметрыОбработки);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Оповещение, НастройкаОбмена);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ПараметрыВозврата = Новый Структура("Успех", Истина);
		
	Если НЕ ФормаОткрыта Тогда
		Если НЕ ОписаниеОповещенияОЗакрытии = Неопределено Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, ПараметрыВозврата);
		КонецЕсли;
		ЗакрытьФорму = Истина;
	Иначе
		Закрыть(ПараметрыВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыпискуЧерезДополнительнуюОбработку()
	
	ВнешнийПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		ДополнительнаяОбработка);
	
	ДанныеСертификата = ПолучитьИзВременногоХранилища(ДанныеСертификатаСсылка);
	
	ПараметрыВыписки = Новый Структура;
	
	КолПолученных = 0;
	
	МассивНомеровБанковскихСчетов = ПолучитьИзВременногоХранилища(МассивБанковскихСчетовСсылка);
	СообщенияОбменаДляПроверки = Новый Массив;
	
	Для Каждого НомерСчета Из МассивНомеровБанковскихСчетов Цикл
		ПараметрыВыписки.Вставить("НомерСчета",        НомерСчета);
		ПараметрыВыписки.Вставить("БИК",               БИК);
		ПараметрыВыписки.Вставить("ДатаНачала"   ,     Формат(ДатаНачала,    "ДЛФ=D"));
		ПараметрыВыписки.Вставить("ДатаОкончания",     Формат(ДатаОкончания, "ДЛФ=D"));
		ПараметрыВыписки.Вставить("ВерсияСхемыДанных", ОбменСБанкамиКлиентСервер.ВерсияФорматаСинхронногоОбмена());
		ДанныеВыписки = ОбменСБанкамиСлужебныйКлиент.ОтправитьЗапросЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, 2, ПараметрыВыписки);
		Если ДанныеВыписки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого Подпись Из ДанныеВыписки.Подписи Цикл
			
			ДанныеСертификатаПодписи = ОбменСБанкамиСлужебныйКлиент.ДанныеСертификатаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, Подпись.Сертификат);
			Если ДанныеСертификатаПодписи = Неопределено Тогда
				ПроизошлаОшибка = Истина;
				Возврат;
			КонецЕсли;
			Подпись.Вставить("ДанныеСертификата", ДанныеСертификатаПодписи);
			
		КонецЦикла;
		
		СообщениеОбменаВыписка = СохранитьВыписку(ДанныеВыписки, НастройкаОбмена, ДатаНачала, ДатаОкончания);
		СообщенияОбменаДляПроверки.Добавить(СообщениеОбменаВыписка);
		КолПолученных = КолПолученных + 1;
	КонецЦикла;
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить(
		"МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку", СообщенияОбменаДляПроверки);
	ПараметрыПроверки.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	ПараметрыПроверки.Вставить("ТекущийИндексПроверкиПодписейЧерезДополнительнуюОбработку", 0);
	ПараметрыПроверки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОбменСБанкамиСлужебныйКлиент.НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ПараметрыПроверки);
	
	ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк';
								|en = '1C:DirectBank'");
			
	Если КолПолученных = 0 Тогда
		ТекстОповещения = НСтр("ru = 'Полученных документов нет.';
								|en = 'No received documents.'");
	Иначе
		ТекстОповещения = НСтр("ru = 'Получено документов: (%1).';
								|en = 'Documents received: (%1).'");
		ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОповещения, КолПолученных);
	КонецЕсли;
		
	Оповестить("ОбновитьСостояниеОбменСБанками");
		
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
	ОповеститьОВыборе(СообщениеОбменаВыписка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СохранитьВыписку(Знач ДанныеВыписки, Знач НастройкаОбмена, Знач ДатаНачала, Знач ДатаОкончания)
	
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОбмена, "Организация, Банк");
	
	АдресФайла = ПоместитьВоВременноеХранилище(ДанныеВыписки.Выписка);
	
	ПараметрыСообщения = Новый Структура;
	ПараметрыСообщения.Вставить("АдресФайлаВоВременномХранилище", АдресФайла);
	ПараметрыСообщения.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыСообщения.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ВыпискаБанка);
	ПараметрыСообщения.Вставить("Направление", Перечисления.НаправленияЭД.Входящий);
	ПараметрыСообщения.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Получен);
	ПараметрыСообщения.Вставить("СсылкаНаОбъект", НастройкаОбмена);
	ПараметрыСообщения.Вставить("ДатаНачала", ДатаНачала);
	ПараметрыСообщения.Вставить("ДатаОкончания", ДатаОкончания);
	
	НовоеСообщение = Неопределено;
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(ПараметрыСообщения, НовоеСообщение);
	
	Для Каждого Подпись Из ДанныеВыписки.Подписи Цикл
		СтруктураСертификата = Новый Структура;
		СтруктураСертификата.Вставить("Отпечаток", Подпись.ДанныеСертификата.Отпечаток);
		СтруктураСертификата.Вставить("КомуВыдан", Подпись.ДанныеСертификата.ВладелецФИО);
		СтруктураСертификата.Вставить("ДвоичныеДанные", Подпись.Сертификат);
		ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодпись(НовоеСообщение, Подпись.Подпись, СтруктураСертификата);
	КонецЦикла;
	
	ОбменСБанкамиСлужебный.ОпределитьИсполненныеПлатежныеПоручения(НовоеСообщение);
	
	Возврат НовоеСообщение;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПолучениеДанныхАутентификации(ДанныеАутентификации, ПараметрыОбработки) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = Новый Структура("АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	Обработчик = Новый ОписаниеОповещения(ПараметрыОбработки.ПроцедураОбработчик, ЭтотОбъект, ПараметрыОбработки);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
		РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
		НастройкаОбмена);

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПолучениеВыпискиПослеПолученияМаркераБанка(Маркер, ПараметрыОбработки) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессии = Маркер;
	
	ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОжиданияПолученияВыписки);
	
	ПодключитьОбработчикОжидания(
		"Подключаемый_ПолучитьВыпискуАсинхронныйОбмен", ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПолучениеИзвещенияПослеПолученияМаркераБанка(Маркер, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессии = Маркер;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыЗапроса.Вставить("ИдентификаторСессии", ИдентификаторСессии);
	ПараметрыЗапроса.Вставить("ВидОперации", ВидОперации);
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПолученияИзвещенияОСостоянииЭДНаСервере(
		ПараметрыЗапроса, УникальныйИдентификатор);
		
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПолучитьИзвещениеОСостоянииЭД", ЭтотОбъект, ДополнительныеПараметры);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПолучитьИзвещениеОСостоянииЭД(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуНаКлиенте(ЕстьОшибка = Ложь, ПараметрЗакрытия = Неопределено)
	
	ПроизошлаОшибка = ЕстьОшибка;
	
	Если НЕ ФормаОткрыта Тогда
		ЗакрытьФорму = Истина;
	Иначе
		Закрыть(ПараметрЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

#Область ОбменЧерезВК

&НаКлиенте
Процедура ПолучитьВыпискуЧерезВК()
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьНовыеДокументыПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьИИнициализироватьВК(Оповещение, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНовыеДокументыПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	ИначеЕсли Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПолучитьНовыеДокументыВК(Оповещение, Результат, НастройкаОбмена, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСостояниеЭДЧерезВК()
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьНовыеДокументыПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьИИнициализироватьВК(Оповещение, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияНовыхДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.Результат);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	Если Результат.Результат = Неопределено ИЛИ Результат.Результат = Ложь Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	Если ОбменСБанкамиСлужебныйКлиент.ПроцессПрерван(НастройкаОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбмена) Тогда
		Извещение = Неопределено;
		ИзвещениеПолучено = ОбменСБанкамиСлужебныйВызовСервера.ПолученоИзвещениеПоЗапросу(СообщениеОбмена, ПроизошлаОшибка);
		
		Если ПроизошлаОшибка ИЛИ ИзвещениеПолучено Тогда
			ЗакрытьФормуНаКлиенте(ПроизошлаОшибка);
			Возврат;
		КонецЕсли;
		
		ПолучитьНовыеДокументыПослеПодключенияВК(ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры);
		
	Иначе
	
		ВыпискиБанка = ВыпискиПоЗапросам(НастройкаОбмена, МассивСообщенийОбменаСсылка);
	
		Если ВыпискиБанка = Неопределено Тогда // Произошла ошибка
			ЗакрытьФормуНаКлиенте(Истина);
		ИначеЕсли ВыпискиБанка.Количество() Тогда
			ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк';
										|en = '1C:DirectBank'");
			ТекстОповещения = НСтр("ru = 'Получено документов: (%1).';
									|en = 'Documents received: (%1).'");
			ТекстОповещения = СтрШаблон(ТекстОповещения, ВыпискиБанка.Количество());
			Оповестить("ОбновитьСостояниеОбменСБанками");
			ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
			ОповеститьОВыборе(ВыпискиБанка);
		Иначе
			ПолучитьНовыеДокументыПослеПодключенияВК(ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сбербанк

&НаСервере
Функция ДанныеЗапросовВыписокСбербанк(МассивСообщенийОбмена)
	
	СоответствиеВозврата = Новый Соответствие;
	
	Для Каждого СообщениеОбмена Из МассивСообщенийОбмена Цикл
		ПакетXML = ОбменСБанкамиСлужебныйВызовСервера.ПакетXMLСбербанка(СообщениеОбмена, НастройкаОбмена);
		СоответствиеВозврата.Вставить(СообщениеОбмена, ПакетXML);
	КонецЦикла;
	
	Возврат СоответствиеВозврата;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьАвтоматическиеЗапросыВыписокСбербанк()

	Оповещение = Новый ОписаниеОповещения(
		"НачатьФормированиеЗапросовВыпискиПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);

КонецПроцедуры

&НаКлиенте
Процедура НачатьФормированиеЗапросовВыпискиПослеОпределенияСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	СертификатПодписиСбербанк = Результат.СертификатСсылка;
	ИдентификаторСертификатаСбербанк = Результат.ИдентификаторСертификата;
	
	МассивЗапросов = ЗапросыВыписокСбербанк(НастройкаОбмена);
	ДополнительныеПараметры = Новый Структура("МассивЗапросов", МассивЗапросов);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОтправитьЗапросыВыписокПослеПодписиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодписатьЭДСбербанк(ОписаниеОповещения, НастройкаОбмена, МассивЗапросов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапросыВыписокСбербанк(Знач НастройкаОбмена)
	
	Возврат ОбменСБанкамиСлужебный.ЗапросыВыписокСбербанк(НастройкаОбмена);
	
КонецФункции

&НаКлиенте
Процедура ОтправитьЗапросыВыписокПослеПодписиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПолучитьДокументыПоТикетамСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ОтправитьСообщенияОбменаВСбербанк(
		Оповещение, НастройкаОбмена, ДополнительныеПараметры.МассивЗапросов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДокументыПоТикетамСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросНовыхДокументовСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьДокументыПоТикетамСбербанк(Оповещение, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаНовыхДокументовВСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ОтправитьЗапросНовыхДокументовСбербанк(
		Оповещение, НастройкаОбмена, СертификатПодписиСбербанк, ИдентификаторСертификатаСбербанк);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаНовыхДокументовВСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = Результат.Тикет;
	
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиОчередногоЗапросаВыпискиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;

	МассивТикетовСбербанк.Добавить(Результат);
	
	ОтправитьЗапросыВыписокРекурсивноСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСтатусаЗапросаСостоянияСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"ПолучитьИзвещениеОСостоянииДокументаСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.';
							|en = 'An error occurred on the bank server: the request was not found. 
							|Please retry the operation after some time.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.';
							|en = 'An error occurred on the bank server: a request with this ID already exists.
							|Please retry the operation after some time.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	Иначе
		МассивНовыхСообщенийОбмена = Новый Массив;
		Попытка
			ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
				Ответ, НастройкаОбмена, МассивНовыхСообщенийОбмена, ИсходныйТикетСбербанк);
		Исключение
			ВидОперации = НСтр("ru = 'Чтение ответа Сбербанка.';
								|en = 'Read Sberbank response.'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
			ЗакрытьФормуНаКлиенте(Истина);
			Возврат;
		КонецПопытки;
		
		Оповещение = Новый ОписаниеОповещения("ЗакрытьФормуПослеПроверкиПодписейСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуПослеПроверкиПодписейСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	ЗакрытьФормуНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	Попытка
		КоличествоТомов = 0;
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
			Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, ТикетСбербанк, КоличествоТомов);
		Если КоличествоТомов > 0 Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ОтправитьЗапросСостоянияПлатежныхДокументовПослеПолученияБольшогоПакетаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьМноготомныйДокументЧерезТокенСбербанк(
				Оповещение, НастройкаОбмена, ИдентификаторОрганизации, ТикетСбербанк, КоличествоТомов);
			Возврат;
		КонецЕсли;
	Исключение
		ВидОперации = НСтр("ru = 'Чтение ответа Сбербанка.';
							|en = 'Read Sberbank response.'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина, Ложь);
		Возврат;
	КонецПопытки;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросСостоянияПлатежныхДокументовЧерезТокенСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		КоличествоПолученных = КоличествоПолученных + МассивНовыхСообщенийОбмена.Количество();
		Оповестить("ОбновитьСостояниеОбменСБанками");
	Иначе
		ОтправитьЗапросСостоянияПлатежныхДокументовЧерезТокенСбербанк()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСостоянияПлатежныхДокументовЧерезТокенСбербанк(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	МассивТикетовСбербанк = Новый Массив;
	ТекстЗапросаСостоянияПлатежныхДокументов = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаСостоянияОбработкиДокументовСбербанк(
		НастройкаОбмена, МассивТикетовСбербанк);
	
	Если ЗначениеЗаполнено(ТекстЗапросаСостоянияПлатежныхДокументов) Тогда
		ТикетСбербанк = Неопределено;
		
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСостоянияПлатежныхДокументов", ЭтотОбъект);
		
		ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(
			Оповещение, ТекстЗапросаСостоянияПлатежныхДокументов, НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	Если МассивТикетовСбербанк.Количество() = 0 Тогда
		ОбменСБанкамиСлужебныйКлиент.ПоказатьРезультатСинхронизации(КоличествоОтправленных, КоличествоПолученных);
		ЗакрытьФормуНаКлиенте( , Истина);
	Иначе
		НачатьПолучениеДокументовПоТикетамСбербанк();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСостоянияПлатежныхДокументовПослеПолученияБольшогоПакетаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте(Истина, Ложь);
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;

	ОбменСБанкамиСлужебныйВызовСервера.СохранитьБольшойПакетСбербанк(
		НастройкаОбмена, ТикетСбербанк, Результат.МассивТомов, МассивНовыхСообщенийОбмена);
		
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросСостоянияПлатежныхДокументовЧерезТокенСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		КоличествоПолученных = КоличествоПолученных + МассивНовыхСообщенийОбмена.Количество();
		Оповестить("ОбновитьСостояниеОбменСБанками");
	Иначе
		ОтправитьЗапросСостоянияПлатежныхДокументовЧерезТокенСбербанк()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаПоТикетуЗапросовВыпискиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	Попытка
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена);
	Исключение
		ВидОперации = НСтр("ru = 'Чтение ответа Сбербанка.';
							|en = 'Read Sberbank response.'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецПопытки;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПолучитьКонечныеСтатусыЗапросовВыпискиПослеПроверкиПодписиСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		Оповестить("ОбновитьСостояниеОбменСБанками");
	Иначе
		ПолучитьКонечныеСтатусыЗапросовВыпискиПослеПроверкиПодписиСбербанк(Неопределено, Неопределено)
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКонечныеСтатусыЗапросовВыпискиПослеПроверкиПодписиСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(
		ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк);
	ПодключитьОбработчикОжидания("ПолучитьКонечныеСтатусыЗапросовВыпискиСбербанк",
		ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк.ТекущийИнтервал, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтветаПоТикетуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	
	Попытка
		КоличествоТомов = 0;
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
			Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, ТикетСбербанк, КоличествоТомов);
		Если КоличествоТомов > 0 Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПродолжитьПолучениеДокументовПоТикетамПослеОбработкиБольшогоПакетаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьМноготомныйДокументЧерезТокенСбербанк(
				Оповещение, НастройкаОбмена, ИдентификаторОрганизации, ТикетСбербанк, КоличествоТомов);
		Возврат;
		КонецЕсли;
	Исключение
		ВидОперации = НСтр("ru = 'Чтение ответа Сбербанка.';
							|en = 'Read Sberbank response.'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецПопытки;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповестить("ОбновитьСостояниеОбменСБанками");
		Оповещение = Новый ОписаниеОповещения("НачатьПолучениеДокументовПоТикетамСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
	Иначе
		НачатьПолучениеДокументовПоТикетамСбербанк();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПолучениеДокументовПоТикетамПослеОбработкиБольшогоПакетаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;

	ОбменСБанкамиСлужебныйВызовСервера.СохранитьБольшойПакетСбербанк(
		НастройкаОбмена, ТикетСбербанк, Результат.МассивТомов, МассивНовыхСообщенийОбмена);
		
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповестить("ОбновитьСостояниеОбменСБанками");
		Оповещение = Новый ОписаниеОповещения("НачатьПолучениеДокументовПоТикетамСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
	Иначе
		НачатьПолучениеДокументовПоТикетамСбербанк()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОчередногоСтатусаЗапросаСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ДополнительныеПараметры.МассивТикетовСбербанкКопия.Добавить(ДополнительныеПараметры.Тикет);
		ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры)
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.';
							|en = 'An error occurred on the bank server: the request was not found. 
							|Please retry the operation after some time.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте(Истина);
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
								|Повторите операцию позже.';
								|en = 'An error occurred on the bank server: a request with this ID already exists.
								|Please retry the operation after some time.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе
		МассивНовыхСообщенийОбмена = Новый Массив;
		
		Попытка
			ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(Ответ, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		Исключение
			ВидОперации = НСтр("ru = 'Чтение ответа Сбербанка.';
								|en = 'Read Sberbank response.'");
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
			ЗакрытьФормуНаКлиенте(Истина);
			Возврат;
		КонецПопытки;
		
		Если МассивНовыхСообщенийОбмена.Количество() Тогда
			Оповещение = Новый ОписаниеОповещения("ПолучитьСтатусыЗапросовПослеПроверкиПодписейСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
				Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
			Оповестить("ОбновитьСостояниеОбменСБанками");
		Иначе
			ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры)
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтатусыЗапросовПослеПроверкиПодписейСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры)
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатусаЗапросаСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ЗакрытьФормуНаКлиенте(Истина, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Ответ = "<!--NOT PROCESSED YET-->" ИЛИ Ответ = "<!--NOT_PROCESSED_YET-->" Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Ответ = "<!--REQUEST NOT FOUND-->" ИЛИ Ответ = "<!--REQUEST_NOT_FOUND-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос не найден.
							|Повторите операцию позже.';
							|en = 'An error occurred on the bank server: the request was not found. 
							|Please retry the operation after some time.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте(Истина, Ложь);
		Возврат;
	ИначеЕсли Ответ = "<!--REQUESTID DUBLIC-->" ИЛИ Ответ = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'На сервере банка произошла ошибка: запрос с таким идентификатором уже существует.
							|Повторите операцию позже.';
							|en = 'An error occurred on the bank server: a request with this ID already exists.
							|Please retry the operation after some time.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		ЗакрытьФормуНаКлиенте(Истина, Ложь);
		Возврат;
	Иначе // Ответ получен
		СохранитьДатуСинхронизации(НастройкаОбмена);
		ВыполнитьОбработкуОповещения(ОповещениеПослеПолученияОтветаПоТикетуСбербанк, Ответ);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСтатусаЗапросаВыпискиСбербанк(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = Ответ;
	
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуЗапросовВыпискиСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСостоянияПлатежныхДокументов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	МассивТикетовСбербанк.Добавить(Результат);
	НачатьПолучениеДокументовПоТикетамСбербанк();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыпискиПолученыСбербанк(Знач НастройкаОбмена, Знач ЗапросыВыписокСсылка, Знач ДатаНачала, Знач ДатаОкончания, Знач БанковскиеСчетаСсылка, Знач ДатаНачалаОперации, ВыпискиБанка)

	МассивЗапросов = ПолучитьИзВременногоХранилища(ЗапросыВыписокСсылка);
	МассивБанковскихСчетов = ПолучитьИзВременногоХранилища(БанковскиеСчетаСсылка);
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	1 КАК Поле1
	               |ИЗ
	               |	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	               |ГДЕ
	               |	СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
	               |	И СообщениеОбменСБанками.Ссылка В(&МассивСсылок)
	               |	И СообщениеОбменСБанками.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОбменСБанками.Обработан)";
	Запрос.УстановитьПараметр("МассивСсылок", МассивЗапросов);
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда // есть хоть один запрос, по которому еще не пришла выписка
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СообщениеОбменСБанками.Ссылка КАК Ссылка,
	               |	СообщениеОбменСБанками.ДатаНачала КАК ДатаНачала,
	               |	СообщениеОбменСБанками.НомерСчета КАК НомерСчета,
	               |	СообщениеОбменСБанками.Дата КАК Дата
	               |ИЗ
	               |	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	               |ГДЕ
	               |	СообщениеОбменСБанками.НастройкаОбмена = &НастройкаОбмена
	               |	И СообщениеОбменСБанками.ДатаНачала >= &ДатаНачала
	               |	И СообщениеОбменСБанками.ДатаНачала <= &ДатаОкончания
	               |	И СообщениеОбменСБанками.ВидЭД = ЗНАЧЕНИЕ(Перечисление.ВидыЭДОбменСБанками.ВыпискаБанка)
	               |	И СообщениеОбменСБанками.НомерСчета В(&МассивСчетов)
	               |	И СообщениеОбменСБанками.Дата >= &ДатаНачалаОперации
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДатаНачала УБЫВ,
	               |	НомерСчета";
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	Запрос.УстановитьПараметр("МассивСчетов", МассивБанковскихСчетов);
	Запрос.УстановитьПараметр("ДатаНачалаОперации", ДатаНачалаОперации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекСчет = Неопределено; ТекДата = Неопределено;
	ВыпискиБанка = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ДатаНачала <> ТекДата ИЛИ Выборка.НомерСчета <> ТекСчет Тогда
			ТекДата = Выборка.ДатаНачала;
			ТекСчет = Выборка.НомерСчета;
			ВыпискиБанка.Добавить(Выборка.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура НачатьОтправкуЗапросовВыписокСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользуетсяКриптография Тогда
		Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросыВыпискиПослеАутентификацииНаСервереСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.УстановитьСоединениеИАутентифицироватьсяНаСервереСбербанк(Оповещение, НастройкаОбмена);
	Иначе
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
		ПараметрыОтправки.Вставить("МассивСообщенийОбменаСсылка", МассивСообщенийОбменаСсылка);
		ПараметрыОтправки.Вставить("ВидЭД", ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки"));
		ПараметрыОтправки.Вставить("ПолучитьСтатусыДокументов", Ложь);
		ПараметрыОтправки.Вставить("АдресРезультата");
		РезультатОтправки = ОбменСБанкамиСлужебныйВызовСервера.ЗапускЗаданияПоОтправкеДокументовВСбербанк(ПараметрыОтправки);
		Если РезультатОтправки.Статус = "Выполняется" Тогда
			ИдентификаторЗадания = РезультатОтправки.ИдентификаторЗадания;
			ПараметрыОбработчикаОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
			ПараметрыОбработчикаОжидания.ВыводитьОкноОжидания = Ложь;
			Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросовВыписокСбербанк", ЭтотОбъект);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОтправки, Оповещение, ПараметрыОбработчикаОжидания);
		Иначе
			ПослеОтправкиЗапросовВыписокСбербанк(РезультатОтправки, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеДокументовПоТикетамСбербанк(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если МассивТикетовСбербанк.Количество() = 0 Тогда // все ответы получены
		ОбменСБанкамиСлужебныйКлиент.ПоказатьРезультатСинхронизации(КоличествоОтправленных, КоличествоПолученных);
		ЗакрытьФормуНаКлиенте(, Истина);
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = МассивТикетовСбербанк[0];
	МассивТикетовСбербанк.Удалить(0);
		
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьВыпискуСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьОтветПоТикетуПослеОтправкиЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ОтправитьЗапросНовыхДокументовСбербанк(
		Оповещение, НастройкаОбмена, СертификатПодписиСбербанк, ИдентификаторСертификатаСбербанк);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросыВыпискиПослеАутентификацииНаСервереСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	СоответствиеТекстовЗапросов = ПолучитьИзВременногоХранилища(ТекстыЗапросовВыписокСбербанк);
	МассивТекстовЗапросов = Новый Массив;
	МассивТикетовСбербанк = Новый Массив;
	
	Для Каждого КлючЗначение Из СоответствиеТекстовЗапросов Цикл
		МассивТекстовЗапросов.Добавить(КлючЗначение.Значение);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивТекстовЗапросов", МассивТекстовЗапросов);
	ДополнительныеПараметры.Вставить("СоответствиеТекстовЗапросов", СоответствиеТекстовЗапросов);
	
	ОтправитьЗапросыВыписокРекурсивноСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросыВыписокРекурсивноСбербанк(ДополнительныеПараметры)
	
	Если НЕ ДополнительныеПараметры.МассивТекстовЗапросов.Количество() Тогда // все запросы отправлены
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"ПолучитьВнешниеИдентификаторыЗапросовВыпискиСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Возврат;
	КонецЕсли;
	
	ТекущийТекстЗапроса = ДополнительныеПараметры.МассивТекстовЗапросов.Получить(0);
	ДополнительныеПараметры.МассивТекстовЗапросов.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиОчередногоЗапросаВыпискиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ТекущийТекстЗапроса, НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВнешниеИдентификаторыЗапросовВыпискиСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("МассивТикетовСбербанкКопия", Новый Массив);
	
	ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыпискуПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;

	Попытка
		КоличествоТомов = 0;
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(
			Результат, НастройкаОбмена, МассивНовыхСообщенийОбмена, ТикетСбербанк, КоличествоТомов);
		Если КоличествоТомов > 0 Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПроверитьНаличиеВыписокПослеПолученияБольшогоПакетаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьМноготомныйДокументЧерезТокенСбербанк(
				Оповещение, НастройкаОбмена, ИдентификаторОрганизации, ТикетСбербанк, КоличествоТомов);
			Возврат;
		КонецЕсли;
	Исключение
		ВидОперации = НСтр("ru = 'Чтение ответа Сбербанка.';
							|en = 'Read Sberbank response.'");
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецПопытки;
	
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПроверитьНаличиеВыписокПослеПроверкиПодписейСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
	Иначе
		ПроверитьНаличиеВыписокСбербанк();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаличиеВыписокПослеПроверкиПодписейСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	ПроверитьНаличиеВыписокСбербанк();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаличиеВыписокПослеПолученияБольшогоПакетаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;

	ОбменСБанкамиСлужебныйВызовСервера.СохранитьБольшойПакетСбербанк(
		НастройкаОбмена, ТикетСбербанк, Результат.МассивТомов, МассивНовыхСообщенийОбмена);
		
	Если МассивНовыхСообщенийОбмена.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПроверитьНаличиеВыписокСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, НастройкаОбмена, МассивНовыхСообщенийОбмена);
		КоличествоПолученных = КоличествоПолученных + МассивНовыхСообщенийОбмена.Количество();
		Оповестить("ОбновитьСостояниеОбменСБанками");
	Иначе
		ПроверитьНаличиеВыписокСбербанк();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНаличиеВыписокСбербанк(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ВыпискиБанка = Неопределено;
	ПолученыВсеВыписки = ВыпискиПолученыСбербанк(НастройкаОбмена, МассивСообщенийОбменаСсылка, ДатаНачала, ДатаОкончания,
		МассивБанковскихСчетовСсылка, ДатаНачалаОперацииСбербанк, ВыпискиБанка);
	
	Если ПолученыВсеВыписки Тогда
		ОповеститьОВыборе(ВыпискиБанка);
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ОжидатьВыпискуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзвещениеОСостоянииДокументаСбербанк()
	
	Если ИспользуетсяКриптография Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСтатусаЗапросаСостоянияСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(
			Оповещение, ИдентификаторОрганизации, ИсходныйТикетСбербанк, НастройкаОбмена);
	Иначе
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьИзвещениеОСостоянииПлатежногоДокумента",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКонечныеСтатусыЗапросовВыпискиСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибка = Ложь;
	ТекстЗапроса = ТекстЗапросаСостоянияЗапросовВыписокСбербанк(НастройкаОбмена, МассивСообщенийОбменаСсылка, ЕстьОшибка);
	
	Если ЕстьОшибка Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстЗапроса) Тогда // есть запросы в не конечном статусе
		ТикетСбербанк = Неопределено;
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаСтатусаЗапросаВыпискиСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ВыполнитьОтправкуДанныхЧерезТокенСбербанк(Оповещение, ТекстЗапроса, НастройкаОбмена);
	Иначе // все запросы выписок имеют конечный статус
		Оповещение = Новый ОписаниеОповещения("ПослеОпределенияСертификатаСбербанк", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСертификатПодписиСбербанк(Оповещение, ИмяВнешнегоМодуля, НастройкаОбмена);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНовыеДокументыСбербанк()

	ТикетСбербанк = ИсходныйТикетСбербанк;
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтветПоТикетуПослеОтправкиЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	КонецЕсли;
	
	ТикетСбербанк = Результат.Тикет;
	
	ОповещениеПослеПолученияОтветаПоТикетуСбербанк = Новый ОписаниеОповещения(
		"ПолучитьВыпискуПослеПолученияОтветаПоТикетуЗапросаНовыхДокументовСбербанк", ЭтотОбъект);
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ПолучитьОтветПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтветПоТикетуСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусаЗапросаСбербанк", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(
		Оповещение, ИдентификаторОрганизации, ТикетСбербанк, НастройкаОбмена);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтатусыЗапросовРекурсивноСбербанк(ДополнительныеПараметры)
	
	Если НЕ МассивТикетовСбербанк.Количество() Тогда
		
		Если ДополнительныеПараметры.МассивТикетовСбербанкКопия.Количество() Тогда
			МассивТикетовСбербанк = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(
				ДополнительныеПараметры.МассивТикетовСбербанкКопия);
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"ПолучитьВнешниеИдентификаторыЗапросовВыпискиСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк);
			ПодключитьОбработчикОжидания("ПолучитьКонечныеСтатусыЗапросовВыпискиСбербанк",
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк.ТекущийИнтервал, Истина);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Тикет = МассивТикетовСбербанк.Получить(0);
	МассивТикетовСбербанк.Удалить(0);
	ДополнительныеПараметры.Вставить("Тикет", Тикет);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияОчередногоСтатусаЗапросаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьСтатусЗапросаСбербанк(
		Оповещение, ИдентификаторОрганизации, Тикет, НастройкаОбмена);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаСостоянияЗапросовВыписокСбербанк(Знач НастройкаОбмена, Знач МассивСообщенийОбменаСсылка, ЕстьОшибка)
	
	МассивСообщенийОбмена = ПолучитьИзВременногоХранилища(МассивСообщенийОбменаСсылка);
	
	Попытка
		ТекстЗапроса = ОбменСБанкамиСлужебный.ТекстЗапросаСостоянияЗапросовВыписокСбербанк(НастройкаОбмена, МассивСообщенийОбмена);
	Исключение
		ВидОперации = НСтр("ru = 'Формирование запроса состояния запроса выписки';
							|en = 'Generate state request statement request'");
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
		ЕстьОшибка = Истина;
	КонецПопытки;
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаКлиенте
Процедура ПослеОпределенияСертификатаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда // Пользователь не выбрал сертификат
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	СертификатПодписиСбербанк = Результат.СертификатСсылка;
	ИдентификаторСертификатаСбербанк = Результат.ИдентификаторСертификата;
	
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("ОжидатьВыпискуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьРезультатПроверкиSMSСбербанк()
	
	Если Не ФормаОткрыта Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии);
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПолученияРезультатовПроверкиСбербанк(
		НастройкаОбмена, СообщениеОбмена, ИсходныйТикетСбербанк, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияРезультатовПроверкиSMSСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияРезультатовПроверкиSMSСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияРезультатовПроверкиSMSСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте();
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		ВидОперации = НСтр("ru = 'Проверка SMS в Сбербанке';
							|en = 'Checking SMS in Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
	Иначе // выполнено
		Результат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если Результат = Неопределено Тогда // банк еще не обработал запрос
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьРезультатПроверкиSMSСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			Возврат;
		ИначеЕсли Результат Тогда
			// Необходимо получить статус документа, т.к. возможно требуется установки второй подписи.
			Результат = ЗапускЗаданияОтправкиЗапросаСтатусаПлатежаСбербанк(
				НастройкаОбмена, СообщениеОбмена, УникальныйИдентификатор);
			ДополнительныеПараметры = Новый Структура("АутентификацияПроизводилась", Ложь);
			Если Результат.Статус = "Выполняется" Тогда
				ПараметрыОбработчикаОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
				ПараметрыОбработчикаОжидания.ВыводитьОкноОжидания = Ложь;
				Оповещение = Новый ОписаниеОповещения(
					"ПослеОтправкиЗапросаСтатусаПлатежаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОбработчикаОжидания);
			Иначе
				ПослеОтправкиЗапросаСтатусаПлатежаСбербанк(Результат, ДополнительныеПараметры)
			КонецЕсли;
			ПоказатьОповещениеПользователя(НСтр("ru = 'Платеж подтвержден';
												|en = 'Payment is confirmed'"), , , БиблиотекаКартинок.Успешно32);
		Иначе
			ПоказатьОповещениеПользователя(НСтр("ru = 'Платеж не подтвержден';
												|en = 'Payment not confirmed'"), , , БиблиотекаКартинок.Ошибка32);
			Оповестить("ОбновитьСостояниеОбменСБанками");
			ЗакрытьФормуНаКлиенте();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросаСтатусаПлатежаСбербанк(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если РезультатЗадания = Неопределено Тогда // задание было отменено
		Возврат;
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Ошибка" Тогда
		ЗакрытьФормуНаКлиенте(Истина);
		ВидОперации = НСтр("ru = 'Отправка запроса статуса платежа в Сбербанк';
							|en = 'Sending payment status request to Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации,
			РезультатЗадания.ПодробноеПредставлениеОшибки, РезультатЗадания.КраткоеПредставлениеОшибки, "ОбменСБанками",
			НастройкаОбмена);
	Иначе // выполнено
		РезультатОперации = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
		Если РезультатОперации.ТребуетсяАутентификация Тогда
			Если ДополнительныеПараметры.АутентификацияПроизводилась Тогда
				ТекстОшибки = НСтр("ru = 'Ошибка аутентификации на сервере банка';
									|en = 'Authentication error on the bank server'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
				ЗакрытьФормуНаКлиенте(Истина);
				Возврат;
			Иначе
				Обработчик = Новый ОписаниеОповещения(
					"ОтправитьЗапросСтатусаПлатежаПослеБазовойАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
				ОбменСБанкамиСлужебныйКлиент.ВыполнитьБазовуюАутентификациюСбербанк(
					Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена, НастройкаОбмена);
			КонецЕсли
		Иначе // запрос был отправлен
			ИсходныйТикетСбербанк = РезультатОперации.Тикет;
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("ПолучитьИзвещениеОСостоянииДокументаСбербанк", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСтатусаПлатежаПослеБазовойАутентификацииСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		Результат = ЗапускЗаданияОтправкиЗапросаСтатусаПлатежаСбербанк(
			НастройкаОбмена, СообщениеОбмена, УникальныйИдентификатор);
		Если Результат.Статус = "Выполняется" Тогда
			ПараметрыОбработчикаОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
			ПараметрыОбработчикаОжидания.ВыводитьОкноОжидания = Ложь;
			ДополнительныеПараметры = Новый Структура("АутентификацияПроизводилась", Истина);
			Оповещение = Новый ОписаниеОповещения(
				"ПослеОтправкиЗапросаСтатусаПлатежаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОбработчикаОжидания);
		Иначе
			ПослеОтправкиЗапросаСтатусаПлатежаСбербанк(Результат, ДополнительныеПараметры)
		КонецЕсли;
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка';
							|en = 'Authentication on Sberbank server'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе
		ЗакрытьФормуНаКлиенте(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияОтправкиЗапросаСтатусаПлатежаСбербанк(Знач НастройкаОбмена, Знач СообщениеОбмена, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение статуса платежа Сбербанка.';
															|en = 'Receive Sberbank payment status.'");
	
	ПараметрыСинхронизации = Новый Структура;
	ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	ПараметрыСинхронизации.Вставить("ТекущаяСессия", ТекущаяСессия);
	ПараметрыСинхронизации.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыСинхронизации.Вставить("СообщениеОбмена", СообщениеОбмена);
		
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ОтправитьЗапросСтатусаПлатежногоДокумента", ПараметрыСинхронизации, ПараметрыВыполнения);

КонецФункции

&НаСервереБезКонтекста
Функция ЗапускЗаданияПолученияРезультатовПроверкиСбербанк(Знач НастройкаОбмена, Знач СообщениеОбмена, Знач Тикет, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение результатов проверки SMS из Сбербанка.';
															|en = 'Receive SMS message check results from Sberbank.'");
	
	ПараметрыОтправки = Новый Структура;
	ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	ПараметрыОтправки.Вставить("ТекущаяСессия", ТекущаяСессия);
	ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОтправки.Вставить("Тикет", Тикет);
	ПараметрыОтправки.Вставить("СообщениеОбмена", СообщениеОбмена);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ПолучитьРезультатПроверкиSMSСбербанк", ПараметрыОтправки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПолучитьИдентификаторКриптопрофиляСбербанк()
	
	Если Не ФормаОткрыта Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии);
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПолученияИдентификатораКриптопрофиляСбербанк(
		НастройкаОбмена, ИсходныйТикетСбербанк, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияИдентификатораКриптопрофиляСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияИдентификатораКриптопрофиляСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияПолученияИдентификатораКриптопрофиляСбербанк(Знач НастройкаОбмена, Знач Тикет, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение идентификатора криптопрофиля из Сбербанка.';
															|en = 'Receive a crypto profile ID from Sberbank.'");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ТекущаяСессия", ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена));
	ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОтправки.Вставить("Тикет", Тикет);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ПолучитьИдентификаторКриптопрофиляСбербанк", ПараметрыОтправки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ПослеПолученияИдентификатораКриптопрофиляСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Не ФормаОткрыта Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ЗакрытьФормуНаКлиенте();
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Закрыть();
		ТекстСообщения = НСтр("ru = 'При подтверждении документа %1 произошла ошибка:
									|%2';
									|en = 'An error occurred while confirming the %1 document: 
									|%2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, СообщениеОбмена, Результат.КраткоеПредставлениеОшибки);
		
		ВидОперации = НСтр("ru = 'Подтверждение документа в Сбербанке';
							|en = 'Document confirmation in Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, Результат.ПодробноеПредставлениеОшибки, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
	Иначе // выполнено
		ИдентификаторКриптопрофиля = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если ИдентификаторКриптопрофиля = Неопределено Тогда // банк еще не успел подготовить ответ
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьИдентификаторКриптопрофиляСбербанк",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			ЗакрытьФормуНаКлиенте( , ИдентификаторКриптопрофиля);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросовВыписокСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	ПараметрыЖурналирования = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыЖурналирования(НастройкаОбмена);
	Если ПараметрыЖурналирования.ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, ПараметрыЖурналирования.КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки, НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки.ТребуетсяАутентификация Тогда // неверный идентификатор сессии.
			ВидОперации = НСтр("ru = 'Отправка запроса выписки в Сбербанк.';
								|en = 'Send a statement request to Sberbank.'");
			ТекстСообщения = НСтр("ru = 'Неверный идентификатора сессии.
										|Обратитесь в техническую поддержку.';
										|en = 'Incorrect session ID. 
										|Contact technical support.'");
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				ВидОперации, ТекстСообщения, ТекстСообщения, "ОбменСБанками", НастройкаОбмена);
			ЗакрытьФормуНаКлиенте(Истина)
		Иначе
			МассивТикетовСсылкаСбербанк = ПоместитьВоВременноеХранилище(
				РезультатОтправки.МассивТикетов, УникальныйИдентификатор);
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПолученияСтатусовЗапросовСбербанк(
		НастройкаОбмена, МассивТикетовСсылкаСбербанк, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусовЗапросовСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияСтатусовЗапросовСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатусовЗапросовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Получение статуса запроса в Сбербанке';
							|en = 'Receiving a request status in Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ПроизошлаОшибка = Истина;
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки.ТребуетсяАутентификация Тогда
			Обработчик = Новый ОписаниеОповещения(
				"ПолучитьВнешниеИдентификаторыЗапросовВыпискиПослеПовторнойАутентификацииПоЛогинуСбербанк", ЭтотОбъект,
				ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ВыполнитьБазовуюАутентификациюСбербанк(
				Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена, НастройкаОбмена);
		ИначеЕсли РезультатОтправки.МассивНеобработанныхТикетов.Количество() Тогда
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк);
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьКонечныеСтатусыЗапросовВыпискиБазоваяАутентификацияСбербанк",
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк.ТекущийИнтервал, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВнешниеИдентификаторыЗапросовВыпискиПослеПовторнойАутентификацииПоЛогинуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка';
							|en = 'Authentication on Sberbank server'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе
		ЗакрытьФормуНаКлиенте(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьКонечныеСтатусыЗапросовВыпискиБазоваяАутентификацияСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	РезультатОтправки = ЗапускЗаданияЗапросаСтатусаЗапросовСбербанк(
		НастройкаОбмена, МассивСообщенийОбменаСсылка, УникальныйИдентификатор);

	Если РезультатОтправки.Статус = "Выполняется" Тогда
		ИдентификаторЗадания = РезультатОтправки.ИдентификаторЗадания;
		ПараметрыОбработчикаОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОбработчикаОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросовСтатусовЗапросовВыписокСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОтправки, Оповещение, ПараметрыОбработчикаОжидания);
	Иначе
		ПослеОтправкиЗапросовСтатусовЗапросовВыписокСбербанк(РезультатОтправки, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиЗапросовСтатусовЗапросовВыписокСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ВидОперации = НСтр("ru = 'Отправка запроса статуса документа в Сбербанк';
							|en = 'Sending a document status request to Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки = Неопределено Тогда // все запросы имеют конечные статусы
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОжиданияПолученияВыписки);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ОжидатьВыпискуСбербанк", ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
		Иначе
			// Ошибка платформы 10206606
			МассивТикетовСсылкаСбербанк = ПоместитьВоВременноеХранилище(РезультатОтправки, УникальныйИдентификатор);
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьИзвещенияНаЗапросыВыписокСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОжидатьВыпискуСбербанк()
	
	Результат = ЗапускЗаданияОтправкиЗапросаНовыхДокументовСбербанк(НастройкаОбмена, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияТикетаНаЗапросНовыхДокументовСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияТикетаНаЗапросНовыхДокументовСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияТикетаНаЗапросНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПроизошлаОшибка = Истина;
		ВидОперации = НСтр("ru = 'Отправка запроса новых документов в Сбербанк';
							|en = 'Sending a new documents request to Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		// Ошибка платформы 10206606
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		МассивТикетовСсылкаСбербанк = ПоместитьВоВременноеХранилище(РезультатВыполнения, УникальныйИдентификатор);
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьНовыеДокументыПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолучитьНовыеДокументыПоТикетуСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПолученияСтатусовЗапросовСбербанк(
		НастройкаОбмена, МассивТикетовСсылкаСбербанк, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияНовыхДокументовСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПроизошлаОшибка = Истина;
		ВидОперации = НСтр("ru = 'Получение новых документов из Сбербанка';
							|en = 'Receiving new documents from Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки.ТребуетсяАутентификация Тогда
			Обработчик = Новый ОписаниеОповещения(
				"ПолучитьНовыеДокументыПослеПовторнойАутентификацииПоЛогинуСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ВыполнитьБазовуюАутентификациюСбербанк(
				Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена, НастройкаОбмена);
		ИначеЕсли РезультатОтправки.МассивНеобработанныхТикетов.Количество() Тогда
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьНовыеДокументыПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			ВыпискиБанка = Неопределено;
			ПолученыВсеВыписки = ВыпискиПолученыСбербанк(НастройкаОбмена, МассивСообщенийОбменаСсылка, ДатаНачала, ДатаОкончания,
				МассивБанковскихСчетовСсылка, ДатаНачалаОперацииСбербанк, ВыпискиБанка);

			Если ПолученыВсеВыписки Тогда
				ОповеститьОВыборе(ВыпискиБанка);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОжиданияПолученияВыписки);
				ПодключитьОбработчикОжидания(
					"Подключаемый_ОжидатьВыпискуСбербанк", ПараметрыОжиданияПолученияВыписки.ТекущийИнтервал, Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНовыеДокументыПослеПовторнойАутентификацииПоЛогинуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьНовыеДокументыПоТикетуСбербанк", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка';
							|en = 'Authentication on Sberbank server'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе
		ЗакрытьФормуНаКлиенте(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияОтправкиЗапросаНовыхДокументовСбербанк(Знач НастройкаОбмена, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка запроса новых документов в Сбербанк.';
															|en = 'Send a new document request to Sberbank.'");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ТекущаяСессия", ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена));
	ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ОтправитьЗапросНовыхДокументовСбербанк", ПараметрыОтправки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПолучитьИзвещенияНаЗапросыВыписокСбербанк()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПолученияСтатусовЗапросовСбербанк(
		НастройкаОбмена, МассивТикетовСсылкаСбербанк, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияПромежуточныхСтатусовЗапросовСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияПромежуточныхСтатусовЗапросовСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияПромежуточныхСтатусовЗапросовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПроизошлаОшибка = Истина;
		ВидОперации = НСтр("ru = 'Получение статусов документов из Сбербанка';
							|en = 'Receiving documents statuses from Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки.ТребуетсяАутентификация Тогда
			Обработчик = Новый ОписаниеОповещения(
				"ПолучитьИзвещенияНаЗапросыВыписокПослеПовторнойАутентификацииПоЛогинуСбербанк", ЭтотОбъект,
				ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ВыполнитьБазовуюАутентификациюСбербанк(
				Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена, НастройкаОбмена);
		ИначеЕсли РезультатОтправки.МассивНеобработанныхТикетов.Количество() Тогда
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк);
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьКонечныеСтатусыЗапросовВыпискиБазоваяАутентификацияСбербанк",
				ПараметрыОжиданияКонечногоСтатусаЗапросовВыпискиСбербанк.ТекущийИнтервал, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИзвещенияНаЗапросыВыписокПослеПовторнойАутентификацииПоЛогинуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПолучитьВнешниеИдентификаторыЗапросовВыпискиБазоваяАутентификацияСбербанк",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка';
							|en = 'Authentication on Sberbank server'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе
		ЗакрытьФормуНаКлиенте(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияЗапросаСтатусаЗапросовСбербанк(Знач НастройкаОбмена, Знач МассивСообщенийОбменаСсылка, Знач УникальныйИдентификатор)
	
	МассивСообщенийОбмена = ПолучитьИзВременногоХранилища(МассивСообщенийОбменаСсылка);
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка запроса статуса запросов выписки Сбербанка.';
															|en = 'Send the request of Sberbank statement request status.'");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ТекущаяСессия", ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена));
	ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОтправки.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ОтправитьЗапросыСтатусовЗапросовСбербанк", ПараметрыОтправки, ПараметрыВыполнения);

КонецФункции

&НаСервереБезКонтекста
Функция ЗапускЗаданияПолученияСтатусовЗапросовСбербанк(Знач НастройкаОбмена, Знач МассивТикетовСсылка, Знач УникальныйИдентификатор)
	
	МассивТикетов = ПолучитьИзВременногоХранилища(МассивТикетовСсылка);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение статусов запросов выписки Сбербанк.';
															|en = 'Receive Sberbank statement request statuses.'");
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("ТекущаяСессия", ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена));
	ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОтправки.Вставить("МассивТикетов", МассивТикетов);
	ПараметрыОтправки.Вставить("СоздаватьОперацииВыписки", Ложь);
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ПолучитьСтатусыЗапросовСбербанк", ПараметрыОтправки, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПолучитьИзвещениеОСостоянииПлатежногоДокумента()
	
	Если Не ФормаОткрыта Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ЗапускЗаданияПолученияСтатусаПлатежногоДокументаСбербанк(
		НастройкаОбмена, ИсходныйТикетСбербанк, УникальныйИдентификатор);
	
	Если Результат.Статус = "Выполняется" Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияСтатусаПлатежаСбербанк", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Оповещение, ПараметрыОжидания);
	Иначе
		ПослеПолученияСтатусаПлатежаСбербанк(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияСтатусаПлатежаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если ИспользоватьЖурналирование Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьЖурналВФайл(НастройкаОбмена, КаталогДляЖурналирования);
	КонецЕсли;

	Если Результат = Неопределено Тогда // задание было отменено
		ЗакрытьФормуНаКлиенте();
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ПроизошлаОшибка = Истина;
		ВидОперации = НСтр("ru = 'Получение статуса платежа из Сбербанка';
							|en = 'Receiving payment status from Sberbank'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, Результат.ПодробноеПредставлениеОшибки,
			Результат.КраткоеПредставлениеОшибки, "ОбменСБанками", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе // выполнено
		РезультатОтправки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОтправки.ТребуетсяАутентификация Тогда
			Обработчик = Новый ОписаниеОповещения("ПолучитьСтатусПлатежаПослеАутентификацииПоЛогинуСбербанк",
				ЭтотОбъект, ДополнительныеПараметры);
			ОбменСБанкамиСлужебныйКлиент.ВыполнитьБазовуюАутентификациюСбербанк(
				Обработчик, ИмяВнешнегоМодуля, НастройкаОбмена, НастройкаОбмена);
		ИначеЕсли РезультатОтправки.МассивНеобработанныхТикетов.Количество() Тогда
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПолучитьИзвещениеОСостоянииПлатежногоДокумента",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Иначе
			Оповестить("ОбновитьСостояниеОбменСБанками");
			ЗакрытьФормуНаКлиенте();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтатусПлатежаПослеАутентификацииПоЛогинуСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПолучитьИзвещениеОСостоянииПлатежногоДокумента",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	ИначеЕсли Результат.ТребуетсяТокен Тогда
		ВидОперации = НСтр("ru = 'Аутентификация на сервере Сбербанка';
							|en = 'Authentication on Sberbank server'");
		ОбменСБанкамиКлиентСервер.СообщитьОбОшибкеСбербанк(ВидОперации, "GA==", НастройкаОбмена);
		ЗакрытьФормуНаКлиенте(Истина);
	Иначе
		ЗакрытьФормуНаКлиенте(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗаданияПолученияСтатусаПлатежногоДокументаСбербанк(Знач НастройкаОбмена, Знач Тикет, Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение статуса платежного документа из Сбербанка.';
															|en = 'Receive the payment document status from Sberbank.'");
	
	МассивТикетов = Новый Массив;
	МассивТикетов.Добавить(Тикет);
	
	ПараметрыОтправки = Новый Структура;
	ТекущаяСессия = ОбменСБанкамиСлужебный.ПараметрыУстановленнойСессииСбербанк(НастройкаОбмена);
	ПараметрыОтправки.Вставить("ТекущаяСессия", ТекущаяСессия);
	ПараметрыОтправки.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыОтправки.Вставить("МассивТикетов", МассивТикетов);
	ПараметрыОтправки.Вставить("СоздаватьОперацииВыписки", Истина);

	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСБанкамиСлужебный.ПолучитьСтатусыЗапросовСбербанк", ПараметрыОтправки, ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация

КоличествоОтправленных = 0;
КоличествоПолученных = 0;

#КонецОбласти
