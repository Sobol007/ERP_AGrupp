
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не Параметры.Свойство("СписокОбъектов") Или ТипЗнч(Параметры.СписокОбъектов) <> Тип("СписокЗначений") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СписокОбъектов.Очистить();
	
	Для Каждого СтрокаСписка Из Параметры.СписокОбъектов Цикл
		
		// Анкету из журнала не вводим.
		СтрокаСпискаНаиментование = ?(ТипЗнч(СтрокаСписка.Значение) = Тип("Строка"), СтрокаСписка.Значение, СтрокаСписка.Значение.Наименование);
			
		Если Найти(СтрокаСпискаНаиментование, "Анкета") = 0 Тогда
			
			СписокОбъектов.Добавить(СтрокаСписка.Значение, СтрокаСписка.Представление, СтрокаСписка.Пометка, СтрокаСписка.Картинка);
			
		КонецЕсли;	
			
	КонецЦикла;
	
	Если Параметры.Свойство("ТекущийОбъект") И ЗначениеЗаполнено(Параметры.ТекущийОбъект) Тогда
		ТекущийОбъект = CRM_КлиентыСервер.ПолучитьСсылкуПВХОбъектыЖурналаПоТипу(ТипЗнч(Параметры.ТекущийОбъект));
	КонецЕсли;
	
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок = Строка(Параметры.Заголовок);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ТекущийОбъект) Тогда
		НайденныйЭлемент = СписокОбъектов.НайтиПоЗначению(ТекущийОбъект);
		Если НайденныйЭлемент <> Неопределено Тогда
			Элементы.СписокОбъектов.ТекущаяСтрока = НайденныйЭлемент.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОбъектов

&НаКлиенте
Процедура СписокОбъектовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	КомандаОК(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура КомандаОК(Команда)
	ТекДанные = Элементы.СписокОбъектов.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда Возврат; КонецЕсли;
	
	Закрыть(ТекДанные.Значение);
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти


