
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ИмяМенеджераПечати = "Справочник.ЭДПрисоединенныеФайлы";
	ПараметрыПечати = Новый Структура;
	
	ДокументДляПечати = ПараметрыВыполненияКоманды.Источник.ПрисоединенныйФайлСсылка;
	
	Если Не ЗначениеЗаполнено(ДокументДляПечати) Тогда
		ВидОперации = НСтр("ru = 'Печать электронного документа';
							|en = 'Print electronic document'");
		ТекстСообщения = НСтр("ru = 'Не указан электронный документ для печати';
								|en = 'Electronic document for printing is not specified'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ТекстСообщения, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоСлужебныйДокумент(ДокументДляПечати)
		И ОбменСКонтрагентамиСлужебныйВызовСервера.ВходящийТитул(ДокументДляПечати) = Неопределено Тогда
		ИмяМакета = "ЭД";
		ПараметрыПечати.Вставить("Идентификатор", "ЭД");
		
	ИначеЕсли ЭтоПроизвольныйДокумент(ДокументДляПечати) Тогда
		ИмяМакета = "КарточкаЭД";
		ПараметрыПечати.Вставить("Идентификатор", "КарточкаЭД");
		
	Иначе
		ИмяМакета = "ЭД,КарточкаЭД";
		ПараметрыПечати.Вставить("Идентификатор", "ЭД,КарточкаЭД");
		
	КонецЕсли;
	
	ПараметрыПечати.Вставить("ЗаголовокФормы", НСтр("ru = 'Печать электронного документа';
													|en = 'Print electronic document'"));
	ПараметрыПечати.Вставить("Представление", НСтр("ru = 'Печать электронного документа';
													|en = 'Print electronic document'"));
	
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") Тогда
		
		ДопПараметрыПечати = Новый Структура;
		ДопПараметрыПечати.Вставить("ОтключитьВыводДопДанных",     Истина);
		ДопПараметрыПечати.Вставить("ОтключитьВыводКопияВерна",    Истина);
		ДопПараметрыПечати.Вставить("ВыводитьБанковскиеРеквизиты", Ложь);
		
		ЗаполнитьЗначенияСвойств(ДопПараметрыПечати, ПараметрыВыполненияКоманды.Источник);
		
		ПараметрыПечати.Вставить("СкрыватьИдентификаторДокумента", Ложь);
		ПараметрыПечати.Вставить("СкрыватьДопДанные",              ДопПараметрыПечати.ОтключитьВыводДопДанных);
		ПараметрыПечати.Вставить("СкрыватьКопияВерна",             ДопПараметрыПечати.ОтключитьВыводКопияВерна);
		ПараметрыПечати.Вставить("ВыводитьБанковскиеРеквизиты",    ДопПараметрыПечати.ВыводитьБанковскиеРеквизиты);
		
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяМенеджераПечати, ИмяМакета, ДокументДляПечати, ПараметрыВыполненияКоманды.Источник, ПараметрыПечати);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЭтоПроизвольныйДокумент(ПараметрКоманды)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрКоманды,"ВладелецФайла.ВидЭД")
		= Перечисления.ВидыЭД.ПроизвольныйЭД;
	
КонецФункции

#КонецОбласти