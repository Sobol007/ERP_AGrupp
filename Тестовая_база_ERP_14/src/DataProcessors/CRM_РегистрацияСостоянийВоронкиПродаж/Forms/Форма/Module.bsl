
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого Тип из Метаданные.ПланыВидовХарактеристик.CRM_ВидыОбъектовБизнесПроцессов.Тип.Типы() Цикл
		Если НЕ Тип = Тип("ДокументСсылка.CRM_Взаимодействие") И CRM_ВоронкиПродажСервер.ЭтоТипОбъектаВоронкиПродаж(Тип) Тогда
			СписокОбъектов.Добавить(Тип, Строка(Тип));
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗарегистрировать(Команда)
	МассивПомеченныхОбъектов = ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект);
	Если МассивПомеченныхОбъектов.Количество() = 0 Тогда Возврат; КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗарегистрироватьЗавершение", ЭтотОбъект, Истина);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Для выбранных объектов будут зарегистрированы состояния воронки продаж. Продолжить?';en='For the selected objects, the sales funnel statuses will be registered. Continue?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаРазрегистрировать(Команда)
	МассивПомеченныхОбъектов = ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект);
	Если МассивПомеченныхОбъектов.Количество() = 0 Тогда Возврат; КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаЗарегистрироватьЗавершение", ЭтотОбъект, Ложь);
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Выбраные объекты будут удалены из журнала. Продолжить?';en='Selected objects will be deleted from log. Continue?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗарегистрироватьЗавершение(Ответ, Зарегистрировать) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗарегистрироватьРазрегистрироватьОбъектыНаСервере(Зарегистрировать);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПериод(Команда)
	ДиалогВыбораПериода = Новый ДиалогРедактированияСтандартногоПериода();
	ДиалогВыбораПериода.Период.ДатаНачала = ПериодНачало;
	ДиалогВыбораПериода.Период.ДатаОкончания = ПериодОкончание;
	Если ТипЗнч(ОтборТекущийВариантСтандартногоПериода) = Тип("ВариантСтандартногоПериода") Тогда
		ДиалогВыбораПериода.Период.Вариант = ОтборТекущийВариантСтандартногоПериода;
		Если НачалоДня(ДиалогВыбораПериода.Период.ДатаНачала) <> НачалоДня(ПериодНачало) Или НачалоДня(ДиалогВыбораПериода.Период.ДатаОкончания) <> НачалоДня(ПериодОкончание) Тогда
			ДиалогВыбораПериода.Период.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
		КонецЕсли;
	КонецЕсли;
	ОписаниеОповещения = Новый ОписаниеОповещения("КомандаВыбратьПериодЗавершение", ЭтотОбъект);
	ДиалогВыбораПериода.Показать(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	Если Период <> Неопределено Тогда
		ПериодНачало = Период.ДатаНачала;
		ПериодОкончание = Период.ДатаОкончания;
		ОтборТекущийВариантСтандартногоПериода = Период.Вариант;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьМассивПомеченныхОбъектов(Форма)
	МассивПомеченныхОбъектов = Новый Массив();
	
	Для Каждого СтрокаСписка Из Форма.СписокОбъектов Цикл
		Если СтрокаСписка.Пометка Тогда
			МассивПомеченныхОбъектов.Добавить(СтрокаСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивПомеченныхОбъектов;
КонецФункции

&НаСервере
Процедура ЗарегистрироватьРазрегистрироватьОбъектыНаСервере(Зарегистрировать)
	Обработки.CRM_РегистрацияСостоянийВоронкиПродаж.ЗарегистрироватьРазрегистрироватьОбъекты(ПолучитьМассивПомеченныхОбъектов(ЭтотОбъект), Зарегистрировать, ПериодНачало, ПериодОкончание);
КонецПроцедуры


#КонецОбласти


