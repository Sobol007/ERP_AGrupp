
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПроцессыИКарты = CRM_БизнесПроцессыСервер.ВернутьСпискиДоступныхПроцессовИКарт(Истина);
	
	ЗаполнитьДоступныеСценарииИнтересов(СписокДоступныхПроцессов, Параметры.Ответственный, Параметры.ТипУслуги);
	//СписокДоступныхПроцессов.Добавить("Интерес", "Интерес клиента",,БиблиотекаКартинок.Документ);
	
	Для Каждого ЭлементСписка ИЗ ПроцессыИКарты.Карты Цикл
		СписокДоступныхПроцессов.Добавить(ЭлементСписка.Значение, Строка(ЭлементСписка.Значение),,БиблиотекаКартинок.БизнесПроцессОбъект);
		Для Каждого ЭлементСпискаПроцесс ИЗ ПроцессыИКарты.Процессы Цикл
			Если ЭлементСпискаПроцесс.Значение.CRM_КартаМаршрута = ЭлементСписка.Значение Тогда
				СписокДоступныхПроцессов.Добавить(ЭлементСпискаПроцесс.Значение, "   "+Строка(ЭлементСпискаПроцесс.Значение),,БиблиотекаКартинок.Справочник);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ЭлементСписка ИЗ ПроцессыИКарты.Процессы Цикл
		Если СписокДоступныхПроцессов.НайтиПоЗначению(ЭлементСписка.Значение) = Неопределено Тогда
			СписокДоступныхПроцессов.Добавить(ЭлементСписка.Значение, Строка(ЭлементСписка.Значение),,БиблиотекаКартинок.Справочник);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СписокДоступныхПроцессов.Количество() = 1 Тогда
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, СписокДоступныхПроцессов[0].Значение);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.СписокДоступныхПроцессов.ТекущаяСтрока = 0;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДоступныхПроцессов

&НаКлиенте
Процедура СписокДоступныхПроцессовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(СписокДоступныхПроцессов[ВыбраннаяСтрока].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДоступныхПроцессовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокДоступныхПроцессовПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СписокДоступныхПроцессовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(СписокДоступныхПроцессов[Элементы.СписокДоступныхПроцессов.ТекущаяСтрока].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДоступныеСценарииИнтересов(СписокСценариев, Ответственный, ТипУслуги)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_СостоянияИнтересов.Ссылка КАК Ссылка,
	                      |	CRM_СостоянияИнтересов.Наименование КАК Наименование
	                      |ИЗ
	                      |	Справочник.CRM_СостоянияИнтересов КАК CRM_СостоянияИнтересов
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.CRM_СостоянияИнтересов.ТипыУслуг КАК CRM_СостоянияИнтересовТипыУслуг
	                      |		ПО (CRM_СостоянияИнтересовТипыУслуг.Ссылка = CRM_СостоянияИнтересов.Ссылка)
	                      |ГДЕ
	                      |	CRM_СостоянияИнтересов.Родитель = ЗНАЧЕНИЕ(Справочник.CRM_СостоянияИнтересов.ПустаяСсылка)
	                      |	И (CRM_СостоянияИнтересов.Подразделение = &Подразделение
	                      |			ИЛИ CRM_СостоянияИнтересов.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	                      |	И (CRM_СостоянияИнтересов.Офис = &Офис
	                      |			ИЛИ CRM_СостоянияИнтересов.Офис = ЗНАЧЕНИЕ(Справочник.CRM_ОфисыКомпании.ПустаяСсылка))
	                      |	И (CRM_СостоянияИнтересовТипыУслуг.ТипУслуги = &ТипУслуги
	                      |			ИЛИ CRM_СостоянияИнтересовТипыУслуг.ТипУслуги ЕСТЬ NULL)
	                      |	И НЕ CRM_СостоянияИнтересов.ПометкаУдаления");
	
	Запрос.УстановитьПараметр("ТипУслуги", ТипУслуги);
	Запрос.УстановитьПараметр("Подразделение", Ответственный.Подразделение);
	Запрос.УстановитьПараметр("Офис", Ответственный.Подразделение.CRM_Офис);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокСценариев.Добавить(Выборка.Ссылка, НСтр("ru='Интерес';en='Lead'") + ": " + Выборка.Наименование, , );
	КонецЦикла;
КонецПроцедуры

#КонецОбласти




