#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	Если КоллекцияПечатныхФорм.Количество() = 1 Тогда 
		Если КоллекцияПечатныхФорм[0].ИмяВРЕГ = "ЗАЯВЛЕНИЕНАПЕРЕВОДВАЛЮТЫ" Тогда 
			Для Каждого Ссылка Из МассивОбъектов Цикл
				СинонимМакета	=  НСтр("ru = 'Заявление на перевод валюты'");
				
				ДатаНомер = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Дата, Номер");
				НомерПечать = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДатаНомер.Номер, Истина, Истина);
				
				ТабДок = РЭЙ_ПечатьЗаявленияНаПереводВалюты(Ссылка, ОбъектыПечати, НомерПечать);
				
				УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
					КоллекцияПечатныхФорм,
					"ЗАЯВЛЕНИЕНАПЕРЕВОДВАЛЮТЫ",
					СинонимМакета,
					ТабДок,,
					"Документ.ПлатежноеПоручение.РЭЙ_MT103");			
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры								

Функция ПолучитьСуммуПрописьюАнгл(СсылкаНаОбъект)
	ПараметрыПрописиНаАнглийском = СсылкаНаОбъект.РЭЙ_КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов.РЭЙ_ПараметрыПрописиНаАнглийском;
	
    СтрокаПараметровПрописи = СтрЗаменить(ПараметрыПрописиНаАнглийском, ",", Символы.ПС);
	ПараметрыПрописиЦелойЧастиНаАнгл = СокрЛП(СтрПолучитьСтроку(СтрокаПараметровПрописи, 1));
	ПараметрыПрописиДробнойЧастиНаАнгл = СокрЛП(СтрПолучитьСтроку(СтрокаПараметровПрописи, 3));
	
	СуммаПрописьюАнгл = ЧислоПрописью(СсылкаНаОбъект.СуммаДокумента, "Л=en_US; НП=ЛОЖЬ; НД=ЛОЖЬ; ДП=ЛОЖЬ", ПараметрыПрописиНаАнглийском);
	СуммаПрописьюАнгл = СуммаПрописьюАнгл + ПараметрыПрописиДробнойЧастиНаАнгл;
	СуммаПрописьюАнгл = СуммаПрописьюАнгл + " " + ПараметрыПрописиЦелойЧастиНаАнгл;
	Возврат СуммаПрописьюАнгл;
КонецФункции

Функция РЭЙ_ПечатьЗаявленияНаПереводВалюты(СсылкаНаОбъект, ОбъектыПечати, НомерПечать)		
	Если Прав(НомерПечать,3) = "000" Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Номер платежного поручения не может оканчиваться на ""000""!
								|	(Приложение 4 к Положению Банка России ""О безналичных расчетах в Российской Федерации"" 
								|	от 3 октября 2002 г. No. 2-П в ред. Указания ЦБ РФ от 03.03.2003 No. 1256-У)";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если СсылкаНаОбъект.СуммаДокумента = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указана сумма.";
		Сообщение.Сообщить();
	КонецЕсли;
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_РЭЙЗаявлениеНаПереводВалюты";
	
	Макет = СсылкаНаОбъект.ПолучитьОбъект().ПолучитьМакет("РЭЙ_MT103");
	Обл   = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	// Шапка
	Обл.Параметры.Номер = НомерПечать;
	Обл.Параметры.ДатаДокумента = Формат(СсылкаНаОбъект.Дата,"ДФ=dd.MM.yyyy");
	
	Если СсылкаНаОбъект.РЭЙ_ФлагСрочно Тогда
		Обл.Параметры.ФлагСрочно = "√";
	КонецЕсли;

	Обл.Параметры.НомерСчетаПлательщика = СсылкаНаОбъект.РЭЙ_СчетОрганизацииВЭД.НомерСчета;
	
	// 32: Сумма в валюте платежа
	Обл.Параметры.КодВалюты = СсылкаНаОбъект.РЭЙ_КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов.Наименование;
	Обл.Параметры.СуммаЧислом = Формат(СсылкаНаОбъект.СуммаДокумента,"ЧДЦ=2; ЧРД=,; ЧГ=0");
	Обл.Параметры.СуммаПрописьюАнгл = ПолучитьСуммуПрописьюАнгл(СсылкаНаОбъект);
	
	// 50: Плательщик
	Обл.Параметры.ТекстПлательщикаАнгл = СсылкаНаОбъект.Организация.РЭЙ_НаименованиеАнгл;
	Обл.Параметры.ИННПлательщика = СсылкаНаОбъект.Организация.ИНН;
	Обл.Параметры.АдресПлательщикаАнгл = СсылкаНаОбъект.Организация.РЭЙ_АдресАнгл;

	// 52: Банковские реквизиты плательщика
	Обл.Параметры.НаименованиеБанкаПлательщикаАнгл = СсылкаНаОбъект.РЭЙ_СчетОрганизацииВЭД.Банк.РЭЙ_НаименованиеАнгл;
	Обл.Параметры.КодСвифтБанкаПлательщика = СсылкаНаОбъект.РЭЙ_СчетОрганизацииВЭД.Банк.РЭЙ_СвифтБИК;
	Обл.Параметры.АдресБанкаПлательщикаАнгл = СсылкаНаОбъект.РЭЙ_СчетОрганизацииВЭД.Банк.РЭЙ_АдресАнгл + " " + СсылкаНаОбъект.РЭЙ_СчетОрганизацииВЭД.Банк.РЭЙ_ГородАнгл;
	
	// 56: Банк-корреспондент банка получателя
	БанкКорреспондент = СсылкаНаОбъект.РЭЙ_КорСчетБанкаКонтрагента.Банк;
	Если ЗначениеЗаполнено(БанкКорреспондент.РЭЙ_СвифтБИК) Тогда
		Обл.Параметры.КодСвифтКорБанка = БанкКорреспондент.РЭЙ_СвифтБИК;
	Иначе
		КлиринговаяСистемаБанкаКорреспондента = СсылкаНаОбъект.РЭЙ_КорСчетБанкаКонтрагента.КлиринговаяСистема;
		Если ЗначениеЗаполнено(КлиринговаяСистемаБанкаКорреспондента) Тогда
			Обл.Параметры.КлиринговаяСистемаКорБанка = ?(Не ПустаяСтрока(КлиринговаяСистемаБанкаКорреспондента.Код), КлиринговаяСистемаБанкаКорреспондента.Код, КлиринговаяСистемаБанкаКорреспондента.НаименованиеКраткое);
			
			СтрокаКодаБанкаКорресподентаВКлиринговойСистеме = БанкКорреспондент.РЭЙ_КлиринговыеСистемы.Найти(КлиринговаяСистемаБанкаКорреспондента, "КлиринговаяСистема");
			Если СтрокаКодаБанкаКорресподентаВКлиринговойСистеме <> Неопределено Тогда
				Обл.Параметры.КодКорБанкаВКлиринговойСистеме	= СокрЛП(СтрокаКодаБанкаКорресподентаВКлиринговойСистеме.КодВКлиринговойСистеме);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Обл.Параметры.НаименованиеКорБанкаАнгл	= БанкКорреспондент.РЭЙ_НаименованиеАнгл;
	Обл.Параметры.АдресКорБанкаАнгл	= БанкКорреспондент.РЭЙ_АдресАнгл+" "+БанкКорреспондент.РЭЙ_ГородАнгл;
	
	
	// 57: Банк получателя
	БанкПолучателя = СсылкаНаОбъект.РЭЙ_СчетКонтрагентаВЭД.Банк;
	Если ЗначениеЗаполнено(БанкПолучателя.РЭЙ_СвифтБИК) Тогда 
		Обл.Параметры.КодСвифтБанкаПолучателя = БанкПолучателя.РЭЙ_СвифтБИК;
	Иначе
		КлиринговаяСистемаБанкаПолучателя = СсылкаНаОбъект.РЭЙ_СчетКонтрагентаВЭД.КлиринговаяСистема;
		Если ЗначениеЗаполнено(КлиринговаяСистемаБанкаПолучателя) Тогда
			Обл.Параметры.КлиринговаяСистемаБанкаПолучателя	= ?(Не ПустаяСтрока(КлиринговаяСистемаБанкаПолучателя.Код), КлиринговаяСистемаБанкаПолучателя.Код, КлиринговаяСистемаБанкаПолучателя.НаименованиеКраткое);
			
			СтрокаКодаБанкаПолучателяВКлиринговойСистеме = БанкПолучателя.РЭЙ_КлиринговыеСистемы.Найти(КлиринговаяСистемаБанкаПолучателя, "КлиринговаяСистема");
			Если СтрокаКодаБанкаПолучателяВКлиринговойСистеме <> Неопределено Тогда
				Обл.Параметры.КодБанкаПолучателяВКлиринговойСистеме	= СокрЛП(СтрокаКодаБанкаПолучателяВКлиринговойСистеме.КодВКлиринговойСистеме);
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(БанкПолучателя.РЭЙ_Страна) Тогда
		Обл.Параметры.КодСтраныБанкаПолучателя = БанкПолучателя.РЭЙ_Страна.Код;
	КонецЕсли;
	
	Обл.Параметры.НаименованиеБанкаПолучателяАнгл = БанкПолучателя.РЭЙ_НаименованиеАнгл;
	Обл.Параметры.АдресБанкаПолучателяАнгл = БанкПолучателя.РЭЙ_АдресАнгл + " " + БанкПолучателя.РЭЙ_ГородАнгл;
	
	// 59: Получатель
	Обл.Параметры.НомерСчетаПолучателя = СсылкаНаОбъект.РЭЙ_СчетКонтрагентаВЭД.НомерСчета;
	Обл.Параметры.ТекстПолучателяАнгл = СсылкаНаОбъект.Контрагент.РЭЙ_НаименованиеАнгл;
	Обл.Параметры.АдресПолучателяАнгл = СсылкаНаОбъект.Контрагент.РЭЙ_АдресАнгл;
	
	// 70: Назначение платежа
	Обл.Параметры.НазначениеПлатежаАнгл	= СсылкаНаОбъект.РЭЙ_НазначениеПлатежаАнгл;
	
	// 71: Расходы по переводу
	Если СсылкаНаОбъект.РЭЙ_РасходыПоПереводу = Перечисления.РЭЙ_РасходыПоВалютномуПереводу.Ben Тогда
		Обл.Параметры.Крестик_Beneficiary = "X";
		Обл.Параметры.НомерСчетаРасходовПлательщика = "";
	ИначеЕсли СсылкаНаОбъект.РЭЙ_РасходыПоПереводу = Перечисления.РЭЙ_РасходыПоВалютномуПереводу.OUR Тогда
		Обл.Параметры.Крестик_Our = "X";
		Обл.Параметры.НомерСчетаРасходовПлательщика = СсылкаНаОбъект.РЭЙ_СчетРасходовОрганизации.НомерСчета;
	ИначеЕсли СсылкаНаОбъект.РЭЙ_РасходыПоПереводу = Перечисления.РЭЙ_РасходыПоВалютномуПереводу.SHA Тогда
		Обл.Параметры.Крестик_Share = "X";
		Обл.Параметры.НомерСчетаРасходовПлательщика = СсылкаНаОбъект.РЭЙ_СчетРасходовОрганизации.НомерСчета;
	КонецЕсли;
	
	// 72: Дополнительная информация
	Обл.Параметры.ДополнительнаяИнформация = СсылкаНаОбъект.РЭЙ_ДополнительнаяИнформация;
	
	// Информация для валютного контроля
	Обл.Параметры.УполномоченныйПоПереводу = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(СсылкаНаОбъект.РЭЙ_УполномоченныйПоПереводу, СсылкаНаОбъект.Дата);
	Обл.Параметры.ТелефонУполномоченногоПоПереводу = РЭЙ_СлужебныйСервер.ПолучитьСлужебныйТелефонФизЛица(СсылкаНаОбъект.РЭЙ_УполномоченныйПоПереводу);
	Обл.Параметры.КонтрактВЭД = СсылкаНаОбъект.РЭЙ_КонтрактВЭД.Наименование;
	Обл.Параметры.ПаспортСделки = СсылкаНаОбъект.РЭЙ_КонтрактВЭД.ПаспортСделки.НомерПаспортаСделки;
	Обл.Параметры.ДатаПостановкиНаУчет = Формат(СсылкаНаОбъект.РЭЙ_КонтрактВЭД.ПаспортСделки.ДатаПаспортаСделки, "ДФ=dd.MM.yyyy");
	Обл.Параметры.НомерГТД = СсылкаНаОбъект.РЭЙ_НомерГТД;
	Обл.Параметры.ПризнакПредставленияДокументов = Сред(СсылкаНаОбъект.РЭЙ_ПризнакПредставленияДокументов, 1, 1);
	Обл.Параметры.КодВидаВалютнойОперации = СсылкаНаОбъект.РЭЙ_ВидВалютнойОперации.Код;
	Обл.Параметры.ДополнительнаяИнформацияВК = СсылкаНаОбъект.РЭЙ_ДополнительнаяИнформацияВК;

	Обл.Параметры.ФИОРуководителя = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(СсылкаНаОбъект.РЭЙ_Руководитель, СсылкаНаОбъект.Дата);
	Обл.Параметры.ФИОБухгалтера = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(СсылкаНаОбъект.РЭЙ_ГлавныйБухгалтер, СсылкаНаОбъект.Дата);
	
	ТабДокумент.Вывести(Обл);
	
	Возврат ТабДокумент;
	
КонецФункции

#КонецЕсли
