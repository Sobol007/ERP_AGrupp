#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.СторнированиеНачислений - Ссылка на документ
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный)
//  Отказ 			- Булево - Признак отказа от выполнения проведения
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам
//  Движения 		- Коллекция движений документа - Передается только при вызове из обработки проведения документа
//  Объект			- ДокументОбъект.СторнированиеНачислений - Передается только при вызове из обработки проведения документа
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеРасширенныйСервер.СтруктураВидовУчета();
	ПроведениеРасширенныйСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	СторнируемыйДокумент = ?(ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("СторнируемыйДокумент"), 
		ДополнительныеПараметры.СторнируемыйДокумент, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "СторнируемыйДокумент"));
	
	РеквизитыДляПроведения = РеквизитыДляПроведения(ДокументСсылка, СторнируемыйДокумент);
	ДанныеДляПроведения = ДанныеДляПроведения(РеквизитыДляПроведения, СтруктураВидовУчета);
	
	РезультатИсправления = Неопределено;
	ИсправлениеДокументовЗарплатаКадры.ПриПроведенииИсправления(ДокументСсылка, Движения, РежимПроведения, Отказ, РеквизитыДляПроведения, СтруктураВидовУчета, Объект,, РезультатИсправления);
	
	Если Не РезультатИсправления.ИсправлениеВТекущемПериоде Тогда
		
		Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
			РасчетЗарплатыРасширенный.СформироватьДвиженияНачислений(Движения, Отказ, РеквизитыДляПроведения.Организация, КонецМесяца(РеквизитыДляПроведения.ПериодРегистрации), ДанныеДляПроведения.Начисления, ДанныеДляПроведения.ПоказателиНачислений, Истина);
		
			РасчетЗарплатыРасширенный.СформироватьДвиженияРаспределенияПоТерриториямУсловиямТруда(Движения, Отказ, РеквизитыДляПроведения.Ссылка, РеквизитыДляПроведения.РаспределениеПоТерриториямУсловиямТруда);
			РасчетЗарплатыРасширенный.СформироватьДвиженияРаспределенияРезультатовНачислений(Движения, Отказ, РеквизитыДляПроведения.Ссылка, РеквизитыДляПроведения.РаспределениеРезультатовНачислений);
			ПерерасчетЗарплаты.СформироватьДвиженияИсходныеДанныхПерерасчетов(Движения, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.Начисления);
			
			// Заполним описание данных для проведения в учете начисленной зарплаты.
			ДанныеДляПроведенияУчетЗарплаты = ОтражениеЗарплатыВУчете.ОписаниеДанныеДляПроведения();
			ДанныеДляПроведенияУчетЗарплаты.Движения 				= Движения;
			ДанныеДляПроведенияУчетЗарплаты.Организация 			= РеквизитыДляПроведения.Организация;
			ДанныеДляПроведенияУчетЗарплаты.ПериодРегистрации 		= РеквизитыДляПроведения.ПериодРегистрации;
			ДанныеДляПроведенияУчетЗарплаты.ПорядокВыплаты 			= Перечисления.ХарактерВыплатыЗарплаты.Зарплата;
			ДанныеДляПроведенияУчетЗарплаты.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
			
			// - Регистрация начислений в учете начислений и удержаний.
			УчетНачисленнойЗарплаты.ЗарегистрироватьНачисления(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.НачисленияПоСотрудникам, Неопределено);
			УчетНачисленнойЗарплаты.ЗарегистрироватьОтработанноеВремя(ДанныеДляПроведенияУчетЗарплаты, Отказ, ДанныеДляПроведения.ОтработанноеВремяПоСотрудникам, Истина);
			
			// - Регистрация начислений в доходах для страховых взносов.
			УчетСтраховыхВзносов.СформироватьСведенияОДоходахСтраховыеВзносы(
				Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, Ложь, Истина, РеквизитыДляПроведения.Ссылка);
			
			// - Регистрация бухучета начислений и удержаний, выполняется до вызова регистрации доходов в учете НДФЛ.
			ОтражениеЗарплатыВБухучетеРасширенный.ЗарегистрироватьНачисленияУдержания(ДанныеДляПроведенияУчетЗарплаты, Отказ,
				ДанныеДляПроведения.НачисленияПоСотрудникам, ДанныеДляПроведения.УдержанияПоСотрудникам, ДанныеДляПроведения.НДФЛПоСотрудникам);
						
			// - Регистрация начислений в учете НДФЛ.
			УчетНДФЛРасширенный.СформироватьДоходыНДФЛПоНачислениям(Движения, Отказ, РеквизитыДляПроведения.Организация,
						КонецМесяца(РеквизитыДляПроведения.ПериодРегистрации), РеквизитыДляПроведения.ДатаВыплатыСторнированныхНачислений,
						ДанныеДляПроведения.МенеджерВременныхТаблиц, РеквизитыДляПроведения.ПериодРегистрацииСторнируемого, , Истина, , РеквизитыДляПроведения.Ссылка);
						
			// Дополняем доходы НДФЛ сведениями о распределении по статьям финансирования и/или статьям расходов.
			ОтражениеЗарплатыВУчетеРасширенный.ДополнитьСведенияОДоходахНДФЛСведениямиОРаспределенииПоСтатьям(Движения);
			
			// Подготовка данных для регистрации удержаний, НДФЛ и Корректировок выплаты в учете начисленной зарплаты.
			УчетНачисленнойЗарплатыРасширенный.СоздатьВТРаспределениеНачисленийТекущегоДокумента(ДанныеДляПроведенияУчетЗарплаты);
			
			// - Регистрация удержаний в учете начисленной зарплаты.
			УчетНачисленнойЗарплатыРасширенный.ЗарегистрироватьУдержанияИЗаймы(ДанныеДляПроведенияУчетЗарплаты, Отказ,
				ДанныеДляПроведения.УдержанияПоСотрудникам, Неопределено, Неопределено);
			
			УчетСтраховыхВзносов.СформироватьПособия(Движения, Отказ, РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.Пособия, ДанныеДляПроведения.ПособияПоУходу);
			
			ПараметрыДвиженийОтпусков = ОстаткиОтпусков.ПараметрыДляСформироватьДвиженияФактическихОтпусков();
			ПараметрыДвиженийОтпусков.ДатаРегистрации = РеквизитыДляПроведения.Дата;
			ПараметрыДвиженийОтпусков.Начисления = ДанныеДляПроведения.Начисления;
			ПараметрыДвиженийОтпусков.ПериодНачисления = РеквизитыДляПроведения.ПериодРегистрации;
			ОстаткиОтпусков.СформироватьДвиженияФактическихОтпусков(Движения, Отказ, ПараметрыДвиженийОтпусков);
			
		КонецЕсли;
		
		Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
			// Учет среднего заработка
			УчетСреднегоЗаработка.ЗарегистрироватьДанныеСреднегоЗаработка(Движения, Отказ, ДанныеДляПроведения.НачисленияДляСреднегоЗаработка);
		КонецЕсли;
		
		// Предотвращение повторного сторнирования движений в учетах.
		ИзолироватьСторнированныеДвижения(Движения, ДанныеДляПроведения);
		
	КонецЕсли;
	
	ПараметрыСторнирования = ИсправлениеДокументовЗарплатаКадры.ПараметрыСторнирования();
	ПараметрыСторнирования.ИсправлениеВТекущемПериоде = РезультатИсправления.ИсправлениеВТекущемПериоде;
	ПараметрыСторнирования.ОтменаДокумента = Истина;
	ПараметрыСторнирования.ПериодРегистрации = РеквизитыДляПроведения.ПериодРегистрации;
	ПараметрыСторнирования.РеквизитыДляПроведения = РеквизитыДляПроведения;
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СторнируемыйДокумент.Метаданные().ПолноеИмя());
	ИсправленныйДокументСторнирован = МенеджерДокумента.СторнироватьПоУчетам(
		Движения, ДокументСсылка, СторнируемыйДокумент, СтруктураВидовУчета, ПараметрыСторнирования);
	
	Если ИсправленныйДокументСторнирован <> Истина Тогда
		ИсправлениеДокументовЗарплатаКадры.СторнироватьДвиженияПоВсемУчетам(
			Движения, СторнируемыйДокумент, ПараметрыСторнирования, СтруктураВидовУчета);
	КонецЕсли;
	
	ПроведениеРасширенныйСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ТипыДокументовСторнируемыхИнтерактивно() Экспорт
	
	ВычитаемыеТипы = Новый Массив;
	ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.ПереносДанных"));
	ВычитаемыеТипы.Добавить(Тип("ДокументСсылка.НачислениеЗарплаты"));
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		ВычитаемыеТипы,
		УчетФактическиПолученныхДоходов.РегистраторыУвольнения());
	
	Возврат
		Новый ОписаниеТипов(ИсправлениеДокументовРасчетЗарплаты.ОписаниеТиповСторнируемогоДокумента(),, ВычитаемыеТипы);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

Функция ДанныеДляПроведения(РеквизитыДляПроведения, СтруктураВидовУчета) 
	
	ДанныеДляПроведения = РасчетЗарплаты.СоздатьДанныеДляПроведенияНачисленияЗарплаты();
	
	Если СтруктураВидовУчета.ОстальныеВидыУчета Тогда
		
		РасчетЗарплатыРасширенный.ЗаполнитьНачисления(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Сторнировано,ДоначисленияИПерерасчеты", "Ссылка.ПериодРегистрации");
		РасчетЗарплатыРасширенный.ЗаполнитьСписокФизическихЛиц(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, "Сторнировано");
		
		ОтражениеЗарплатыВБухучете.СоздатьВТНачисленияСДаннымиЕНВД(РеквизитыДляПроведения.Организация, РеквизитыДляПроведения.ПериодРегистрации, ДанныеДляПроведения.МенеджерВременныхТаблиц, ДанныеДляПроведения.НачисленияПоСотрудникам);
		
		ЗаполнитьСведенияОПособиях(РеквизитыДляПроведения, ДанныеДляПроведения);
		
	КонецЕсли;
	
	Если СтруктураВидовУчета.ДанныеДляРасчетаСреднего Тогда
		ДополнительныеПараметры = УчетСреднегоЗаработка.ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка("Сторнировано,ДоначисленияИПерерасчеты");
		ДополнительныеПараметры.МесяцНачисления = "Ссылка.ПериодРегистрации";
		УчетСреднегоЗаработка.ЗаполнитьТаблицыДляРегистрацииДанныхСреднегоЗаработка(ДанныеДляПроведения, РеквизитыДляПроведения.Ссылка, ДополнительныеПараметры);
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

Процедура ЗаполнитьСведенияОПособиях(РеквизитыДляПроведения, ДанныеДляПроведения)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДанныеДляПроведения.МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&СторнируемыйДокумент КАК Ссылка,
	|	ТаблицаНачислений.Начисление,
	|	ТаблицаНачислений.Сотрудник,
	|	ТаблицаНачислений.ВидЗанятости,
	|	ТаблицаНачислений.Сторно,
	|	ТаблицаНачислений.ОплаченоДней,
	|	ТаблицаНачислений.Результат,
	|	ТаблицаНачислений.РезультатВТомЧислеЗаСчетФБ
	|ПОМЕСТИТЬ ВТНачисленияДляУчетаПособий
	|ИЗ
	|	Документ.СторнированиеНачислений.Сторнировано КАК ТаблицаНачислений
	|ГДЕ
	|	ТаблицаНачислений.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", РеквизитыДляПроведения.Ссылка);
	Запрос.УстановитьПараметр("СторнируемыйДокумент", РеквизитыДляПроведения.СторнируемыйДокумент);
	
	Запрос.Выполнить();
	
	МетаданныеСторнируемогоДокумента = РеквизитыДляПроведения.СторнируемыйДокумент.Метаданные();
	ИмяПериодРегистрации = "ПериодРегистрации";
	Если МетаданныеСторнируемогоДокумента.Реквизиты.Найти(ИмяПериодРегистрации) = Неопределено Тогда
		ИмяПериодРегистрации = "МесяцНачисления";
	КонецЕсли;
	
	РеквизитыСторнируемогоДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыДляПроведения.СторнируемыйДокумент, "Организация," + ИмяПериодРегистрации);
	
	ПособиеПлатитУчастникПилотногоПроекта = ПрямыеВыплатыПособийСоциальногоСтрахования.ПособиеПлатитУчастникПилотногоПроекта(РеквизитыСторнируемогоДокумента.Организация, РеквизитыСторнируемогоДокумента[ИмяПериодРегистрации]);
	
	УчетПособийСоциальногоСтрахованияРасширенный.ЗаполнитьСведенияОПособиях(ДанныеДляПроведения, ПособиеПлатитУчастникПилотногоПроекта);
	
КонецПроцедуры

Функция РеквизитыДляПроведения(ДокументСсылка, СторнируемыйДокумент)
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СторнированиеНачислений.Ссылка КАК Ссылка,
	|	СторнированиеНачислений.Организация КАК Организация,
	|	СторнированиеНачислений.ПериодРегистрации КАК ПериодРегистрации,
	|	СторнированиеНачислений.Дата КАК Дата,
	|	СторнированиеНачислений.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	СторнированиеНачислений.СторнируемыйДокумент.#ИмяПериодРегистрации КАК ПериодРегистрацииСторнируемого,
	|	ВЫРАЗИТЬ(СторнированиеНачислений.СторнируемыйДокумент КАК Документ.Отпуск).Дата КАК ДатаВыплатыСторнированныхНачислений
	|ИЗ
	|	Документ.СторнированиеНачислений КАК СторнированиеНачислений
	|ГДЕ
	|	СторнированиеНачислений.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.НомерСтроки КАК НомерСтроки,
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.Территория КАК Территория,
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.УсловияТруда КАК УсловияТруда,
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.ДоляРаспределения КАК ДоляРаспределения,
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.Результат КАК Результат,
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.ИдентификаторСтрокиПоказателей КАК ИдентификаторСтрокиПоказателей
	|ИЗ
	|	Документ.СторнированиеНачислений.РаспределениеПоТерриториямУсловиямТруда КАК СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда
	|ГДЕ
	|	СторнированиеНачисленийРаспределениеПоТерриториямУсловиямТруда.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.Территория КАК Территория,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.СтатьяРасходов КАК СтатьяРасходов,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.СпособОтраженияЗарплатыВБухучете КАК СпособОтраженияЗарплатыВБухучете,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.ОблагаетсяЕНВД КАК ОблагаетсяЕНВД,
	|	СУММА(СторнированиеНачисленийРаспределениеРезультатовНачислений.Результат) КАК Результат,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.ПодразделениеУчетаЗатрат КАК ПодразделениеУчетаЗатрат
	|ИЗ
	|	Документ.СторнированиеНачислений.РаспределениеРезультатовНачислений КАК СторнированиеНачисленийРаспределениеРезультатовНачислений
	|ГДЕ
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.ПодразделениеУчетаЗатрат,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.СпособОтраженияЗарплатыВБухучете,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.СтатьяФинансирования,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.ОблагаетсяЕНВД,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.Территория,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.СтатьяРасходов,
	|	СторнированиеНачисленийРаспределениеРезультатовНачислений.ИдентификаторСтроки";
	
	МетаданныеСторнируемогоДокумента = СторнируемыйДокумент.Метаданные();
	Если МетаданныеСторнируемогоДокумента.Реквизиты.Найти("ПланируемаяДатаВыплаты") <> НеОпределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ").Дата", ").ПланируемаяДатаВыплаты");
	ИначеЕсли МетаданныеСторнируемогоДокумента.Реквизиты.Найти("ПериодРегистрации") <> НеОпределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫРАЗИТЬ(", "КОНЕЦПЕРИОДА(ВЫРАЗИТЬ(");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ").Дата", ").ПериодРегистрации, МЕСЯЦ)");
	ИначеЕсли МетаданныеСторнируемогоДокумента.Реквизиты.Найти("МесяцНачисления") <> НеОпределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫРАЗИТЬ(", "КОНЕЦПЕРИОДА(ВЫРАЗИТЬ(");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ").Дата", ").МесяцНачисления, МЕСЯЦ)");
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, " КАК Документ.Отпуск", " КАК " + МетаданныеСторнируемогоДокумента.ПолноеИмя());
	
	ИмяПериодРегистрации = "ПериодРегистрации";
	Если МетаданныеСторнируемогоДокумента.Реквизиты.Найти(ИмяПериодРегистрации) = Неопределено Тогда
		ИмяПериодРегистрации = "МесяцНачисления";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяПериодРегистрации", ИмяПериодРегистрации);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	РеквизитыДляПроведения = РеквизитыДляПроведенияПустаяСтруктура();
	
	ВыборкаРеквизиты = Результаты[0].Выбрать();
	
	Пока ВыборкаРеквизиты.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(РеквизитыДляПроведения, ВыборкаРеквизиты);
		
	КонецЦикла;
	
	РаспределениеПоТерриториямУсловиямТруда = Результаты[1].Выгрузить();
	РаспределениеРезультатовНачислений = Результаты[2].Выгрузить();
	РеквизитыДляПроведения.РаспределениеПоТерриториямУсловиямТруда = РаспределениеПоТерриториямУсловиямТруда;
	РеквизитыДляПроведения.РаспределениеРезультатовНачислений = РаспределениеРезультатовНачислений;
	
	Возврат РеквизитыДляПроведения;
	
КонецФункции

Функция РеквизитыДляПроведенияПустаяСтруктура()
	
	РеквизитыДляПроведенияПустаяСтруктура = Новый Структура(
		"Ссылка,
		|Организация,
		|ПериодРегистрации,
		|Дата,
		|СторнируемыйДокумент,
		|ПериодРегистрацииСторнируемого,
		|ДатаВыплатыСторнированныхНачислений,
		|РаспределениеПоТерриториямУсловиямТруда,
		|РаспределениеРезультатовНачислений");
	
	Возврат РеквизитыДляПроведенияПустаяСтруктура;
	
КонецФункции

Функция ОписаниеСторнируемогоДокумента(СторнируемыйДокумент) Экспорт
	
	Описание = Новый Структура();
	Описание.Вставить("МесяцНачисленияИмя", "МесяцНачисления");
	
	МетаданныеСторнируемогоДокумента = СторнируемыйДокумент.Метаданные();
	Если МетаданныеСторнируемогоДокумента.Реквизиты.Найти(Описание.МесяцНачисленияИмя) = Неопределено Тогда
		Описание.МесяцНачисленияИмя = "ПериодРегистрации";
	КонецЕсли;
	
	Возврат Описание;
	
КонецФункции

Процедура ЗаполнитьИсходныеДанныеПерерасчетов(ПараметрыОбновления) Экспорт

	ПараметрыЗаполнения = ПерерасчетЗарплаты.ПараметрыЗаполненияИсходныхДанныхПерерасчетов();
	ПараметрыЗаполнения.ТаблицыНачислений = "Сторнировано,ДоначисленияИПерерасчеты";
	ПерерасчетЗарплаты.ЗаполнитьИсходныеДанныеПерерасчетов(ПараметрыОбновления, Метаданные.Документы.СторнированиеНачислений, ПараметрыЗаполнения);

КонецПроцедуры

Процедура ИзолироватьСторнированныеДвижения(Движения, ДанныеДляПроведения)
	
	ДвиженияВСтруктуре = ТипЗнч(Движения) = Тип("Структура");
	
	// Не пустые наборы не изолированные учетами, могут содержать сторно записи сформированные учетами не умеющими
	// изолировать наборы. Изолируем наборы здесь, что бы в них не попали дублирующие сторно строки при сторнировании
	// по учетам в исправленном документе.
	Для Каждого НаборЗаписей Из Движения Цикл
		Набор = ?(ДвиженияВСтруктуре, НаборЗаписей.Значение, НаборЗаписей);
		Если Не Набор.ДополнительныеСвойства.Свойство("ИзолированоУчетом") И Набор.Количество() > 0 Тогда
			ИсправлениеДокументовЗарплатаКадры.ИзолироватьУчетом(Набор, "<ВнеУчета>");
		КонецЕсли;
	КонецЦикла;
	
	// Некоторые учеты при формировании движений сворачивают данные для проведения,
	// при этом если результат равен 0, то новые движения не формируются и наборы остаются пустыми.
	// Дополнительно изолируем наборы, которые могли оказаться пустыми из-за того что для их формирования
	// использовались данные уже содержащие сторно записи.
	Если ДанныеДляПроведения.НачисленияПоСотрудникам.Колонки.Найти("Сторно") <> Неопределено
		И ДанныеДляПроведения.НачисленияПоСотрудникам.Найти(Истина, "Сторно") <> Неопределено Тогда
			
		МетаданныеРегистров = ВзаиморасчетыССотрудникамиРасширенный.МетаданныеРегистровПодсистемы();
		
		НаборЗаписей = Неопределено;
		Для Каждого МетаданныеРегистра Из МетаданныеРегистров Цикл
			Если ДвиженияВСтруктуре Тогда
				Движения.Свойство(МетаданныеРегистра.Имя, НаборЗаписей);
			Иначе
				НаборЗаписей = Движения.Найти(МетаданныеРегистра.Имя);
			КонецЕсли;
			Если НаборЗаписей <> Неопределено Тогда
				ИсправлениеДокументовЗарплатаКадры.ИзолироватьУчетом(НаборЗаписей, "<ВнеУчета>");	
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли