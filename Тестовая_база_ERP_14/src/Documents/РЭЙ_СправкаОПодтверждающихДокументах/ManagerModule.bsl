#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаОПодтверждающихДокументах_2018") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"СправкаОПодтверждающихДокументах_2018",
		"Справка о подтверждающих документах",
		ПолучитьТабДок_2018(МассивОбъектов));
	КонецЕсли;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаОПодтверждающихДокументах") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"СправкаОПодтверждающихДокументах",
		"Справка о подтверждающих документах (до 01.03.2018)",
		ПолучитьТабДок_2015(МассивОбъектов));
	КонецЕсли;
КонецПроцедуры	
	
Функция ПолучитьТабДок_2018(МассивОбъектов)
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Ссылка Из МассивОбъектов Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		ОбъектСсылки = Ссылка.ПолучитьОбъект();	
		ТабличныйДокумент.Вывести(ОбъектСсылки.ПечатьСПД_2018());
	КонецЦикла;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции	
	
Функция ПолучитьТабДок_2015(МассивОбъектов)
	ТабличныйДокумент	= Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Ссылка Из МассивОбъектов Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		ОбъектСсылки = Ссылка.ПолучитьОбъект();	
		
		Если Ссылка.Дата >= Дата(2015,12,28) Тогда
			ТабличныйДокумент.Вывести(ОбъектСсылки.ПечатьСПД_2015());
		Иначе
			ТабличныйДокумент.Вывести(ПечатьСПД_2012(Ссылка));
		КонецЕсли;
	КонецЦикла;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции	

Функция ПечатьСПД_2012(СсылкаНаОбъект)
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОПодтверждающихДокументах";
	Макет = Документы.РЭЙ_СправкаОПодтверждающихДокументах.ПолучитьМакет("СправкаОПодтверждающихДокументах");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СправкаОПодтверждающихДокументах.ПаспортСделки.НомерПаспортаСделки КАК НомерПаспортаСделки,
	|	СправкаОПодтверждающихДокументах.Контрагент КАК Контрагент,
	|	СправкаОПодтверждающихДокументах.КонтрактВЭД КАК КонтрактВЭД,
	|	СправкаОПодтверждающихДокументах.Банк КАК Банк,
	|	СправкаОПодтверждающихДокументах.Организация,
	|	СправкаОПодтверждающихДокументах.Дата,
	|	СправкаОПодтверждающихДокументах.Руководитель,
	|	СправкаОПодтверждающихДокументах.ГлавныйБухгалтер,
	|	СправкаОПодтверждающихДокументах.РучноеЗаполнение,
	|	СправкаОПодтверждающихДокументах.Корректировка,
	|	СправкаОПодтверждающихДокументах.Основание
	|ИЗ
	|	Документ.РЭЙ_СправкаОПодтверждающихДокументах КАК СправкаОПодтверждающихДокументах
	|ГДЕ
	|	СправкаОПодтверждающихДокументах.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);

	Результат = Запрос.Выполнить();
	ДанныеДляПечатиШапки = Результат.Выбрать();
	ДанныеДляПечатиШапки.Следующий();
	
	// рк+ 26.02.09
	Если НЕ ЗначениеЗаполнено(ДанныеДляПечатиШапки.Банк.РЭЙ_НаименованиеПолное) Тогда
	   	 Сообщить("Печать справки невозможна, т.к. не заполнено полное наименование уполномоченного банка "+ДанныеДляПечатиШапки.Банк);
		 Возврат Неопределено;
	КонецЕсли; 
	//рк- 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.НомерСтрокиДляПечати КАК НомерСтрокиДляПечати,
	|	ТЧ.НомерСтроки КАК НомерСтроки,
	|	ТЧ.СуммаВВалютеКонтракта КАК СуммаВВалютеКонтракта,
	|	ТЧ.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|	ТЧ.СуммаАвансаВВалютеКонтракта КАК СуммаАвансаВВалютеКонтракта,
	|	ТЧ.СуммаАвансаВВалютеДокумента КАК СуммаАвансаВВалютеДокумента
	|ИЗ
	|	Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПодтверждающиеДокументы КАК ТЧ
	|ГДЕ
	|	ТЧ.Ссылка = &Ссылка
	|	И (ТЧ.Ссылка.Первичное
	|			ИЛИ ТЧ.Ссылка.Корректировка
	|				И ТЧ.Корректировка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДляПечати,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(СуммаВВалютеКонтракта),
	|	СУММА(СуммаВВалютеДокумента),
	|	СУММА(СуммаАвансаВВалютеКонтракта),
	|	СУММА(СуммаАвансаВВалютеДокумента)
	|ПО
	|	НомерСтрокиДляПечати";
	
	Если СсылкаНаОбъект.РучноеЗаполнение Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПодтверждающиеДокументы", "Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПодтверждающиеДокументыРучные");
	КонецЕсли;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);

	Результат = Запрос.Выполнить();
	ВыборкаИтоги = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	
	//+ШАПКА1
	Область = Макет.ПолучитьОбласть("Шапка1");
	Область.Параметры.Банк = ДанныеДляПечатиШапки.Банк.РЭЙ_НаименованиеПолное;
	Если ЗначениеЗаполнено(ДанныеДляПечатиШапки.Организация) Тогда
		Область.Параметры.ОрганизацияНаименование = ?(ПустаяСтрока(ДанныеДляПечатиШапки.Организация.НаименованиеПолное),?(ПустаяСтрока(ДанныеДляПечатиШапки.Организация.НаименованиеПолное),ДанныеДляПечатиШапки.Организация.Наименование,ДанныеДляПечатиШапки.Организация.НаименованиеПолное),ДанныеДляПечатиШапки.Организация.НаименованиеПолное);
	КонецЕсли;
	
	Если ДанныеДляПечатиШапки.Корректировка и ЗначениеЗаполнено(ДанныеДляПечатиШапки.Основание) Тогда
		Область.Параметры.Дата = Формат(ДанныеДляПечатиШапки.Основание.Дата, "ДФ=dd.MM.yyyy");
	Иначе
		Область.Параметры.Дата = Формат(ДанныеДляПечатиШапки.Дата, "ДФ=dd.MM.yyyy");
	КонецЕсли; 
	
	//вывод номера паспорта сделки
	Для счетчик = 1 По 22 Цикл
		Если (счетчик = 9) или (счетчик = 14) или (счетчик = 19) или (счетчик = 21) Тогда
			//ОбластьЛист1.Параметры["НПС_"+счетчик] = "/";
		Иначе 
			Область.Параметры["НПС_"+счетчик] = Сред(ДанныеДляПечатиШапки.НомерПаспортаСделки, счетчик ,1);
		КонецЕсли; 
	КонецЦикла; 
	
	Если ДанныеДляПечатиШапки.Корректировка Тогда
		Область.Параметры.Корректировка = "*";	
	КонецЕсли;
	
	ТабДокумент.Вывести(Область);
	//-ШАПКА1
	
	ТабличныйДокументПримечания = Новый ТабличныйДокумент;
	
	//+СТРОКА1
	Если ВыборкаИтоги.Количество() = 0 Тогда
		Область = Макет.ПолучитьОбласть("Строка1");
		ТабДокумент.Вывести(Область);
	Иначе
		Область = Макет.ПолучитьОбласть("Строка1");

		Пока ВыборкаИтоги.Следующий() Цикл
			ВыборкаСтроки = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВыборкаСтроки.Следующий();
			
			Если Не СсылкаНаОбъект.РучноеЗаполнение Тогда
				ИмяТЧ = "ПодтверждающиеДокументы";
				СтрокаТЧ = СсылкаНаОбъект[ИмяТЧ][ВыборкаСтроки.НомерСтроки - 1];
				текДокумент = СтрокаТЧ.Документ;
			Иначе
				ИмяТЧ = "ПодтверждающиеДокументыРучные";
				СтрокаТЧ = СсылкаНаОбъект[ИмяТЧ][ВыборкаСтроки.НомерСтроки - 1];
				текДокумент = Неопределено;
			КонецЕсли;
			
			Область.Параметры.НомерСтроки = ВыборкаИтоги.НомерСтрокиДляПечати;
			Область.Параметры.ДокументНомер = РЭЙ_СлужебныйСервер.ПолучитьНомерПодтверждающегоДокумента(СтрокаТЧ, СсылкаНаОбъект.РучноеЗаполнение);
			Если СсылкаНаОбъект.Дата >= Дата("20131001") И ЗначениеЗаполнено(СтрокаТЧ.НомерГТД) Тогда
				Область.Параметры.Дата = "";
			Иначе
				Область.Параметры.Дата = Формат(РЭЙ_СлужебныйСервер.ПолучитьДатуПодтверждающегоДокумента(СтрокаТЧ, СсылкаНаОбъект.РучноеЗаполнение), "ДФ=dd.MM.yyyy");
			КонецЕсли;
			Область.Параметры.ВидДокумента = СтрокаТЧ.ВидДокумента.Код; 
			Область.Параметры.КодВалДок = СтрокаТЧ.ВалютаДокумента.Код;
			Если ВыборкаИтоги.СуммаАвансаВВалютеДокумента = 0 Тогда
				Область.Параметры.СуммаВалДок = Формат(ВыборкаИтоги.СуммаВВалютеДокумента, "ЧДЦ=2");
				Область.Параметры.СуммаКонтракт = Формат(ВыборкаИтоги.СуммаВВалютеКонтракта, "ЧДЦ=2");
			Иначе
				Если ЗначениеЗаполнено(СтрокаТЧ.НомерГТД) Тогда
					ПоляПоиска = "НомерГТД"; 
				Иначе
					Если Не СсылкаНаОбъект.РучноеЗаполнение Тогда
						ПоляПоиска = "Документ";
					Иначе
						Если ЗначениеЗаполнено(СтрокаТЧ.НомерНакладной) Тогда
							ПоляПоиска = "НомерНакладной";
						Иначе
							ПоляПоиска = "";
							СуммаДокументаВВалютеДокумента = 0;
							СуммаДокументаВВалютеКонтракта = 0;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				Если Не ПустаяСтрока(ПоляПоиска) Тогда
					СтруктураПоиска = Новый Структура(ПоляПоиска);
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТЧ);
					СтрокиДляСуммирования = СсылкаНаОбъект[ИмяТЧ].НайтиСтроки(СтруктураПоиска);
					ТаблицаДляСуммирования = СсылкаНаОбъект[ИмяТЧ].Выгрузить(СтрокиДляСуммирования);
					СуммаДокументаВВалютеДокумента = ТаблицаДляСуммирования.Итог("СуммаВВалютеДокумента");
					СуммаДокументаВВалютеКонтракта = ТаблицаДляСуммирования.Итог("СуммаВВалютеКонтракта");
				КонецЕсли;
				Если СуммаДокументаВВалютеДокумента = ВыборкаИтоги.СуммаАвансаВВалютеДокумента Тогда
					Область.Параметры.СуммаВалДок = Формат(ВыборкаИтоги.СуммаАвансаВВалютеДокумента, "ЧДЦ=2");
				Иначе	
					Область.Параметры.СуммаВалДок = Формат(СуммаДокументаВВалютеДокумента, "ЧДЦ=2") + ?(ВыборкаИтоги.СуммаАвансаВВалютеДокумента = 0, "", "/" + Формат(ВыборкаИтоги.СуммаАвансаВВалютеДокумента, "ЧДЦ=2"));
				КонецЕсли;
				Если СуммаДокументаВВалютеКонтракта = ВыборкаИтоги.СуммаАвансаВВалютеКонтракта Тогда
					Область.Параметры.СуммаКонтракт = Формат(ВыборкаИтоги.СуммаАвансаВВалютеКонтракта, "ЧДЦ=2");
				Иначе	
					Область.Параметры.СуммаКонтракт = Формат(СуммаДокументаВВалютеКонтракта, "ЧДЦ=2") + ?(ВыборкаИтоги.СуммаАвансаВВалютеДокумента = 0, "", "/" + Формат(ВыборкаИтоги.СуммаАвансаВВалютеКонтракта, "ЧДЦ=2"));
				КонецЕсли;
			КонецЕсли;
			Область.Параметры.КодВалКонтракт = СтрокаТЧ.ВалютаКонтракта.Код;
			Область.Параметры.ПризнакПоставки = Сред("" + СтрокаТЧ.ПризнакПоставки, 1, 1);
			Область.Параметры.ОжидаемыйСрок = СтрокаТЧ.ОжидаемыйСрок;
			Область.Параметры.КодСтраныГрузоотправителя = СтрокаТЧ.СтранаГрузоотправителяГрузополучателя.Код;
			ТабДокумент.Вывести(Область);
			
			//С 01.10.2013, если в качестве номера подтверждающего документа указывается номер ГТД (случай товарного контракта), то при выводе на печать не надо заполнять Графу 3 (Дата подтверждающего документа) 
			Если СсылкаНаОбъект.Дата >= Дата("20131001") Тогда
				Если ЗначениеЗаполнено(СтрокаТЧ.НомерГТД) Тогда
					Область.Параметры.Дата = "";
				КонецЕсли;
			КонецЕсли;
			
			ВыборкаСтроки.Сбросить();
			ТекстПримечания = "";
			Пока ВыборкаСтроки.Следующий() Цикл
				СтрокаТЧ_Примечание = СсылкаНаОбъект[ИмяТЧ][ВыборкаСтроки.НомерСтроки - 1];
				Если Не ПустаяСтрока(СтрокаТЧ_Примечание.Примечание) Тогда
					ТекстПримечания = ТекстПримечания + ?(ТекстПримечания = "", "", "; ") + СокрЛП(СтрокаТЧ_Примечание.Примечание);
				КонецЕсли;
			КонецЦикла;
			Если Не ПустаяСтрока(ТекстПримечания) Тогда
				ОбластьПримечание = Макет.ПолучитьОбласть("Строка2");
				ОбластьПримечание.Параметры.НомерСтроки = ВыборкаИтоги.НомерСтрокиДляПечати;
				ОбластьПримечание.Параметры.Примечание = ТекстПримечания;
				ТабличныйДокументПримечания.Вывести(ОбластьПримечание);
			КонецЕсли;

			//------------------------------------------
			Если Не СсылкаНаОбъект.РучноеЗаполнение Тогда
				Если ЗначениеЗаполнено(ТекДокумент) Тогда
					Если СсылкаНаОбъект.Банк.РЭЙ_ВидПредоставленияДокументовПоСделке = 1 Тогда
						РЭЙ_ЗадачиСервер.УстановитьСтатусЗадачиВЭД(ТекДокумент, "Распечатана");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//-СТРОКА1
	
	
	//+ШАПКА2
	Область = Макет.ПолучитьОбласть("Шапка2");
	ТабДокумент.Вывести(Область);
	//-ШАПКА2
	
	//+СТРОКА2
	Если ВыборкаИтоги.Количество() = 0 Тогда
		Область = Макет.ПолучитьОбласть("Строка2");
		ТабДокумент.Вывести(Область);
	Иначе
		ТабДокумент.Вывести(ТабличныйДокументПримечания);
	КонецЕсли;
	//-СТРОКА2
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.ФИОРуководителя = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(ДанныеДляПечатиШапки.Руководитель, ДанныеДляПечатиШапки.Дата);
	Область.Параметры.ФИОБухгалтера   = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(ДанныеДляПечатиШапки.ГлавныйБухгалтер, ДанныеДляПечатиШапки.Дата);
	ТабДокумент.Вывести(Область);
	Возврат ТабДокумент;
	

КонецФункции // ПечатьСправкиОПодтверждающихДокументах2012()

Процедура ПолучитьСтруктуруПараметров(СсылкаНаДокумент, ИмяМакета, СтруктураПараметров) Экспорт
	СтруктураПараметров = СсылкаНаДокумент.ПолучитьОбъект().ПолучитьДанныеДляПечати();
КонецПроцедуры

#КонецЕсли

#Область ПроцедурыИФункцииПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "РЭЙ_СлужебныйКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РЭЙ_СправкаОПодтверждающихДокументах";
	КомандаПечати.Идентификатор = "СправкаОПодтверждающихДокументах_2018";
	КомандаПечати.Представление = НСтр("ru = 'Справка о подтверждающих документах'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "РЭЙ_СлужебныйКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РЭЙ_СправкаОПодтверждающихДокументах";
	КомандаПечати.Идентификатор = "СправкаОПодтверждающихДокументах";
	КомандаПечати.Представление = НСтр("ru = 'Справка о подтверждающих документах (до 01.03.2018)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 20;
КонецПроцедуры

#КонецОбласти
