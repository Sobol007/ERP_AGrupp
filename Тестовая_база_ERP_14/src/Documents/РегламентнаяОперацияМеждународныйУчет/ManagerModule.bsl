#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Регламентная операция (международный учет)".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РегламентнаяОперацияМеждународныйУчет);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьМеждународныйФинансовыйУчет";
		
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

// Определяет количество всех валютных остатков для которых не расчитаны курсовые разницы.
// (ОстатокВВалюте*КурсФункциональнойВалюты <> ОстатокВфункциональнойВалюте).
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация по которой определяются валютные остатки, если незадана, то
//                                               все.
//  НаДату - Дата - дата на которую определяются остатки и курс функциональной валюты, если незадана, то текущая.
//  ТолькоПервую - Булево - Истина, если нужно проверить только факт отсутствия курсовых разниц.
//
// Возвращаемое значение:
//  Число - количество валютных остатков у которых есть расхождения между расчетными и реальными остатками в
//          функциональной валюте.
//
Функция НезакрытыеКурсовыеРазницы(Организация = Неопределено, Знач НаДату = Неопределено, ТолькоПервую = Ложь) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КурсВалютыФункциональной.Валюта КАК ВалютаФункциональная,
	|	КурсВалютыФункциональной.Курс КАК КурсФВ,
	|	КурсВалютыФункциональной.Кратность КАК КратностьФВ,
	|	КурсВалютыПредставления.Валюта КАК ВалютаПредставления,
	|	КурсВалютыПредставления.Курс КАК КурсВП,
	|	КурсВалютыПредставления.Кратность КАК КратностьВП
	|ПОМЕСТИТЬ втВалюты
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&НаДату,
	|			Валюта В
	|				(ВЫБРАТЬ
	|					ВалютаФункциональная.Значение
	|				ИЗ
	|					Константа.ВалютаФункциональная КАК ВалютаФункциональная)) КАК КурсВалютыФункциональной,
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&НаДату,
	|			Валюта В
	|				(ВЫБРАТЬ
	|					ВалютаПредставления.Значение
	|				ИЗ
	|					Константа.ВалютаПредставления КАК ВалютаПредставления)) КАК КурсВалютыПредставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокКт) КАК СуммаОстатокКт,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет.Валютный И НЕ Счет.ИсключитьИзРасчетаКурсовыхРазниц,
	|			,
	|			Организация В (&Организация)) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОстаткиСчетов.Счет,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * КурсыВалют.Курс / КурсыВалют.Кратность / втВалюты.КурсФВ * втВалюты.КратностьФВ КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.Организация,
	|	ОстаткиСчетов.Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1,
	|	ОстаткиСчетов.Субконто2,
	|	ОстаткиСчетов.Субконто3
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&НаДату, ) КАК КурсыВалют
	|		ПО ОстаткиСчетов.Валюта = КурсыВалют.Валюта,
	|	втВалюты КАК втВалюты
	|ГДЕ
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.ВалютнаяСуммаОстаток * КурсыВалют.Курс / КурсыВалют.Кратность / втВалюты.КурсФВ * втВалюты.КратностьФВ КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаОстаток <> 0";
	
	Возврат ВсегоОсталосьКурсовыхРазниц(ТекстЗапроса, Организация, НаДату, ТолькоПервую);
	
	
КонецФункции

// Определяет количество всех остатков в функциональной валюте которые не пересчитаны в валюту представления.
// (ОстатокВФункциональнойВалюте*КоэффициентПересчетаВВалютуПредставления <> ОстатокВВалютеПредставления).
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация по которой определяются валютные остатки, если незадана, то
//                                               все.
//  НаДату - Дата - дата на которую определяются остатки и курс функциональной валюты, если незадана, то текущая.
//  ТолькоПервую - Булево - Истина, если нужно проверить только факт отсутствия курсовых разниц.
//
// Возвращаемое значение:
//  Число - количество остатков в функциональной валюте у которых есть расхождения между расчетными и реальными
//          остатками в валюте представления.
//
Функция НепересчитаноВВалютуПредставления(Организация = Неопределено, НаДату = Неопределено, ТолькоПервую = Ложь) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизацийМУ.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизацийМУ.УчетнаяПолитика КАК УчетнаяПолитика,
	|	УчетныеПолитики.ДоходыОтРазницПриПересчетеВП КАК ДоходыКР,
	|	УчетныеПолитики.РасходыОтРазницПриПересчетеВП КАК РасходыКР
	|ПОМЕСТИТЬ втСчетаУчетаКР
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета.СрезПоследних(
	|			&НаДату,
	|			Организация В (&Организация)) КАК УчетнаяПолитикаОрганизацийМУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитики КАК УчетныеПолитики
	|		ПО УчетнаяПолитикаОрганизацийМУ.УчетнаяПолитика = УчетныеПолитики.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсключитьИзПересчетаВП.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаИсключения
	|ИЗ
	|	Справочник.УчетныеПолитики.ИсключитьИзПересчетаВП КАК ИсключитьИзПересчетаВП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаУчетаКР.ДоходыКР
	|ИЗ
	|	втСчетаУчетаКР КАК втСчетаУчетаКР
	|ГДЕ
	|	втСчетаУчетаКР.ДоходыКР <> ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втСчетаУчетаКР.РасходыКР
	|ИЗ
	|	втСчетаУчетаКР КАК втСчетаУчетаКР
	|ГДЕ
	|	втСчетаУчетаКР.РасходыКР <> ЗНАЧЕНИЕ(ПланСчетов.Международный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсВалютыФункциональной.Валюта КАК ВалютаФункциональная,
	|	КурсВалютыФункциональной.Курс КАК КурсФВ,
	|	КурсВалютыФункциональной.Кратность КАК КратностьФВ,
	|	КурсВалютыПредставления.Валюта КАК ВалютаПредставления,
	|	КурсВалютыПредставления.Курс КАК КурсВП,
	|	КурсВалютыПредставления.Кратность КАК КратностьВП
	|ПОМЕСТИТЬ втВалюты
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&НаДату,
	|			Валюта В
	|				(ВЫБРАТЬ
	|					ВалютаФункциональная.Значение
	|				ИЗ
	|					Константа.ВалютаФункциональная КАК ВалютаФункциональная)) КАК КурсВалютыФункциональной,
	|	РегистрСведений.КурсыВалют.СрезПоследних(
	|			&НаДату,
	|			Валюта В
	|				(ВЫБРАТЬ
	|					ВалютаПредставления.Значение
	|				ИЗ
	|					Константа.ВалютаПредставления КАК ВалютаПредставления)) КАК КурсВалютыПредставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет КАК Счет,
	|	МеждународныйОстатки.Организация КАК Организация,
	|	МеждународныйОстатки.Подразделение КАК Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаПредставленияОстаток) КАК СуммаПредставленияОстаток
	|ПОМЕСТИТЬ втОстаткиСчетов
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			НЕ Счет В (ВЫБРАТЬ Счет ИЗ втСчетаИсключения),
	|			,
	|			Организация В (&Организация)) КАК МеждународныйОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСчетаУчетаКР КАК СчетаУчетаКР
	|		ПО МеждународныйОстатки.Организация = СчетаУчетаКР.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|			И МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|			И МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|			И МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОстаткиСчетов.Счет КАК Счет,
	|	ОстаткиСчетов.Валюта КАК Валюта,
	|	ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * втВалюты.КурсФВ / втВалюты.КратностьФВ / втВалюты.КурсВП * втВалюты.КратностьВП КАК ЧИСЛО(31,2)) КАК СуммаВП,
	|	ОстаткиСчетов.СуммаПредставленияОстаток КАК СуммаВПОстаток,
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * втВалюты.КурсФВ / втВалюты.КратностьФВ / втВалюты.КурсВП * втВалюты.КратностьВП КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток КАК КурсоваяРазница,
	|	ОстаткиСчетов.Организация КАК Организация,
	|	ОстаткиСчетов.Подразделение КАК Подразделение,
	|	ОстаткиСчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОстаткиСчетов.Субконто1 КАК Субконто1,
	|	ОстаткиСчетов.Субконто2 КАК Субконто2,
	|	ОстаткиСчетов.Субконто3 КАК Субконто3
	|ИЗ
	|	втОстаткиСчетов КАК ОстаткиСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&НаДату, ) КАК КурсыВалют
	|		ПО ОстаткиСчетов.Валюта = КурсыВалют.Валюта,
	|	втВалюты КАК втВалюты
	|ГДЕ
	|	(ВЫРАЗИТЬ(ОстаткиСчетов.СуммаОстаток * втВалюты.КурсФВ / втВалюты.КратностьФВ / втВалюты.КурсВП * втВалюты.КратностьВП КАК ЧИСЛО(31,2))) - ОстаткиСчетов.СуммаПредставленияОстаток <> 0";
	
	Возврат ВсегоОсталосьКурсовыхРазниц(ТекстЗапроса, Организация, НаДату, ТолькоПервую);
	
КонецФункции

Функция ЕстьНезакрытыеСчетаУчетаДоходовРасходов(Организация = Неопределено, НаДату = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(НаДату) Тогда
		НаДату = КонецДня(НаДату);
	Иначе
		НаДату = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТекущиеУчетныеПолитики.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ втУчетныеПолитики
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета.СрезПоследних(
	|			&НаДату,
	|			Организация В (&Организация) ИЛИ &ВсеОрганизации) КАК ТекущиеУчетныеПолитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаЗакрытия
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетаДоходов.Счет КАК Счет
	|	ИЗ
	|		втУчетныеПолитики КАК ТекущиеУчетныеПолитики
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитики.СчетаДоходов КАК СчетаДоходов
	|			ПО ТекущиеУчетныеПолитики.УчетнаяПолитика = СчетаДоходов.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СчетаРасходов.Счет
	|	ИЗ
	|		втУчетныеПолитики КАК ТекущиеУчетныеПолитики
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитики.СчетаРасходов КАК СчетаРасходов
	|			ПО ТекущиеУчетныеПолитики.УчетнаяПолитика = СчетаРасходов.Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СчетаПрибылейУбытков.Счет
	|	ИЗ
	|		втУчетныеПолитики КАК ТекущиеУчетныеПолитики
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитики.СчетаПрибылейУбытков КАК СчетаПрибылейУбытков
	|			ПО ТекущиеУчетныеПолитики.УчетнаяПолитика = СчетаПрибылейУбытков.Ссылка) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МеждународныйОстатки.Счет КАК Счет,
	|	МеждународныйОстатки.Организация КАК Организация,
	|	МеждународныйОстатки.Подразделение КАК Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто1,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто2,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Субконто3,
	|	СУММА(МеждународныйОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокДт) КАК СуммаОстатокДт,
	|	СУММА(МеждународныйОстатки.СуммаРазвернутыйОстатокКт) КАК СуммаОстатокКт,
	|	СУММА(МеждународныйОстатки.ВалютнаяСуммаОстаток) КАК ВалютнаяСуммаОстаток
	|
	|ИЗ
	|	РегистрБухгалтерии.Международный.Остатки(
	|			&Граница,
	|			Счет В
	|				(ВЫБРАТЬ Т.Счет ИЗ втСчетаЗакрытия КАК Т),
	|			,
	|			Организация В (&Организация) ИЛИ &ВсеОрганизации) КАК МеждународныйОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто1
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто1.Ссылка
	|			И (МеждународныйВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто2
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто2.Ссылка
	|			И (МеждународныйВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК МеждународныйВидыСубконто3
	|		ПО МеждународныйОстатки.Счет = МеждународныйВидыСубконто3.Ссылка
	|			И (МеждународныйВидыСубконто3.НомерСтроки = 3)
	|
	|СГРУППИРОВАТЬ ПО
	|	МеждународныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто1.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто1
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто2.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто2
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МеждународныйВидыСубконто3.Валютный
	|			ТОГДА МеждународныйОстатки.Субконто3
	|		ИНАЧЕ NULL
	|	КОНЕЦ,
	|	МеждународныйОстатки.Организация,
	|	МеждународныйОстатки.Подразделение,
	|	МеждународныйОстатки.НаправлениеДеятельности,
	|	МеждународныйОстатки.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(МеждународныйОстатки.СуммаОстаток) <> 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка.Количество();
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ТипОперации");
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	ТипыОпераций = Перечисления.ТипыРегламентныхОперацийМеждународныйУчет;
	Если Данные.ТипОперации = ТипыОпераций.ЗакрытиеСчетовДоходовРасходов Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(
							МеждународнаяОтчетностьВызовСервера.ПредставлениеЗакрытияДоходовРасходов(),
							Данные.Номер,
							Данные.Дата);
							
	ИначеЕсли Данные.ТипОперации = ТипыОпераций.РасчетКурсовыхРазницФункциональнаяВалюта Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(
							МеждународнаяОтчетностьВызовСервера.ПредставлениеРасчетКурсовыхРазницФункциональнойВалюте(),
							Данные.Номер,
							Данные.Дата);
							
	ИначеЕсли Данные.ТипОперации = ТипыОпераций.РасчетКурсовыхРазницФункциональнаяВалюта Тогда
		СтандартнаяОбработка = Ложь;
		Представление = СтрШаблон(
							МеждународнаяОтчетностьВызовСервера.ПредставлениеРасчетКурсовыхРазницВалютеПредставления(),
							Данные.Номер,
							Данные.Дата);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ВсегоОсталосьКурсовыхРазниц(ТекстЗапроса, Знач Организация, Знач НаДату, ТолькоПервую)
	
	Если ЗначениеЗаполнено(НаДату) Тогда
		НаДату = КонецДня(НаДату);
	Иначе
		НаДату = КонецДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = Неопределено;
	КонецЕсли;
	
	Если НЕ ТолькоПервую Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, " ПЕРВЫЕ 1", "");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаДату", НаДату);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Граница", Новый Граница(НаДату, ВидГраницы.Включая));
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
	
КонецФункции

#КонецОбласти

#КонецЕсли