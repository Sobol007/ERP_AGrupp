#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,ОбъектыПечати, ПараметрыВывода) Экспорт
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеВБанкПС") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ЗаявлениеВБанкПС",
//		"Заявление в уполномоченный банк ПС",
		"Заявление в банк УК",
		ПолучитьТабДок(МассивОбъектов));
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьТабДок(МассивОбъектов)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Ссылка Из МассивОбъектов Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		ОбъектСсылки = Ссылка.ПолучитьОбъект();
		Если ОбъектСсылки.Дата >= Дата(2018,03,01) Тогда
			Если ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.пункт181_17_4_ПредоставлениеВБК Тогда
				ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2018_ЗаявлениеНаПредоставлениеВБК());
			ИначеЕсли ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.пункт181_6_1_СнятиеСУчетаКонтракта Тогда
				Если ОбъектСсылки.ОснованиеЗакрытияПС <> Перечисления.РЭЙ_ОснованияЗакрытияПС.пункт181_6_7 Тогда
					ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2018_ЗаявлениеОСнятииСУчетаКонтрактов());
				КонецЕсли;
			ИначеЕсли ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.пункт181_7_1_ВнесениеИзмененийВРаздел1ВБК Тогда
				ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2018_ЗаявлениеОВнесенииИзмененийВРаздел1ВБК());
			ИначеЕсли ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.пункт181_2_25_ИзменениеКодовВидовОпераций Тогда
				ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2018_ЗаявлениеОбИзмененииКодовВидовОпераций());
			КонецЕсли;
		Иначе
			Если ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.ЗакрытиеПаспортаСделки Тогда
				ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2012_ЗаявлениеОЗакрытииПС());
			ИначеЕсли ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.ПредоставлениеВедомостиБанковскогоКонтроля Тогда
				ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2012_ЗаявлениеНаПредоставлениеВедомостиБанковскогоКонтроля());
			ИначеЕсли ОбъектСсылки.ВидОперации = Перечисления.РЭЙ_ВидыОперацийЗаявлениеВБанк.ПереоформлениеПаспортаСделки Тогда
				ТабличныйДокумент.Вывести(ОбъектСсылки.Печать2012_ЗаявлениеОПереоформленииПС());
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
КонецФункции

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаДокумента" И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВыбраннаяФорма = "ФормаДокумента"; 
	КонецЕсли;

	Если Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;

	Если Параметры.Свойство("ЗначенияЗаполнения") 
		И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура") Тогда
		Если Параметры.ЗначенияЗаполнения.Свойство("ВидОперации") Тогда
			ВыбраннаяФорма = "ФормаДокумента";
		КонецЕсли;
	КонецЕсли;

	Если Параметры.Свойство("Основание")
		И ЗначениеЗаполнено(Параметры.Основание) Тогда
			ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаВыбораОперации";
	КонецЕсли;

КонецПроцедуры


#Область ПроцедурыИФункцииПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "РЭЙ_СлужебныйКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РЭЙ_ЗаявлениеВБанкПС";
	КомандаПечати.Идентификатор = "ЗаявлениеВБанкПС";
	КомандаПечати.Представление = НСтр("ru = 'Заявление в банк УК'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
КонецПроцедуры

#КонецОбласти
