
&НаСервере
Процедура ПрименитьКоэффициентНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.СводнаяНоменклатурнаяГруппа КАК СводнаяНоменклатурнаяГруппа,
	               |	ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.Месяц КАК Месяц,
	               |	ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.ПрименяемыйКоэффициент КАК ПрименяемыйКоэффициент,
	               |	ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.ЗначениеКоэффициента * &РегиональныйКоэффициент * &НовыйКоэффициент КАК ЗначениеКоэффициента
	               |ИЗ
	               |	Документ.АГ_ПлановыйКоэффицентЗапасов.КоэффициентыЗапасов КАК ПлановыйКоэффицентЗапасовКоэффициентыЗапасов
	               |ГДЕ
	               |	ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.Ссылка.Проведен = ИСТИНА
	               |	И ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.Ссылка.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.АГ_ТипыДокументаПлановыйКоэффициентЗапасов.Базовый)
	               |	И ПлановыйКоэффицентЗапасовКоэффициентыЗапасов.Ссылка.Период = &Период";
	
	Запрос.УстановитьПараметр("Период", Объект.Период);
	Запрос.УстановитьПараметр("РегиональныйКоэффициент", Объект.РегиональныйКоэффициент);
	Запрос.УстановитьПараметр("НовыйКоэффициент", Объект.НовыйКоэффициент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаКоэффициентов = РезультатЗапроса.Выгрузить();
	Объект.КоэффициентыЗапасов.Загрузить(ТаблицаКоэффициентов);
	
	ПреобразоватьТаблицуКоэффициентовВГоризонтальную();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьКоэффициент(Команда)
	ПрименитьКоэффициентНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьТаблицуКоэффициентовВГоризонтальную()
	
	ТаблицаКоэффициентыЗапасовГоризонтальная.Очистить();
	
	Для Каждого СтрокаТаблицыКоэффициентов Из Объект.КоэффициентыЗапасов Цикл
		СтруктураПоиска = Новый Структура("СводнаяНоменклатурнаяГруппа", СтрокаТаблицыКоэффициентов.СводнаяНоменклатурнаяГруппа);
		НайденныеСтроки = ТаблицаКоэффициентыЗапасовГоризонтальная.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущаяСтрока = НайденныеСтроки[0];
		Иначе
			ТекущаяСтрока = ТаблицаКоэффициентыЗапасовГоризонтальная.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТаблицыКоэффициентов);
		КонецЕсли;
		ДатаМесяца = СтрокаТаблицыКоэффициентов.Месяц.ДатаНачала;
		НомерМесяца = Месяц(ДатаМесяца);
		Если НомерМесяца >= 1 И НомерМесяца <= 12 Тогда
			ТекущаяСтрока["ЗначениеКоэффициента" + Строка(НомерМесяца)] = СтрокаТаблицыКоэффициентов.ЗначениеКоэффициента;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПреобразоватьГоризонтальнуюТаблицуВТаблицуКоэффициентов(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПреобразоватьГоризонтальнуюТаблицуВТаблицуКоэффициентов(ДокументОбъект)
	
	ДокументОбъект.КоэффициентыЗапасов.Очистить();
	
	Для Каждого СтрокаГоризонтальнойТаблицы Из ТаблицаКоэффициентыЗапасовГоризонтальная Цикл
		Для СчетчикМесяцев = 1 По 12 Цикл
			НоваяСтрока = ДокументОбъект.КоэффициентыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаГоризонтальнойТаблицы);
			НачалоПериода = ДобавитьМесяц(Объект.Период.ДатаНачала, СчетчикМесяцев - 1);
			НоваяСтрока.Месяц = ПолучитьПериодПоДатеНачала(НачалоПериода, Перечисления.Периодичность.Месяц);
			НоваяСтрока.ЗначениеКоэффициента = СтрокаГоризонтальнойТаблицы["ЗначениеКоэффициента" + Строка(СчетчикМесяцев)];
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПериодПоДатеНачала(ДатаНачала, Периодичность)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	СОБи_Казначейство_Периоды.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.CRM_ПериодыПланирования КАК СОБи_Казначейство_Периоды
	               |ГДЕ
	               |	СОБи_Казначейство_Периоды.ДатаНачала = &ДатаНачала
	               |	И СОБи_Казначейство_Периоды.ДатаОкончания = &ДатаОкончания
	               |	И СОБи_Казначейство_Периоды.Периодичность = &Периодичность";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", НачалоДня(КонецМесяца(ДатаНачала)));
	Запрос.УстановитьПараметр("Периодичность", Периодичность);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;	
	Иначе
		Возврат Справочники.CRM_ПериодыПланирования.ПустаяСсылка();	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	ЭтоКорректировочныйОСН = (Объект.ТипДокумента = ПредопределенноеЗначение("Перечисление.АГ_ТипыДокументаПлановыйКоэффициентЗапасов.КорректировочныйОСН"));
	ЭтоКорректировочныйХРН = (Объект.ТипДокумента = ПредопределенноеЗначение("Перечисление.АГ_ТипыДокументаПлановыйКоэффициентЗапасов.КорректировочныйХРН"));
	ЭтоКорректировочныйИнвестиция = (Объект.ТипДокумента = ПредопределенноеЗначение("Перечисление.АГ_ТипыДокументаПлановыйКоэффициентЗапасов.КорректировочныйИнвестиция"));
	
	Элементы.ГруппаПериодДействияКонсолидированныйСклад.Видимость = (ЭтоКорректировочныйОСН ИЛИ ЭтоКорректировочныйХРН ИЛИ ЭтоКорректировочныйИнвестиция);
	
	Элементы.РегиональныйКоэффициент.Видимость = (ЭтоКорректировочныйОСН ИЛИ ЭтоКорректировочныйХРН ИЛИ ЭтоКорректировочныйИнвестиция);
	Элементы.НовыйКоэффициент.Видимость = (ЭтоКорректировочныйОСН ИЛИ ЭтоКорректировочныйХРН ИЛИ ЭтоКорректировочныйИнвестиция);
	
	Элементы.ТаблицаКоэффициентыЗапасовГоризонтальнаяПрименитьКоэффициент.Видимость = (ЭтоКорректировочныйОСН ИЛИ ЭтоКорректировочныйХРН ИЛИ ЭтоКорректировочныйИнвестиция); 
	
	Элементы.ТаблицаКоэффициентыЗапасовГоризонтальнаяПрименяемыйКоэффициент.Видимость = (ЭтоКорректировочныйХРН ИЛИ ЭтоКорректировочныйИнвестиция);
	
	Если ЭтоКорректировочныйХРН Тогда
		Элементы.ТаблицаКоэффициентыЗапасовГоризонтальнаяПрименяемыйКоэффициент.Заголовок = "Коэффициент хранения"
	ИначеЕсли ЭтоКорректировочныйИнвестиция Тогда
		Элементы.ТаблицаКоэффициентыЗапасовГоризонтальнаяПрименяемыйКоэффициент.Заголовок = "Инвестиционный коэффициент"
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКоэффициентыЗапасовГоризонтальнаяПрименяемыйКоэффициентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаКоэффициентыЗапасовГоризонтальная.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для СчетчикКолонок = 1 По 12 Цикл
		ТекущиеДанные["ЗначениеКоэффициента" + Строка(СчетчикКолонок)] = ТекущиеДанные["ЗначениеКоэффициента" + Строка(СчетчикКолонок)] * ТекущиеДанные.ПрименяемыйКоэффициент;		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипДокументаПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПреобразоватьТаблицуКоэффициентовВГоризонтальную();
	
КонецПроцедуры





