#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает статус для объекта документа
//
// Параметры:
//	НовыйСтатус - Строка - Имя статуса, который будет установлен у заказов
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров установки статуса.
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешной установки нового статуса.
//
Функция УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	Статус = Перечисления.СтатусыДвиженияПродукцииИМатериалов[НовыйСтатус];
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
	Возврат ПроверитьЗаполнение();
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	// Очистим реквизиты документа не используемые для хозяйственной операции.
	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ХозяйственнаяОперация,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
		
	ДенежныеСредстваСервер.ОчиститьНеиспользуемыеРеквизиты(
		ЭтотОбъект,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
	
	Для Каждого ДанныеСтроки Из Товары Цикл
		Если ПоРаспоряжениям И ЗначениеЗаполнено(Распоряжение)
			И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую
			И Не ЗначениеЗаполнено(ДанныеСтроки.Распоряжение) Тогда
			
			ДанныеСтроки.Распоряжение = Распоряжение;
			
		ИначеЕсли Не ЗначениеЗаполнено(ДанныеСтроки.Распоряжение)
			И ДанныеСтроки.Распоряжение <> Неопределено Тогда
			
			ДанныеСтроки.Распоряжение = Неопределено;
			
		КонецЕсли;
	КонецЦикла;
	
	ПроведениеСерверУТ.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	// Округление количества если движение не через кладовые
	Если ТипЗнч(Отправитель) = Тип("СправочникСсылка.Склады")
			И ЗначениеЗаполнено(Отправитель)
			И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отправитель, "ЦеховаяКладовая")
		ИЛИ ТипЗнч(Получатель) = Тип("СправочникСсылка.Склады")
			И ЗначениеЗаполнено(Получатель)
			И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "ЦеховаяКладовая") Тогда
		ОбщегоНазначенияУТ.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи);
	КонецЕсли;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
	
	// Заполнение ТЧ ВидыЗапасов
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(ХозяйственнаяОперация, Отправитель, Получатель, Неопределено);
		ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
		ИменаПолей.Вставить("СтатусУказанияСерий", "СтатусУказанияСерийОтправитель");
		ИменаПолей.Вставить("Назначение", "НазначениеОтправителя");
		РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(Товары, МестаУчета, ИменаПолей);
		
		ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(Товары);
		
		ЗаполнитьВидыЗапасов(Отказ);
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если Не ВидыЗапасовУказаныВручную Тогда
			ВидыЗапасов.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	// ИнтеграцияГИСМ
	ЕстьМаркируемаяПродукцияГИСМ = ИнтеграцияГИСМ_УТ.ЕстьМаркируемаяПродукцияГИСМ(Товары);
	// Конец ИнтеграцияГИСМ
	
	ДвижениеПродукцииИМатериаловЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отказ
		И Не ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		РегистрыСведений.РеестрДокументов.ИнициализироватьИЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
		
	КонецЕсли;
	
	ДвижениеПродукцииИМатериаловЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Распоряжение = ДокументОснованиеПриЗаполнении(ДанныеЗаполнения);
	ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров(Распоряжение);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ОбъектыОснований") Тогда
		
		ОбъектОснование = ДанныеЗаполнения.ОбъектыОснований[0];
		
		Если ДанныеЗаполнения.ОбъектыОснований.Количество() = 1 Тогда
			ДокументОснование = ОбъектОснование;
		КонецЕсли;
		
		Если ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			
			ЗаполнитьВыпускПродукцииНаОснованииЗаказаКлиента(ОбъектОснование);
			
		//++ НЕ УТКА
		ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ЗаказДавальца") Тогда
			
			Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
				ЗаполнитьПередачуМатериаловНаОснованииЗаказаДавальца(ДанныеЗаполнения.ОбъектыОснований);
			ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
				ЗаполнитьВыпускПродукцииНаОснованииЗаказаДавальца(ДанныеЗаполнения.ОбъектыОснований);
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение на основании документа %1 с хозяйственной операцией
					|""%2"" не предусмотрено. Обратитесь к администратору.';
					|en = 'Population on the %1 document basis with the 
					|""%2"" business transaction is not provided. Contact administrator.'"), ОбъектОснование, ДанныеЗаполнения.ХозяйственнаяОперация);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ПоступлениеСырьяОтДавальца") Тогда
			
			ЗаполнитьПередачуМатериаловНаОснованииПоступленияСырьяОтДавальца(ДанныеЗаполнения.ОбъектыОснований);
			
		//-- НЕ УТКА
		ИначеЕсли ТипЗнч(ОбъектОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
			
			ЗаполнитьПередачуМатериаловНаОснованииПриобретения(ДанныеЗаполнения.ОбъектыОснований);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС") Тогда

			ИнтеграцияВЕТИСУТ.ЗаполнитьДвижениеПродукцииИМатериаловНаОснованииИсходящейТранспортнойОперацииВЕТИС(ЭтотОбъект, ДанныеЗаполнения,, СтандартнаяОбработка);

		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение на основании документа %1 не предусмотрено. Обратитесь к администратору.';
											|en = 'Population on the %1 document basis is not provided. Contact administrator.'"), ОбъектОснование);
		КонецЕсли; 
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Товары") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
		
		Для Каждого Строка Из ДанныеЗаполнения.Товары Цикл
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.НазначениеОтправителя = НоваяСтрока.Назначение;
		КонецЦикла;
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
		НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект,ПараметрыУказанияСерий, Ложь);
		
	Иначе
		
		// Заполнение по распоряжениям.
		
		СтруктураЗаполнения = Неопределено;
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда // команда ввода на основании
			
			СтруктураЗаполнения = Новый Структура("МассивЗаказов", ДанныеЗаполнения);
			
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			
			Если ДанныеЗаполнения.Свойство("ОпределитьРаспоряжения") Тогда
				
				СтруктураЗаполнения = ДанныеЗаполнения;
				СтруктураЗаполнения.Вставить("МассивЗаказов", ОпределитьРаспоряжения(ДанныеЗаполнения.РеквизитыШапки));
				
			ИначеЕсли ДанныеЗаполнения.Свойство("МассивЗаказов") Тогда // из спец. формы оформления накладных
				
				СтруктураЗаполнения = ДанныеЗаполнения;
				
			ИначеЕсли ДанныеЗаполнения.Свойство("РеквизитыШапки") Тогда
				
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
				
			Иначе
				
				ЗаполнитьПоОтбору(ДанныеЗаполнения);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтруктураЗаполнения <> Неопределено Тогда
			
			СтруктураЗаполнения.РеквизитыШапки.Вставить("ВариантПриемкиТоваров", ВариантПриемкиТоваров);
			ЗаполнитьПоРаспоряжениям(СтруктураЗаполнения);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитовОперации;
	
	ЗатратыСервер.ПроверитьИспользованиеПартионногоУчета22(ЭтотОбъект, Дата, Отказ);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Отправитель");
	МассивНепроверяемыхРеквизитов.Добавить("Получатель");
	МассивНепроверяемыхРеквизитов.Добавить("Товары");
	
	ОтключитьПроверкуЗаполненияПоХозяйственнойОперации(МассивНепроверяемыхРеквизитов);
	
	МенеджерВременныхТаблиц = СформироватьВременныеТаблицыДляПроверки();
	ПредставлениеПолей = Документы.ДвижениеПродукцииИМатериалов.ПредставлениеПолей(ЭтотОбъект);
	
	ПроверитьРеквизитыШапки(ПредставлениеПолей, Отказ);
	
	ПроверитьТовары(ПредставлениеПолей, Отказ);
	
	//++ НЕ УТКА
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство И Не Организация.Пустая() Тогда
		
		// Проверка позволяет избежать ситуации, когда образуются резервы товаров организаций в подразделении.
		ПроверитьСоответствиеОрганизацииНакладнойИЭтапов(
			Организация,
			Товары.Выгрузить(, "НомерСтроки,Распоряжение"),
			ПредставлениеПолей.Товары,
			Отказ);
		
	КонецЕсли;
	//-- НЕ УТКА
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ПроверитьВозможностьОкругления = 
		ТипЗнч(Отправитель) = Тип("СправочникСсылка.Склады")
			И ЗначениеЗаполнено(Отправитель)
			И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отправитель, "ЦеховаяКладовая")
		ИЛИ ТипЗнч(Получатель) = Тип("СправочникСсылка.Склады")
			И ЗначениеЗаполнено(Получатель)
			И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "ЦеховаяКладовая");
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	// Проверка заполнения серий в т.ч. серии.
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий, Отказ, МассивНепроверяемыхРеквизитов);
	
	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(Организация, Дата);
	
	Если НЕ ПараметрыУчетнойПолитики.УчетГотовойПродукцииПоПлановойСтоимости
	ИЛИ (ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
	  И ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Цена");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Сумма");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗаказыСервер.ОтразитьТоварыКОтгрузке(ДополнительныеСвойства, Движения, Отказ);
	ЗаказыСервер.ОтразитьТоварыКПоступлению(ДополнительныеСвойства, Движения, Отказ);
	ЗаказыСервер.ОтразитьДвижениеТоваров(ДополнительныеСвойства, Движения, Отказ);
	ЗаказыСервер.ОтразитьЗаказыМатериаловВПроизводство(ДополнительныеСвойства, Движения, Отказ);
	
	ЗапасыСервер.ОтразитьТоварыОрганизаций(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьТоварыНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	ДоходыИРасходыСервер.ОтразитьСебестоимостьТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	ЗапасыСервер.ОтразитьТоварыОрганизацийКПередаче(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьТоварыКОформлениюОтчетовКомитента(ДополнительныеСвойства, Движения, Отказ);
	
	УправленческийУчетПроведениеСервер.ОтразитьДвиженияНоменклатураНоменклатура(ДополнительныеСвойства, Движения, Отказ);
	
	ЗапасыСервер.ОтразитьСвободныеОстатки(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьОбеспечениеЗаказов(ДополнительныеСвойства, Движения, Отказ);
	
	СкладыСервер.ОтразитьДвиженияСерийТоваров(ДополнительныеСвойства, Движения, Отказ);
	
	//++ НЕ УТКА
	УправлениеПроизводством.ОтразитьРаспоряженияНаПередачуИзПроизводства(ДополнительныеСвойства, Движения, Отказ);
	//-- НЕ УТКА
	
	РегистрыСведений.РеестрДокументов.ЗаписатьДанныеДокумента(Ссылка, ДополнительныеСвойства, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ЗапасыСервер.ПодготовитьЗаписьТоваровОрганизаций(ЭтотОбъект, РежимЗаписиДокумента.Проведение);
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);

	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	//++ НЕ УТКА
	МеждународныйУчетПроведениеСервер.ЗарегистрироватьКОтражению(ЭтотОбъект, ДополнительныеСвойства, Движения, Отказ);
	//-- НЕ УТКА
	
	ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
	Если Не ПоРаспоряжениям
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) Тогда
		// Если на склад приходуется продукция, произведенная без заказов, заполняем виды запасов без проверки.
		ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию = Истина;
		ПараметрыЗаполнения.ТаблицаРеквизитовВидовЗапасовПоУмолчанию = ТаблицаРеквизитовВидовЗапасовПоУмолчаниюПроведение();
	КонецЕсли;
	ЗапасыСервер.СформироватьРезервыПоТоварамОрганизаций(ЭтотОбъект, Отказ, ПараметрыЗаполнения);
	
	ПроведениеСерверУТ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСерверУТ.ЗаписатьПодчиненныеНаборамЗаписейДанные(ЭтотОбъект, Отказ);
	
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	//++ НЕ УТКА
	Если Не Отказ Тогда
		ЗатратыСервер.ДобавитьВОчередьРасходТоваровОрганизацийНаПроизводство(
			Организация,
			Получатель,
			ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЭтаповПроизводства.ВыгрузитьКолонку("Ссылка"),
			НачалоМесяца(Дата));
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	//++ НЕ УТКА
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства, "ТаблицаЭтаповПроизводства");
	//-- НЕ УТКА
	
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ЗапасыСервер.ПодготовитьЗаписьТоваровОрганизаций(ЭтотОбъект, РежимЗаписиДокумента.ОтменаПроведения);
	
	ДвижениеПродукцииИМатериаловЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);

	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
	ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Ложь;
	Если Не ПоРаспоряжениям
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) Тогда
		ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию = Истина;
		ПараметрыЗаполнения.ТаблицаРеквизитовВидовЗапасовПоУмолчанию = ТаблицаРеквизитовВидовЗапасовПоУмолчаниюПроведение();
	КонецЕсли;
	
	Если ПоРаспоряжениям И ТипЗнч(Получатель) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		ДополнительныеСвойства.Вставить("НужноКонтролироватьОстаткиТоваровОрганизаций", Ложь);
	КонецЕсли;
	
	ЗапасыСервер.СформироватьРезервыПоТоварамОрганизаций(ЭтотОбъект, Отказ, ПараметрыЗаполнения);
	
	ПроведениеСерверУТ.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	ПроведениеСерверУТ.ЗаписатьПодчиненныеНаборамЗаписейДанные(ЭтотОбъект, Отказ);
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(ЭтотОбъект, Отказ);
	
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
	//++ НЕ УТКА
	Если Не Отказ Тогда
		ЗатратыСервер.ДобавитьВОчередьРасходТоваровОрганизацийНаПроизводство(
			Организация,
			Получатель,
			ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЭтаповПроизводства.ВыгрузитьКолонку("Ссылка"),
			НачалоМесяца(Дата));
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Статус = Перечисления.СтатусыДвиженияПродукцииИМатериалов.Отгружено;
	СостояниеЗаполненияМногооборотнойТары = Перечисления.СостоянияЗаполненияМногооборотнойТары.ПустаяСсылка();
	ЕстьМаркируемаяПродукцияГИСМ = Ложь;
	
	ВидыЗапасовУказаныВручную = Ложь;
	ВидыЗапасов.Очистить();
	Серии.Очистить();
	
	Документы.ДвижениеПродукцииИМатериалов.ОтвязатьОтРаспоряжений(ЭтотОбъект);
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		СтрокаТЧ.ИдентификаторСтроки = "";
	КонецЦикла;
	
	ВариантПриемкиТоваров = ЗакупкиСервер.ПолучитьВариантПриемкиТоваров();
	
	ИнициализироватьДокумент();
	
	ДвижениеПродукцииИМатериаловЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	СписокОпераций = Документы.ДвижениеПродукцииИМатериалов.СписокОпераций();
	Если СписокОпераций.Количество() = 1 Тогда
		ХозяйственнаяОперация = СписокОпераций[0];
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыДвиженийПродукцииИМатериалов") Тогда
		Статус = Перечисления.СтатусыДвиженияПродукцииИМатериалов.Принято;
	КонецЕсли;
	
	ПредставлениеПолей = Документы.ДвижениеПродукцииИМатериалов.ПредставлениеПолей(ЭтотОбъект);
	Если ПредставлениеПолей.ТипОтправителя = "Подразделение" Тогда
		Отправитель = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный, Отправитель);
	ИначеЕсли ПредставлениеПолей.ТипОтправителя = "Кладовая" Тогда
		Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный);
		Отправитель = ЗначениеНастроекПовтИсп.ПолучитьЦеховуюКладовуюПоУмолчанию(Подразделение, Отправитель);
	Иначе
		Отправитель = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Отправитель);
	КонецЕсли;
	
	Если ПредставлениеПолей.ТипПолучателя = "Подразделение" Тогда
		Получатель = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный, Получатель);
	ИначеЕсли ПредставлениеПолей.ТипПолучателя = "Кладовая" Тогда
		Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Ответственный);
		Получатель = ЗначениеНастроекПовтИсп.ПолучитьЦеховуюКладовуюПоУмолчанию(Подразделение, Получатель);
	Иначе
		Получатель = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Получатель);
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
		ВидЦены = Справочники.Склады.УчетныйВидЦены(Отправитель);
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство Тогда
		ПоРаспоряжениям = Истина;
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами Тогда
		ПоРаспоряжениям = Ложь;
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияВидаДеятельностиНДС(ЭтотОбъект);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(НалогообложениеНДС, ПараметрыЗаполнения);
	
КонецПроцедуры

Функция ОпределитьРаспоряжения(РеквизитыШапки)

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаОстатки.Распоряжение
	|ИЗ
	|	РегистрНакопления.ЗаказыМатериаловВПроизводство.Остатки(
	|			,
	|			Подразделение = &Подразделение
	|				И Склад = &Склад) КАК ТаблицаОстатки
	|ГДЕ
	|	ТаблицаОстатки.Распоряжение.Организация = &Организация
	|	И ТаблицаОстатки.КОформлениюОстаток <> 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", РеквизитыШапки.Организация);
	Запрос.УстановитьПараметр("Подразделение", РеквизитыШапки.Получатель);
	Запрос.УстановитьПараметр("Склад", РеквизитыШапки.Отправитель);
	
	МассивЗаказов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение");
	
	Возврат МассивЗаказов;

КонецФункции

Процедура ЗаполнитьПоОтбору(СтруктураОтбора)
	
	СтруктураОтбора.Свойство("Организация", Организация);
	
	Если СтруктураОтбора.Свойство("ХозяйственнаяОперация", ХозяйственнаяОперация) Тогда
		
		ПредставлениеПолей = Документы.ДвижениеПродукцииИМатериалов.ПредставлениеПолей(ЭтотОбъект);
		
		Если ПредставлениеПолей.ТипОтправителя = "Склад" Тогда
			СтруктураОтбора.Свойство("Склад", Отправитель);
		ИначеЕсли ПредставлениеПолей.ТипПолучателя = "Склад" Тогда
			СтруктураОтбора.Свойство("Склад", Получатель);
		КонецЕсли;
		
		Если ПредставлениеПолей.ТипОтправителя = "Подразделение" Тогда
			СтруктураОтбора.Свойство("Подразделение", Отправитель);
		ИначеЕсли ПредставлениеПолей.ТипПолучателя = "Подразделение" Тогда
			СтруктураОтбора.Свойство("Подразделение", Получатель);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоРаспоряжениям(СтруктураЗаполнения)
	
	Если СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
		И СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Заполнение по распоряжениям для операции ""%1"" не предусмотрено.';
										|en = 'Population by references for the ""%1"" operation is not provided.'"), СтруктураЗаполнения.РеквизитыШапки.ХозяйственнаяОперация);
	КонецЕсли;
	
	МассивЗаказов = СтруктураЗаполнения.МассивЗаказов;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, СтруктураЗаполнения);
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения,
		СтруктураЗаполнения.РеквизитыШапки, МассивЗаказов);
	
	ТаблицаНакладная = Товары.Выгрузить();
	
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьПоЗаказамОрдерам(ТаблицаНакладная, Ссылка, ПараметрыЗаполнения);
	
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру Тогда
		ТаблицаНакладная.Колонки.Количество.Имя        = "КоличествоДоИзменения";
		ТаблицаНакладная.Колонки.КоличествоВОрдере.Имя = "Количество";
	Иначе
		ТаблицаНакладная.Колонки.Количество.Имя        = "КоличествоДоИзменения";
		ТаблицаНакладная.Колонки.КоличествоВЗаказе.Имя = "Количество";
	КонецЕсли;
	
	НакладныеСервер.УдалитьПустыеСтроки(ТаблицаНакладная, "Количество");
	
	Товары.Загрузить(ТаблицаНакладная);
	
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьШапкуДокументаПоЗаказу(ЭтотОбъект, ПараметрыЗаполнения, МассивЗаказов);
	
	Если ПараметрыЗаполнения.ЗаполнятьПоОрдеру Тогда
		Если ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
			Или ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
			// Перенос складских серий из ордеров для получателя
			НастройкиОтборов = Документы.ДвижениеПродукцииИМатериалов.ПолучитьНастройкиОтборов(ПараметрыЗаполнения.РеквизитыШапки);
			Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьСерииПоОтгрузке(
				ЭтотОбъект, ?(НастройкиОтборов <> Неопределено, НастройкиОтборов.ТаблицаОтборов, Неопределено));
		КонецЕсли;
	//++ НЕ УТКА
	ИначеЕсли ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства Тогда
		Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьСерииПоЭтапамПроизводства(ЭтотОбъект);
	//-- НЕ УТКА
	КонецЕсли;
	
	Документы.ДвижениеПродукцииИМатериалов.ОбновитьЗависимыеРеквизитыТабличнойЧасти(Товары, ПараметрыЗаполнения);
	
КонецПроцедуры

Процедура ЗаполнитьПередачуМатериаловНаОснованииПриобретения(МассивЗаказов)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую) КАК ХозяйственнаяОперация,
		|	МАКСИМУМ(ТаблицаДокумента.Организация),
		|	МАКСИМУМ(ТаблицаТовары.Склад) КАК Отправитель,
		|	МАКСИМУМ(ТаблицаДокумента.НаправлениеДеятельности) КАК НаправлениеДеятельности,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаДокумента.Организация) КАК КоличествоОрганизаций,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТаблицаТовары.Склад) КАК КоличествоСкладов
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ТаблицаДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаТовары
		|		ПО ТаблицаТовары.Ссылка = ТаблицаДокумента.Ссылка
		|			И ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
		|ГДЕ
		|	ТаблицаДокумента.Ссылка В (&МассивЗаказов)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугТовары.Склад,
		|	ПриобретениеТоваровУслугТовары.Номенклатура,
		|	ПриобретениеТоваровУслугТовары.Характеристика,
		|	ПриобретениеТоваровУслугТовары.Назначение КАК Назначение,
		|	ПриобретениеТоваровУслугТовары.Назначение КАК НазначениеОтправителя,
		|	ПриобретениеТоваровУслугТовары.Упаковка,
		|	ПриобретениеТоваровУслугТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ПриобретениеТоваровУслугТовары.Количество КАК Количество,
		|	ПриобретениеТоваровУслугТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ПриобретениеТоваровУслугТовары.СтатусУказанияСерий КАК СтатусУказанияСерийОтправитель,
		|	ПриобретениеТоваровУслугТовары.Серия КАК Серия
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка В (&МассивЗаказов)
		|	И ПриобретениеТоваровУслугТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПриобретениеТоваровУслугСерии.Склад,
		|	ПриобретениеТоваровУслугСерии.Серия,
		|	ПриобретениеТоваровУслугСерии.Количество,
		|	ПриобретениеТоваровУслугСерии.Номенклатура,
		|	ПриобретениеТоваровУслугСерии.Характеристика,
		|	ПриобретениеТоваровУслугСерии.Назначение КАК Назначение,
		|	ПриобретениеТоваровУслугСерии.Назначение КАК НазначениеОтправителя
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Серии КАК ПриобретениеТоваровУслугСерии
		|ГДЕ
		|	ПриобретениеТоваровУслугСерии.Ссылка В (&МассивЗаказов)
		|	И ПриобретениеТоваровУслугСерии.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар))");
	
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаРеквизиты = РезультатЗапроса[0].Выбрать();
	Если Не ВыборкаРеквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка шапки и табличной части
	Если ВыборкаРеквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов по разным организациям.';
							|en = 'Cannot register material transfer for different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Если ВыборкаРеквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов, т.к материалы поступили на разные склады.';
							|en = 'Cannot register material transfer as materials were delivered to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ТоварыОснования = РезультатЗапроса[1].Выгрузить();
	Если ТоварыОснования.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Поступление не содержит товаров. Ввод на основании невозможен.';
							|en = 'Receipt does not contain goods. Input on the basis is not supported.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаРеквизиты);
	
	// Разбиение строк, заполнение серий со статусом 10.
	ИндексыСтрок = Новый Массив();
	
	Для Каждого СтрокаТовары Из ТоварыОснования Цикл
		Если СтрокаТовары.СтатусУказанияСерий = 10 Тогда
			ИндексыСтрок.Вставить(0, ТоварыОснования.Индекс(СтрокаТовары));
		КонецЕсли;
	КонецЦикла;
	
	СерииОснования = РезультатЗапроса[2].Выгрузить();
	
	Если ИндексыСтрок.Количество() > 0 Тогда
		КлючСерии = "Номенклатура, Характеристика, Склад, Назначение";
		ОбеспечениеСервер.ПеренестиСерииИзТаблицыВСтроки(ТоварыОснования, ИндексыСтрок, СерииОснования, КлючСерии);
	КонецЕсли;
	
	Товары.Загрузить(ТоварыОснования);
	
	Для Каждого СтрокаСерии Из СерииОснования Цикл
		Если СтрокаСерии.Количество > 0 Тогда
			НоваяСтрока = Серии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСерии);
			НоваяСтрока.НазначениеОтправителя = НоваяСтрока.Назначение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТКА

Процедура ЗаполнитьПередачуМатериаловНаОснованииЗаказаДавальца(МассивЗаказов)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую) КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(Реквизиты.Организация)                                          КАК Организация,
	|	МАКСИМУМ(Реквизиты.СкладПоступления)                                     КАК Отправитель,
	|	МАКСИМУМ(Реквизиты.НалогообложениеНДС)                                   КАК ПотреблениеДляДеятельности,
	|	МАКСИМУМ(Реквизиты.НаправлениеДеятельности)                              КАК НаправлениеДеятельности,
	|	МАКСИМУМ(Реквизиты.ЕстьОшибкиПроведен)                                   КАК ЕстьОшибкиПроведен,
	|	МАКСИМУМ(Реквизиты.ЕстьОшибкиСтатус)                                     КАК ЕстьОшибкиСтатус,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Реквизиты.Организация)                              КАК КоличествоОрганизаций,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Реквизиты.СкладПоступления)                         КАК КоличествоСкладов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Реквизиты.НалогообложениеНДС)                       КАК КоличествоВариантовНалогообложения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказДавальца.Организация                             КАК Организация,
	|		ЗаказДавальца.СкладПоступления                        КАК СкладПоступления,
	|		ЗаказДавальца.НалогообложениеНДС                      КАК НалогообложениеНДС,
	|		ЗаказДавальца.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
	|		НЕ ЗаказДавальца.Проведен                             КАК ЕстьОшибкиПроведен,
	|		НЕ ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов) КАК ЕстьОшибкиСтатус 
	|	ИЗ
	|		Документ.ЗаказДавальца КАК ЗаказДавальца
	|	ГДЕ
	|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
	|		И НЕ ЗаказДавальца.Склад.ЭтоГруппа
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказДавальца.Организация                             КАК Организация,
	|		ТабличнаяЧастьМатериалы.Склад                         КАК СкладПоступления,
	|		ЗаказДавальца.НалогообложениеНДС                      КАК НалогообложениеНДС,
	|		ЗаказДавальца.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
	|		НЕ ЗаказДавальца.Проведен                             КАК ЕстьОшибкиПроведен,
	|		НЕ ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов) КАК ЕстьОшибкиСтатус 
	|	ИЗ
	|		Документ.ЗаказДавальца КАК ЗаказДавальца
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца.Материалы КАК ТабличнаяЧастьМатериалы
	|			ПО ЗаказДавальца.Ссылка = ТабличнаяЧастьМатериалы.Ссылка
	|	ГДЕ
	|		ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
	|		И ЗаказДавальца.Склад.ЭтоГруппа) КАК Реквизиты
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	Товары.Склад                КАК Склад,
	|	Товары.Назначение           КАК Назначение,
	|	СУММА(Товары.Количество)    КАК Количество,
	|	
	|	МИНИМУМ(Товары.Упаковка)    КАК Упаковка,
	|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки
	|	
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ЗаказДавальца.Материалы КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&ЗаказыДавальца)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура, Товары.Характеристика, Товары.Склад, Товары.Назначение
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.НаличиеПодЗаказОстаток КАК Количество
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
	|		(Номенклатура, Характеристика, Склад, Назначение) В(
	|			ВЫБРАТЬ
	|				Товары.Номенклатура   КАК Номенклатура,
	|				Товары.Характеристика КАК Характеристика,
	|				Товары.Склад          КАК Склад,
	|				Товары.Назначение     КАК Назначение
	|			ИЗ
	|				ВтТовары КАК Товары)) КАК ТаблицаОстатки
	|ГДЕ
	|	ТаблицаОстатки.НаличиеПодЗаказОстаток > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.Назначение     КАК НазначениеОтправителя,
	|	
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
	|				Товары.Количество
	|			ИНАЧЕ
	|				ТаблицаОстатки.Количество
	|		КОНЕЦ                     КАК Количество,
	|	
	|	Товары.Упаковка               КАК Упаковка,
	|	Товары.НомерСтроки            КАК НомерСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
	|				Товары.Количество
	|			ИНАЧЕ
	|				ТаблицаОстатки.Количество
	|		КОНЕЦ / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТаблицаОстатки
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТовары КАК Товары
	|	ПО Товары.Номенклатура   = ТаблицаОстатки.Номенклатура
	|	И Товары.Характеристика = ТаблицаОстатки.Характеристика
	|	И Товары.Склад          = ТаблицаОстатки.Склад
	|	И Товары.Назначение     = ТаблицаОстатки.Назначение
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	
	Запрос.УстановитьПараметр("ЗаказыДавальца", МассивЗаказов);
	Запрос.УстановитьПараметр("МассивДопустимыхСтатусов", МассивДопустимыхСтатусов);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Реквизиты = Результат[0].Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов по разным организациям.';
							|en = 'Cannot register material transfer for different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов, т.к. материалы поступили на разные склады.';
							|en = 'Cannot register material transfer as materials were delivered to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.ЕстьОшибкиСтатус Тогда
		ТекстОшибки = НСтр("ru = 'Для оформления передачи в производстово заказ должен быть в статусе ""К производству"", ""К отгрузке"" или ""Закрыт"".';
							|en = 'To register transfer to production, the order must be in the ""For production"", ""For shipment"", or ""Closed"" status.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Результат[3].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Поступление сырья от давальца не оформлено или все материалы уже переданы. Ввод на основании невозможен.';
							|en = 'Receipt from material provider is not registered or all materials have been already passed. Input on the basis is not available.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоВариантовНалогообложения > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. выбранные заказы оформлены под разную деятельность по НДС.';
							|en = 'Cannot register one document as the selected orders are registered for different activity on VAT.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Товары.Загрузить(Результат[3].Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьВыпускПродукцииНаОснованииЗаказаДавальца(МассивЗаказов)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой) КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(ЗаказДавальца.Организация)        КАК Организация,
	|	МАКСИМУМ(ЗаказДавальца.Склад)              КАК Получатель,
	|	
	|	МАКСИМУМ(
	|		ВЫБОР КОГДА ЗаказДавальца.НаправлениеДеятельности.УчетЗатрат ТОГДА
	|					ЗаказДавальца.НаправлениеДеятельности
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			КОНЕЦ)                             КАК НаправлениеДеятельности,
	|	
	|	МАКСИМУМ(ЗаказДавальца.НалогообложениеНДС) КАК НалогообложениеНДС,
	|	МАКСИМУМ(НЕ ЗаказДавальца.Проведен)        КАК ЕстьОшибкиПроведен,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЗаказДавальца.Статус В (&МассивДопустимыхСтатусов)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ЕстьОшибкиСтатус,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца.Организация)   КАК КоличествоОрганизаций,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца.Склад)         КАК КоличествоСкладов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказДавальца.НалогообложениеНДС) КАК КоличествоВариантовНалогообложения,
	|	
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ 
	|		ВЫБОР КОГДА ЗаказДавальца.НаправлениеДеятельности.УчетЗатрат ТОГДА
	|					ЗаказДавальца.НаправлениеДеятельности
	|				ИНАЧЕ
	|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			КОНЕЦ)                                    КАК КоличествоНаправленийДеятельности
	|ИЗ
	|	Документ.ЗаказДавальца КАК ЗаказДавальца
	|ГДЕ
	|	ЗаказДавальца.Ссылка В(&ЗаказыДавальца)
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	Товары.Склад                КАК Склад,
	|	ТабДокументы.Назначение     КАК Назначение,
	|	СУММА(Товары.Количество)    КАК Количество,
	|	
	|	МИНИМУМ(Товары.Упаковка)     КАК Упаковка,
	|	МИНИМУМ(Товары.НомерСтроки)  КАК НомерСтроки
	|	
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК Товары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца КАК ТабДокументы
	|		ПО ТабДокументы.Ссылка = Товары.Ссылка
	|ГДЕ
	|	Товары.Ссылка В(&ЗаказыДавальца)
	|	И НЕ Товары.Отменено
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура, Товары.Характеристика, Товары.Склад, ТабДокументы.Назначение
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.ПотребностьОстаток
	|		- ТаблицаОстатки.НаличиеПодЗаказОстаток КАК Количество
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
	|		(Номенклатура, Характеристика, Склад, Назначение) В(
	|			ВЫБРАТЬ
	|				Товары.Номенклатура   КАК Номенклатура,
	|				Товары.Характеристика КАК Характеристика,
	|				Товары.Склад          КАК Склад,
	|				Товары.Назначение     КАК Назначение
	|			ИЗ
	|				ВтТовары КАК Товары)) КАК ТаблицаОстатки
	|ГДЕ
	|	ТаблицаОстатки.ПотребностьОстаток
	|		- ТаблицаОстатки.НаличиеПодЗаказОстаток > 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.Назначение     КАК НазначениеОтправителя,
	|	
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
	|				Товары.Количество
	|			ИНАЧЕ
	|				ТаблицаОстатки.Количество
	|		КОНЕЦ                     КАК Количество,
	|	
	|	Товары.Упаковка               КАК Упаковка,
	|	Товары.НомерСтроки            КАК НомерСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество > Товары.Количество ТОГДА // передается не более чем количество, указанное в заказе.
	|				Товары.Количество
	|			ИНАЧЕ
	|				ТаблицаОстатки.Количество
	|		КОНЕЦ / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТаблицаОстатки
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТовары КАК Товары
	|	ПО Товары.Номенклатура   = ТаблицаОстатки.Номенклатура
	|	И Товары.Характеристика  = ТаблицаОстатки.Характеристика
	|	И Товары.Склад           = ТаблицаОстатки.Склад
	|	И Товары.Назначение      = ТаблицаОстатки.Назначение
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	
	Запрос.УстановитьПараметр("ЗаказыДавальца", МассивЗаказов);
	Запрос.УстановитьПараметр("МассивДопустимыхСтатусов", МассивДопустимыхСтатусов);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Реквизиты = Результат[0].Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить выпуск продукции по разным организациям.';
							|en = 'Cannot register product release by different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. продукция должна быть выпущена на разные склады.';
							|en = 'Cannot register one document as the products should be released to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.ЕстьОшибкиСтатус Тогда
		ТекстОшибки = НСтр("ru = 'Для оформления выпуска продукции заказ должен быть в статусе ""К производству"", ""К отгрузке"" или ""Закрыт"".';
							|en = 'To register the product release, the order must be in the ""For production"", ""For shipment"", or ""Closed"" status. '");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Результат[3].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Вся продукция уже выпущена, ввод на основании не требуется.';
							|en = 'All products have already been released, input on the basis is not required.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоВариантовНалогообложения > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. выбранные заказы оформлены под разную деятельность по НДС.';
							|en = 'Cannot register one document as the selected orders are registered for different activity on VAT.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоНаправленийДеятельности > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. выбранные заказы оформлены под разные направления деятельности.';
							|en = 'Cannot register one document as the selected orders are registered for different lines of business.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Товары.Загрузить(Результат[3].Выгрузить());
	
КонецПроцедуры

Процедура ЗаполнитьПередачуМатериаловНаОснованииПоступленияСырьяОтДавальца(ПоступленияСырья)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВКладовую) КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(ПоступлениеСырьяОтДавальца.Организация) КАК Организация,
	|	МАКСИМУМ(ПоступлениеСырьяОтДавальца.Склад) КАК Отправитель,
	|	МАКСИМУМ(ПоступлениеСырьяОтДавальцаТовары.ЗаказДавальца.НалогообложениеНДС) КАК ПотреблениеДляДеятельности,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеСырьяОтДавальца.Организация) КАК КоличествоОрганизаций,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеСырьяОтДавальца.Склад) КАК КоличествоСкладов,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПоступлениеСырьяОтДавальцаТовары.ЗаказДавальца.НалогообложениеНДС) КАК КоличествоВариантовНалогообложения
	|ИЗ
	|	Документ.ПоступлениеСырьяОтДавальца.Товары КАК ПоступлениеСырьяОтДавальцаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеСырьяОтДавальца КАК ПоступлениеСырьяОтДавальца
	|		ПО ПоступлениеСырьяОтДавальцаТовары.Ссылка = ПоступлениеСырьяОтДавальца.Ссылка
	|ГДЕ
	|	ПоступлениеСырьяОтДавальца.Ссылка В(&ПоступленияСырья)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура         КАК Номенклатура,
	|	Товары.Характеристика       КАК Характеристика,
	|	Товары.Склад                КАК Склад,
	|	Товары.Назначение           КАК Назначение,
	|	СУММА(Товары.Количество)    КАК Количество,
	|	МИНИМУМ(Товары.Упаковка)    КАК Упаковка,
	|	МИНИМУМ(Товары.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ПоступлениеСырьяОтДавальца.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&ПоступленияСырья)
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура, Товары.Характеристика, Товары.Склад, Товары.Назначение
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.НаличиеПодЗаказОстаток КАК Количество
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(,
	|		(Номенклатура, Характеристика, Склад, Назначение) В(
	|			ВЫБРАТЬ
	|				Товары.Номенклатура   КАК Номенклатура,
	|				Товары.Характеристика КАК Характеристика,
	|				Товары.Склад          КАК Склад,
	|				Товары.Назначение     КАК Назначение
	|			ИЗ
	|				ВтТовары КАК Товары)) КАК ТаблицаОстатки
	|ГДЕ
	|	ТаблицаОстатки.НаличиеПодЗаказОстаток > 0
	|;
	|
	|//////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатки.Номенклатура   КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.Склад          КАК Склад,
	|	ТаблицаОстатки.Назначение     КАК Назначение,
	|	ТаблицаОстатки.Назначение     КАК НазначениеОтправителя,
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество < Товары.Количество ТОГДА
	|				ТаблицаОстатки.Количество
	|			ИНАЧЕ
	|				Товары.Количество
	|		КОНЕЦ                     КАК Количество,
	|	
	|	Товары.Упаковка               КАК Упаковка,
	|	Товары.НомерСтроки            КАК НомерСтроки,
	|
	|	ВЫБОР КОГДА ТаблицаОстатки.Количество < Товары.Количество ТОГДА
	|				ТаблицаОстатки.Количество
	|			ИНАЧЕ
	|				Товары.Количество
	|		КОНЕЦ / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок
	|ИЗ
	|	ВтОстатки КАК ТаблицаОстатки
	|		
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТовары КАК Товары
	|	ПО Товары.Номенклатура   = ТаблицаОстатки.Номенклатура
	|	И Товары.Характеристика = ТаблицаОстатки.Характеристика
	|	И Товары.Склад          = ТаблицаОстатки.Склад
	|	И Товары.Назначение     = ТаблицаОстатки.Назначение
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	Запрос.УстановитьПараметр("ПоступленияСырья", ПоступленияСырья);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Товары.Упаковка",
		"Товары.Номенклатура"));
	
	Результат = Запрос.ВыполнитьПакет();
	
	Реквизиты = Результат[0].Выбрать();
	
	Если Не Реквизиты.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты.КоличествоОрганизаций > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов по разным организациям.';
							|en = 'Cannot register material transfer for different companies.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоСкладов > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить передачу материалов, т.к. материалы поступили на разные склады.';
							|en = 'Cannot register material transfer as materials were delivered to different warehouses.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Реквизиты.КоличествоВариантовНалогообложения > 1 Тогда
		ТекстОшибки = НСтр("ru = 'Невозможно оформить один документ, т.к. материал передается под разную деятельность по НДС.';
							|en = 'Cannot register one document as the material is transferred for different activity subject to VAT.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	Если Результат[3].Пустой() Тогда
		ТекстОшибки = НСтр("ru = 'Поступление сырья от давальца не оформлено или все материалы уже переданы. Ввод на основании невозможен.';
							|en = 'Receipt from material provider is not registered or all materials have been already passed. Input on the basis is not available.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	Товары.Загрузить(Результат[3].Выгрузить());
	
КонецПроцедуры
//-- НЕ УТКА

Процедура ЗаполнитьВыпускПродукцииНаОснованииЗаказаКлиента(ЗаказКлиента)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзКладовой) КАК ХозяйственнаяОперация,
	|	ЗаказКлиента.Организация          КАК Организация,
	|	ЗаказКлиента.Склад                КАК Получатель,
	|	ЗаказКлиента.НалогообложениеНДС   КАК ВыпускПодДеятельность,
	|	НЕ ЗаказКлиента.Проведен          КАК ЕстьОшибкиПроведен,
	|	
	|	ВЫБОР КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат ТОГДА
	|			ЗаказКлиента.НаправлениеДеятельности
	|		КОНЕЦ КАК НаправлениеДеятельности,
	|	
	|	ЗаказКлиента.Статус КАК СтатусДокумента
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка = &ЗаказКлиента");
	
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Документы.ЗаказКлиента.ПроверитьВозможностьВводаНаОсновании(
		ЗаказКлиента,
		Реквизиты.СтатусДокумента,
		Реквизиты.ЕстьОшибкиПроведен,
		Истина);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты);
	
	ПараметрыТаблицыТовары = ОбеспечениеСервер.ПараметрыТаблицыОстатковПоЗаказу();
	ПараметрыТаблицыТовары.ПолучатьУслуги = Ложь;
	ПараметрыТаблицыТовары.ПолучатьРаботы = Ложь;
	
	ТаблицаТовары = ОбеспечениеСервер.ТаблицаОстатковКЗаказу(ЗаказКлиента, ПараметрыТаблицыТовары);
	
	Товары.Загрузить(ТаблицаТовары);
	
	Для каждого ДанныеСтроки Из Товары Цикл
		ДанныеСтроки.НазначениеОтправителя = ДанныеСтроки.Назначение;
	КонецЦикла;
	
КонецПроцедуры

Функция ДокументОснованиеПриЗаполнении(ДанныеЗаполнения)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("ОбъектыОснований") Тогда
		
		Возврат ДанныеЗаполнения.ОбъектыОснований[0];
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("Товары")
		И ТипЗнч(ДанныеЗаполнения.Товары) = Тип("ТаблицаЗначений")
		И ДанныеЗаполнения.Товары.Колонки.Найти("Распоряжение") <> Неопределено Тогда
		
		Возврат ДанныеЗаполнения.Товары[0].Распоряжение;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("МассивЗаказов") Тогда
		
		Возврат ДанныеЗаполнения.МассивЗаказов[0];
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Массив") Тогда
		
		Возврат ДанныеЗаполнения[0];
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ВидыЗапасов

Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	ТекстЗапроса = 	
	"ВЫБРАТЬ
	|	&Дата КАК Дата,
	|	&Организация КАК Организация,
	|	&Склад КАК Склад,
	|	Неопределено КАК Партнер,
	|	Неопределено КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	&НалогообложениеНДС КАК НалогообложениеНДС,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ЛОЖЬ КАК ЕстьСделкиВТабличнойЧасти,
	|
	|	ВЫБОР КОГДА СтруктураПредприятия.ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|		И &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|	ТОГДА
	|		&Получатель
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|	КОНЕЦ КАК Подразделение,
	|
	|	ВЫБОР КОГДА СтруктураПредприятия.ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|		И &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|	ТОГДА
	|		&Менеджер
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|	КОНЕЦ КАК Менеджер,
	|
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|	ПО
	|		СтруктураПредприятия.Ссылка = &Получатель
	|
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Назначение КАК НазначениеПолучателя,
	|	ТаблицаТоваров.НазначениеОтправителя КАК Назначение,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.СтатусУказанияСерийПолучатель КАК СтатусУказанияСерийПолучатель,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ТаблицаТоваров.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаТоваров.Распоряжение КАК Заказ,
	|	ТаблицаТоваров.КодСтроки КАК КодСтроки,
	|	ТаблицаТоваров.ГруппаПродукции
	|	
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Серия КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.СтатусУказанияСерийПолучатель,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	&Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка) КАК СтавкаНДС,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаНДС,
	|	0 КАК СуммаВознаграждения,
	|	0 КАК СуммаНДСВознаграждения,
	|	ТаблицаТоваров.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаТоваров.Назначение КАК Назначение,
	|	ТаблицаТоваров.НазначениеПолучателя КАК НазначениеПолучателя,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	ИСТИНА КАК ПодбиратьВидыЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	&Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ТаблицаВидыЗапасов.Серия,
	|	ТаблицаВидыЗапасов.ГруппаПродукции,
	|	ТаблицаВидыЗапасов.НазначениеПолучателя,
	|	&ВидыЗапасовУказаныВручную КАК ВидыЗапасовУказаныВручную
	|	
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	ТаблицаВидыЗапасов.Сделка КАК Сделка,
	|	ТаблицаВидыЗапасов.Серия КАК СерияПолучатель,
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную КАК ВидыЗапасовУказаныВручную,
	|	ТаблицаВидыЗапасов.НазначениеПолучателя КАК НазначениеПолучателя,
	|	ТаблицаВидыЗапасов.ГруппаПродукции КАК ГруппаПродукции
	|	
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Отправитель);
	Запрос.УстановитьПараметр("Менеджер", Ответственный);
	Запрос.УстановитьПараметр("Получатель", Получатель);
	Запрос.УстановитьПараметр("ПоРаспоряжениям", ПоРаспоряжениям);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("ВидыЗапасовУказаныВручную", ВидыЗапасовУказаныВручную);
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоПодразделениямМенеджерам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоСделкам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоСделкам"));
	Запрос.УстановитьПараметр("ТаблицаТоваров", Товары.Выгрузить());
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов", ВидыЗапасов.Выгрузить());
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Функция ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
	
	ИменаРеквизитов = "Организация, Дата";
	
	Возврат ЗапасыСервер.ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц, Ссылка, ИменаРеквизитов);
	
КонецФункции

Процедура ЗаполнитьВидыЗапасов(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	ПерезаполнитьВидыЗапасов = ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект);
	
	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц) Тогда
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
		
		// Если на склад приходуется продукция, произведенная без заказов, заполняем виды запасов без проверки.
		Если Не ПоРаспоряжениям
			И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
				ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) Тогда
			ПараметрыЗаполнения.ВладелецТовараВШапке = Ложь;
			ПараметрыЗаполнения.ТаблицаРеквизитовВидовЗапасовПоУмолчанию = ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗаполнение();
			ПараметрыЗаполнения.СообщатьОбОшибкахЗаполнения = Ложь;
			ПараметрыЗаполнения.ПриНехваткеТоваровОрганизацииЗаполнятьВидамиЗапасовПоУмолчанию = Истина;
		КонецЕсли;
		
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоТоварамОрганизаций(ЭтотОбъект, МенеджерВременныхТаблиц, Отказ, ПараметрыЗаполнения);
		
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД", "Количество");
		
		Если Не Отказ Тогда
			ЗаполнитьАналитикуПолучателяВидовЗапасов(МенеджерВременныхТаблиц);
			ЗаполнитьВидЗапасовПолучателя();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыЗаполненияВидовЗапасов()
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	
	ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Добавить(Перечисления.ТипыЗапасов.ПродукцияДавальца);
	Если Не ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
		И Не ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства Тогда
		ПараметрыЗаполнения.ОтборыВидовЗапасов.ТипЗапасов.Добавить(Перечисления.ТипыЗапасов.МатериалДавальца);
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзКладовой Тогда
		ПараметрыЗаполнения.ПодбиратьВТЧТоварыПринятыеНаОтветственноеХранение = "Всегда";
	ИначеЕсли Константы.ПорядокОформленияВыкупаПриСписанииТоваров.Получить() = Перечисления.НастройкиОформленияВыкупаПриСписанииТоваров.ПриФактическомПотреблении
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство) Тогда
		ПараметрыЗаполнения.ПодбиратьВТЧТоварыПринятыеНаОтветственноеХранение = "Всегда";
	Иначе
		ПараметрыЗаполнения.ПодбиратьВТЧТоварыПринятыеНаОтветственноеХранение = "Никогда";
	КонецЕсли;
	
	// Отключаем подбор по интеркампани т.к. передача продукции\материалов по этапу должна быть оформлена только от
	// организации этапа.
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства Тогда
		ПараметрыЗаполнения.ПодбиратьВидыЗапасовПоИнтеркампани = Ложь;
	КонецЕсли;
	
	ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ТаблицаРеквизитовВидовЗапасовПоУмолчаниюЗаполнение()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки					КАК НомерСтроки,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаТоваров.Назначение					КАК Назначение
		|ПОМЕСТИТЬ ВтТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров;
		|
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки	КАК НомерСтроки,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
		|	&Организация									КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)	КАК ВидЗапасов,
		|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)		КАК НомерГТД,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА СпрНазначения.Заказ ССЫЛКА Документ.ЗаказДавальца
		|			ТОГДА СпрНазначения.Партнер
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА &Организация
		|	КОНЕЦ											КАК ВладелецТовара,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ЗаказДавальца.Контрагент ЕСТЬ НЕ NULL
		|			ТОГДА ЗаказДавальца.Контрагент
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	КОНЕЦ											КАК Контрагент,
		|	СпрНазначения.Договор							КАК Договор,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА СпрНазначения.Заказ ССЫЛКА Документ.ЗаказДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	КОНЕЦ											КАК ТипЗапасов,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварТребующийПодтвержденияВыпуска) КАК ТипЗапасовРезервы
		|ИЗ
		|	ВтТаблицаТоваров КАК ТаблицаТоваров
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
		|	ПО СпрНазначения.Ссылка = ТаблицаТоваров.Назначение
		//++ НЕ УТКА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца КАК ЗаказДавальца
		|	ПО СпрНазначения.Заказ = ЗаказДавальца.Ссылка
		//-- НЕ УТКА
		|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТаблицаТоваров", Товары.Выгрузить(, "НомерСтроки, АналитикаУчетаНоменклатуры, Назначение"));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ТаблицаРеквизитовВидовЗапасовПоУмолчаниюПроведение()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	0							КАК НомерСтроки,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	&Организация				КАК Организация,
		|	ТаблицаТоваров.ВидЗапасов	КАК ВидЗапасов,
		|	ТаблицаТоваров.НомерГТД		КАК НомерГТД,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ ССЫЛКА Документ.ЗаказДавальца
		|			ТОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Партнер
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА &Организация
		|	КОНЕЦ											КАК ВладелецТовара,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ЗаказДавальца.Контрагент ЕСТЬ НЕ NULL
		|			ТОГДА ЗаказДавальца.Контрагент
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	КОНЕЦ											КАК Контрагент,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Договор		КАК Договор,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ ССЫЛКА Документ.ЗаказДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	КОНЕЦ											КАК ТипЗапасов,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварТребующийПодтвержденияВыпуска) КАК ТипЗапасовРезервы
		|ИЗ
		|	Документ.ДвижениеПродукцииИМатериалов.ВидыЗапасов КАК ТаблицаТоваров
		//++ НЕ УТКА
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказДавальца КАК ЗаказДавальца
		|	ПО ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ = ЗаказДавальца.Ссылка
		//-- НЕ УТКА
		|ГДЕ
		|	ТаблицаТоваров.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
		|	ТаблицаТоваров.ВидЗапасов,
		|	ТаблицаТоваров.НомерГТД,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Партнер,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ ССЫЛКА Документ.ЗаказДавальца
		|			ТОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Партнер
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА &Организация
		|	КОНЕЦ,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Заказ ССЫЛКА Документ.ЗаказДавальца
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
		|	КОНЕЦ,
		|	ВЫБОР
		//++ НЕ УТКА
		|		КОГДА ЗаказДавальца.Контрагент ЕСТЬ НЕ NULL
		|			ТОГДА ЗаказДавальца.Контрагент
		//-- НЕ УТКА
		|		КОГДА ИСТИНА
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|	КОНЕЦ,
		|	ТаблицаТоваров.АналитикаУчетаНоменклатуры.Назначение.Договор";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка",      Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаполнитьВидЗапасовПолучателя() Экспорт
			
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Запасы.НомерСтроки,
	|	Запасы.АналитикаУчетаНоменклатуры,
	|	Запасы.ВидЗапасов,
	|	Запасы.ГруппаПродукции
	|ПОМЕСТИТЬ Запасы
	|ИЗ
	|	&ВидыЗапасов КАК Запасы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Запасы.НомерСтроки КАК НомерСтроки,
	|	СпрВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	&Организация КАК ОрганизацияПолучательЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК ВладелецТовара,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК Соглашение,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ВидЦены,
	|	&НалогообложениеНДС КАК НалогообложениеНДС,
	|	&НалогообложениеОрганизации КАК НалогообложениеОрганизации,
	|	Ключи.Номенклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	Запасы.ГруппаПродукции КАК ГруппаПродукции,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) КАК ХозОперацияВидаЗапаса
	|ИЗ
	|	Запасы КАК Запасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО (СпрВидыЗапасов.Ссылка = Запасы.ВидЗапасов)
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Ключи.Ссылка = Запасы.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	СпрВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И &ГенерироватьВидЗапасовПриПередачеВПроизводство
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Запасы.НомерСтроки КАК НомерСтроки,
	|	СпрВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	&ОрганизацияПолучатель КАК ОрганизацияПолучательЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК ВладелецТовара,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК Соглашение,
	|	НЕОПРЕДЕЛЕНО КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК ВидЦены,
	|	&НалогообложениеНДС КАК НалогообложениеНДС,
	|	&НалогообложениеОрганизации КАК НалогообложениеОрганизации,
	|	Ключи.Номенклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	Запасы.ГруппаПродукции КАК ГруппаПродукции,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) КАК ХозОперацияВидаЗапаса
	|ИЗ
	|	Запасы КАК Запасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
	|		ПО (СпрВидыЗапасов.Ссылка = Запасы.ВидЗапасов)
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|		ПО Ключи.Ссылка = Запасы.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|");
	
	Если Константы.ПорядокОформленияВыкупаПриСписанииТоваров.Получить() = Перечисления.НастройкиОформленияВыкупаПриСписанииТоваров.ПриПеремещении Тогда
		ГенерироватьВидЗапасовПриПередачеВПроизводство = Истина;
	Иначе
		ГенерироватьВидЗапасовПриПередачеВПроизводство = Ложь;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидыЗапасов", ВидыЗапасов.Выгрузить(, "НомерСтроки, АналитикаУчетаНоменклатуры, ВидЗапасов, ГруппаПродукции"));
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами Тогда
		НалогообложениеОрганизации = Справочники.Организации.НалогообложениеНДС(ОрганизацияПолучатель, Неопределено, Дата);
	Иначе
		НалогообложениеОрганизации = Справочники.Организации.НалогообложениеНДС(Организация, Неопределено, Дата);
	КонецЕсли;
	Запрос.УстановитьПараметр("НалогообложениеОрганизации", Справочники.Организации.НалогообложениеНДС(Организация, Неопределено, Дата));
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОрганизацияПолучатель", ОрганизацияПолучатель);
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ГенерироватьВидЗапасовПриПередачеВПроизводство", ГенерироватьВидЗапасовПриПередачеВПроизводство);
	
	ВидыЗапасовПолучателя = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВидЗапасовПолучателя = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
			Выборка.ОрганизацияПолучательЗапасов,
			Выборка.ХозОперацияВидаЗапаса,
			Выборка);
		ВидыЗапасовПолучателя.Вставить(Выборка.НомерСтроки, ВидЗапасовПолучателя);
	КонецЦикла;
	
	Для Каждого Запас Из ВидыЗапасов Цикл
		ВидЗапасовПолучателя = ВидыЗапасовПолучателя[Запас.НомерСтроки];
		Если Не ЗначениеЗаполнено(ВидЗапасовПолучателя) Тогда
			ВидЗапасовПолучателя = ?(
				Запас.ВидЗапасов.РеализацияЗапасовДругойОрганизации, 
				Справочники.ВидыЗапасов.ВидЗапасовДокумента(
					Запас.ВидЗапасов.Организация,, 
					Запас.ВидЗапасов), 
				Запас.ВидЗапасов);
		КонецЕсли;
		Запас.ВидЗапасовПолучателя = ВидЗапасовПолучателя;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполнения

Процедура ПроверитьРеквизитыШапки(ПредставлениеПолей, Отказ)

	Если НЕ ЗначениеЗаполнено(Отправитель) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено';
										|en = 'The ""%1"" field is not filled in'"), ПредставлениеПолей.Отправитель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Отправитель",, Отказ); 
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Получатель) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено';
										|en = 'The ""%1"" field is not filled in'"), ПредставлениеПолей.Получатель);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Получатель",, Отказ); 
	КонецЕсли; 
	Если Товары.Количество() = 0 Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не введено ни одной строки в список ""%1""';
										|en = 'No line is entered into the ""%1"" list'"), ПредставлениеПолей.Товары);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Товары",, Отказ); 
	КонецЕсли;
	
	Если Отправитель = Получатель И ЗначениеЗаполнено(Отправитель)
		И Не ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеПолуфабрикатовМеждуФилиалами Тогда
		Если ПредставлениеПолей.ТипОтправителя = "Склад" 
				И ПредставлениеПолей.ТипПолучателя = "Кладовая"
			ИЛИ ПредставлениеПолей.ТипОтправителя = "Кладовая" 
				И ПредставлениеПолей.ТипПолучателя = "Склад" Тогда
			ТекстСообщения = НСтр("ru = 'Склад и цеховая кладовая должны быть разными';
									|en = 'Warehouse and shop storeroom should be different'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Отправитель и получатель должны быть разными';
									|en = 'Sender and recipient should be different'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Отправитель",, Отказ); 
	КонецЕсли;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
		ЗакупкиСервер.ПроверитьКорректностьЗаполненияДокументаЗакупки(ЭтотОбъект,Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьТовары(ПредставлениеПолей, Отказ)
	
	ПроверятьКодСтроки = ПоРаспоряжениям
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство);
	
	ШаблонСообщения = НСтр("ru = 'Не допускается превышение запланированного количества товара по этапу в строке %1 списка ""%2"".';
							|en = 'Cannot exceed the planned goods quantity for the stage in line %1 of the ""%2"" list.'");
	ШаблонСообщенияПустоеРаспоряжение = НСтр("ru = 'В строке %1 списка ""%2"" не заполнено распоряжение.';
											|en = 'Reference is not filled in line %1 of the ""%2"" list.'");
	
	Для Каждого ДанныеСтроки Из Товары Цикл
		Если ПроверятьКодСтроки
			И ДанныеСтроки.КодСтроки = 0
			И ЗначениеЗаполнено(ДанныеСтроки.Распоряжение) Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Формат(ДанныеСтроки.НомерСтроки, "ЧГ="), ПредставлениеПолей.Товары);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ДанныеСтроки.НомерСтроки, "Номенклатура");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
		Если ПоРаспоряжениям
			И Не ЗначениеЗаполнено(Распоряжение)
			И Не ЗначениеЗаполнено(ДанныеСтроки.Распоряжение) Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщенияПустоеРаспоряжение, Формат(ДанныеСтроки.НомерСтроки, "ЧГ="), ПредставлениеПолей.Товары);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ДанныеСтроки.НомерСтроки, "Распоряжение");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТКА
Процедура ПроверитьСоответствиеОрганизацииНакладнойИЭтапов(Организация, СтрокиТабЧасти, СинонимТабличнойЧасти, Отказ)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтрокиТабЧасти.Распоряжение КАК Распоряжение,
		|	СтрокиТабЧасти.НомерСтроки  КАК НомерСтроки
		|ПОМЕСТИТЬ ВТСтрокиТабЧасти
		|ИЗ
		|	&СтрокиТабЧасти КАК СтрокиТабЧасти
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение
		|;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЭтапыДругойОрганизации.Ссылка      КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЭтапыДругойОрганизации.Организация) КАК Организация,
		|	СтрокиТабЧасти.НомерСтроки         КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СтрокиТабЧасти.Распоряжение)        КАК Распоряжение
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК ЭтапыДругойОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтрокиТабЧасти КАК СтрокиТабЧасти
		|		ПО СтрокиТабЧасти.Распоряжение = ЭтапыДругойОрганизации.Ссылка
		|ГДЕ
		|	ЭтапыДругойОрганизации.Ссылка В(&Этапы)
		|	И ЭтапыДругойОрганизации.Организация <> &Организация
		|	И НЕ СтрокиТабЧасти.Распоряжение ЕСТЬ NULL";
		
	Запрос.УстановитьПараметр("Этапы", СтрокиТабЧасти.ВыгрузитьКолонку("Распоряжение"));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СтрокиТабЧасти", СтрокиТабЧасти);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'Оганизация документа ""%1"" не соотвествует организации ""%2"", указанной в распоряжении ""%3"" в строке ""%4"" списка ""%5"".';
							|en = 'The company of the ""%1"" document does not correspond to the ""%2"" company specified in the ""%3"" reference in line ""%4"" of the ""%5"" list.'");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрШаблон(
			ШаблонСообщения,
			Организация,
			Выборка.Организация,
			Выборка.Распоряжение,
			Формат(Выборка.НомерСтроки, "ЧГ="),
			СинонимТабличнойЧасти);
			
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Распоряжение");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры
//-- НЕ УТКА

Процедура ОтключитьПроверкуЗаполненияПоХозяйственнойОперации(МассивНепроверяемыхРеквизитов)

	МассивВсехРеквизитов = Новый Массив;
	МассивРеквизитовОперации = Новый Массив;
	
	Документы.ДвижениеПродукцииИМатериалов.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ХозяйственнаяОперация,
		МассивВсехРеквизитов,
		МассивРеквизитовОперации);
	
	Для Каждого ЭлементМассива Из МассивВсехРеквизитов Цикл
		
		// Если реквизит не используется для хозяйственной операции, исключаем данный реквизит из проверки.
		Если МассивРеквизитовОперации.Найти(ЭлементМассива) = Неопределено Тогда
			МассивНепроверяемыхРеквизитов.Добавить(ЭлементМассива);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СформироватьВременныеТаблицыДляПроверки()

	СписокРеквизитов = "НомерСтроки,Назначение,НазначениеОтправителя,Распоряжение,КодСтроки";
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	" + СписокРеквизитов + "
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК ТабличнаяЧасть";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить(, СписокРеквизитов));
	
	Результат = Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц;

КонецФункции

#КонецОбласти

#Область Прочее

// См. описание в комментарии к одноименной процедуре в модуле УправлениеДоступом.
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.ЗначениеДоступа = Организация;
	
	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.ЗначениеДоступа = Отправитель;
	
	СтрокаТаб = Таблица.Добавить();
	СтрокаТаб.ЗначениеДоступа = Получатель;
	
КонецПроцедуры

Процедура СформироватьСписокРегистровДляКонтроля()

	Массив = Новый Массив;
	
	// Контроль при перепроведении и отмене проведения
	Если ПоРаспоряжениям Или Не ДополнительныеСвойства.ЭтоНовый Тогда
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВПроизводство
			Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаМатериаловВКладовую Тогда
			Массив.Добавить(Движения.ТоварыКОтгрузке);
		КонецЕсли;
	КонецЕсли;

	// Приходы в регистр (сторно расхода из регистра) контролируем при перепроведении и отмене проведения.
	КонтролироватьТоварыОрганизаций = Истина;
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		КонтролироватьТоварыОрганизаций = Ложь;
	ИначеЕсли Не ПоРаспоряжениям
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства) Тогда
		КонтролироватьТоварыОрганизаций = Ложь;
	ИначеЕсли ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		И ПоРаспоряжениям
		И ТипЗнч(Получатель) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		КонтролироватьТоварыОрганизаций = Ложь;
	КонецЕсли;
	
	Если КонтролироватьТоварыОрганизаций Тогда
		Массив.Добавить(Движения.ТоварыОрганизаций);
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение
		И ПоРаспоряжениям 
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратМатериаловИзПроизводства) Тогда
		Массив.Добавить(Движения.РаспоряженияНаПередачуИзПроизводства);
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Массив.Добавить(Движения.ЗаказыМатериаловВПроизводство);
		
		ОтключитьКонтрольПередачиПродукции = Ложь;
		
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзКладовой Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СтруктураПредприятия.Ссылка
			|ИЗ
			|	Справочник.Склады КАК Склады
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|		ПО Склады.Подразделение = СтруктураПредприятия.Ссылка
			|ГДЕ
			|	Склады.Ссылка = &Склад
			|	И СтруктураПредприятия.ПроизводствоБезЗаказов";
			
			Запрос.УстановитьПараметр("Склад", Отправитель);
			
			Если Не Запрос.Выполнить().Пустой() Тогда
				ОтключитьКонтрольПередачиПродукции = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ОтключитьКонтрольПередачиПродукции Тогда
			Массив.Добавить(Движения.СвободныеОстатки);
			Массив.Добавить(Движения.ОбеспечениеЗаказов);
			
			Если НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ДвижениеПродукцииИМатериалов).ИспользоватьСерииНоменклатуры Тогда
				Массив.Добавить(Движения.ТоварыНаСкладах);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);

КонецПроцедуры

// Функция проверят изменение табличной части "Товары" относительно табличной части "Виды запасов" документа.
//
// Параметры:
//	МенеджерВременныхТаблиц - Менеджер временных таблиц.
//
// Возвращаемое значение:
//	Булево - Истина - товары изменены
//           Ложь - товары не изменены.
//
Функция ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.Назначение КАК Назначение,
	|		ТаблицаТоваров.ГруппаПродукции КАК ГруппаПродукции,
	|		(ВЫБОР ТаблицаТоваров.СтатусУказанияСерийПолучатель
	|			КОГДА 14 ТОГДА ТаблицаТоваров.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КОНЕЦ) КАК Серия,
	|		ТаблицаТоваров.Количество КАК Количество
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|		ТаблицаВидыЗапасов.ГруппаПродукции КАК ГруппаПродукции,
	|		ТаблицаВидыЗапасов.СерияПолучатель КАК Серия,
	|		-ТаблицаВидыЗапасов.Количество КАК Количество
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Назначение,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	ТаблицаТоваров.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат (Не РезультатЗапрос.Пустой());
	
КонецФункции

Процедура ЗаполнитьАналитикуПолучателяВидовЗапасов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерийПолучатель = 14
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ТаблицаТоваров.НазначениеПолучателя КАК НазначениеПолучателя,
	|	ТаблицаТоваров.ГруппаПродукции КАК ГруппаПродукции,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СтатусУказанияСерийПолучатель = 14
	|			ТОГДА ТаблицаТоваров.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаТоваров.НазначениеПолучателя,
	|	ТаблицаТоваров.ГруппаПродукции
	|
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Количество УБЫВ";
	
	ВыборкаТовары = Запрос.Выполнить().Выбрать();
	
	ОтборТоваров = Новый Структура("АналитикаУчетаНоменклатуры");
	
	Пока ВыборкаТовары.Следующий() Цикл
		
		КоличествоТоваров = ВыборкаТовары.Количество;
		
		ЗаполнитьЗначенияСвойств(ОтборТоваров, ВыборкаТовары);
		
		Для Каждого СтрокаЗапасов Из ВидыЗапасов.НайтиСтроки(ОтборТоваров) Цикл
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары, "Серия, ГруппаПродукции, НазначениеПолучателя");
			НоваяСтрока.Количество = Количество;
			
			СтрокаЗапасов.Количество = СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			
			КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;
			
			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МассивУдаляемыхСтрок = ВидыЗапасов.НайтиСтроки(Новый Структура("Количество", 0));
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		ВидыЗапасов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
