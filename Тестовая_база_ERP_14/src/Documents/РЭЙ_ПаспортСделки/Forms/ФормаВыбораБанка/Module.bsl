&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РЭЙ_КонтрактыВЭДБанковскиеСчета.БанковскийСчет.Банк КАК Банк,
	|	РЭЙ_КонтрактыВЭДБанковскиеСчета.БанковскийСчет
	|ИЗ
	|	Справочник.РЭЙ_КонтрактыВЭД.БанковскиеСчета КАК РЭЙ_КонтрактыВЭДБанковскиеСчета
	|ГДЕ
	|	РЭЙ_КонтрактыВЭДБанковскиеСчета.Ссылка = &КонтрактВЭД
	|	И РЭЙ_КонтрактыВЭДБанковскиеСчета.БанковскийСчет.Банк.РЭЙ_НеЯвляетсяРезидентом = ЛОЖЬ
	|ИТОГИ ПО
	|	Банк";
	
	Запрос.УстановитьПараметр("КонтрактВЭД", Параметры.КонтрактВЭД);
	
	ДеревоЗначений = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДеревоЗначений.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ЗначениеВРеквизитФормы(ДеревоЗначений, "Дерево");
	
	ТекСтрокаДерева = Неопределено;
	
	Для Каждого СтрокаДерева Из Дерево.ПолучитьЭлементы() Цикл
		СтрокаДерева.Наименование = Строка(СтрокаДерева.Банк);
		Для Каждого СтрокаДерева2 Из СтрокаДерева.ПолучитьЭлементы() Цикл
			СтрокаДерева2.Наименование = Строка(СтрокаДерева2.БанковскийСчет);
			Если ЗначениеЗаполнено(Параметры.БанковскийСчет) и (СтрокаДерева2.БанковскийСчет = Параметры.БанковскийСчет) Тогда
				ТекСтрокаДерева = СтрокаДерева2;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ТекСтрокаДерева <> Неопределено Тогда
		Элементы.Дерево.ТекущаяСтрока = ТекСтрокаДерева.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекДанные = Элементы.Дерево.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
		
	Если ЗначениеЗаполнено(ТекДанные.БанковскийСчет) Тогда
		ОповеститьОВыборе(ТекДанные.БанковскийСчет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПередСворачиванием(Элемент, Строка, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры



