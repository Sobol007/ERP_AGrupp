
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет список команд создания на основании.
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = Документы.ПринятиеКУчетуОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 1;
	КонецЕсли;
	
	Команда = Документы.ПринятиеКУчетуОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 2;
	КонецЕсли;
	
	Команда = Документы.ПеремещениеОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 3;
	КонецЕсли;
	
	Команда = Документы.ПеремещениеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 4;
	КонецЕсли;
	
	Команда = Документы.ПереоценкаОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 5;
	КонецЕсли;
	
	Команда = Документы.СписаниеОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 6;
	КонецЕсли;
	
	Команда = Документы.СписаниеОС2_4.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 7;
	КонецЕсли;
	
	Команда = Документы.ОперацияБух.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.Порядок = 8;
	КонецЕсли;
	
КонецПроцедуры

// Добавляет команду создания документа "Инвентаризация ОС".
//
// Параметры:
//   КомандыСозданияНаОсновании - ТаблицаЗначений - Таблица с командами создания на основании. Для изменения.
//       См. описание 1 параметра процедуры СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.ИнвентаризацияОС) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ИнвентаризацияОС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ИнвентаризацияОС);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_2,ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения.
//       См. описание 1 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//   Параметры - Структура - Вспомогательные параметры. Для чтения.
//       См. описание 2 параметра процедуры ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	
КонецПроцедуры

#Область ЗаполнениеНаОсновании

// Возвращает результаты запросов данных для заполнения документа на основании инвентаризации ОС.
//
// Параметры:
// 		ДокументОснование - ДокументСсылка.ИнвентаризацияОС - Ссылка на документ инвентаризации
// 		ДействияДокументовНаОсновании - Строка - Перечисленные через запятую строковые идентификаторы типа документа созданного на основании
// 		МассивНомеровСтрок - Массив - Массив с номерами строк, на основании которыех требуется заполнить документ.
//
// Возвращаемое значение:
// 		Структура - Поименованные результаты запроса выборки данных для заполнения документа на основании инвентаризации ОС
// 			* Реквизиты - РезультатЗапроса - Результат запроса данных реквизитов документа
// 			* ТабличнаяЧасть - РезультатЗапроса - Результат запроса данных табличной части.
//
Функция ДанныеЗаполненияДокументовНаОсновании(ДокументОснование, ДействияДокументовНаОсновании = "", Знач МассивНомеровСтрок=Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура("Реквизиты, ТабличнаяЧасть");
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	#Область ТекстЗапроса
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ИнвентаризацияОС.Дата КАК ДатаДокумента,
	|	ИнвентаризацияОС.Организация КАК Организация
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|ГДЕ
	|	ИнвентаризацияОС.Ссылка = &Ссылка";
	#КонецОбласти
	
	Результат = Запрос.Выполнить();
	ВозвращаемоеЗначение.Вставить("Реквизиты", Результат);
	
	Выборка = Результат.Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ДобавитьВременнуюТаблицуСозданныхДокументовНаОсновании(МВТ, ДокументОснование, ДействияДокументовНаОсновании);
	
	Запрос.УстановитьПараметр("Дата", Выборка.ДатаДокумента);
	МассивДействий = Перечисления.ДействияНаОснованииИнвентаризации.МассивЗначенийПоЗаданнымИменам(ДействияДокументовНаОсновании);
	Запрос.УстановитьПараметр("МассивДействийЗадан", МассивДействий.Количество()<>0);
	Запрос.УстановитьПараметр("МассивДействий", МассивДействий);
	Запрос.УстановитьПараметр("МассивНомеровСтрок", ?(МассивНомеровСтрок=Неопределено, Новый Массив, МассивНомеровСтрок));
	Запрос.УстановитьПараметр("МассивНомеровСтрокЗадан", ?(МассивНомеровСтрок=Неопределено, Ложь, Истина));
	
	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	МассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	Запрос.УстановитьПараметр("МассивСубконтоСчетовАрендодателя", МассивСубконто);
	
	
	#Область ТекстЗапроса
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК ИнвентаризацияСсылка,
	|	Операция.Организация КАК ИнвентаризацияОрганизация,
	|	Операция.ПодразделениеОрганизации КАК ИнвентаризацияПодразделение,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ТабличнаяЧасть.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	ТабличнаяЧасть.НаличиеПоДаннымУчета КАК НаличиеПоДаннымУчета,
	|	ТабличнаяЧасть.СтоимостьФактическая КАК СтоимостьФактическая,
	|	ТабличнаяЧасть.НаличиеФактическое КАК НаличиеФактическое,
	|	ТабличнаяЧасть.СчетУчета КАК СчетУчета,
	|	ТабличнаяЧасть.МОЛ КАК МОЛ,
	|	ТабличнаяЧасть.Действие
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОС.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (НЕ &МассивНомеровСтрокЗадан
	|			ИЛИ ТабличнаяЧасть.НомерСтроки В (&МассивНомеровСтрок))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто2 КАК ОсновноеСредство,
	|	МАКСИМУМ(ХозрасчетныйОстатки.Субконто1) КАК Арендодатель
	|ПОМЕСТИТЬ втДанныеАрендодателя
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Дата,
	|			Счет В (ВЫБРАТЬ Т.СчетУчета ИЗ втДанныеДокумента КАК Т),
	|			&МассивСубконтоСчетовАрендодателя,
	|			(НЕ &МассивДействийЗадан
	|				ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ВыбытиеАрендованныхОС) В (&МассивДействий))
	|				И (Организация, Подразделение, Субконто2) В
	|					(ВЫБРАТЬ
	|						Т.ИнвентаризацияОрганизация,
	|						Т.ИнвентаризацияПодразделение,
	|						Т.ОсновноеСредство
	|					ИЗ
	|						втДанныеДокумента КАК Т)) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(НЕ &МассивДействийЗадан
	|			ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ВыбытиеАрендованныхОС) В (&МассивДействий))
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	втДанныеДокумента.ИнвентаризацияПодразделение КАК Подразделение,
	|	втДанныеДокумента.СчетУчета КАК СчетУчета,
	|	втДанныеДокумента.МОЛ КАК МОЛ,
	|	Местонахождения.МОЛ КАК УчетМОЛ,
	|	Местонахождения.Местонахождение КАК УчетПодразделение,
	|	Состояния.Состояние КАК УчетСостояние,
	|	ВЫБОР
	|		КОГДА Счета.ГруппаФинансовогоУчета <> ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаВнеоборотныхАктивов.ПустаяСсылка)
	|			ТОГДА Счета.ГруппаФинансовогоУчета.СчетУчета 
	|		ИНАЧЕ Счета.СчетУчета
	|	КОНЕЦ КАК УчетСчетУчета,
	|	втДанныеДокумента.СчетУчета В ИЕРАРХИИ (
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеОбъектовОсновныхСредств), 
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеЗемельныхУчастков), 
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПриобретениеОбъектовПриродопользования), 
	|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств)) КАК ЭтоСчетГруппы08,
	|	втДанныеДокумента.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	втДанныеДокумента.НаличиеПоДаннымУчета КАК НаличиеПоДаннымУчета,
	|	втДанныеДокумента.СтоимостьФактическая КАК СтоимостьФактическая,
	|	втДанныеДокумента.НаличиеФактическое КАК НаличиеФактическое,
	|	втДанныеДокумента.Действие КАК Действие
	|ПОМЕСТИТЬ втДанныеЗаполнения
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|				&Дата,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						втДанныеДокумента.ИнвентаризацияОрганизация,
	|						втДанныеДокумента.ОсновноеСредство
	|					ИЗ
	|						втДанныеДокумента КАК втДанныеДокумента)) КАК Счета
	|		ПО втДанныеДокумента.ОсновноеСредство = Счета.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|				&Дата,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						втДанныеДокумента.ИнвентаризацияОрганизация,
	|						втДанныеДокумента.ОсновноеСредство
	|					ИЗ
	|						втДанныеДокумента КАК втДанныеДокумента)) КАК Местонахождения
	|		ПО втДанныеДокумента.ОсновноеСредство = Местонахождения.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(
	|				&Дата,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						втДанныеДокумента.ИнвентаризацияОрганизация,
	|						втДанныеДокумента.ОсновноеСредство
	|					ИЗ
	|						втДанныеДокумента КАК втДанныеДокумента)) КАК Состояния
	|		ПО втДанныеДокумента.ОсновноеСредство = Состояния.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеЗаполнения.ОсновноеСредство,
	|	втДанныеЗаполнения.Подразделение,
	|	втДанныеЗаполнения.СчетУчета,
	|	втДанныеЗаполнения.МОЛ,
	|	втДанныеЗаполнения.УчетМОЛ,
	|	втДанныеЗаполнения.УчетПодразделение,
	|	втДанныеЗаполнения.УчетСостояние,
	|	втДанныеЗаполнения.УчетСчетУчета,
	|	втДанныеЗаполнения.ЭтоСчетГруппы08,
	|	втДанныеЗаполнения.СтоимостьПоДаннымУчета,
	|	втДанныеЗаполнения.НаличиеПоДаннымУчета,
	|	втДанныеЗаполнения.СтоимостьФактическая,
	|	втДанныеЗаполнения.НаличиеФактическое,
	|	втДанныеЗаполнения.Действие,
	|	втДокументыНаОсновании.Ссылка КАК АктуальныйДокумент,
	|	втДанныеЗаполнения.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	втДанныеЗаполнения.ОсновноеСредство.Код КАК ИнвентарныйНомер,
	|	втДанныеЗаполнения.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	втДанныеЗаполнения.ОсновноеСредство.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	втДанныеАрендодателя.Арендодатель КАК Арендодатель
	|ИЗ
	|	втДанныеЗаполнения КАК втДанныеЗаполнения
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДокументыНаОсновании КАК втДокументыНаОсновании
	|		ПО втДанныеЗаполнения.ОсновноеСредство = втДокументыНаОсновании.ОсновноеСредство
	|			И втДанныеЗаполнения.Действие = втДокументыНаОсновании.Действие
	|			И втДанныеЗаполнения.Подразделение = втДокументыНаОсновании.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДанныеАрендодателя КАК втДанныеАрендодателя
	|		ПО втДанныеЗаполнения.ОсновноеСредство = втДанныеАрендодателя.ОсновноеСредство
	|ГДЕ
	|	(НЕ &МассивДействийЗадан
	|			ИЛИ втДанныеЗаполнения.Действие В (&МассивДействий))
	|	И втДокументыНаОсновании.Ссылка ЕСТЬ NULL ";
	#КонецОбласти
	
	ВозвращаемоеЗначение.Вставить("ТабличнаяЧасть", Запрос.Выполнить());
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Добавляет в менеджер временную таблицу созданных на основании инвентаризации документов.
//
// Параметры:
// 		МВТ - МенеджерВременныхТаблиц - Менеджер назначения
// 		ДокументОснование - ДокументСсылка.ИнвентаризацияОС - Ссылка на документ инвентаризации
// 		ДействияДокументовНаОсновании - Строка - Перечисленные через запятую строковые идентификаторы типа документа созданного на основании
// 			Принимаемые значения -- идентификаторы значений перечисления "ДействияНаОснованииИнвентаризации"
// 			Если параметр не задан, отбор по типу не накладывается.
//
Процедура ДобавитьВременнуюТаблицуСозданныхДокументовНаОсновании(МВТ, ДокументОснование, ДействияДокументовНаОсновании = "") Экспорт
	
	МассивДействий = Перечисления.ДействияНаОснованииИнвентаризации.МассивЗначенийПоЗаданнымИменам(ДействияДокументовНаОсновании);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.УстановитьПараметр("МассивДействийЗадан", МассивДействий.Количество()<>0);
	Запрос.УстановитьПараметр("МассивДействий", МассивДействий);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	ДобавитьСозданныеДокументыНаОсновании2_4(МВТ, ДокументОснование, МассивДействий);
	
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Перемещение) КАК Действие,
	|	Операция.ПодразделениеПолучатель КАК Подразделение,
	|	Операция.Подразделение КАК ПодразделениеПолучатель
	|ПОМЕСТИТЬ втДокументыНаОснованииДанные
	|ИЗ
	|	Документ.ПеремещениеОС КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументНаОсновании
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Перемещение) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ПринятиеКУчету),
	|	Операция.Подразделение,
	|	NULL
	|ИЗ
	|	Документ.ПринятиеКУчетуОС КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументНаОсновании
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ПринятиеКУчету) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Списание),
	|	Операция.Подразделение,
	|	Операция.Подразделение
	|ИЗ
	|	Документ.СписаниеОС КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументНаОсновании
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Списание) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ВыбытиеАрендованныхОС),
	|	Операция.Подразделение,
	|	Операция.Подразделение
	|ИЗ
	|	Документ.ВыбытиеАрендованныхОС КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыбытиеАрендованныхОС.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументНаОсновании
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ВыбытиеАрендованныхОС) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ХозрасчетныйСубконто.Значение,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция),
	|	Хозрасчетный.ПодразделениеДт,
	|	NULL
	|ИЗ
	|	Документ.ОперацияБух.ЗаполнениеДвижений КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО Операция.Ссылка = Хозрасчетный.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО (Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор)
	|			И (Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки)
	|			И (ХозрасчетныйСубконто.Вид В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства)))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|ГДЕ
	|	Операция.Ссылка.Проведен
	|	И Операция.Документ = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ХозрасчетныйСубконто.Значение,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция),
	|	Хозрасчетный.ПодразделениеКт,
	|	NULL
	|ИЗ
	|	Документ.ОперацияБух.ЗаполнениеДвижений КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО Операция.Ссылка = Хозрасчетный.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО (Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор)
	|			И (Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки)
	|			И (ХозрасчетныйСубконто.Вид В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства)))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит))
	|ГДЕ
	|	Операция.Ссылка.Проведен
	|	И Операция.Документ = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция) В (&МассивДействий))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(втДокументыНаОснованииДанные.Ссылка) КАК Ссылка,
	|	втДокументыНаОснованииДанные.ОсновноеСредство КАК ОсновноеСредство,
	|	втДокументыНаОснованииДанные.Действие КАК Действие,
	|	втДокументыНаОснованииДанные.Подразделение КАК Подразделение,
	|	втДокументыНаОснованииДанные.ПодразделениеПолучатель КАК ПодразделениеПолучатель
	|ПОМЕСТИТЬ втДокументыНаОсновании
	|ИЗ
	|	(ВЫБРАТЬ
	|		втДокументыНаОснованииДанные.Ссылка КАК Ссылка,
	|		втДокументыНаОснованииДанные.ОсновноеСредство КАК ОсновноеСредство,
	|		втДокументыНаОснованииДанные.Действие КАК Действие,
	|		втДокументыНаОснованииДанные.Подразделение КАК Подразделение,
	|		втДокументыНаОснованииДанные.ПодразделениеПолучатель КАК ПодразделениеПолучатель
	|	ИЗ
	|		втДокументыНаОснованииДанные КАК втДокументыНаОснованииДанные
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		втДокументыНаОснованииДанные.Ссылка,
	|		втДокументыНаОснованииДанные.ОсновноеСредство,
	|		втДокументыНаОснованииДанные.Действие,
	|		втДокументыНаОснованииДанные.Подразделение,
	|		втДокументыНаОснованииДанные.ПодразделениеПолучатель
	|	ИЗ
	|		втДокументыНаОсновании2_4 КАК втДокументыНаОснованииДанные) КАК втДокументыНаОснованииДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	втДокументыНаОснованииДанные.ОсновноеСредство,
	|	втДокументыНаОснованииДанные.Действие,
	|	втДокументыНаОснованииДанные.Подразделение,
	|	втДокументыНаОснованииДанные.ПодразделениеПолучатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втДокументыНаОснованииДанные";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает результаты запросов данных для заполнения документа на основании инвентаризации ОС.
//
// Параметры:
//  ДокументОснование				 - ДокументСсылка.ИнвентаризацияОС	 - Ссылка на документ инвентаризации
//  Дата							 - Дата								 - Дата документа.
//  ДействияДокументовНаОсновании	 - Строка							 - Перечисленные через запятую строковые идентификаторы типа документа созданного на основании
//  МассивНомеровСтрок				 - Массив							 - Массив с номерами строк, на основании которыех требуется заполнить документ.
// 
// Возвращаемое значение:
//  Структура - Поименованные результаты запроса выборки данных для заполнения документа на основании инвентаризации ОС
//  * Реквизиты - РезультатЗапроса - Результат запроса данных реквизитов документа
//  * ТабличнаяЧасть - РезультатЗапроса - Результат запроса данных табличной части.
//
Функция ДанныеЗаполненияНаОснованииИнвентаризации(ДокументОснование, Дата, ДействияДокументовНаОсновании = "", Знач МассивНомеровСтрок = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура("Реквизиты, ТабличнаяЧасть");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИнвентаризацияОС.Дата КАК ДатаДокумента,
	|	ИнвентаризацияОС.Организация КАК Организация,
	|	ИнвентаризацияОС.ПодразделениеОрганизации КАК Местонахождение,
	|	ИСТИНА КАК ОтражатьВРегл,
	|	ИСТИНА КАК ОтражатьВУпрУчете
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|ГДЕ
	|	ИнвентаризацияОС.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	ВозвращаемоеЗначение.Вставить("Реквизиты", Результат);
	
	Выборка = Результат.Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	МенеджерТаблиц = Новый МенеджерВременныхТаблиц;
	МассивДействий = Перечисления.ДействияНаОснованииИнвентаризации.МассивЗначенийПоЗаданнымИменам(ДействияДокументовНаОсновании);
	ДобавитьСозданныеДокументыНаОсновании2_4(МенеджерТаблиц, ДокументОснование, МассивДействий);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("МассивДействийЗадан", ЗначениеЗаполнено(МассивДействий));
	Запрос.УстановитьПараметр("МассивДействий", МассивДействий);
	Запрос.УстановитьПараметр("МассивНомеровСтрок", ?(МассивНомеровСтрок = Неопределено, Новый Массив, МассивНомеровСтрок));
	Запрос.УстановитьПараметр("МассивНомеровСтрокЗадан", ?(МассивНомеровСтрок = Неопределено, Ложь, Истина));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИнвентаризацияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ИнвентаризацияОС.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ИнвентаризацияОС.ОсновноеСредство.Код КАК ИнвентарныйНомер,
	|	ИнвентаризацияОС.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ИнвентаризацияОС.ОсновноеСредство.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ИнвентаризацияОС.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	ИнвентаризацияОС.СтоимостьФактическая КАК СтоимостьФактическая
	|ПОМЕСТИТЬ ИнвентаризацияОС
	|ИЗ
	|	Документ.ИнвентаризацияОС.ОС КАК ИнвентаризацияОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДокументыНаОсновании2_4 КАК ДокументыНаОсновании
	|		ПО ИнвентаризацияОС.ОсновноеСредство = ДокументыНаОсновании.ОсновноеСредство
	|			И ИнвентаризацияОС.Действие = ДокументыНаОсновании.Действие
	|ГДЕ
	|	ИнвентаризацияОС.Ссылка = &Ссылка
	|	И (НЕ &МассивНомеровСтрокЗадан
	|			ИЛИ ИнвентаризацияОС.НомерСтроки В (&МассивНомеровСтрок))
	|	И (НЕ &МассивДействийЗадан
	|			ИЛИ ИнвентаризацияОС.Действие В (&МассивДействий))
	|	И ДокументыНаОсновании.Ссылка ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Организация КАК Организация
	|ПОМЕСТИТЬ МестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОС.СрезПоследних(
	|			&Дата,
	|			ОсновноеСредство В
	|				(ВЫБРАТЬ
	|					ИнвентаризацияОС.ОсновноеСредство
	|				ИЗ
	|					ИнвентаризацияОС КАК ИнвентаризацияОС)) КАК МестонахождениеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОСБУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСБУ.Состояние КАК Состояние
	|ПОМЕСТИТЬ ПорядокУчетаОСБУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|			&Дата,
	|			(ОсновноеСредство, Организация) В
	|				(ВЫБРАТЬ
	|					МестонахождениеОС.ОсновноеСредство,
	|					МестонахождениеОС.Организация
	|				ИЗ
	|					МестонахождениеОС КАК МестонахождениеОС)) КАК ПорядокУчетаОСБУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокУчетаОСУУ.ОсновноеСредство КАК ОсновноеСредство,
	|	ПорядокУчетаОСУУ.Состояние КАК Состояние
	|ПОМЕСТИТЬ ПорядокУчетаОСУУ
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСУУ.СрезПоследних(
	|			&Дата,
	|			(ОсновноеСредство, Организация) В
	|				(ВЫБРАТЬ
	|					МестонахождениеОС.ОсновноеСредство,
	|					МестонахождениеОС.Организация
	|				ИЗ
	|					МестонахождениеОС КАК МестонахождениеОС)) КАК ПорядокУчетаОСУУ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ИнвентаризацияОС.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ИнвентаризацияОС.ОсновноеСредство.Код КАК ИнвентарныйНомер,
	|	ИнвентаризацияОС.ОсновноеСредство.ГруппаОС КАК ГруппаОС,
	|	ИнвентаризацияОС.ОсновноеСредство.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ИнвентаризацияОС.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	ИнвентаризацияОС.СтоимостьФактическая КАК СтоимостьФактическая,
	|	ЕСТЬNULL(ПорядокУчетаОСБУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ОтражатьВРеглУчете,
	|	ЕСТЬNULL(ПорядокУчетаОСУУ.Состояние, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК ОтражатьВУпрУчете
	|ИЗ
	|	ИнвентаризацияОС КАК ИнвентаризацияОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
	|		ПО (ПорядокУчетаОСБУ.ОсновноеСредство = ИнвентаризацияОС.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|		ПО (ПорядокУчетаОСУУ.ОсновноеСредство = ИнвентаризацияОС.ОсновноеСредство)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИнвентаризацияОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МестонахождениеОС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаОСБУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаОСУУ";
	ВозвращаемоеЗначение.Вставить("ТабличнаяЧасть", Запрос.Выполнить());
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ИнвентаризацияОС";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоОС" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.ПометкаУдаления,
	|	ДанныеДокумента.Номер,
	|	ДанныеДокумента.Проведен,
	|	ДанныеДокумента.Комментарий,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.ПодразделениеОрганизации,
	|	ДанныеДокумента.Ответственный
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)

	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ИнвентаризацияОС"));
	ЗначенияПараметровПроведения.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ИнвентаризацияОС);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли; 
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	&ПодразделениеОрганизации               КАК Подразделение,
	|	&ХозяйственнаяОперация                  КАК ХозяйственнаяОперация,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&Период                                 КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                          КАК НомерПервичногоДокумента,
	|	&Период    КАК ДатаОтраженияВУчете";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоОС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоОС";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаОС.НомерСтроки-1, 0) КАК НомерЗаписи,
	|	&Ссылка                           КАК Ссылка,
	|	&Период                           КАК Дата,
	|	&Организация                      КАК Организация,
	|	&ПодразделениеОрганизации         КАК Подразделение,
	|	&Проведен                         КАК Проведен,
	|	&ХозяйственнаяОперация            КАК ХозяйственнаяОперация,
	|	&ИдентификаторМетаданных          КАК ТипСсылки,
	|	ЛОЖЬ                              КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                              КАК ОтражатьВУпрУчете,
	|	ТаблицаОС.ОсновноеСредство        КАК ОсновноеСредство
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОС.ОС КАК ТаблицаОС
	|		ПО ТаблицаОС.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// ИНВ-1 (Инвентаризационная опись)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ1";
	КомандаПечати.Представление = НСтр("ru = 'ИНВ-1 (Инвентаризационная опись)';
										|en = 'INV-1 (Stocktaking sheet)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ИНВ-18 (Сличительная ведомость)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ18";
	КомандаПечати.Представление = НСтр("ru = 'ИНВ-18 (Сличительная ведомость)';
										|en = 'INV-18 (Inventory reconciliation report)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// ИНВ-22 (Приказ о проведении инвентаризации)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ИНВ22";
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьИНВ22";
	КомандаПечати.Представление = НСтр("ru = 'ИНВ-22 (Приказ о проведении инвентаризации)';
										|en = 'INV-22 (Stocktaking order)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

// Функция формирует данные для печатной формы ИНВ22
//
Функция ДанныеДляПечатнойФормыИНВ22(ПараметрыПечати, МассивОбъектов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивОбъектов);
	Запрос.УстановитьПараметр("НаименованиеИмуществаОбязательствШапка", НСтр("ru = 'основных средств';
																			|en = 'fixed assets'"));
	Запрос.УстановитьПараметр("НаименованиеИмуществаОбязательствПодвал", НСтр("ru = 'основные средства';
																				|en = 'fixed assets'"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИнвентаризацияОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОС.Дата КАК Дата,
	|	ИнвентаризацияОС.Номер КАК Номер,
	|	&НаименованиеИмуществаОбязательствШапка КАК НаименованиеИмуществаОбязательствШапка,
	|	&НаименованиеИмуществаОбязательствПодвал КАК НаименованиеИмуществаОбязательствПодвал,
	|	ИнвентаризацияОС.Организация КАК Организация,
	|	ИнвентаризацияОС.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ИнвентаризацияОС.ПодразделениеОрганизации.Наименование КАК Подразделение,
	|	ИнвентаризацияОС.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	ИнвентаризацияОС.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ИнвентаризацияОС.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	ИнвентаризацияОС.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	ИнвентаризацияОС.ПричинаПроведенияИнвентаризации КАК ПричинаПроведенияИнвентаризации,
	|	ИнвентаризацияОС.ИнвентаризационнаяКомиссия.(
	|		Ссылка,
	|		НомерСтроки,
	|		ФизЛицо.Наименование КАК ФИОПолные,
	|		"""" КАК Должность,
	|		Председатель КАК Председатель
	|	) КАК ИнвентаризационнаяКомиссия
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|ГДЕ
	|	ИнвентаризацияОС.Ссылка В(&МассивСсылок)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Проверяем, нужно ли для макета СчетЗаказа формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ1") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ИНВ1",
			НСтр("ru = 'ИНВ-1 (Инвентаризационная опись)';
				|en = 'INV-1 (Stocktaking sheet)'"),
			ПечатьИНВ1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ИНВ18") Тогда
		
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ИНВ18",
			НСтр("ru = 'ИНВ-18 (Сличительная ведомость)';
				|en = 'INV-18 (Inventory reconciliation report)'"),
			ПечатьИНВ18(МассивОбъектов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
		
КонецПроцедуры

Функция ПечатьИНВ1(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	// Создаем колонки Таблицы итогов
	ТаблицаИтогов = Новый ТаблицаЗначений();
	ТаблицаИтогов.Колонки.Добавить("ПорядковыйНомерИтогов");
	ТаблицаИтогов.Колонки.Добавить("ИтогНаличиеФактическое");
	ТаблицаИтогов.Колонки.Добавить("ИтогСтоимостьФактическая");
	ТаблицаИтогов.Колонки.Добавить("ИтогНаличиеПоДаннымУчета");
	ТаблицаИтогов.Колонки.Добавить("ИтогСтоимостьПоДаннымУчета");
	ТаблицаИтогов.Колонки.Добавить("КоличествоФактическоеПрописью");
	ТаблицаИтогов.Колонки.Добавить("СтоимостьФактическаяПрописью");
	
	// Варианты заголовков разделов с подписями печатной формы	
	ЗаголовокРазделаПодписей = Новый Структура();
	ЗаголовокРазделаПодписей.Вставить("МОЛ",                  НСтр("ru = 'Лицо(а), ответственное(ые) за сохранность основных средств:';
																	|en = 'Person(s) responsible for fixed asset safety:'"));
	ЗаголовокРазделаПодписей.Вставить("ПредседательКомиссии", НСтр("ru = 'Председатель комиссии';
																	|en = 'Commission chairperson'"));
	ЗаголовокРазделаПодписей.Вставить("ЧленыКомиссии",        НСтр("ru = 'Члены комиссии';
																	|en = 'Commission members'"));
	ЗаголовокРазделаПодписей.Вставить("Проверяющий",          НСтр("ru = 'Указанные в настоящей описи данные и расчеты проверил';
																	|en = 'Data and calculations specified in the given sheet are checked by'"));
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	// Загрузим настройки пользователя
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияОС_ИНВ1";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияОС.ПФ_MXL_ИНВ1");
	
	// Получаем области макета для вывода в табличный документ
	Шапка            = Макет.ПолучитьОбласть("Шапка");
	Расписка         = Макет.ПолучитьОбласть("Расписка");
	Примечание       = Макет.ПолучитьОбласть("Примечание");
	Подпись          = Макет.ПолучитьОбласть("Подпись");
	ПолеДаты         = Макет.ПолучитьОбласть("ПолеДаты");
	ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ИтогоПоСтранице  = Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ИтогоПоОписи     = Макет.ПолучитьОбласть("ИтогоПоОписи");
	Итоги            = Макет.ПолучитьОбласть("Итоги");
	Подсчеты         = Макет.ПолучитьОбласть("Подсчеты");
	Претензии        = Макет.ПолучитьОбласть("Претензии");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИнвентаризацияОСОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОСОС.Ссылка.Дата КАК Дата,
	|	ИнвентаризацияОСОС.НомерСтроки КАК НомерСтроки,
	|	ИнвентаризацияОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ИнвентаризацияОСОС.МОЛ КАК МОЛ,
	|	ИнвентаризацияОСОС.Ссылка.Организация КАК Организация,
	|	ИнвентаризацияОСОС.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	ИнвентаризацияОСОС.СтоимостьФактическая КАК СтоимостьФактическая,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеПоДаннымУчета = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеПоДаннымУчета,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеФактическое = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеФактическое,
	|	МАКСИМУМ(МестонахождениеОСБухгалтерскийУчет.Период) КАК МаксПериодМеста,
	|	МАКСИМУМ(ПервоначальныеСведенияОСБухгалтерскийУчет.Период) КАК МаксПериодБУ
	|ПОМЕСТИТЬ ОСИОрганизации
	|ИЗ
	|	Документ.ИнвентаризацияОС.ОС КАК ИнвентаризацияОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
	|		ПО ИнвентаризацияОСОС.ОсновноеСредство = МестонахождениеОСБухгалтерскийУчет.ОсновноеСредство
	|			И ИнвентаризацияОСОС.Ссылка.Организация = МестонахождениеОСБухгалтерскийУчет.Организация
	|			И ИнвентаризацияОСОС.Ссылка.Дата >= МестонахождениеОСБухгалтерскийУчет.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ИнвентаризацияОСОС.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|			И ИнвентаризацияОСОС.Ссылка.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	|			И ИнвентаризацияОСОС.Ссылка.Дата >= ПервоначальныеСведенияОСБухгалтерскийУчет.Период
	|			И НЕ ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор ССЫЛКА Документ.ПоступлениеПредметовЛизинга
	|ГДЕ
	|	ИнвентаризацияОСОС.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияОСОС.Ссылка,
	|	ИнвентаризацияОСОС.ОсновноеСредство,
	|	ИнвентаризацияОСОС.МОЛ,
	|	ИнвентаризацияОСОС.СтоимостьПоДаннымУчета,
	|	ИнвентаризацияОСОС.СтоимостьФактическая,
	|	ИнвентаризацияОСОС.НомерСтроки,
	|	ИнвентаризацияОСОС.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеПоДаннымУчета = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеФактическое = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ИнвентаризацияОСОС.Ссылка.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОСИОрганизации.Ссылка,
	|	ОСИОрганизации.НомерСтроки,
	|	ОСИОрганизации.ОсновноеСредство,
	|	ОСИОрганизации.Организация,
	|	ОСИОрганизации.СтоимостьПоДаннымУчета,
	|	ОСИОрганизации.СтоимостьФактическая,
	|	ОСИОрганизации.НаличиеПоДаннымУчета,
	|	ОСИОрганизации.НаличиеФактическое,
	|	ОСИОрганизации.МаксПериодМеста,
	|	ОСИОрганизации.МаксПериодБУ,
	|	ОСИОрганизации.ОсновноеСредство.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ЕСТЬNULL(МестонахождениеОСБухгалтерскийУчет.МОЛ, НЕОПРЕДЕЛЕНО) КАК МОЛ,
	|	ЕСТЬNULL(ПоступлениеАрендованныхОС.Ссылка, НЕОПРЕДЕЛЕНО) КАК ДокументАренды,
	|	ВЫБОР
	|		КОГДА НЕ ПринятиеКУчетуОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Принятие к учету""
	|		КОГДА НЕ ПоступлениеАрендованныхОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Поступление арендованных ОС""
	|		КОГДА НЕ ПоступлениеПредметовЛизинга.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Поступление предметов лизинга""
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL И ВводОстатковВнеоборотныхАктивов.НазваниеДокументаПринятияКУчету <> """"
	|			ТОГДА ВводОстатковВнеоборотныхАктивов.НазваниеДокументаПринятияКУчету
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL
	|				ИЛИ НЕ ВводОстатковВнеоборотныхАктивов24.Ссылка ЕСТЬ NULL
	|			ТОГДА ""Ввод остатков""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДокументАрендыПредставление,
	|	ВЫБОР
	|		КОГДА НЕ ПринятиеКУчетуОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПринятиеКУчетуОС.Номер
	|		КОГДА НЕ ПоступлениеАрендованныхОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПоступлениеАрендованныхОС.Номер
	|		КОГДА НЕ ПоступлениеПредметовЛизинга.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПоступлениеПредметовЛизинга.Номер
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL И ВводОстатковВнеоборотныхАктивов.НомерДокументаПринятияКУчету <> """"
	|			ТОГДА ВводОстатковВнеоборотныхАктивов.НомерДокументаПринятияКУчету
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL
	|			ТОГДА ВводОстатковВнеоборотныхАктивов.Ссылка.Номер
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов24.Ссылка ЕСТЬ NULL
	|			ТОГДА ВводОстатковВнеоборотныхАктивов24.Ссылка.Номер
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДокументАрендыНомер,
	|	ВЫБОР
	|		КОГДА НЕ ПринятиеКУчетуОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПринятиеКУчетуОС.Дата
	|		КОГДА НЕ ПоступлениеАрендованныхОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПоступлениеАрендованныхОС.Дата
	|		КОГДА НЕ ПоступлениеПредметовЛизинга.Ссылка ЕСТЬ NULL 
	|			ТОГДА ПоступлениеПредметовЛизинга.Дата
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL
	|			ТОГДА ВводОстатковВнеоборотныхАктивов.ДатаПринятияКУчету
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов24.Ссылка ЕСТЬ NULL
	|			ТОГДА ВводОстатковВнеоборотныхАктивов24.ДатаПринятияКУчетуБУ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДокументАрендыДата,
	|	ЕСТЬNULL(ПоступлениеАрендованныхОС.Арендодатель, 
	|				ЕСТЬNULL(ПоступлениеПредметовЛизинга.Контрагент, 
	|							ЕСТЬNULL(ВводОстатковВнеоборотныхАктивов.Контрагент, НЕОПРЕДЕЛЕНО))) КАК Арендодатель
	|ПОМЕСТИТЬ ОСИСведения
	|ИЗ
	|	ОСИОрганизации КАК ОСИОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
	|		ПО ОСИОрганизации.ОсновноеСредство = МестонахождениеОСБухгалтерскийУчет.ОсновноеСредство
	|			И ОСИОрганизации.Организация = МестонахождениеОСБухгалтерскийУчет.Организация
	|			И ОСИОрганизации.МаксПериодМеста = МестонахождениеОСБухгалтерскийУчет.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ОСИОрганизации.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|			И ОСИОрганизации.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	|			И ОСИОрганизации.МаксПериодБУ = ПервоначальныеСведенияОСБухгалтерскийУчет.Период
	|			И НЕ ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор ССЫЛКА Документ.ПоступлениеПредметовЛизинга
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ПринятиеКУчетуОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеАрендованныхОС КАК ПоступлениеАрендованныхОС
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ПоступлениеАрендованныхОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеПредметовЛизинга КАК ПоступлениеПредметовЛизинга
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ПоступлениеПредметовЛизинга.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК ВводОстатковВнеоборотныхАктивов
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ВводОстатковВнеоборотныхАктивов.Ссылка)
	|			И (ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство = ВводОстатковВнеоборотныхАктивов.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4.ОС КАК ВводОстатковВнеоборотныхАктивов24
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ВводОстатковВнеоборотныхАктивов24.Ссылка)
	|			И (ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство = ВводОстатковВнеоборотныхАктивов24.ОсновноеСредство)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИнвентаризацияОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОС.Дата КАК Дата,
	|	ОСИОрганизации.МОЛ
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОСИОрганизации КАК ОСИОрганизации
	|		ПО ИнвентаризацияОС.Ссылка = ОСИОрганизации.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияОС.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР КОГДА ОСИСведения.Арендодатель = Неопределено ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ) КАК ЕстьСобственные,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОСИСведения.Арендодатель) КАК КоличествоАрендодателей,
	|	МАКСИМУМ(ОСИСведения.Арендодатель) КАК Арендодатель
	|ИЗ
	|	Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОСИСведения КАК ОСИСведения
	|		ПО (ОСИСведения.Ссылка = ОСИСведения.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияОС.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОС.Дата КАК Дата,
	|	ИнвентаризацияОС.Номер КАК Номер,
	|	ИнвентаризацияОС.Организация КАК Организация,
	|	ИнвентаризацияОС.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ИнвентаризацияОС.ПодразделениеОрганизации.Представление КАК ПодразделениеПредставление,
	|	ИнвентаризацияОС.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	ИнвентаризацияОС.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ИнвентаризацияОС.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	ИнвентаризацияОС.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	ИнвентаризацияОС.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	ОСИСведения.НомерСтроки КАК НомерСтроки,
	|	ОСИСведения.НомерСтроки КАК МинНомерСтроки,
	|	ОСИСведения.НомерСтроки КАК МаксНомерСтроки,
	|	ОСИСведения.ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ОСИСведения.ОсновноеСредство.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ОСИСведения.ОсновноеСредство.Наименование
	|		ИНАЧЕ ОСИСведения.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК ОсновноеСредствоНаименованиеПолное,
	|	ОСИСведения.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	ОСИСведения.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ОСИСведения.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
	|	ОСИСведения.СтоимостьПоДаннымУчета,
	|	ОСИСведения.СтоимостьФактическая КАК СтоимостьФактическая,
	|	ОСИСведения.НаличиеПоДаннымУчета КАК НаличиеПоДаннымУчета,
	|	ОСИСведения.НаличиеФактическое КАК НаличиеФактическое,
	|	ОСИСведения.МаксПериодМеста,
	|	ОСИСведения.МаксПериодБУ,
	|	ОСИСведения.ИнвентарныйНомер,
	|	ИнвентаризацияОС.ИнвентаризационнаяКомиссия.(
	|		ФизЛицо,
	|		Председатель КАК Председатель
	|	) КАК Комиссия,
	|	ОСИСведения.ДокументАрендыПредставление,
	|	ОСИСведения.ДокументАрендыНомер,
	|	ОСИСведения.ДокументАрендыДата,
	|	ОСИСведения.Арендодатель
	|ИЗ
	|	ОСИСведения КАК ОСИСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|		ПО ОСИСведения.Ссылка = ИнвентаризацияОС.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	ОСИСведения.НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаМОЛ = РезультатЗапроса[2].Выбрать();
	ВыборкаАрендодатель = РезультатЗапроса[3].Выбрать();
	Док = РезультатЗапроса[4].Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ТаблицаИтогов.Очистить();
		
		ТаблицаИнвентаризационнаяКомиссия = Док.Комиссия.Выгрузить();
		
		РежимСобственности = НСтр("ru = 'в собственности';
									|en = 'in property'");
		
		//////////////////////////////////////////////////////////////////////
		// 1-я страница формы
		
		// Выборка по Арендодателям обходится синхронно с выборкой документов
		Арендодатель = Неопределено;
		Если ВыборкаАрендодатель.НайтиСледующий(Док.Ссылка, "Ссылка") Тогда
			Если ВыборкаАрендодатель.ЕстьСобственные И ВыборкаАрендодатель.КоличествоАрендодателей > 1 Тогда
				РежимСобственности = НСтр("ru = 'в собственности организации, в т.ч. арендованные';
											|en = 'owned by company, including leased'");
			ИначеЕсли НЕ ВыборкаАрендодатель.ЕстьСобственные Тогда
				Если ВыборкаАрендодатель.КоличествоАрендодателей = 1 Тогда
					Арендодатель = ВыборкаАрендодатель.Арендодатель;
				КонецЕсли;
				РежимСобственности = НСтр("ru = 'арендованные';
											|en = 'rented'");
			КонецЕсли; 
		КонецЕсли;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		
		Шапка.Параметры.Заполнить(Док);
		Шапка.Параметры.Организация          = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации);
		Шапка.Параметры.ОрганизацияКодПоОКПО = СведенияОбОрганизации.КодПоОКПО;
		Шапка.Параметры.РежимСобственности   = РежимСобственности;
		Шапка.Параметры.Подразделение        = Док.ПодразделениеПредставление;
		Шапка.Параметры.Местонахождение      = Док.ПодразделениеПредставление;
		
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер);
		Шапка.Параметры.ДатаДокумента  = Док.Дата;
		
		Шапка.Параметры.Арендодатель = Арендодатель;
		
		ТабличныйДокумент.Вывести(Шапка);
		
		// Выведем расписку МОЛов 
		ТабличныйДокумент.Вывести(Расписка);
		
		// Выборка по МОЛ обходится синхронно с выборкой документов
		ВыборкаМОЛ.СледующийПоЗначениюПоля("Ссылка");
		МассивМОЛ = Новый Массив;
		Пока ВыборкаМОЛ.Следующий() Цикл
			Если МассивМОЛ.Найти(ВыборкаМОЛ.МОЛ) = Неопределено Тогда
				МассивМОЛ.Добавить(ВыборкаМОЛ.МОЛ);
			КонецЕсли;
		КонецЦикла;
		
		// Выведем подписи МОЛов
		ВыводитьЗаголовок  = Истина; // Флаг вывода заголовка набора подписей
		ВыводитьПримечание = Истина; // Флаг вывода примечания-звездочки
		
		ДанныеМОЛ = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛиц(Док.Организация, МассивМОЛ, Док.Дата);
		
		// Сначала выведем МОЛов из выборки
		Для каждого СтрокаМОЛ Из ДанныеМОЛ Цикл
			
			ПодписьСПримечанием = Новый Массив;
			ПодписьСПримечанием.Добавить(Подпись);
			ПодписьСПримечанием.Добавить(Примечание);
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ПодписьСПримечанием) Тогда
				
				// Выведем примечание
				ТабличныйДокумент.Вывести(Примечание);
				ВыводитьПримечание = Ложь;
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.МОЛ, "");
			Подпись.Параметры.Должность                = СтрокаМОЛ.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = СтрокаМОЛ.Представление;
			
			ТабличныйДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 3-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если ДанныеМОЛ.Количество() < 3 Тогда
			
			ПодписьСПримечанием = Новый Массив;
			ПодписьСПримечанием.Добавить(Подпись);
			ПодписьСПримечанием.Добавить(Примечание);
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ПодписьСПримечанием) Тогда
				
				// Выведем примечание
				ТабличныйДокумент.Вывести(Примечание);
				ВыводитьПримечание = Ложь;
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			Для Итератор = ДанныеМОЛ.Количество() + 1 По 3 Цикл
				
				Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.МОЛ, "");
				Подпись.Параметры.Должность                = "";
				Подпись.Параметры.РасшифровкаПодписи       = "";
				
				ТабличныйДокумент.Вывести(Подпись);
				
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Выведем примечание
		Если ВыводитьПримечание Тогда
			ТабличныйДокумент.Вывести(Примечание);
		КонецЕсли;
		
		// Выведем разрыв страницы
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		//////////////////////////////////////////////////////////////////////
		// 2-я страница формы
		
		// Выведем заголовок таблицы	
		ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
		
		ПорядковыйНомерИтогов = 1; // счетчик количества итоговых строк в таблице
		
		НаличиеФактическоеПоСтранице          = 0;
		СтоимостьФактическаяПоСтранице        = 0;
		НаличиеПоДаннымУчетаПоСтранице        = 0;
		СтоимостьПоДаннымУчетаПоСтранице      = 0;
		КоличествоПорядковыхНомеровНаСтранице = 0;
		
		НаличиеФактическоеПоОписи          = 0;
		СтоимостьФактическаяПоОписи        = 0;
		КоличествоПорядковыхНомеровПоОписи = 0;
		
		// Выведем строки таблицы
		Пока Док.Следующий() Цикл
			
			СтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
			
			СтрокаТаблицы.Параметры.Заполнить(Док);
			
			СтрокаТаблицы.Параметры.ДокументАрендыНомер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.ДокументАрендыНомер, Истина, Ложь);
			
			// Присвоим имена с номерами строк областям таблицы.
			// Номера потребуются для включения/отключения видимости значений в ячейках.
			СтрокаТаблицы.Области.НаличиеФактическое.Имя     = "НаличиеФактическое"        + Формат(Док.НомерСтроки, "ЧДЦ=0; ЧН=; ЧГ=");
			СтрокаТаблицы.Области.СтоимостьФактическая.Имя   = "СтоимостьФактическая"      + Формат(Док.НомерСтроки, "ЧДЦ=0; ЧН=; ЧГ=");
			СтрокаТаблицы.Области.НаличиеПоДаннымУчета.Имя   = "НаличиеПоДаннымУчета"      + Формат(Док.НомерСтроки, "ЧДЦ=0; ЧН=; ЧГ=");
			СтрокаТаблицы.Области.СтоимостьПоДаннымУчета.Имя = "СтоимостьПоДаннымУчета"    + Формат(Док.НомерСтроки, "ЧДЦ=0; ЧН=; ЧГ=");
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(СтрокаТаблицы);
			СтрокаСПодвалом.Добавить(ИтогоПоСтранице);
			СтрокаСПодвалом.Добавить(Итоги);
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда
				
				ИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
				
				// Присвоим имена с порядковыми номерами итогов областям таблицы.
				// Номера потребуются для включения/отключения видимости значений в ячейках.
				ИтогоПоСтранице.Области.ИтогНаличиеФактическое.Имя     = "ИтогНаличиеФактическое"     + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
				ИтогоПоСтранице.Области.ИтогСтоимостьФактическая.Имя   = "ИтогСтоимостьФактическая"   + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
				ИтогоПоСтранице.Области.ИтогНаличиеПоДаннымУчета.Имя   = "ИтогНаличиеПоДаннымУчета"   + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
				ИтогоПоСтранице.Области.ИтогСтоимостьПоДаннымУчета.Имя = "ИтогСтоимостьПоДаннымУчета" + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
				
				// Выведем заголовок итогов по странице
				ИтогоПоСтранице.Параметры.ИтогСтоимостьФактическая   = СтоимостьФактическаяПоСтранице;
				ИтогоПоСтранице.Параметры.ИтогНаличиеФактическое     = НаличиеФактическоеПоСтранице;
				ИтогоПоСтранице.Параметры.ИтогСтоимостьПоДаннымУчета = СтоимостьПоДаннымУчетаПоСтранице;
				ИтогоПоСтранице.Параметры.ИтогНаличиеПоДаннымУчета   = НаличиеПоДаннымУчетаПоСтранице;
				
				ТабличныйДокумент.Вывести(ИтогоПоСтранице);
				
				Итоги = Макет.ПолучитьОбласть("Итоги");
				
				// Присвоим имена с порядковыми номерами итогов областям таблицы.
				// Номера потребуются для включения/отключения видимости значений в ячейках.
				Итоги.Области.КоличествоФактическоеПрописью.Имя   = "КоличествоФактическоеПрописью" + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
				Итоги.Области.СтоимостьФактическаяПрописью.Имя    = "СтоимостьФактическаяПрописью"  + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
				
				КоличествоФактическоеПрописью = ЧислоПрописью(НаличиеФактическоеПоСтранице,, ",,,ж,,,,,0");
				СтоимостьФактическаяПрописью  = РаботаСКурсамиВалют.СформироватьСуммуПрописью(СтоимостьФактическаяПоСтранице, ВалютаРегламентированногоУчета);
				
				// Выведем итоговые строки
				Итоги.Параметры.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоличествоПорядковыхНомеровНаСтранице,, ",,,м,,,,,0");
				Итоги.Параметры.КоличествоФактическоеПрописью       = КоличествоФактическоеПрописью;
				Итоги.Параметры.СтоимостьФактическаяПрописью        = СтоимостьФактическаяПрописью;
				
				ТабличныйДокумент.Вывести(Итоги);
				
				// Добавим значения итогов по странице в таблицу итогов.
				// Они потребуются при включении видимости значений ячеек.
				НоваяСтрока = ТаблицаИтогов.Добавить();
				НоваяСтрока.ПорядковыйНомерИтогов         = ПорядковыйНомерИтогов;
				НоваяСтрока.ИтогНаличиеФактическое        = НаличиеФактическоеПоСтранице;
				НоваяСтрока.ИтогСтоимостьФактическая      = СтоимостьФактическаяПоСтранице;
				НоваяСтрока.ИтогНаличиеПоДаннымУчета      = НаличиеПоДаннымУчетаПоСтранице;
				НоваяСтрока.ИтогСтоимостьПоДаннымУчета    = СтоимостьПоДаннымУчетаПоСтранице;
				НоваяСтрока.КоличествоФактическоеПрописью = КоличествоФактическоеПрописью;
				НоваяСтрока.СтоимостьФактическаяПрописью  = СтоимостьФактическаяПрописью;
				
				ПорядковыйНомерИтогов = ПорядковыйНомерИтогов + 1;
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				// Выведем заголовок таблицы	
				ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
				
				НаличиеФактическоеПоСтранице          = 0;
				СтоимостьФактическаяПоСтранице        = 0;
				НаличиеПоДаннымУчетаПоСтранице        = 0;
				СтоимостьПоДаннымУчетаПоСтранице      = 0;
				КоличествоПорядковыхНомеровНаСтранице = 0;
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(СтрокаТаблицы);
			
			НаличиеФактическоеПоСтранице          = НаличиеФактическоеПоСтранице          + Док.НаличиеФактическое;
			СтоимостьФактическаяПоСтранице        = СтоимостьФактическаяПоСтранице        + Док.СтоимостьФактическая;
			НаличиеПоДаннымУчетаПоСтранице        = НаличиеПоДаннымУчетаПоСтранице        + Док.НаличиеПоДаннымУчета;
			СтоимостьПоДаннымУчетаПоСтранице      = СтоимостьПоДаннымУчетаПоСтранице      + Док.СтоимостьПоДаннымУчета;
			КоличествоПорядковыхНомеровНаСтранице = КоличествоПорядковыхНомеровНаСтранице + 1;
			
			НаличиеФактическоеПоОписи          = НаличиеФактическоеПоОписи + Док.НаличиеФактическое;
			СтоимостьФактическаяПоОписи        = СтоимостьФактическаяПоОписи + Док.СтоимостьФактическая;
			КоличествоПорядковыхНомеровПоОписи = КоличествоПорядковыхНомеровПоОписи + 1;
			
		КонецЦикла;
		
		ИтогоПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		
		// Присвоим имена с порядковыми номерами итогов областям таблицы.
		// Номера потребуются для включения/отключения видимости значений в ячейках.
		ИтогоПоСтранице.Области.ИтогНаличиеФактическое.Имя     = "ИтогНаличиеФактическое"     + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
		ИтогоПоСтранице.Области.ИтогСтоимостьФактическая.Имя   = "ИтогСтоимостьФактическая"   + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
		ИтогоПоСтранице.Области.ИтогНаличиеПоДаннымУчета.Имя   = "ИтогНаличиеПоДаннымУчета"   + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
		ИтогоПоСтранице.Области.ИтогСтоимостьПоДаннымУчета.Имя = "ИтогСтоимостьПоДаннымУчета" + Формат(ПорядковыйНомерИтогов, "ЧДЦ=0; ЧН=; ЧГ=");
		
		// Выведем заголовок итогов по странице
		ИтогоПоСтранице.Параметры.ИтогСтоимостьФактическая   = СтоимостьФактическаяПоСтранице;
		ИтогоПоСтранице.Параметры.ИтогНаличиеФактическое     = НаличиеФактическоеПоСтранице;
		ИтогоПоСтранице.Параметры.ИтогСтоимостьПоДаннымУчета = СтоимостьПоДаннымУчетаПоСтранице;
		ИтогоПоСтранице.Параметры.ИтогНаличиеПоДаннымУчета   = НаличиеПоДаннымУчетаПоСтранице;
		
		ТабличныйДокумент.Вывести(ИтогоПоСтранице);
		
		Итоги = Макет.ПолучитьОбласть("Итоги");	
		
		// Присвоим имена с порядковыми номерами итогов областям таблицы.
		// Номера потребуются для включения/отключения видимости значений в ячейках.
		Итоги.Области.КоличествоФактическоеПрописью.Имя   = "КоличествоФактическоеПрописью" + ПорядковыйНомерИтогов;
		Итоги.Области.СтоимостьФактическаяПрописью.Имя    = "СтоимостьФактическаяПрописью" + ПорядковыйНомерИтогов;
		
		КоличествоФактическоеПрописью = ЧислоПрописью(НаличиеФактическоеПоСтранице,, ",,,ж,,,,,0");
		СтоимостьФактическаяПрописью  = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СтоимостьФактическаяПоСтранице, ВалютаРегламентированногоУчета);
		
		// Выведем итоговые строки
		Итоги.Параметры.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоличествоПорядковыхНомеровНаСтранице,, ",,,м,,,,,0");
		Итоги.Параметры.КоличествоФактическоеПрописью       = КоличествоФактическоеПрописью;
		Итоги.Параметры.СтоимостьФактическаяПрописью        = СтоимостьФактическаяПрописью;
		
		ТабличныйДокумент.Вывести(Итоги);
		
		// Добавим значения итогов по странице в таблицу итогов.
		// Они потребуются при включении видимости значений ячеек.
		НоваяСтрока = ТаблицаИтогов.Добавить();
		НоваяСтрока.ПорядковыйНомерИтогов         = ПорядковыйНомерИтогов;
		НоваяСтрока.ИтогНаличиеФактическое        = НаличиеФактическоеПоСтранице;
		НоваяСтрока.ИтогСтоимостьФактическая      = СтоимостьФактическаяПоСтранице;
		НоваяСтрока.ИтогНаличиеПоДаннымУчета      = НаличиеПоДаннымУчетаПоСтранице;
		НоваяСтрока.ИтогСтоимостьПоДаннымУчета    = СтоимостьПоДаннымУчетаПоСтранице;
		НоваяСтрока.КоличествоФактическоеПрописью = КоличествоФактическоеПрописью;
		НоваяСтрока.СтоимостьФактическаяПрописью  = СтоимостьФактическаяПрописью;
		
		ПорядковыйНомерИтогов = ПорядковыйНомерИтогов + 1;
		
		// Выведем разрыв страницы
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		//////////////////////////////////////////////////////////////////////
		// 3-я страница формы
		
		// Выведем заголовок итогов по описи
		ТабличныйДокумент.Вывести(ИтогоПоОписи);
		
		Итоги = Макет.ПолучитьОбласть("Итоги");
		
		// Присвоим имена с порядковыми номерами итогов областям таблицы.
		// Номера потребуются для включения/отключения видимости значений в ячейках.
		Итоги.Области.КоличествоФактическоеПрописью.Имя   = "КоличествоФактическоеПрописьюПоОписи";
		Итоги.Области.СтоимостьФактическаяПрописью.Имя    = "СтоимостьФактическаяПрописьюПоОписи";
		
		// Выведем итоговые строки
		Итоги.Параметры.КоличествоПорядковыхНомеровПрописью = ЧислоПрописью(КоличествоПорядковыхНомеровПоОписи,, ",,,м,,,,,0");
		Итоги.Параметры.КоличествоФактическоеПрописью       = ЧислоПрописью(НаличиеФактическоеПоОписи,, ",,,ж,,,,,0");
		Итоги.Параметры.СтоимостьФактическаяПрописью        = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СтоимостьФактическаяПоОписи, ВалютаРегламентированногоУчета);
		
		ТабличныйДокумент.Вывести(Итоги);
		
		// Выведем фразу о подсчетах
		ТабличныйДокумент.Вывести(Подсчеты);
		
		// Выведем подпись председателя инвентаризационной комиссии
		ПредседательКомиссии = ТаблицаИнвентаризационнаяКомиссия.Найти(Истина, "Председатель");
		
		Если ПредседательКомиссии <> Неопределено И ПредседательКомиссии.Председатель Тогда
			
			ДанныеПредседателя = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Док.Организация, ПредседательКомиссии.ФизЛицо, Док.Дата);
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			Подпись.Параметры.Должность                = ДанныеПредседателя.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = ДанныеПредседателя.Представление;
			
		Иначе
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.ПредседательКомиссии;
			Подпись.Параметры.Должность                = "";
			Подпись.Параметры.РасшифровкаПодписи       = "";
			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Подпись);
		
		// Выведем подписи членов комиссии
		ВыводитьЗаголовок = Истина;
		
		КоличествоЧленовКомиссии = 0;
		
		// Сначала выведем членов комиссии из выборки
		Для каждого ЧленКомиссии Из ТаблицаИнвентаризационнаяКомиссия Цикл
			
			Если ЧленКомиссии.Председатель Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			КоличествоЧленовКомиссии = КоличествоЧленовКомиссии + 1;
			
			ДанныеЧленаКомиссии = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛица(Док.Организация, ЧленКомиссии.ФизЛицо, Док.Дата);
			
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.ЧленыКомиссии, "");
			Подпись.Параметры.Должность                = ДанныеЧленаКомиссии.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = ДанныеЧленаКомиссии.Представление;
			
			ТабличныйДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 3-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если КоличествоЧленовКомиссии < 3 Тогда
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			Для Итератор = КоличествоЧленовКомиссии + 1 По 3 Цикл
				
				Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.ЧленыКомиссии, "");
				Подпись.Параметры.Должность                = "";
				Подпись.Параметры.РасшифровкаПодписи       = "";
				
				ТабличныйДокумент.Вывести(Подпись);
				
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Перед выводом следующего набора подписей, нужно проверить, поместится ли он
		// на страницу целиком. Если нет, нужно начать вывод со следующей страницы.
		
		// Создаем массив строк для проверки, помещаются ли они на страницу
		СтрокаСПодвалом = Новый Массив;
		СтрокаСПодвалом.Добавить(Претензии);
		Для Итератор = 1 По ДанныеМОЛ.Количество() Цикл
			СтрокаСПодвалом.Добавить(Подпись);
		КонецЦикла;
		СтрокаСПодвалом.Добавить(ПолеДаты);
		СтрокаСПодвалом.Добавить(Подпись);
		СтрокаСПодвалом.Добавить(ПолеДаты);
		
		// Если планируемый набор подписей не помещается, начинаем новую страницу
		Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда
			
			// Выведем разрыв страницы
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЕсли;
		
		// Выведем раздел об отсутствии претензий МОЛов
		Если Док.Количество() > 0 Тогда
			Претензии.Параметры.НомерСтрокиНачальный = 1;
			Претензии.Параметры.НомерСтрокиКонечный  = Док.МаксНомерСтроки;
		Иначе	
			Претензии.Параметры.НомерСтрокиНачальный = "______";
			Претензии.Параметры.НомерСтрокиКонечный  = "______";
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(Претензии);
		
		// Выведем подписи МОЛов
		ВыводитьЗаголовок = Истина;
		
		// Сначала выведем МОЛов из выборки
		Для каждого СтрокаМОЛ Из ДанныеМОЛ Цикл
			
			ПодписьСДатой = Новый Массив;
			ПодписьСДатой.Добавить(Подпись);
			ПодписьСДатой.Добавить(ПолеДаты);
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ПодписьСДатой) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.МОЛ, "");
			Подпись.Параметры.Должность                = СтрокаМОЛ.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = СтрокаМОЛ.Представление;
			
			ТабличныйДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 3-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если ДанныеМОЛ.Количество() < 3 Тогда
			
			ПодписьСДатой = Новый Массив;
			ПодписьСДатой.Добавить(Подпись);
			ПодписьСДатой.Добавить(ПолеДаты);
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ПодписьСДатой) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
				
			КонецЕсли;
			
			Для Итератор = (ДанныеМОЛ.Количество() + 1) По 3 Цикл
				
				Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей.МОЛ, "");
				Подпись.Параметры.Должность                = "";
				Подпись.Параметры.РасшифровкаПодписи       = "";
				
				ТабличныйДокумент.Вывести(Подпись);
				
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Выведем строку с полем для заполнения даты
		ПолеДаты.Параметры.Дата = Формат(Док.Дата, "ДЛФ=DD");
		ТабличныйДокумент.Вывести(ПолеДаты);
		
		// Проверим, поместится ли проверяющий, если нет, то начнем новую страницу
		ПодписьСДатой = Новый Массив;
		ПодписьСДатой.Добавить(Подпись);
		ПодписьСДатой.Добавить(ПолеДаты);
		
		Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ПодписьСДатой) Тогда
			
			// Выведем разрыв страницы
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
		КонецЕсли;
		
		// Выведем подпись проверяющего
		Подпись.Параметры.ЗаголовокРазделаПодписей = ЗаголовокРазделаПодписей.Проверяющий;
		
		ТабличныйДокумент.Вывести(Подпись);
		
		// Выведем строку с полем для заполнения даты
		ПолеДаты.Параметры.Дата = НСтр("ru = '""______"" ___________________________ _______ г.';
										|en = '__/__/__'");
		ТабличныйДокумент.Вывести(ПолеДаты);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьИНВ18(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИнвентаризацияОС_ИНВ18";;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИнвентаризацияОС.ПФ_MXL_ИНВ18");
	
	// Получаем области макета для вывода в табличный документ
	Шапка                     = Макет.ПолучитьОбласть("Шапка");
	СекцияМОЛ                 = Макет.ПолучитьОбласть("СекцияМОЛ");
	СтрокаНадТаблицей         = Макет.ПолучитьОбласть("СтрокаНадТаблицей");
	ЗаголовокТаблицы          = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	СтрокаТаблицы             = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ПодвалТаблицы             = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ПодписьГлавногоБухгалтера = Макет.ПолучитьОбласть("ПодписьГлавногоБухгалтера");
	ШапкаПодписейМОЛ          = Макет.ПолучитьОбласть("ШапкаПодписейМОЛ");
	Подпись                   = Макет.ПолучитьОбласть("Подпись");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИнвентаризацияОСОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОСОС.Ссылка.Дата КАК Дата,
	|	ИнвентаризацияОСОС.НомерСтроки КАК НомерСтроки,
	|	ИнвентаризацияОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ИнвентаризацияОСОС.МОЛ КАК МОЛ,
	|	ИнвентаризацияОСОС.Ссылка.Организация КАК Организация,
	|	ИнвентаризацияОСОС.СтоимостьПоДаннымУчета КАК СтоимостьПоДаннымУчета,
	|	ИнвентаризацияОСОС.СтоимостьФактическая КАК СтоимостьФактическая,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеПоДаннымУчета = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеПоДаннымУчета,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеФактическое = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НаличиеФактическое,
	|	МАКСИМУМ(МестонахождениеОСБухгалтерскийУчет.Период) КАК МаксПериодМеста,
	|	МАКСИМУМ(ПервоначальныеСведенияОСБухгалтерскийУчет.Период) КАК МаксПериодБУ
	|ПОМЕСТИТЬ ОСИОрганизации
	|ИЗ
	|	Документ.ИнвентаризацияОС.ОС КАК ИнвентаризацияОСОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
	|		ПО ИнвентаризацияОСОС.ОсновноеСредство = МестонахождениеОСБухгалтерскийУчет.ОсновноеСредство
	|			И ИнвентаризацияОСОС.Ссылка.Организация = МестонахождениеОСБухгалтерскийУчет.Организация
	|			И ИнвентаризацияОСОС.Ссылка.Дата >= МестонахождениеОСБухгалтерскийУчет.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ИнвентаризацияОСОС.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|			И ИнвентаризацияОСОС.Ссылка.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	|			И ИнвентаризацияОСОС.Ссылка.Дата >= ПервоначальныеСведенияОСБухгалтерскийУчет.Период
	|			И НЕ ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор ССЫЛКА Документ.ПоступлениеПредметовЛизинга
	|ГДЕ
	|	ИнвентаризацияОСОС.Ссылка В(&МассивОбъектов)
	|	И ИнвентаризацияОСОС.НаличиеПоДаннымУчета <> ИнвентаризацияОСОС.НаличиеФактическое
	|
	|СГРУППИРОВАТЬ ПО
	|	ИнвентаризацияОСОС.Ссылка,
	|	ИнвентаризацияОСОС.ОсновноеСредство,
	|	ИнвентаризацияОСОС.МОЛ,
	|	ИнвентаризацияОСОС.СтоимостьПоДаннымУчета,
	|	ИнвентаризацияОСОС.СтоимостьФактическая,
	|	ИнвентаризацияОСОС.НомерСтроки,
	|	ИнвентаризацияОСОС.Ссылка.Организация,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеПоДаннымУчета = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИнвентаризацияОСОС.НаличиеФактическое = ИСТИНА
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ИнвентаризацияОСОС.Ссылка.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОСИОрганизации.Ссылка,
	|	ОСИОрганизации.НомерСтроки,
	|	ОСИОрганизации.ОсновноеСредство,
	|	ОСИОрганизации.Организация,
	|	ОСИОрганизации.СтоимостьПоДаннымУчета,
	|	ОСИОрганизации.СтоимостьФактическая,
	|	ОСИОрганизации.НаличиеПоДаннымУчета,
	|	ОСИОрганизации.НаличиеФактическое,
	|	ОСИОрганизации.МаксПериодМеста,
	|	ОСИОрганизации.МаксПериодБУ,
	|	ПервоначальныеСведенияОСБухгалтерскийУчет.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ЕСТЬNULL(МестонахождениеОСБухгалтерскийУчет.МОЛ, НЕОПРЕДЕЛЕНО) КАК МОЛ,
	|	ЕСТЬNULL(ПоступлениеАрендованныхОС.Ссылка, НЕОПРЕДЕЛЕНО) КАК ДокументАренды,
	|	ВЫБОР
	|		КОГДА НЕ ПоступлениеАрендованныхОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Поступление арендованных ОС""
	|		КОГДА НЕ ПоступлениеПредметовЛизинга.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Поступление предметов лизинга""
	|		КОГДА НЕ ВводОстатковВнеоборотныхАктивов.Ссылка ЕСТЬ NULL 
	|				ИЛИ НЕ ВводОстатковВнеоборотныхАктивов24.Ссылка ЕСТЬ NULL 
	|			ТОГДА ""Ввод остатков""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ДокументАрендыПредставление,
	|
	|	ЕСТЬNULL(ПоступлениеАрендованныхОС.Номер, 
	|				ЕСТЬNULL(ПоступлениеПредметовЛизинга.Номер, 
	|							ЕСТЬNULL(ВводОстатковВнеоборотныхАктивов.Ссылка.Номер, НЕОПРЕДЕЛЕНО))) КАК ДокументАрендыНомер,
	|
	|	ЕСТЬNULL(ПоступлениеАрендованныхОС.Дата, 
	|				ЕСТЬNULL(ПоступлениеПредметовЛизинга.Дата, 
	|							ЕСТЬNULL(ВводОстатковВнеоборотныхАктивов.Ссылка.Дата, НЕОПРЕДЕЛЕНО))) КАК ДокументАрендыДата,
	|
	|	ЕСТЬNULL(ПоступлениеАрендованныхОС.Арендодатель, 
	|				ЕСТЬNULL(ПоступлениеПредметовЛизинга.Контрагент, 
	|							ЕСТЬNULL(ВводОстатковВнеоборотныхАктивов.Контрагент, НЕОПРЕДЕЛЕНО))) КАК Арендодатель
	|ПОМЕСТИТЬ ОСИСведения
	|ИЗ
	|	ОСИОрганизации КАК ОСИОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет КАК МестонахождениеОСБухгалтерскийУчет
	|		ПО ОСИОрганизации.ОсновноеСредство = МестонахождениеОСБухгалтерскийУчет.ОсновноеСредство
	|			И ОСИОрганизации.Организация = МестонахождениеОСБухгалтерскийУчет.Организация
	|			И ОСИОрганизации.МаксПериодМеста = МестонахождениеОСБухгалтерскийУчет.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОСБухгалтерскийУчет
	|		ПО ОСИОрганизации.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство
	|			И ОСИОрганизации.Организация = ПервоначальныеСведенияОСБухгалтерскийУчет.Организация
	|			И ОСИОрганизации.МаксПериодБУ = ПервоначальныеСведенияОСБухгалтерскийУчет.Период
	|			И НЕ ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор ССЫЛКА Документ.ПоступлениеПредметовЛизинга
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеАрендованныхОС КАК ПоступлениеАрендованныхОС
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ПоступлениеАрендованныхОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоступлениеПредметовЛизинга КАК ПоступлениеПредметовЛизинга
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ПоступлениеПредметовЛизинга.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК ВводОстатковВнеоборотныхАктивов
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ВводОстатковВнеоборотныхАктивов.Ссылка)
	|			И (ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство = ВводОстатковВнеоборотныхАктивов.ОсновноеСредство)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов2_4.ОС КАК ВводОстатковВнеоборотныхАктивов24
	|		ПО (ПервоначальныеСведенияОСБухгалтерскийУчет.Регистратор = ВводОстатковВнеоборотныхАктивов24.Ссылка)
	|			И (ПервоначальныеСведенияОСБухгалтерскийУчет.ОсновноеСредство = ВводОстатковВнеоборотныхАктивов24.ОсновноеСредство)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИнвентаризацияОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОС.Дата КАК Дата,
	|	ОСИОрганизации.МОЛ
	|ИЗ
	|	ОСИОрганизации КАК ОСИОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|		ПО ОСИОрганизации.Ссылка = ИнвентаризацияОС.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИнвентаризацияОС.Ссылка КАК Ссылка,
	|	ИнвентаризацияОС.Дата КАК Дата,
	|	ИнвентаризацияОС.Номер КАК Номер,
	|	ИнвентаризацияОС.Организация КАК Организация,
	|	ИнвентаризацияОС.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ИнвентаризацияОС.ПодразделениеОрганизации.Представление КАК ПодразделениеПредставление,
	|	ИнвентаризацияОС.ДатаНачалаИнвентаризации КАК ДатаНачалаИнвентаризации,
	|	ИнвентаризацияОС.ДатаОкончанияИнвентаризации КАК ДатаОкончанияИнвентаризации,
	|	ИнвентаризацияОС.ДокументОснованиеВид КАК ДокументОснованиеВид,
	|	ИнвентаризацияОС.ДокументОснованиеДата КАК ДокументОснованиеДата,
	|	ИнвентаризацияОС.ДокументОснованиеНомер КАК ДокументОснованиеНомер,
	|	ОСИСведения.НомерСтроки КАК НомерСтроки,
	|	ОСИСведения.НомерСтроки КАК МинНомерСтроки,
	|	ОСИСведения.НомерСтроки КАК МаксНомерСтроки,
	|	ОСИСведения.ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ОСИСведения.ОсновноеСредство.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ОСИСведения.ОсновноеСредство.Наименование
	|		ИНАЧЕ ОСИСведения.ОсновноеСредство.НаименованиеПолное
	|	КОНЕЦ КАК ОсновноеСредствоНаименованиеПолное,
	|	ОСИСведения.ОсновноеСредство.ДатаВыпуска КАК ДатаВыпуска,
	|	ОСИСведения.ОсновноеСредство.ЗаводскойНомер КАК ЗаводскойНомер,
	|	ОСИСведения.ОсновноеСредство.НомерПаспорта КАК НомерПаспорта,
	|	ОСИСведения.СтоимостьПоДаннымУчета,
	|	ОСИСведения.СтоимостьФактическая КАК СтоимостьФактическая,
	|	ОСИСведения.НаличиеПоДаннымУчета КАК НаличиеПоДаннымУчета,
	|	ОСИСведения.НаличиеФактическое КАК НаличиеФактическое,
	|	ОСИСведения.МаксПериодМеста,
	|	ОСИСведения.МаксПериодБУ,
	|	ОСИСведения.ИнвентарныйНомер,
	|	ИнвентаризацияОС.ИнвентаризационнаяКомиссия.(
	|		ФизЛицо,
	|		Председатель КАК Председатель
	|	) КАК Комиссия,
	|	ОСИСведения.ДокументАрендыПредставление,
	|	ОСИСведения.ДокументАрендыНомер,
	|	ОСИСведения.ДокументАрендыДата,
	|	ОСИСведения.Арендодатель
	|ИЗ
	|	ОСИСведения КАК ОСИСведения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИнвентаризацияОС КАК ИнвентаризацияОС
	|		ПО (ИнвентаризацияОС.Ссылка = ОСИСведения.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка,
	|	ОСИСведения.НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаМОЛ = РезультатЗапроса[2].Выбрать();
	Док = РезультатЗапроса[3].Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока Док.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		// Выведем шапку документа
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Док.Организация, Док.Дата);
		
		Шапка.Параметры.Подразделение        = Строка(Док.ПодразделениеОрганизации);
		Шапка.Параметры.Заполнить(Док);
		Шапка.Параметры.Организация          = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации);
		Шапка.Параметры.ОрганизацияКодПоОКПО = СведенияОбОрганизации.КодПоОКПО;
		
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Док.Номер);
		Шапка.Параметры.ДатаДокумента  = Док.Дата;
		
		ТабличныйДокумент.Вывести(Шапка);
		
		// Выборка по МОЛ обходится синхронно с выборкой документов
		ВыборкаМОЛ.СледующийПоЗначениюПоля("Ссылка");
		МассивМОЛ = Новый Массив;
		Пока ВыборкаМОЛ.Следующий() Цикл
			Если МассивМОЛ.Найти(ВыборкаМОЛ.МОЛ) = Неопределено Тогда
				МассивМОЛ.Добавить(ВыборкаМОЛ.МОЛ);
			КонецЕсли;
		КонецЦикла;
		
		// Выведем подписи МОЛов
		ДанныеМОЛ = УправлениеВнеоборотнымиАктивамиПереопределяемый.ДанныеФизЛиц(Док.Организация, МассивМОЛ, Док.Дата);
		
		// Сначала выведем МОЛов из выборки
		Для каждого СтрокаМОЛ Из ДанныеМОЛ Цикл
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СекцияМОЛ) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			КонецЕсли;
			
			СекцияМОЛ.Параметры.Должность = СтрокаМОЛ.Должность;
			СекцияМОЛ.Параметры.ФИО       = СтрокаМОЛ.Представление;
			
			ТабличныйДокумент.Вывести(СекцияМОЛ);
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 2-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если ДанныеМОЛ.Количество() < 2 Тогда
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СекцияМОЛ) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
			КонецЕсли;
			
			Для Итератор = ДанныеМОЛ.Количество() + 1 По 2 Цикл
				
				СекцияМОЛ.Параметры.Должность = "";
				СекцияМОЛ.Параметры.ФИО       = "";
				
				ТабличныйДокумент.Вывести(СекцияМОЛ);
				
			КонецЦикла;
		
		КонецЕсли;
		
		// Проверим, помещаются ли строка над таблицей, заголовок и первая строка.
		ШапкаТаблицы = Новый Массив;
		ШапкаТаблицы.Добавить(СтрокаНадТаблицей);
		ШапкаТаблицы.Добавить(ЗаголовокТаблицы);
		ШапкаТаблицы.Добавить(СтрокаТаблицы);
		
		Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, ШапкаТаблицы) Тогда
				
			// Выведем разрыв страницы
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();

		КонецЕсли;
		
		// Выведем строку над таблицей
		ТабличныйДокумент.Вывести(СтрокаНадТаблицей);
		
		// Выведем заголовок таблицы	
		ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
		
		// Создадим переменные для накопления итогов по каждой странице
		ИтогИзлишекКоличество   = 0;
		ИтогИзлишекСтоимость    = 0;
		ИтогНедостачаКоличество = 0;
		ИтогНедостачаСтоимость  = 0;
		
		// Выведем строки таблицы
		Пока Док.Следующий() Цикл
			
			СтрокаТаблицы.Параметры.Заполнить(Док);
			
			СтрокаСПодвалом = Новый Массив;
			СтрокаСПодвалом.Добавить(СтрокаТаблицы);
			СтрокаСПодвалом.Добавить(ПодвалТаблицы);
			Если Док.МаксНомерСтроки = Док.Количество() Тогда  // если последняя строка, должна
				СтрокаСПодвалом.Добавить(ПодписьГлавногоБухгалтера); // помещаться и подпись гл.бухгалтера
			КонецЕсли;
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, СтрокаСПодвалом) Тогда
				
				// Выведем подвал таблицы
				ПодвалТаблицы.Параметры.ИтогИзлишекКоличество   = ИтогИзлишекКоличество;
				ПодвалТаблицы.Параметры.ИтогИзлишекСтоимость    = ИтогИзлишекСтоимость;
				ПодвалТаблицы.Параметры.ИтогНедостачаКоличество = ИтогНедостачаКоличество;
				ПодвалТаблицы.Параметры.ИтогНедостачаСтоимость  = ИтогНедостачаСтоимость;
				
				ТабличныйДокумент.Вывести(ПодвалТаблицы);
				
				// Обнулим переменные для накопления итогов по каждой странице
				ИтогИзлишекКоличество   = 0;
				ИтогИзлишекСтоимость    = 0;
				ИтогНедостачаКоличество = 0;
				ИтогНедостачаСтоимость  = 0;
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				// Выведем заголовок таблицы	
				ТабличныйДокумент.Вывести(ЗаголовокТаблицы);
				
			КонецЕсли;
			
			РазницаПоНаличию   = Док.НаличиеФактическое - Док.НаличиеПоДаннымУчета;
			РазницаПоСтоимости = Док.СтоимостьФактическая - Док.СтоимостьПоДаннымУчета;
			
			ИзлишекКоличество   = ?(РазницаПоНаличию > 0, РазницаПоНаличию, 0);
			ИзлишекСтоимость    = ?(РазницаПоСтоимости > 0, РазницаПоСтоимости, 0);
			НедостачаКоличество = ?(РазницаПоНаличию < 0, -РазницаПоНаличию, 0);
			НедостачаСтоимость  = ?(РазницаПоСтоимости < 0, -РазницаПоСтоимости, 0);
			
			ИтогИзлишекКоличество   = ИтогИзлишекКоличество + ИзлишекКоличество;
			ИтогИзлишекСтоимость    = ИтогИзлишекСтоимость + ИзлишекСтоимость;
			ИтогНедостачаКоличество = ИтогНедостачаКоличество + НедостачаКоличество;
			ИтогНедостачаСтоимость  = ИтогНедостачаСтоимость + НедостачаСтоимость;
			
			СтрокаТаблицы.Параметры.ИзлишекКоличество   = ИзлишекКоличество;
			СтрокаТаблицы.Параметры.ИзлишекСтоимость    = ИзлишекСтоимость;
			СтрокаТаблицы.Параметры.НедостачаКоличество = НедостачаКоличество;
			СтрокаТаблицы.Параметры.НедостачаСтоимость  = НедостачаСтоимость;
			
			ТабличныйДокумент.Вывести(СтрокаТаблицы);
			
		КонецЦикла;
		
		// Выведем подвал таблицы
		ПодвалТаблицы.Параметры.ИтогИзлишекКоличество   = ИтогИзлишекКоличество;
		ПодвалТаблицы.Параметры.ИтогИзлишекСтоимость    = ИтогИзлишекСтоимость;
		ПодвалТаблицы.Параметры.ИтогНедостачаКоличество = ИтогНедостачаКоличество;
		ПодвалТаблицы.Параметры.ИтогНедостачаСтоимость  = ИтогНедостачаСтоимость;
		
		ТабличныйДокумент.Вывести(ПодвалТаблицы);
		
		// Выведем подпись бухгалтера
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(Док.Организация, Док.Дата, Док.ПодразделениеОрганизации);
		
		ПодписьГлавногоБухгалтера.Параметры.РасшифровкаПодписи = Руководители.ГлавныйБухгалтерПредставление;
		
		ТабличныйДокумент.Вывести(ПодписьГлавногоБухгалтера);
		
		// Проверим, помещаются ли шапка подписей и одна подпись
		Подписи = Новый Массив;
		Подписи.Добавить(ШапкаПодписейМОЛ);
		Подписи.Добавить(Подпись);
		
		Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подписи) Тогда
				
			// Выведем разрыв страницы
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();

		КонецЕсли;
		
		// Выведем шапку подписей МОЛ
		ТабличныйДокумент.Вывести(ШапкаПодписейМОЛ);
		
		// Выведем подписи МОЛов
		ЗаголовокРазделаПодписей = НСтр("ru = 'Материально ответственное(ые) лицо(а)';
										|en = 'Financially liable person(s)'");
		ВыводитьЗаголовок = Истина;
		
		// Сначала выведем МОЛов из выборки
		Для каждого СтрокаМОЛ Из ДанныеМОЛ Цикл
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
			КонецЕсли;
			
			Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей, "");
			Подпись.Параметры.Должность                = СтрокаМОЛ.Должность;
			Подпись.Параметры.РасшифровкаПодписи       = СтрокаМОЛ.Представление;
			
			ТабличныйДокумент.Вывести(Подпись);
			
			ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
			
		КонецЦикла;
		
		// Затем выведем пустые места для подписей (чтобы в итоге получилось не менее 2-х
		// подписей, как в форме, утвержденной Госкомстатом).
		Если ДанныеМОЛ.Количество() < 2 Тогда
			
			Если НЕ ОбщегоНазначенияБПВызовСервера.ПроверитьВыводТабличногоДокумента(ТабличныйДокумент, Подпись) Тогда
				
				// Выведем разрыв страницы
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ВыводитьЗаголовок = Истина; // на новой странице выведем заголовок набора подписей
			
			КонецЕсли;
			
			Для Итератор = (ДанныеМОЛ.Количество() + 1) По 2 Цикл
				
				Подпись.Параметры.ЗаголовокРазделаПодписей = ?(ВыводитьЗаголовок, ЗаголовокРазделаПодписей, "");
				Подпись.Параметры.Должность                = "";
				Подпись.Параметры.РасшифровкаПодписи       = "";
				
				ТабличныйДокумент.Вывести(Подпись);
				
				ВыводитьЗаголовок = Ложь; // в следующей итерации вывод заголовка не нужен
				
			КонецЦикла;
		
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Док.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеНаОсновании

// Добавляет в менеджер временную таблицу созданных на основании инвентаризации документов.
//
// Параметры:
// 		МенеджерТаблиц - МенеджерВременныхТаблиц - Менеджер назначения
// 		ДокументОснование - ДокументСсылка.ИнвентаризацияОС - Ссылка на документ инвентаризации
// 		ДействияДокументовНаОсновании - Строка - Перечисленные через запятую строковые идентификаторы типа документа созданного на основании
// 			Принимаемые значения -- идентификаторы значений перечисления "ДействияНаОснованииИнвентаризации"
// 			Если параметр не задан, отбор по типу не накладывается.
//
Процедура ДобавитьСозданныеДокументыНаОсновании2_4(МенеджерТаблиц, ДокументОснование, МассивДействий = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерТаблиц;
	Запрос.УстановитьПараметр("МассивДействийЗадан", ЗначениеЗаполнено(МассивДействий));
	Запрос.УстановитьПараметр("МассивДействий", МассивДействий);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	Запрос.Текст=
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство КАК ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Перемещение) КАК Действие,
	|	Операция.ПодразделениеПолучатель КАК Подразделение,
	|	Операция.Подразделение КАК ПодразделениеПолучатель
	|ПОМЕСТИТЬ втДокументыНаОснованииДанные
	|ИЗ
	|	Документ.ПеремещениеОС2_4 КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПеремещениеОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Перемещение) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ПринятиеКУчету),
	|	Операция.Местонахождение,
	|	Операция.Местонахождение
	|ИЗ
	|	Документ.ПринятиеКУчетуОС2_4 КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ПринятиеКУчету) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Списание),
	|	Операция.Подразделение,
	|	Операция.Подразделение
	|ИЗ
	|	Документ.СписаниеОС2_4 КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Списание) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Переоценка),
	|	Операция.Подразделение,
	|	Операция.Подразделение
	|ИЗ
	|	Документ.ПереоценкаОС2_4 КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереоценкаОС2_4.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Переоценка) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ТабличнаяЧасть.ОсновноеСредство,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ВыбытиеАрендованныхОС),
	|	Операция.Подразделение,
	|	Операция.Подразделение
	|ИЗ
	|	Документ.ВыбытиеАрендованныхОС КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыбытиеАрендованныхОС.ОС КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Проведен
	|	И Операция.ДокументНаОсновании
	|	И Операция.ДокументОснование = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.ВыбытиеАрендованныхОС) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ХозрасчетныйСубконто.Значение,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция),
	|	Хозрасчетный.ПодразделениеДт,
	|	NULL
	|ИЗ
	|	Документ.ОперацияБух.ЗаполнениеДвижений КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО Операция.Ссылка = Хозрасчетный.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО (Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор)
	|			И (Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки)
	|			И (ХозрасчетныйСубконто.Вид В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства)))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|ГДЕ
	|	Операция.Ссылка.Проведен
	|	И Операция.Документ = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция) В (&МассивДействий))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	ХозрасчетныйСубконто.Значение,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция),
	|	Хозрасчетный.ПодразделениеКт,
	|	NULL
	|ИЗ
	|	Документ.ОперацияБух.ЗаполнениеДвижений КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ПО Операция.Ссылка = Хозрасчетный.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Субконто КАК ХозрасчетныйСубконто
	|		ПО (Хозрасчетный.Регистратор = ХозрасчетныйСубконто.Регистратор)
	|			И (Хозрасчетный.НомерСтроки = ХозрасчетныйСубконто.НомерСтроки)
	|			И (ХозрасчетныйСубконто.Вид В (ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства)))
	|			И (ХозрасчетныйСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит))
	|ГДЕ
	|	Операция.Ссылка.Проведен
	|	И Операция.Документ = &ДокументОснование
	|	И (НЕ &МассивДействийЗадан ИЛИ ЗНАЧЕНИЕ(Перечисление.ДействияНаОснованииИнвентаризации.Операция) В (&МассивДействий))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(втДокументыНаОснованииДанные.Ссылка) КАК Ссылка,
	|	втДокументыНаОснованииДанные.ОсновноеСредство КАК ОсновноеСредство,
	|	втДокументыНаОснованииДанные.Действие КАК Действие,
	|	втДокументыНаОснованииДанные.Подразделение КАК Подразделение,
	|	втДокументыНаОснованииДанные.ПодразделениеПолучатель КАК ПодразделениеПолучатель
	|ПОМЕСТИТЬ втДокументыНаОсновании2_4
	|ИЗ
	|	втДокументыНаОснованииДанные КАК втДокументыНаОснованииДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	втДокументыНаОснованииДанные.ОсновноеСредство,
	|	втДокументыНаОснованииДанные.Действие,
	|	втДокументыНаОснованииДанные.Подразделение,
	|	втДокументыНаОснованииДанные.ПодразделениеПолучатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втДокументыНаОснованииДанные";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

