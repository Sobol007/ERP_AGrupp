#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаОКодахВидовОпераций") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СправкаОКодахВидовОпераций", "Справка о кодах видов операций", ПолучитьТабДок2018(МассивОбъектов));
	КонецЕсли;

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СправкаОВалютныхОперациях") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СправкаОВалютныхОперациях", "Справка о валютных операциях (до 01.03.2018)", ПолучитьТабДок2015(МассивОбъектов));
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьТабДок2018(МассивОбъектов)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Ссылка Из МассивОбъектов Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		ОбъектСсылки = Ссылка.ПолучитьОбъект();	
		
		ТабличныйДокумент.Вывести(ОбъектСсылки.ПечатьСВО_2018());
	КонецЦикла;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции	

Функция ПолучитьТабДок2015(МассивОбъектов)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ПервыйДокумент = Истина;
	Для Каждого Ссылка Из МассивОбъектов Цикл
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		Иначе
			ПервыйДокумент = Ложь;
		КонецЕсли;
		
		ОбъектСсылки = Ссылка.ПолучитьОбъект();	
		
		Если Ссылка.Дата >= Дата(2015,12,28) Тогда
			ТабличныйДокумент.Вывести(ОбъектСсылки.ПечатьСВО_2015());
		Иначе
			ТабличныйДокумент.Вывести(ПечатьСВО_2012(Ссылка));
		КонецЕсли;
	КонецЦикла;
	
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
КонецФункции	

Функция ПечатьСВО_2012(СсылкаНаОбъект)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаОбъект);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СВО.Номер,
	|	ВЫБОР
	|		КОГДА СВО.Корректировка
	|			ТОГДА СВО.Основание.Дата
	|		ИНАЧЕ СВО.Дата
	|	КОНЕЦ КАК Дата,
	|	СВО.Организация,
	|	СВО.Банк КАК _Банк,
	|	СВО.Банк.РЭЙ_НаименованиеПолное КАК Банк,
	|	СВО.Инобанк.РЭЙ_Страна.Код КАК КодСтраныИнобанка,
	|	ВЫБОР
	|		КОГДА СВО.Организация.НаименованиеПолное = """"
	|			ТОГДА СВО.Организация.Наименование
	|		ИНАЧЕ СВО.Организация.НаименованиеПолное
	|	КОНЕЦ КАК ОрганизацияНаименование,
	|	СВО.Руководитель,
	|	СВО.ГлавныйБухгалтер,
	|	СВО.Корректировка,
	|	СВО.БанковскийСчет КАК БанковскийСчет,
	|	ЕСТЬNULL(РЭЙ_БанковскиеСчетаВалютные.НомерСчета, СВО.БанковскийСчет.НомерСчета) КАК НомерСчетаОрганизации
	|ИЗ
	|	Документ.РЭЙ_СправкаОВалютныхОперациях КАК СВО
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РЭЙ_БанковскиеСчетаВалютные КАК РЭЙ_БанковскиеСчетаВалютные
	|		ПО СВО.БанковскийСчет = РЭЙ_БанковскиеСчетаВалютные.БанковскийСчет
	|			И (РЭЙ_БанковскиеСчетаВалютные.ПометкаУдаления = ЛОЖЬ)
	|ГДЕ
	|	СВО.Ссылка = &ТекущийДокумент";
	
	ДанныеДляПечатиШапки = Запрос.Выполнить().Выбрать();
	ДанныеДляПечатиШапки.Следующий();
	
	Если НЕ ЗначениеЗаполнено(ДанныеДляПечатиШапки.Банк) Тогда
		Сообщить("Печать справки невозможна, т.к. не заполнено полное наименование уполномоченного банка " + ДанныеДляПечатиШапки._Банк);
		Возврат Неопределено;
	КонецЕсли; 
		
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОВалютныхОперациях";
	
	Макет = Документы.РЭЙ_СправкаОВалютныхОперациях.ПолучитьМакет("СправкаОВалютныхОперациях");
	
	Область = Макет.ПолучитьОбласть("Шапка");
	Область.Параметры.Заполнить(ДанныеДляПечатиШапки);
	ТабДокумент.Вывести(Область);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЧ.НомерСтрокиДляПечати КАК НомерСтрокиДляПечати,
	|	ТЧ.НомерСтроки КАК НомерСтроки,
	|	ТЧ.СуммаВВалютеКонтракта КАК СуммаВВалютеКонтракта,
	|	ТЧ.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента
	|ИЗ
	|	Документ.РЭЙ_СправкаОВалютныхОперациях.ВалютныеОперации КАК ТЧ
	|ГДЕ
	|	ТЧ.Ссылка = &Ссылка
	|	И (ТЧ.Ссылка.Первичное
	|			ИЛИ ТЧ.Ссылка.Корректировка
	|				И ТЧ.Корректировка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДляПечати,
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(СуммаВВалютеКонтракта),
	|	СУММА(СуммаВВалютеДокумента)
	|ПО
	|	НомерСтрокиДляПечати";
	
	Если СсылкаНаОбъект.РучноеЗаполнение Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.РЭЙ_СправкаОВалютныхОперациях.ВалютныеОперации", "Документ.РЭЙ_СправкаОВалютныхОперациях.ВалютныеОперацииРучные");
	КонецЕсли;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Если ВыборкаИтоги.Количество() = 0 Тогда
		Область = Макет.ПолучитьОбласть("Строка");
		ТабДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ПустаяСтрока");
		ТабДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ШапкаПримечание");
		ТабДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("Примечание");
		ТабДокумент.Вывести(Область);
	Иначе
		ТабличныйДокументПримечания = Новый ТабличныйДокумент;
		
		Пока ВыборкаИтоги.Следующий() Цикл
			ВыборкаСтроки = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			ВыборкаСтроки.Следующий();
			Если Не СсылкаНаОбъект.РучноеЗаполнение Тогда
				ИмяТЧ = "ВалютныеОперации";
			Иначе
				ИмяТЧ = "ВалютныеОперацииРучные";
			КонецЕсли;
			СтрокаТЧ = СсылкаНаОбъект[ИмяТЧ][ВыборкаСтроки.НомерСтроки - 1];
			текРасчетныйДокумент = СтрокаТЧ.РасчетныйДокумент;
			
			Область = Макет.ПолучитьОбласть("Строка");
			
			Область.Параметры.НомерСтроки = ВыборкаИтоги.НомерСтрокиДляПечати;
			
			Если СсылкаНаОбъект.РучноеЗаполнение Тогда
				текДатаОперации = СтрокаТЧ.ДатаРасчетногоДокумента;
			Иначе
				текДатаОперации = текРасчетныйДокумент.Дата;
			КонецЕсли;
						
			Если ЗначениеЗаполнено(СтрокаТЧ.НомерРасчетногоДокумента) Тогда
				текНомерРасчетногоДокумента = СтрокаТЧ.НомерРасчетногоДокумента;
			Иначе
				текНомерРасчетногоДокумента = "БН";
			КонецЕсли;
			
			Область.Параметры.ПредставлениеРасчетногоДокумента = СокрЛП(текНомерРасчетногоДокумента) + "/" + Формат(СтрокаТЧ.ДатаРасчетногоДокумента, "ДФ=dd.MM.yyyy");
			Область.Параметры.ДатаОперации = Формат(текДатаОперации, "ДФ=dd.MM.yyyy");
			
			Область.Параметры.НаправлениеПлатежа = СтрокаТЧ.НаправлениеПлатежа; 
			Область.Параметры.КодВидаВалютнойОперации = СтрокаТЧ.ВидВалютнойОперации.Код;
			
			Область.Параметры.ПлатежКодВалюты = СтрокаТЧ.ВалютаДокумента.Код;
			Область.Параметры.ПлатежСумма = Формат(ВыборкаИтоги.СуммаВВалютеДокумента, "ЧДЦ=2");
			
			ПечНомерПаспортаСделки = СтрокаТЧ.ПаспортСделки.НомерПаспортаСделки;
			Если ПустаяСтрока(ПечНомерПаспортаСделки) Тогда
				Если ЗначениеЗаполнено(СтрокаТЧ.КонтрактВЭД) Тогда
					текНомерКонтракта = СтрокаТЧ.КонтрактВЭД.КонтрактНомер;
					текДатаКонтракта = РЭЙ_СлужебныйСервер.ПолучитьДатуКонтракта(СтрокаТЧ.КонтрактВЭД);
				КонецЕсли;
				
				Если ПустаяСтрока(текНомерКонтракта) Тогда
					текНомерКонтракта = "БН";
				КонецЕсли;
				
				Если ЗначениеЗаполнено(текДатаКонтракта) Тогда
					ПечНомерПаспортаСделки = текНомерКонтракта + "/" + Формат(текДатаКонтракта, "ДФ=dd.MM.yyyy");
				Иначе
					ПечНомерПаспортаСделки = текНомерКонтракта;
				КонецЕсли;
			КонецЕсли; 
			Область.Параметры.НомерПаспортаСделки = ПечНомерПаспортаСделки;
			
			Область.Параметры.ВалютаКонтрактаКод	= СтрокаТЧ.ВалютаКонтракта.Код;
			Область.Параметры.СуммаВВалютеКонтракта	= Формат(ВыборкаИтоги.СуммаВВалютеКонтракта, "ЧДЦ=2");
			
			Если СтрокаТЧ.ВидВалютнойОперации.ОжидаемыйСрок Тогда
				Область.Параметры.ОжидаемыйСрок = СтрокаТЧ.ОжидаемыйСрок;
			КонецЕсли;
			ТабДокумент.Вывести(Область);
			
			
			ВыборкаСтроки.Сбросить();
			ТекстПримечания = "";
			Пока ВыборкаСтроки.Следующий() Цикл
				СтрокаТЧ_Примечание = СсылкаНаОбъект[ИмяТЧ][ВыборкаСтроки.НомерСтроки - 1];
				Если Не ПустаяСтрока(СтрокаТЧ_Примечание.Примечание) Тогда
					ТекстПримечания = ТекстПримечания + ?(ТекстПримечания = "", "", "; ") + СокрЛП(СтрокаТЧ_Примечание.Примечание);
				КонецЕсли;
			КонецЦикла;
			Если Не ПустаяСтрока(ТекстПримечания) Тогда
				ОбластьПримечание = Макет.ПолучитьОбласть("Примечание");
				ОбластьПримечание.Параметры.НомерСтроки = ВыборкаИтоги.НомерСтрокиДляПечати;
				ОбластьПримечание.Параметры.Содержание = ТекстПримечания;
				ТабличныйДокументПримечания.Вывести(ОбластьПримечание);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(текРасчетныйДокумент) Тогда
				Если СсылкаНаОбъект.Банк.РЭЙ_ВидПредоставленияДокументовПоСделке = 1 Тогда
					РЭЙ_ЗадачиСервер.УстановитьСтатусЗадачиВЭД(текРасчетныйДокумент, "Распечатана");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла; 
		
		Область = Макет.ПолучитьОбласть("ШапкаПримечание");
		ТабДокумент.Вывести(Область);
		
		ТабДокумент.Вывести(ТабличныйДокументПримечания);
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("Подвал");
	Область.Параметры.ФИОРуководителя = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(ДанныеДляПечатиШапки.Руководитель, ДанныеДляПечатиШапки.Дата);
	Область.Параметры.ФИОБухгалтера   = РЭЙ_СлужебныйСервер.ПолучитьФИОФизлица(ДанныеДляПечатиШапки.ГлавныйБухгалтер, ДанныеДляПечатиШапки.Дата);
	ТабДокумент.Вывести(Область);
	
	Возврат ТабДокумент;
КонецФункции

Процедура ПолучитьСтруктуруПараметров(СсылкаНаДокумент, ИмяМакета, СтруктураПараметров) Экспорт
	СтруктураПараметров = СсылкаНаДокумент.ПолучитьОбъект().ПолучитьДанныеДляПечати();
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	
	Если ВидФормы <> "ФормаДокумента"
		И ВидФормы <> "ФормаОбъекта" Тогда
		Возврат;
	КонецЕсли;

	ВидОперации = Неопределено; 

	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВыбраннаяФорма = "ФормаДокумента"; 
	КонецЕсли;

	Если Параметры.Свойство("ЗначениеКопирования")
		И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;

	Если Параметры.Свойство("ЗначенияЗаполнения") 
		И ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура") Тогда
		Если Параметры.ЗначенияЗаполнения.Свойство("ВидОперации") Тогда
			ВыбраннаяФорма = "ФормаДокумента";
		КонецЕсли;
	КонецЕсли;

	Если Параметры.Свойство("Основание")
		И ЗначениеЗаполнено(Параметры.Основание) Тогда
			ВыбраннаяФорма = "ФормаДокумента";
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	
	Если ВыбраннаяФорма = Неопределено Тогда
		ВыбраннаяФорма = "ФормаВыбораОперации";
	КонецЕсли;

КонецПроцедуры

#Область ПроцедурыИФункцииПечати

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "РЭЙ_СлужебныйКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РЭЙ_СправкаОВалютныхОперациях";
	КомандаПечати.Идентификатор = "СправкаОКодахВидовОпераций";
	КомандаПечати.Представление = НСтр("ru = 'Справка о кодах видов операций'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "РЭЙ_СлужебныйКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.МенеджерПечати = "Документ.РЭЙ_СправкаОВалютныхОперациях";
	КомандаПечати.Идентификатор = "СправкаОВалютныхОперациях";
	КомандаПечати.Представление = НСтр("ru = 'Справка о валютных операциях (до 01.03.2018)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 20;
КонецПроцедуры

#КонецОбласти
