
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СПД.Ссылка КАК Справка,
	|	СПД.Документ КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА СПД.ВалютаДокумента = СПД.Ссылка.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов
	|			ТОГДА СПД.СуммаВВалютеДокумента
	|		ИНАЧЕ СПД.СуммаВВалютеКонтракта
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ АвансовыеСПД
	|ИЗ
	|	Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПодтверждающиеДокументы КАК СПД
	|ГДЕ
	|	СПД.Ссылка.Проведен
	|	И СПД.Ссылка В(&СсылкиСПД)
	|	И СПД.Ссылка.КонтрактВЭД = &КонтрактВЭД
	|	И СПД.ПризнакПоставки = ЗНАЧЕНИЕ(Перечисление.РЭЙ_ПризнакиПоставки.ПредоставлениеРезидентомОтсрочкиОплатыНерезиденту)
	|	И СПД.Ссылка.Дата <= &Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СВО.АвансоваяСправка,
	|	СВО.АвансовыйРасчетныйДокумент,
	|	СУММА(ВЫБОР
	|			КОГДА СВО.ВалютаДокумента = СВО.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов
	|				ТОГДА СВО.СуммаВВалютеДокумента
	|			ИНАЧЕ СВО.СуммаВВалютеКонтракта
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ЗакрытыеСПД
	|ИЗ
	|	Документ.РЭЙ_СправкаОВалютныхОперациях.ВалютныеОперации КАК СВО
	|ГДЕ
	|	СВО.Ссылка.Проведен
	|	И СВО.Ссылка В(&СсылкиСВО)
	|	И СВО.КонтрактВЭД = &КонтрактВЭД
	|	И СВО.АвансоваяСправка <> ЗНАЧЕНИЕ(Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПустаяСсылка)
	|	И СВО.Ссылка.Дата <= &Период
	|	И СВО.Ссылка <> &СсылкаСВО
	|
	|СГРУППИРОВАТЬ ПО
	|	СВО.АвансоваяСправка,
	|	СВО.АвансовыйРасчетныйДокумент,
	|	СВО.ВалютаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеТекущегоДокумента.АвансоваяСправка,
	|	ДанныеТекущегоДокумента.АвансовыйРасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА ДанныеТекущегоДокумента.ВалютаДокумента = &ВалютаКонтракта
	|			ТОГДА ДанныеТекущегоДокумента.СуммаВВалютеДокумента
	|		ИНАЧЕ ДанныеТекущегоДокумента.СуммаВВалютеКонтракта
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ПредварительныеДанныеТекущегоДокумента
	|ИЗ
	|	&ДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента
	|ГДЕ
	|	ДанныеТекущегоДокумента.АвансоваяСправка <> ЗНАЧЕНИЕ(Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредварительныеДанныеТекущегоДокумента.АвансоваяСправка,
	|	ПредварительныеДанныеТекущегоДокумента.АвансовыйРасчетныйДокумент,
	|	СУММА(ПредварительныеДанныеТекущегоДокумента.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ДанныеТекущегоДокумента
	|ИЗ
	|	ПредварительныеДанныеТекущегоДокумента КАК ПредварительныеДанныеТекущегоДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредварительныеДанныеТекущегоДокумента.АвансоваяСправка,
	|	ПредварительныеДанныеТекущегоДокумента.АвансовыйРасчетныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АвансовыеСПД.Справка КАК Справка,
	|	АвансовыеСПД.Сумма КАК Аванс,
	|	ЕСТЬNULL(ЗакрытыеСПД.Сумма, 0) + ЕСТЬNULL(ДанныеТекущегоДокумента.Сумма, 0) КАК Зачтено,
	|	АвансовыеСПД.Сумма - ЕСТЬNULL(ЗакрытыеСПД.Сумма, 0) - ЕСТЬNULL(ДанныеТекущегоДокумента.Сумма, 0) КАК НеЗачтено,
	|	АвансовыеСПД.РасчетныйДокумент
	|ПОМЕСТИТЬ ДоступныеАвансы
	|ИЗ
	|	АвансовыеСПД КАК АвансовыеСПД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗакрытыеСПД КАК ЗакрытыеСПД
	|		ПО АвансовыеСПД.Справка = ЗакрытыеСПД.АвансоваяСправка
	|			И АвансовыеСПД.РасчетныйДокумент = ЗакрытыеСПД.АвансовыйРасчетныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеТекущегоДокумента КАК ДанныеТекущегоДокумента
	|		ПО АвансовыеСПД.Справка = ДанныеТекущегоДокумента.АвансоваяСправка
	|			И АвансовыеСПД.РасчетныйДокумент = ДанныеТекущегоДокумента.АвансовыйРасчетныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоступныеАвансы.Справка КАК Справка,
	|	ДоступныеАвансы.Аванс КАК Аванс,
	|	ДоступныеАвансы.Зачтено КАК Зачтено,
	|	ДоступныеАвансы.НеЗачтено КАК НеЗачтено,
	|	ДоступныеАвансы.РасчетныйДокумент,
	|	ДоступныеАвансы.Справка.Дата КАК Дата
	|ИЗ
	|	ДоступныеАвансы КАК ДоступныеАвансы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|ИТОГИ ПО
	|	Справка";
		
	Период = КонецДня(Параметры.РасчетныйДокумент.Дата);
	Запрос.УстановитьПараметр("КонтрактВЭД", Параметры.КонтрактВЭД);
	Запрос.УстановитьПараметр("ВалютаКонтракта", Параметры.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов);
	Запрос.УстановитьПараметр("СсылкаСВО", Параметры.СсылкаСВО);
	Запрос.УстановитьПараметр("ДанныеТекущегоДокумента", ПолучитьИзВременногоХранилища(Параметры.ДанныеТекущегоДокумента));
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("СсылкиСВО", РЭЙ_СлужебныйСервер.ПолучитьСсылкиНаАктуальныеСВО(, Период));
	Запрос.УстановитьПараметр("СсылкиСПД", РЭЙ_СлужебныйСервер.ПолучитьСсылкиНаАктуальныеСПД(, Период));
	
    ДанныеДерево = РеквизитФормыВЗначение("Данные");

	ДанныеДерево.Строки.Очистить();
	
	ВыборкаСправки = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСправки.Следующий() Цикл
		СтрокаСправки = ДанныеДерево.Строки.Добавить();
		СтрокаСправки.Документ = ВыборкаСправки.Справка;
		СтрокаСправки.ПредставлениеДокумента = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(СтрокаСправки.Документ);
		
		ВыборкаРасчетныеДокументы = ВыборкаСправки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРасчетныеДокументы.Следующий() Цикл
			СтрокаРасчетныйДокумент = СтрокаСправки.Строки.Добавить();
			СтрокаРасчетныйДокумент.Документ = ВыборкаРасчетныеДокументы.РасчетныйДокумент;
			СтрокаРасчетныйДокумент.ПредставлениеДокумента = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(СтрокаРасчетныйДокумент.Документ);
			СтрокаРасчетныйДокумент.Аванс = ВыборкаРасчетныеДокументы.Аванс;
			СтрокаРасчетныйДокумент.Зачтено = ВыборкаРасчетныеДокументы.Зачтено;
			СтрокаРасчетныйДокумент.НеЗачтено = ВыборкаРасчетныеДокументы.НеЗачтено;
			СтрокаРасчетныйДокумент.ВалютаКонтракта = Параметры.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов;
		КонецЦикла;
	КонецЦикла;
    ЗначениеВРеквизитФормы(ДанныеДерево, "Данные");
КонецПроцедуры

&НаКлиенте
Процедура ДанныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если ТипЗнч(Элемент.ТекущиеДанные.Документ) = Тип("ДокументСсылка.РЭЙ_СправкаОПодтверждающихДокументах") Тогда
		ПоказатьПредупреждение(, "Необходимо выбрать расчетный документ.");
		Возврат;
	КонецЕсли;
	
	СтрокаРодитель = Данные.НайтиПоИдентификатору(ВыбраннаяСтрока).ПолучитьРодителя();
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Справка", СтрокаРодитель.Документ);
	СтруктураВозврата.Вставить("РасчетныйДокумент", Элемент.ТекущиеДанные.Документ);
	СтруктураВозврата.Вставить("Сумма", Элемент.ТекущиеДанные.НеЗачтено);
	
	ОповеститьОВыборе(СтруктураВозврата);
	
КонецПроцедуры
	