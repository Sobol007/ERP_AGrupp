
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Статус = Перечисления.АГ_СтатусыЗаявок.Согласован И ТипОперации = Перечисления.АГ_ТипыОперацийЗаявок.Создание Тогда
		Если ЗначениеЗаполнено(ИНН) И ЗначениеЗаполнено(КПП) Тогда
			Дубль = Документы.АГ_ЗаявкаНаСозданиеИзменениеНовогоКонтрагента.ПроверитьНаДублирование(ИНН, КПП);
			Если ТипЗнч(Дубль) = Тип("СправочникСсылка.Контрагенты") Тогда
				Сообщить("В базе существует контрагент " + Дубль + " с указанными ИНН и КПП. Создание невозможно");
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		Иначе
			Сообщить("Не заполнены ИНН или КПП. Создание невозможно");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ПартнерОбъект = Справочники.Партнеры.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(ПартнерОбъект, ЭтотОбъект);
		ПартнерОбъект.АГ_КодОКВЭД = КодОКВЭД;
		Если ЮрФизЛицо = Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель ИЛИ ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
			ПартнерОбъект.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо;
		Иначе
			ПартнерОбъект.ЮрФизЛицо = Перечисления.КомпанияЧастноеЛицо.Компания;
		КонецЕсли;
		ПартнерОбъект.Записать();
		
		Партнер = ПартнерОбъект.Ссылка;
		
		КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		КонтрагентОбъект.Партнер = ПартнерОбъект.Ссылка;
		ЗаполнитьЗначенияСвойств(КонтрагентОбъект, ЭтотОбъект);
		КонтрагентОбъект.АГ_ОГРН = ОГРН;
		ТЗ_КИ = КонтактнаяИнформация.Выгрузить();
		Для Каждого Строка Из ТЗ_КИ Цикл
			НаименованиеКИ = Строка.Вид.ИмяПредопределенныхДанных;
			НаименованиеКИ = СтрЗаменить(НаименованиеКИ, "Заявка", "");
			Строка.Вид = Справочники.ВидыКонтактнойИнформации[НаименованиеКИ];
		КонецЦикла;
		КонтрагентОбъект.КонтактнаяИнформация.Загрузить(ТЗ_КИ);
		
		КонтрагентОбъект.Записать();
		
		Для Каждого Строка Из КонтактныеЛица Цикл
			КЛОбъект = Справочники.КонтактныеЛицаПартнеров.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(КЛОбъект, Строка);
			КЛОбъект.Владелец = ПартнерОбъект.Ссылка;
			
			ФИО = КЛОбъект.Наименование;
			ПервоеСлово = CRM_КлиентыСервер.ВыделитьЧастьНаименованияКонтактногоЛица(ФИО);
			КЛОбъект.CRM_Фамилия = ПервоеСлово;			
			КЛОбъект.CRM_Имя = CRM_КлиентыСервер.ВыделитьЧастьНаименованияКонтактногоЛица(ФИО);
			Если НЕ ЗначениеЗаполнено(КЛОбъект.Пол) Тогда
				КЛОбъект.Пол = CRM_КлиентыСервер.ОпределитьПол(КЛОбъект.CRM_Имя);
			КонецЕсли;
			КЛОбъект.CRM_Отчество = CRM_КлиентыСервер.ВыделитьЧастьНаименованияКонтактногоЛица(ФИО);

			КЛОбъект.Записать();
		КонецЦикла;
		
		СчетОбъект = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(СчетОбъект, ЭтотОбъект);
		СчетОбъект.Владелец = КонтрагентОбъект.Ссылка;
		СчетОбъект.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	ЕстьДубли = Истина;
	Статус = Перечисления.АГ_СтатусыЗаявок.ПодготовкаКПодачеЗаявки;
КонецПроцедуры


