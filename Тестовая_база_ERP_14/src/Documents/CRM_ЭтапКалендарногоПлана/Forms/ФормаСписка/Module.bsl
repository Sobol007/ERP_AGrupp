
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ CRM_ЛицензированиеСервер.ПодсистемаCRMИспользуется() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Невозможно открыть форму списка контрольных точек. Подсистема 1С:CRM не используется! (см. Персональные настройки пользователя)';en='Can not open checkpoint list form. Subsystem 1C: CRM is not used! (see Personal settings of the user)'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	CRM_ЛицензированиеСервер.ПолучитьЗащищеннуюОбработку().Документ_CRM_ЭтапКалендарногоПлана_ФормаСписка_ПриСозданииНаСервере();
	
	СписокВидовОтбора = Новый СписокЗначений;
	СписокВидовОтбора.Добавить(ВидСравненияКомпоновкиДанных.Равно);
	СписокВидовОтбора.Добавить(ВидСравненияКомпоновкиДанных.НеРавно);
	СписокВидовОтбора.Добавить(ВидСравненияКомпоновкиДанных.Меньше);
	СписокВидовОтбора.Добавить(ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
	СписокВидовОтбора.Добавить(ВидСравненияКомпоновкиДанных.Больше);
	СписокВидовОтбора.Добавить(ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	
	Элементы.ВидОтбораДатаНачалаПлан.СписокВыбора.ЗагрузитьЗначения(СписокВидовОтбора.ВыгрузитьЗначения());
	Элементы.ВидОтбораДатаОкончанияПлан.СписокВыбора.ЗагрузитьЗначения(СписокВидовОтбора.ВыгрузитьЗначения());
	Элементы.ВидОтбораДатаНачалаФакт.СписокВыбора.ЗагрузитьЗначения(СписокВидовОтбора.ВыгрузитьЗначения());
	Элементы.ВидОтбораДатаОкончанияФакт.СписокВыбора.ЗагрузитьЗначения(СписокВидовОтбора.ВыгрузитьЗначения());
	
	Если Параметры.Отбор.Свойство("Проект") И ЗначениеЗаполнено(Параметры.Отбор.Проект) Тогда
		Элементы.ОтборПроект.Видимость = Ложь;
		ПроектВладелец = Параметры.Отбор.Проект;
		ЗадачиПоОбъекту = Истина;
	ИначеЕсли Параметры.Отбор.Свойство("Этап") Тогда
		ЗадачиПоОбъекту = Истина;
	Иначе
		ОтборУчастник = Пользователи.ТекущийПользователь();
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Участники.Пользователь", ОтборУчастник, Истина);
	КонецЕсли;
	
	ДобавитьЭлементыДляОтбора();
	
	// +Рабочий стол
	CRM_СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// -Рабочий стол
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Перейдем сразу на вкладку отборов "Быстрые отборы" и оставим ее в свернутом режиме.
	Элементы.ПанельЗначенийОтбора.Видимость			= Истина;
	Элементы.ПанельЗначенийОтбора.ТекущаяСтраница	= Элементы.ЗначенияОтбораБыстрыеОтборы;
	//
	Если НЕ ПанельОтборовРазвернута Тогда
		БыстрыеОтборы(Элементы.БыстрыеОтборы);
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
	
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	Если ЗадачиПоОбъекту Тогда
		Настройки.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	РеквизитыФормы = ПолучитьРеквизиты();
	МассивРеквизитов = Новый Массив;
	Для каждого Реквизит из РеквизитыФормы Цикл
		МассивРеквизитов.Добавить(Реквизит.Имя);
	КонецЦикла;
	Для каждого Настройка Из Настройки Цикл
		Если МассивРеквизитов.Найти(Настройка.Ключ) <> Неопределено Тогда
			ЭтотОбъект[Настройка.Ключ] = ?(ЗадачиПоОбъекту, Неопределено, Настройка.Значение);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(ОтборПроект) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект", ОтборПроект, Истина);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборТипЭтапа) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ТипЭтапа", ОтборТипЭтапа, Истина);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборСтатусСписокРеквизитов) Тогда
		Для каждого Элемент Из ОтборСтатусСписокРеквизитов Цикл
			Если Метаданные.Перечисления.CRM_СтатусыЭтаповКалендарногоПлана.ЗначенияПеречисления.Найти(Сред(Элемент.Значение, СтрНайти(Элемент.Значение, "__")+2)) = Неопределено Тогда
				ОтборСтатусСписокРеквизитов.Очистить();	
				Для каждого Статус из Метаданные.Перечисления.CRM_СтатусыЭтаповКалендарногоПлана.ЗначенияПеречисления Цикл
					СтатусИмя = "CRM_СтатусыЭтаповКалендарногоПлана__"+Статус.Имя;
					ОтборСтатусСписокРеквизитов.Добавить(СтатусИмя);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборСтатус) Тогда
		Для каждого Элемент Из ОтборСтатусСписокРеквизитов Цикл
			Если ОтборСтатус.НайтиПоЗначению(ПредопределенноеЗначение("Перечисление."+СтрЗаменить(Элемент.Значение, "__", ".")))<>Неопределено Тогда
				ЭтотОбъект[Элемент.Значение] = Истина;
			КонецЕсли;
		КонецЦикла;
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Статус", ОтборСтатус, Истина, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборСтатусПроекта) Тогда
		Для каждого Элемент Из ОтборСтатусПроектаСписокРеквизитов Цикл
			Если ОтборСтатусПроекта.НайтиПоЗначению(ПредопределенноеЗначение("Перечисление."+СтрЗаменить(Элемент.Значение, "__", ".")))<>Неопределено Тогда
				ЭтотОбъект[Элемент.Значение] = Истина;
			КонецЕсли;
		КонецЦикла;
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект.CRM_Статус", ОтборСтатусПроекта, Истина, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборРуководительПроекта) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект.Ответственный", ОтборРуководительПроекта, Истина);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборДатаНачалаПлан) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ПлановаяДатаНачала", ОтборДатаНачалаПлан, Истина, ВидОтбораДатаНачалаПлан);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборДатаОкончанияПлан) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ПлановаяДатаОкончания", ОтборДатаОкончанияПлан, Истина, ВидОтбораДатаОкончанияПлан);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборДатаНачалаФакт) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ФактическаяДатаНачала", ОтборДатаНачалаФакт, Истина, ВидОтбораДатаНачалаФакт);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборДатаОкончанияФакт) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ФактическаяДатаОкончания", ОтборДатаОкончанияФакт, Истина, ВидОтбораДатаОкончанияФакт);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДобавитьЗадачу(Команда)
	ДобавитьЭтапКонтрольнуюТочку(Команда.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКТ(Команда)
	ДобавитьЭтапКонтрольнуюТочку(Команда.Имя);
КонецПроцедуры

#КонецОбласти

#Область Отборы

&НаСервере
Процедура ДобавитьЭлементыДляОтбора()
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для каждого Статус из Метаданные.Перечисления.CRM_СтатусыЭтаповКалендарногоПлана.ЗначенияПеречисления Цикл
		СтатусИмя = "CRM_СтатусыЭтаповКалендарногоПлана__"+Статус.Имя;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(СтатусИмя, Новый ОписаниеТипов("Булево"),, Статус.Синоним));
		ОтборСтатусСписокРеквизитов.Добавить(СтатусИмя);
	КонецЦикла;
	
	Для каждого Статус из Метаданные.Перечисления.CRM_СтатусыПроектов.ЗначенияПеречисления Цикл
		СтатусИмя = "CRM_СтатусыПроектов__"+Статус.Имя;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(СтатусИмя, Новый ОписаниеТипов("Булево"),, Статус.Синоним));
		ОтборСтатусПроектаСписокРеквизитов.Добавить(СтатусИмя);
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для каждого Статус из ОтборСтатусСписокРеквизитов Цикл
		НовыйЭлемент = Элементы.Добавить(Статус.Значение, Тип("ПолеФормы"), Элементы.ГруппаСтатусы);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПутьКДанным = Статус.Значение;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "ОтборСтатусПриИзменении");
	КонецЦикла;
	
	Для каждого Статус из ОтборСтатусПроектаСписокРеквизитов Цикл
		НовыйЭлемент = Элементы.Добавить(Статус.Значение, Тип("ПолеФормы"), Элементы.ГруппаСтатусыПроекта);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПутьКДанным = Статус.Значение;
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "ОтборСтатусПроектаПриИзменении");
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Элементы.ГруппаСтатусы.Показать();
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусПроектаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Элементы.ГруппаСтатусыПроекта.Показать();
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик команды "БыстрыеОтборы".
//
Процедура БыстрыеОтборы(Команда)
	CRM_ТегиКлиент.ВыборВариантаОтбора(ЭтотОбъект, Команда);
	Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки.Видимость = Истина;
	ПанельОтборовРазвернута = Элементы.ПанельЗначенийОтбора.Видимость;
КонецПроцедуры // БыстрыеОтборы()

&НаКлиенте
// Процедура - обработчик команды "ОтборПериод".
//
Процедура ОтборПериод(Команда)
	CRM_ТегиКлиент.ВыборВариантаОтбора(ЭтотОбъект, Команда);
	Элементы.СписокКомпоновщикНастроекПользовательскиеНастройки.Видимость = Ложь;
	ПанельОтборовРазвернута = Элементы.ПанельЗначенийОтбора.Видимость;
КонецПроцедуры // ОтборПериод()

&НаКлиенте
Процедура ОтборПроектПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборПроект) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект", ОтборПроект, Истина);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
	
КонецПроцедуры // ОтборПроектПриИзменении()

&НаКлиенте
Процедура ОтборТипЭтапаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ОтборТипЭтапа) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ТипЭтапа", ОтборТипЭтапа, Истина);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ТипЭтапа");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)
	Если ОтборСтатусСписокРеквизитов.НайтиПоЗначению(Элемент.Имя)<>Неопределено Тогда
		ЗначениеЭлемента = ПредопределенноеЗначение("Перечисление."+СтрЗаменить(Элемент.Имя, "__", "."));
		Если ЭтотОбъект[Элемент.Имя] Тогда
			ОтборСтатус.Добавить(ЗначениеЭлемента);
		Иначе
			ОтборСтатус.Удалить(ОтборСтатус.НайтиПоЗначению(ЗначениеЭлемента));
		КонецЕсли;
	Иначе	
		Если НЕ ЗначениеЗаполнено(ОтборСтатус) Тогда
			Для каждого Элемент из ОтборСтатусСписокРеквизитов Цикл
				ЭтотОбъект[Элемент.Значение] = Ложь;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборСтатус) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Статус", ОтборСтатус, Истина, ВидСравненияКомпоновкиДанных.ВСписке);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Статус");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусПроектаПриИзменении(Элемент)
	Если ОтборСтатусПроектаСписокРеквизитов.НайтиПоЗначению(Элемент.Имя)<>Неопределено Тогда
		ЗначениеЭлемента = ПредопределенноеЗначение("Перечисление."+СтрЗаменить(Элемент.Имя, "__", "."));
		Если ЭтотОбъект[Элемент.Имя] Тогда
			ОтборСтатусПроекта.Добавить(ЗначениеЭлемента);
		Иначе
			ОтборСтатусПроекта.Удалить(ОтборСтатусПроекта.НайтиПоЗначению(ЗначениеЭлемента));
		КонецЕсли;
	Иначе	
		Если НЕ ЗначениеЗаполнено(ОтборСтатусПроекта) Тогда
			Для каждого Элемент из ОтборСтатусПроектаСписокРеквизитов Цикл
				ЭтотОбъект[Элемент.Значение] = Ложь;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтборСтатусПроекта) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект.CRM_Статус", ОтборСтатусПроекта, Истина, ВидСравненияКомпоновкиДанных.ВСписке);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект.CRM_Статус");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
КонецПроцедуры

&НаКлиенте
Процедура ОтборРуководительПроектаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ОтборРуководительПроекта) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект.Ответственный", ОтборРуководительПроекта, Истина);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Проект.Ответственный");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ОтборОтветственный) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ОтветственныйИсполнитель", ОтборОтветственный, Истина);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ОтветственныйИсполнитель");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчастникПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ОтборУчастник) Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Участники.Пользователь", ОтборУчастник, Истина);
	Иначе	
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Участники.Пользователь");
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ОтборПроект, ОтборТипЭтапа, ОтборСтатус, ОтборСтатусПроекта, ОтборРуководительПроекта, ОтборОтветственный, ОтборУчастник);
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаНачалаПланПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ВидОтбораДатаНачалаПлан)Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ПлановаяДатаНачала", ОтборДатаНачалаПлан, Истина, ВидОтбораДатаНачалаПлан);
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаНачалаПланОчистить(Команда)
	ВидОтбораДатаНачалаПлан = "";
	ОтборДатаНачалаПлан     = "";
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ПлановаяДатаНачала");
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаОкончанияПланПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ВидОтбораДатаОкончанияПлан)Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ПлановаяДатаОкончания", ОтборДатаОкончанияПлан, Истина, ВидОтбораДатаОкончанияПлан);
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаОкончанияПланОчистить(Команда)
	ВидОтбораДатаОкончанияПлан = "";
	ОтборДатаОкончанияПлан 	   = "";
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ПлановаяДатаОкончания");
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаНачалаФактПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ВидОтбораДатаНачалаФакт)Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ФактическаяДатаНачала", ОтборДатаНачалаФакт, Истина, ВидОтбораДатаНачалаФакт);
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаНачалаФактОчистить(Команда)
	ВидОтбораДатаНачалаФакт = "";
	ОтборДатаНачалаФакт 	= "";
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ФактическаяДатаНачала");
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаОкончанияФактПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ВидОтбораДатаОкончанияФакт)Тогда
		CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ФактическаяДатаОкончания", ОтборДатаОкончанияФакт, Истина, ВидОтбораДатаОкончанияФакт);
	КонецЕсли;
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

&НаКлиенте
Процедура ОтборДатаОкончанияФактОчистить(Команда)
	ВидОтбораДатаОкончанияФакт = "";
	ОтборДатаОкончанияФакт 	   = "";
	CRM_ОбщегоНазначенияКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "ФактическаяДатаОкончания");
	CRM_ТегиКлиент.БыстрыйОтборИзменение(ЭтотОбъект, ВидОтбораДатаНачалаПлан, ВидОтбораДатаНачалаФакт, ВидОтбораДатаОкончанияПлан, ВидОтбораДатаОкончанияФакт,,,,,,, "ОтборПериод");
КонецПроцедуры

#КонецОбласти // Отборы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭтапКонтрольнуюТочку(ИмяКоманды)
	
	ПараметрыФормы = Новый Структура;
	
	Если ИмяКоманды = "ДобавитьКТ" Тогда
		ТипЭтапа = ПредопределенноеЗначение("Перечисление.CRM_ТипыЭтапов.КонтрольнаяТочка");
	Иначе
		ТипЭтапа = ПредопределенноеЗначение("Перечисление.CRM_ТипыЭтапов.Этап");
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипЭтапа", ТипЭтапа);
	
	Если ЗначениеЗаполнено(ПроектВладелец) Тогда
		ЗначенияЗаполнения.Вставить("Проект", ПроектВладелец);
	ИначеЕсли ЗначениеЗаполнено(ОтборПроект) Тогда
		ЗначенияЗаполнения.Вставить("Проект", ОтборПроект);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.CRM_ЭтапКалендарногоПлана.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры // ДобавитьЭтапКонтрольнуюТочку()

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	CRM_СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции

// +Рабочий стол
#Область Подключаемый_РабочийСтол

&НаКлиенте
Процедура Подключаемый_ПолеHTMLНапоминанийПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	CRM_РабочийСтолКлиент.ПолеHTMLНапоминанийПриНажатии(ЭтаФорма, Элемент, ДанныеСобытия, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_ПолеHTMLНапоминанийПриНажатии()

&НаКлиенте
Процедура Подключаемый_ПолеHTMLЗаметокПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	CRM_РабочийСтолКлиент.ПолеHTMLЗаметокПриНажатии(ЭтаФорма, Элемент, ДанныеСобытия, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_ПолеHTMLЗаметокПриНажатии()

#КонецОбласти
// -Рабочий стол
