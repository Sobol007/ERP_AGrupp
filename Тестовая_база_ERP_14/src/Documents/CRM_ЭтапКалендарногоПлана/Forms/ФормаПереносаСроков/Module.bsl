
&НаКлиенте
Процедура КомандаПрименить(Команда)
	
	ОчиститьСообщения();
	ВсеОК = ПроверитьЗаполнение();
	
	Если НЕ ВсеОК Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим что новая дата больше старой.
	Если НачалоДня(ДатаНачала) <= НачалоДня(ДатаСтарая) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						НСтр("ru='Ошибка! Новая дата должна быть больше старой.'"), ,
						, "ДатаНачала",
						);
		Возврат;
	КонецЕсли;
	ПередаваемыеДанные = Новый Структура;
	ПередаваемыеДанные.Вставить("ДатаНачала", ДатаНачала);
	ПередаваемыеДанные.Вставить("ДатаОкончания",ДатаОкончания);
	ПередаваемыеДанные.Вставить("Комментарий", Комментарий);
	Закрыть(ПередаваемыеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СкорректироватьДату()
	
	Если ДатаНачала <= ДатаСтарая Тогда
		ДатаНачала = ДатаСтарая;
	КонецЕсли;
	
	Если ДатаОкончания < ДатаНачала Тогда
		ДатаОкончания = ДатаНачала;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипЭтапа = Неопределено;
	Параметры.Свойство("ДатаСтарая", ДатаСтарая);
	Параметры.Свойство("ТипЭтапа", ТипЭтапа);
	
	ЭтоКонтрольнаяТочка = Ложь;
	Если ТипЗнч(ТипЭтапа) = Тип("ПеречислениеСсылка.CRM_ТипыЭтапов") И ТипЭтапа = Перечисления.CRM_ТипыЭтапов.КонтрольнаяТочка Тогда
		ЭтоКонтрольнаяТочка = Истина;
	КонецЕсли;
	
	Если НачалоДня(CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса()) < НачалоДня(ДатаСтарая) Тогда
		ДатаНачала = НачалоДня(ДатаСтарая) + 60*60*24;
		ДатаОкончания = ДатаНачала + 60*60*24;
	Иначе
		ДатаНачала = НачалоДня(CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса()) + 60*60*24;
		ДатаОкончания = ДатаНачала + 60*60*24;
	КонецЕсли;
	
	Если ЭтоКонтрольнаяТочка Тогда
		ДатаОкончания = ДатаНачала;
		Элементы.ДатаОкончания.Видимость = Ложь;
		Элементы.ДатаНачала.Заголовок = НСтр("ru='Дата'");
	КонецЕсли;
	
	// из планировщика
	тКТ = Неопределено;
	Если Параметры.Свойство("Ссылка") Тогда
		тКТ = Параметры.Ссылка;
		ДатаСтарая = тКТ.Дата;
	КонецЕсли;
	
	Если Параметры.Свойство("ПериодНачало") Тогда
		ДатаНачала = Параметры.ПериодНачало;
		Если ЗначениеЗаполнено(тКТ) Тогда
			Если НачалоДня(ДатаНачала) <= НачалоДня(ДатаСтарая) Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Ошибка! Новая дата должна быть больше старой.'"), ,
				, "ДатаНачала",
				);
				Возврат;
			КонецЕсли;
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("ПериодОкончание") Тогда
		ДатаОкончания = ?(ЭтоКонтрольнаяТочка, Параметры.ПериодНачало, Параметры.ПериодОкончание);
	КонецЕсли;
	Если Параметры.Свойство("ИзПланировщика") Тогда
		ИзПланировщика = Истина;
		Элементы.ГруппаДатаВремя.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	СкорректироватьДату();
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияПриИзменении(Элемент)
	СкорректироватьДату();
КонецПроцедуры

