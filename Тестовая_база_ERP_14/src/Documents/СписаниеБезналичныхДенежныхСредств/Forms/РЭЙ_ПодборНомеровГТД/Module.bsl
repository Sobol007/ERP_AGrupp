
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВходящиеДанные = Параметры.ВходящиеДанные;
	Если Не ПустаяСтрока(ВходящиеДанные) Тогда
		ВходящиеДанные = СтрЗаменить(ВходящиеДанные, ",", Символы.ПС);
		Для т = 1 По СтрЧислоСтрок(ВходящиеДанные) Цикл
			стрНомерГТД = СокрЛП(СтрПолучитьСтроку(ВходящиеДанные, т));
			текНомерГТД = Справочники.НомераГТД.НайтиПоКоду(стрНомерГТД);
			Если ЗначениеЗаполнено(текНомерГТД) Тогда
				НоваяСтрока = НомераГТД.Добавить();
				НоваяСтрока.НомерГТД = текНомерГТД;
			Иначе
				Сообщить("Номер ГТД " + стрНомерГТД + " не найден в справочнике ""Номера ГТД""");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	ОповеститьОВыборе(ПолучитьСтрокуНомеровГТД());
КонецПроцедуры

&НаСервере
Функция ПолучитьСтрокуНомеровГТД()
	Результат = "";
	Для Каждого СтрокаТЧ Из НомераГТД Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.НомерГТД) Тогда
			Результат = Результат + ?(ПустаяСтрока(Результат), "", ", ") + СокрЛП(СтрокаТЧ.НомерГТД.Код)
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.НомераГТД.ФормаВыбора", ПараметрыОткрытия, Элементы.НомераГТД,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура НомераГТДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтруктураПоиска = Новый Структура("НомерГТД", ВыбранноеЗначение);
	Если НомераГТД.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
		НоваяСтрока = НомераГТД.Добавить();
		НоваяСтрока.НомерГТД = ВыбранноеЗначение;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НомераГТДНомерГТДПриИзменении(Элемент)
	текНомерГТД = Элементы.НомераГТД.ТекущиеДанные.НомерГТД;
	Если ЗначениеЗаполнено(текНомерГТД) Тогда
		Строки = НомераГТД.НайтиСтроки(Новый Структура("НомерГТД", текНомерГТД));
	
		Если Строки.Количество() > 1 Тогда
			ПоказатьПредупреждение(, "Этот номер уже выбран");
			Элементы.НомераГТД.ТекущиеДанные.НомерГТД = Неопределено;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
