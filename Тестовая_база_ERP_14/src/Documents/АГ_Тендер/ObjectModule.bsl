
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ШагТендера=Перечисления.АГ_ШагиТендера.Шаг1_ПередачаВРаботу Тогда
		
		//Если НЕ ЗначениеЗаполнено(Партнер) Тогда
		//	ПроверяемыеРеквизиты.Добавить("ОтветственныйЗаРаспределение");
		//	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Партнер"));
		//	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Ответственный"));
		//КонецЕсли;
		
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Партнер"));
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Ответственный"));
		Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
			ПроверяемыеРеквизиты.Добавить("ОтветственныйЗаРаспределение");
		КонецЕсли;	
		
	ИначеЕсли ШагТендера=Перечисления.АГ_ШагиТендера.Шаг2_Распределение Тогда
		
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Партнер"));
		
	//Фирсанов О.И. 20.06.2019 21:36:30->	
	ИначеЕсли ШагТендера=Перечисления.АГ_ШагиТендера.Шаг7_Результат
		И СтатусТендера=Перечисления.АГ_СтатусТендера.ОтказОтУчастия Тогда	
		
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Партнер"));	
	//<-
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//Фирсанов О.И. 20.06.2019 20:36:30->
	Если РежимЗаписи=РежимЗаписиДокумента.ОтменаПроведения Тогда
		ШагТендера=Перечисления.АГ_ШагиТендера.Шаг1_ПередачаВРаботу;	
		
		НоваяСтрокаИсторияСтатусов	= ИсторияСтатусов.Добавить();
		НоваяСтрокаИсторияСтатусов.ДатаСтатуса	= ТекущаяДатаСеанса();
		НоваяСтрокаИсторияСтатусов.Шаг	= ШагТендера;
		НоваяСтрокаИсторияСтатусов.Ответственный	= ПользователиКлиентСервер.ТекущийПользователь();
		
		СтатусТендера	= Документы.АГ_Тендер.СтатусТендераПоШагу(ШагТендера,СтатусТендера,Неопределено);

	Иначе	
	//<-
	
	
	Если ПроверитьЗаполнение() Тогда
		
		ИспользуемАвтоматическоеИзменениеТендера	= (РежимЗаписи=РежимЗаписиДокумента.Проведение) И (НЕ ЗначениеЗаполнено(Ссылка) ИЛИ Ссылка.ШагТендера=ШагТендера); 
		
		Если ИспользуемАвтоматическоеИзменениеТендера Тогда 
			Если ШагТендера=Перечисления.АГ_ШагиТендера.Шаг1_ПередачаВРаботу Тогда
				
				Если ЗначениеЗаполнено(Партнер) Тогда
					ШагТендера=Перечисления.АГ_ШагиТендера.Шаг3_ПредварительноеПодтверждение;
					
					
					ТекстНапоминания	= "Добрый день!
                          |Создан новый тендер по контрагенту """+Партнер.Наименование+""" необходимо принять тендер в работу";

					ДополнительныеСвойства.Вставить("СоздатьНапоминание",Новый Структура("Текст,Пользователь",ТекстНапоминания,Ответственный));
					
					ДополнительныеСвойства.Вставить("СоздатьИОтправитьПисьмо",Истина);
					
				Иначе
					ШагТендера=Перечисления.АГ_ШагиТендера.Шаг2_Распределение;
					
					ТекстНапоминания	= "Добрый день! 
						|Сообщаем, что вам необходимо назначить ответственного менеджера, который будет участвовать в тендере "
						+"№"+Номер+" от "+Формат(Дата,"ДФ=dd.MM.yyyy")+".  по контрагенту "+НаименованиеЗаказчика+"  
						|С уважением, отдел электронных торгов!";
						
					ДополнительныеСвойства.Вставить("СоздатьНапоминание",Новый Структура("Текст,Пользователь",ТекстНапоминания,ОтветственныйЗаРаспределение));	
						
				КонецЕсли;
				
			ИначеЕсли ШагТендера=Перечисления.АГ_ШагиТендера.Шаг2_Распределение Тогда	
				
				ШагТендера=Перечисления.АГ_ШагиТендера.Шаг3_ПредварительноеПодтверждение;
				
				
				ТекстНапоминания	= "Добрый день!
					|Вы назначены ответственным по тендеру №"+Номер+" от "+Формат(Дата,"ДФ=dd.MM.yyyy")+"
					|Просьба завести контрагента в базу и принять тендер в работу";
				
				ДополнительныеСвойства.Вставить("СоздатьНапоминание",Новый Структура("Текст,Пользователь",ТекстНапоминания,Ответственный));
				ДополнительныеСвойства.Вставить("СоздатьИОтправитьПисьмо",Истина);
				
			КонецЕсли;
		КонецЕсли;
		
		Если ИспользуемАвтоматическоеИзменениеТендера ИЛИ Ссылка.ШагТендера<>ШагТендера Тогда
			НоваяСтрокаИсторияСтатусов	= ИсторияСтатусов.Добавить();
			НоваяСтрокаИсторияСтатусов.ДатаСтатуса	= ТекущаяДатаСеанса();
			НоваяСтрокаИсторияСтатусов.Шаг	= ШагТендера;
			НоваяСтрокаИсторияСтатусов.Ответственный	= ПользователиКлиентСервер.ТекущийПользователь();
		КонецЕсли;
		
		ПараметрыДляОпределенияСтатуса	= Документы.АГ_Тендер.ПараметрыДляОпределенияСтатуса(ЭтотОбъект);
		
		СтатусТендера	= Документы.АГ_Тендер.СтатусТендераПоШагу(ШагТендера,СтатусТендера,ПараметрыДляОпределенияСтатуса);

	КонецЕсли;
	
	//Фирсанов О.И. 20.06.2019 20:36:30->
	КонецЕсли;
	//<-
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ПараметрыНапоминания	= Неопределено;
	Если ДополнительныеСвойства.Свойство("СоздатьНапоминание",ПараметрыНапоминания) Тогда
		СоздатьНапоминаниеПользователю(ПараметрыНапоминания.Текст,ПараметрыНапоминания.Пользователь);	
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СоздатьИОтправитьПисьмо") Тогда
		Документы.АГ_Тендер.ОтправитьПисьмоОтветственному(Ссылка);	
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПриКопировании(ОбъектКопирования)
	
	МассивКопируемыхРеквизитов	= Новый Массив;
	МассивКопируемыхРеквизитов.Добавить("Организация");
	МассивКопируемыхРеквизитов.Добавить("Партнер");
	МассивКопируемыхРеквизитов.Добавить("НаименованиеЗаказчика");
	МассивКопируемыхРеквизитов.Добавить("Ответственный");
	МассивКопируемыхРеквизитов.Добавить("БизнесРегион");
	
	Для Каждого РеквизитДокТендер Из Метаданные.Документы.АГ_Тендер.Реквизиты Цикл
		Если МассивКопируемыхРеквизитов.Найти(РеквизитДокТендер.Имя)=Неопределено Тогда
			ЭтотОбъект[РеквизитДокТендер.Имя]	= Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	КритерииОценки.Очистить();
	ИсторияСтатусов.Очистить();
	ИсторияСогласования.Очистить();
	
	ШагТендера	= Перечисления.АГ_ШагиТендера.Шаг1_ПередачаВРаботу;
	СтатусТендера	= Перечисления.АГ_СтатусТендера.Новый;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура СоздатьНапоминаниеПользователю(ТекстНапоминания,Пользователь)
	
	Документы.АГ_Тендер.СоздатьНапоминаниеПользователю(Ссылка,ТекстНапоминания,Пользователь);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр АГ_ИсторияТендеров
	Движения.АГ_ИсторияТендеров.Записывать = Истина;
	Движение = Движения.АГ_ИсторияТендеров.Добавить();
	ЗаполнитьЗначенияСвойств(Движение,ЭтотОбъект);
	Движение.Тендер = Ссылка;

КонецПроцедуры

#КонецОбласти