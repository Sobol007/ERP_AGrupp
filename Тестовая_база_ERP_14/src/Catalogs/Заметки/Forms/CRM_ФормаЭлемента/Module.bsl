
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Предмет") Тогда 
		Объект.Предмет = Параметры.Предмет;
		Объект.ПредставлениеПредмета = ОбщегоНазначения.ПредметСтрокой(Объект.Предмет);
	КонецЕсли;
	
	Элементы.Предмет.Заголовок = Объект.ПредставлениеПредмета;
	Элементы.ГруппаПредмет.Видимость = ЗначениеЗаполнено(Объект.Предмет);
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Автор = Пользователи.ТекущийПользователь();
		ФорматированныйТекст = Параметры.ЗначениеКопирования.Содержание.Получить();
		
		Объект.ДляРабочегоСтола = Ложь;
		
	Иначе
		Элементы.ДатаЗаметки.Заголовок = Формат(Объект.ДатаИзменения, "ДФ='dd.MM.yyyy ""в"" HH:mm'");
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ФорматированныйТекст = ТекущийОбъект.Содержание.Получить();

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Содержание = Новый ХранилищеЗначения(ФорматированныйТекст, Новый СжатиеДанных(9));
	
	ТекстHTML = "";
	Вложения = Новый Структура;
	ФорматированныйТекст.ПолучитьHTML(ТекстHTML, Вложения);
	
	ТекущийОбъект.ТекстСодержания = СтроковыеФункцииКлиентСервер.ИзвлечьТекстИзHTML(ТекстHTML);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Элементы.ДатаЗаметки.Заголовок = Формат(Объект.ДатаИзменения, "ДФ='dd.MM.yyyy ""в"" HH:mm'");
	ОповеститьОбИзменении(Объект.Ссылка);
	Оповестить("ЛентаСобытий_Обновить");
КонецПроцедуры

// +CRM
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "ПроверкаПравописания_ИзмененТекст") И (Источник = ЭтотОбъект) Тогда
		ОбновитьТекстПисьма(Параметр.Текст, Параметр.Вложения);
	КонецЕсли;
	
КонецПроцедуры
// -CRM

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредметНажатие(Элемент)
	ПоказатьЗначение(,Объект.Предмет);
КонецПроцедуры

&НаКлиенте
Процедура АвторНажатие(Элемент)
	ПоказатьЗначение(,Объект.Автор);
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик команды "ПроверкаПравописания".
//
Процедура ПроверкаПравописания(Команда)
	Если ПустаяСтрока(ФорматированныйТекст.ПолучитьТекст()) Тогда Возврат; КонецЕсли;
	
	ПроверяемыйТекстПисьма	= "";
	ВложенияПисьма	= Новый Структура;
	ФорматированныйТекст.ПолучитьHTML(ПроверяемыйТекстПисьма, ВложенияПисьма);
	CRM_ОбщегоНазначенияКлиент.ПроверитьПравописаниеТекста(ПроверяемыйТекстПисьма, ВложенияПисьма,  "ФорматированныйТекст", ЭтотОбъект);
	
КонецПроцедуры // ПроверкаПравописания()

// +CRM
&НаКлиенте
Процедура СодержаниеПриИзменении(Элемент)
	Заголовок = Лев(ФорматированныйТекст.ПолучитьТекст(), 50);
КонецПроцедуры
// -CRM

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимость()
	Элементы.Автор.Заголовок = Объект.Автор;
	ОткрытаАвтором = Объект.Автор = Пользователи.ТекущийПользователь();
	Элементы.ПараметрыОтображения.Видимость = ОткрытаАвтором;
	//Элементы.ИнформацияОбАвторе.Видимость = Не ОткрытаАвтором;
	
	ТолькоПросмотр = Не ОткрытаАвтором;
	Элементы.Содержание.ТолькоПросмотр = Не ОткрытаАвтором;
	Элементы.КоманднаяПанельРедактирования.Видимость = ОткрытаАвтором;
КонецПроцедуры

// +CRM
&НаСервере
// Процедура обновляет значение поля "ПроверенныйТекстПисьма".
//
// Параметры:
//	ПроверенныйТекстПисьма	- Строка	- Текст письма HTML.
//
Процедура ОбновитьТекстПисьма(ПроверенныйТекстПисьма, ВложенияПисьма)
	Объект.ТекстHTML = ПроверенныйТекстПисьма;
	ФорматированныйТекст.УстановитьHTML(Объект.ТекстHTML, ВложенияПисьма);
КонецПроцедуры // ОбновитьТекстВопросаHTML()
// -CRM

&НаСервере
Процедура ПометитьНаУдалениеСервер()
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	
	ТекстВопроса = НСтр("ru = 'Примечание будет удалено. Продолжить?'");
	ОповещениеЗавершения = Новый ОписаниеОповещения("ПометитьНаУдалениеЗавершение", ЭтотОбъект, Новый Структура);
	ПоказатьВопрос(ОповещениеЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Модифицированность = Ложь;
		Закрыть();
	Иначе
		Объект.ПометкаУдаления = Истина;
		Записать();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
