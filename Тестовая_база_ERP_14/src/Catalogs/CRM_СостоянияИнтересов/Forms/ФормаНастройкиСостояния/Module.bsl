
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Настройки") Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметры.Настройки);
	ИначеЕсли Параметры.Свойство("Ссылка") Тогда
		ЗначениеВРеквизитФормы(Параметры.Ссылка.ПолучитьОбъект(), "Объект");
		ЗаписыватьОбъект = Истина;
	КонецЕсли;
	Если Объект.ВидСостояния = Перечисления.CRM_ВидыСостоянияИнтереса.Первое Тогда
		Элементы.ЗавершатьЗапланированныеАктивности.ТолькоПросмотр = Истина;
		Элементы.УказыватьДостигнутыйРезультат.ТолькоПросмотр = Истина;
		Элементы.ИнтерактивноеПланированиеАктивностей.ТолькоПросмотр = Истина;
		Элементы.УказаниеОжидаемойВыручки.ТолькоПросмотр  = Истина;
		Элементы.ОбязательноеЗаполнениеКлиента.ТолькоПросмотр  = Истина;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ОбязательноеПланированиеАктивности = Перечисления.CRM_ПланируемыеТипыАктивности.Взаимодействие;
	КонецЕсли;
	УправлениеВидимостьюЭлементов(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИндексЦветаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповещениеНовое = Новый ОписаниеОповещения("ЦветНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("ТекущийЦвет", Объект.ИндексЦвета);
	ФормаВыбораЦвета = ОткрытьФорму("Справочник.CRM_Категории.Форма.ФормаВыбораЦвета", ПараметрыФормы, Элемент,,,,
		ОповещениеНовое, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
КонецПроцедуры // ЦветНачалоВыбора()

&НаКлиенте
// Продолжение процедуры "ЦветНачалоВыбора"
//
Процедура ЦветНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ (Результат = Неопределено) И НЕ (Результат = КодВозвратаДиалога.Отмена) Тогда
		Объект.ИндексЦвета = Результат[0].Картинка;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗавершатьЗапланированныеАктивностиПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.ЗавершатьЗапланированныеАктивности) И Объект.УказыватьДостигнутыйРезультат Тогда
		Объект.УказыватьДостигнутыйРезультат = Ложь;
	КонецЕсли;
	УправлениеВидимостьюЭлементов(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Если ЗаписыватьОбъект Тогда
		Записать();
	КонецЕсли;
	Настройки = Новый Структура;
	Настройки.Вставить("УказаниеОжидаемойВыручки", Объект.УказаниеОжидаемойВыручки);
	Настройки.Вставить("ОбязательноеЗаполнениеКлиента", Объект.ОбязательноеЗаполнениеКлиента);
	Настройки.Вставить("ОбязательноеПланированиеАктивности", Объект.ОбязательноеПланированиеАктивности);
	Настройки.Вставить("ИнтерактивноеПланированиеАктивностей", Объект.ИнтерактивноеПланированиеАктивностей);
	Настройки.Вставить("ЗавершатьЗапланированныеАктивности", Объект.ЗавершатьЗапланированныеАктивности);
	Настройки.Вставить("ОтборЗавершаемыхАктивностей", Объект.ОтборЗавершаемыхАктивностей);
	Настройки.Вставить("УказыватьДостигнутыйРезультат", Объект.УказыватьДостигнутыйРезультат);
	Настройки.Вставить("ДобавитьВероятностьВпредставлениеСостояния", Объект.ДобавитьВероятностьВпредставлениеСостояния);
	Настройки.Вставить("ВероятностьСделки", Объект.ВероятностьСделки);
	Настройки.Вставить("ИндексЦвета", Объект.ИндексЦвета);
	Закрыть(Настройки);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюЭлементов(Форма)
		
	Форма.Элементы.УказыватьДостигнутыйРезультат.Видимость = ЗначениеЗаполнено(Форма.Объект.ЗавершатьЗапланированныеАктивности); 
	Форма.Элементы.ОтборЗавершаемыхАктивностей.Видимость = ЗначениеЗаполнено(Форма.Объект.ЗавершатьЗапланированныеАктивности);
	Форма.Элементы.ОтборЗавершаемыхАктивностей.АвтоОтметкаНезаполненного = ЗначениеЗаполнено(Форма.Объект.ЗавершатьЗапланированныеАктивности);
		
КонецПроцедуры

#КонецОбласти 
