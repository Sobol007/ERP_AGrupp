
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = Справочники.Валюты.ПолучитьМакет("РЭЙ_КлассификаторВалют");

	Макет.Параметры.Расшифровка = Истина;

	ПолеТабличногоДокумента.Вывести(Макет);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ДОКУМЕНТА ФОРМЫ ПолеТабличногоДокумента

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.
	ТабличныйДокумент = Элементы.ПолеТабличногоДокумента;
	ТекущаяОбласть = ТабличныйДокумент.ТекущаяОбласть;
	
	Если ТекущаяОбласть.Низ <> ТекущаяОбласть.Верх Тогда
		
		Сообщить("Действие не доступно! Выделенная область состоит из нескольких строк. Алгоритм подразумевает использование общей формы. По требованиям задачи не переводиться на упр. формы.");
		Возврат;
		
	КонецЕсли; 

	ОбластьКодЧисловой 			= ПолеТабличногоДокумента.Области.КодЧисловой;
	ОбластьНаименованиеКраткое 	= ПолеТабличногоДокумента.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное 	= ПолеТабличногоДокумента.Области.НаименованиеПолное;
	ОбластьНаименованиеАнгл 	= ПолеТабличногоДокумента.Области.НаименованиеАнгл;

	КодЧисловой 		= ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.Право).Текст;
	НаименованиеКраткое = ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
	НаименованиеПолное 	= ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное. Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст;
	НаименованиеАнгл 	= ПолеТабличногоДокумента.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеАнгл.   Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеАнгл.   Право).Текст;
	НаименованиеЦел 	= НаименованиеАнгл+"s";
	НаименованиеДроб 	= "/100";
	
	ПараметрыЗаполнения	= Новый Структура;
	ПараметрыЗаполнения.Вставить("КодЧисловой", КодЧисловой);
	ПараметрыЗаполнения.Вставить("НаименованиеКраткое", НаименованиеКраткое);
	ПараметрыЗаполнения.Вставить("НаименованиеПолное", НаименованиеПолное);
	ПараметрыЗаполнения.Вставить("НаименованиеАнгл", НаименованиеАнгл);
	ПараметрыЗаполнения.Вставить("НаименованиеЦел", НаименованиеЦел);
	ПараметрыЗаполнения.Вставить("НаименованиеДроб", НаименованиеДроб);
	
	// Проверка наличия выбранной валюты.
	Ссылка =  НайтиЭлементСправочникаПоКодуНаСервере("Валюты", КодЧисловой);//Справочники.Валюты.НайтиПоКоду(Валюты);

	Если НЕ Ссылка.Пустая() Тогда
		
		ПараметрыЗаполнения.Вставить("Ссылка", Ссылка);
		
		ТекстВопроса = "В справочнике ""Валюты"" уже существует элемент с кодом """ + КодЧисловой + """! Открыть существующий?";
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОткрытиеСуществующейВалюты", ЭтотОбъект, ПараметрыЗаполнения);
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Отмена);
		
	Иначе 
		
		ОткрытиеНовойВалюты(ПараметрыЗаполнения);

	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


&НаСервереБезКонтекста
Функция НайтиЭлементСправочникаПоКодуНаСервере(СправочникНаименование, Код)
	
	Возврат Справочники[СправочникНаименование].НайтиПоКоду(Код);
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиЭлементСправочникаПоНаименованиюНаСервере(СправочникНаименование, Наименование)
	
	Возврат Справочники[СправочникНаименование].НайтиПоНаименованию(Наименование);
	
КонецФункции

&НаКлиенте
Процедура ОткрытиеНовойВалюты(ПараметрыЗаполнения)
	
	ФормаНовогоЭлемента = ПолучитьФорму("Справочник.Валюты.ФормаОбъекта",, ЭтаФорма.ВладелецФормы);

	ФормаНовогоЭлемента.Объект.Код = ПараметрыЗаполнения.КодЧисловой;
	ФормаНовогоЭлемента.Объект.Наименование = ПараметрыЗаполнения.НаименованиеКраткое;
	ФормаНовогоЭлемента.Объект.НаименованиеПолное = ПараметрыЗаполнения.НаименованиеПолное;
	ФормаНовогоЭлемента.Объект.РЭЙ_НаименованиеАнгл = ПараметрыЗаполнения.НаименованиеАнгл;
	ФормаНовогоЭлемента.Объект.РЭЙ_ПараметрыПрописиНаАнглийском = "" + ПараметрыЗаполнения.НаименованиеЦел + ", " + ПараметрыЗаполнения.НаименованиеЦел + 
			", " + ПараметрыЗаполнения.НаименованиеДроб + ", " + ПараметрыЗаполнения.НаименованиеДроб +", "+2;

	ФормаНовогоЭлемента.Открыть();
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытиеСуществующейВалюты(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Справочник.Валюты.ФормаОбъекта", Новый Структура("Ключ", ДополнительныеПараметры.Ссылка),  ЭтаФорма.ВладелецФормы); 
	ИначеЕсли Результат <> КодВозвратаДиалога.Отмена Тогда
		ОткрытиеНовойВалюты(ДополнительныеПараметры);
	КонецЕсли;
		
КонецПроцедуры	
	

