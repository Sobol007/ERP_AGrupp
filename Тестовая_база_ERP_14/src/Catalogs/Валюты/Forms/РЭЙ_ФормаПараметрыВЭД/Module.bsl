#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПрочитатьРеквизитыИзВременногоХранилища();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПрочитатьПараметрыПрописи();
	УстановитьПолеПрописиДробнойЧастиНаАнглПоУмолчанию();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы Тогда 
		Если Модифицированность Тогда
			Отказ = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	
	Если Модифицированность Тогда 
		СформироватьПараметрыПрописиНаАнглийском();
		
		АдресХранилища = ПоместитьРеквизитыВоВременноеХранилище();
		ПараметрыЗакрытия.Вставить("АдресХранилища", АдресХранилища);
	КонецЕсли;
	
	ПараметрыЗакрытия.Вставить("Модифицированность", Модифицированность);
	
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, ПараметрыЗакрытия); 
	ОписаниеОповещенияОЗакрытии = Неопределено;
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПрочитатьПараметрыПрописи()
	ПараметрыПрописи = Объект.РЭЙ_ПараметрыПрописиНаАнглийском;
	СтрокаПараметров = СтрЗаменить(ПараметрыПрописи, ",", Символы.ПС);
	
	рЦелаяЧасть = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 1));
	рДробнаяЧасть = СокрЛП(СтрПолучитьСтроку(СтрокаПараметров, 3));
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПараметрыПрописиНаАнглийском()
	ПараметрыПрописиНаАнглийском = рЦелаяЧасть + ", " + рЦелаяЧасть + ", " + рДробнаяЧасть + ", " + рДробнаяЧасть + ", " + 2;
	
	Если ПараметрыПрописиНаАнглийском <> Объект.РЭЙ_ПараметрыПрописиНаАнглийском Тогда
		Объект.РЭЙ_ПараметрыПрописиНаАнглийском = ПараметрыПрописиНаАнглийском;		
	КонецЕСли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПолеПрописиДробнойЧастиНаАнглПоУмолчанию()
	Если Не ЗначениеЗаполнено(рДробнаяЧасть) Тогда
		рДробнаяЧасть = "/100";
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРеквизитыИзВременногоХранилища()
	Если Параметры.Свойство("АдресВременногоХранилища")  Тогда 
		РЭЙ_СлужебныйСервер.ЗаполнитьСвойстваОбъектаИзВременногоХранилища(Объект, Параметры.АдресВременногоХранилища);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция ПоместитьРеквизитыВоВременноеХранилище()
	МетаданныеОбъекта = РеквизитФормыВЗначение("Объект").Метаданные();
	АдресХранилища = РЭЙ_СлужебныйСервер.ВременноеХранилищеРеквизитов(Объект, МетаданныеОбъекта, УникальныйИдентификатор);
	
	Возврат АдресХранилища;
КонецФункции

#КонецОбласти 
