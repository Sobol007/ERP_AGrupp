#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Ссылка.Пустая() Тогда
		ОпределитьДатыПакетаЗадач();
		Если Родитель <> Ссылка.Родитель Тогда
			ДополнительныеСвойства.Вставить("ПредыдущийРодитель", Ссылка.Родитель);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПредыдущийРодитель") Тогда
		// Обновим все даты всех родителей этого этапа (если родитель был изменен).
		Если ЗначениеЗаполнено(Родитель) Тогда
			CRM_УправлениеПроектамиВызовСервера.ОбновитьДатыПакетаЗадачИВсехРодителей(Родитель);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДополнительныеСвойства.ПредыдущийРодитель) Тогда
			CRM_УправлениеПроектамиВызовСервера.ОбновитьДатыПакетаЗадачИВсехРодителей(ДополнительныеСвойства.ПредыдущийРодитель);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = CRM_ОбщегоНазначенияСервер.ТекущийПользователь();
	Описание = "";
	ОписаниеHTML = "";
	Затраты.Очистить();
	СуммарнаяДатаНачалаПлановая = "";
	СуммарнаяДатаНачалаФакт = "";
	СуммарнаяДатаОкончанияПлановая = "";
	СуммарнаяДатаОкончанияФакт = "";
	СуммарнаяДлительностьПлановая = "";
	СуммарнаяДлительностьФакт = "";
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Ответственный = CRM_ОбщегоНазначенияСервер.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОпределитьДатыПакетаЗадач() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапПроекта.Ссылка КАК Ссылка,
		|	МИНИМУМ(ЗадачаПроекта.ПлановаяДатаНачала) КАК ПлановаяДатаНачала,
		|	МАКСИМУМ(ЗадачаПроекта.ПлановаяДатаОкончания) КАК ПлановаяДатаОкончания,
		|	МИНИМУМ(ЗадачаПроекта.ФактическаяДатаНачала) КАК ФактическаяДатаНачала,
		|	МАКСИМУМ(ЗадачаПроекта.ФактическаяДатаОкончания) КАК ДатаЗакрытияМакс,
		|	МИНИМУМ(ЗадачаПроекта.ФактическаяДатаОкончания) КАК ДатаЗакрытияМин
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	Документ.CRM_ЭтапКалендарногоПлана КАК ЗадачаПроекта
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.CRM_ЭтапыПроектов КАК ЭтапПроекта
		|		ПО ЗадачаПроекта.Этап = ЭтапПроекта.Ссылка
		|			И (НЕ ЗадачаПроекта.ПометкаУдаления)
		|			И НЕ ЗадачаПроекта.Статус = ЗНАЧЕНИЕ(Перечисление.CRM_СтатусыЭтаповКалендарногоПлана.Отменена)
		|ГДЕ
		|	ЭтапПроекта.Ссылка В ИЕРАРХИИ(&ЭтаСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЭтапПроекта.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВременнаяТаблица.ПлановаяДатаНачала) КАК ПлановаяДатаНачала,
		|	МАКСИМУМ(ВременнаяТаблица.ПлановаяДатаОкончания) КАК ПлановаяДатаОкончания,
		|	МИНИМУМ(ВременнаяТаблица.ФактическаяДатаНачала) КАК ФактическаяДатаНачала,
		|	МАКСИМУМ(ВременнаяТаблица.ДатаЗакрытияМакс) КАК ДатаЗакрытияМакс,
		|	МИНИМУМ(ВременнаяТаблица.ДатаЗакрытияМин) КАК ДатаЗакрытияМин
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица";
	
	Запрос.УстановитьПараметр("ЭтаСсылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СуммарнаяДатаНачалаПлановая = ВыборкаДетальныеЗаписи.ПлановаяДатаНачала;
		СуммарнаяДатаОкончанияПлановая = ВыборкаДетальныеЗаписи.ПлановаяДатаОкончания;
		СуммарнаяДатаНачалаФакт = ВыборкаДетальныеЗаписи.ФактическаяДатаНачала;
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаЗакрытияМин) Тогда
			СуммарнаяДатаОкончанияФакт = ВыборкаДетальныеЗаписи.ДатаЗакрытияМакс;
		Иначе
			СуммарнаяДатаОкончанияФакт = Дата(1,1,1);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СуммарнаяДатаОкончанияПлановая) Тогда
			СуммарнаяДлительностьПлановая = (КонецДня(СуммарнаяДатаОкончанияПлановая) - НачалоДня(СуммарнаяДатаНачалаПлановая) + 1) / 86400;
		Иначе	
			СуммарнаяДлительностьПлановая = 0;
		КонецЕсли;	
		
		Если ЗначениеЗаполнено(СуммарнаяДатаОкончанияФакт) Тогда
			СуммарнаяДлительностьФакт = (КонецДня(СуммарнаяДатаОкончанияФакт) - НачалоДня(СуммарнаяДатаНачалаФакт) + 1) / 86400;
		Иначе
			СуммарнаяДлительностьФакт = 0;
		КонецЕсли;
	Иначе
		СуммарнаяДатаНачалаПлановая = Дата(1,1,1);
		СуммарнаяДатаОкончанияПлановая = Дата(1,1,1);
		СуммарнаяДатаНачалаФакт = Дата(1,1,1);
		СуммарнаяДатаОкончанияФакт = Дата(1,1,1);
		СуммарнаяДлительностьПлановая = 0;
		СуммарнаяДлительностьФакт = 0;
	КонецЕсли;
	

КонецПроцедуры


#КонецЕсли