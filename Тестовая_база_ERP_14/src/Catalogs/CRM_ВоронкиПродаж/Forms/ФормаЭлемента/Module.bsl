
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		Если	Параметры.Свойство("ЗначениеКопирования")
			И	ТипЗнч(Параметры.ЗначениеКопирования) = Тип("СправочникСсылка.CRM_ВоронкиПродаж")
			И	(Параметры.ЗначениеКопирования.Предопределенный ИЛИ Параметры.ЗначениеКопирования.Родитель.Предопределенный) Тогда
			//
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Копирование предопределенного элемента запрещено!';en='Copying of the predetermined element are forbidden!'"));
			Возврат;
		ИначеЕсли ЗначениеЗаполнено(Объект.Родитель) И Объект.Родитель.Предопределенный Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Изменение состава этапов предопределенного элемента запрещено!';en='Change of composition of stages of the predetermined element are forbidden!'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.СценарийИнтереса.Видимость = ЗначениеЗаполнено(Объект.СценарийИнтереса);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИндексЦветаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОповещениеНовое = Новый ОписаниеОповещения("ЦветНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("ТекущийЦвет", Объект.ИндексЦвета);
	ФормаВыбораЦвета = ОткрытьФорму("Справочник.CRM_Категории.Форма.ФормаВыбораЦвета", ПараметрыФормы, Элемент,,,,
		ОповещениеНовое, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
КонецПроцедуры // ЦветНачалоВыбора()

&НаКлиенте
// Продолжение процедуры "ЦветНачалоВыбора"
//
Процедура ЦветНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если НЕ (Результат = Неопределено) И НЕ (Результат = КодВозвратаДиалога.Отмена) Тогда
		Объект.ИндексЦвета = Результат[0].Картинка;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры	

#КонецОбласти

