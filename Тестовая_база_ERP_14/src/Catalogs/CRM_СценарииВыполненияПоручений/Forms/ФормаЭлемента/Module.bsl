
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ИспользуемыеТипы = CRM_БизнесПроцессыСервер.ИспользуемыеТипыГрупповыхПредметовПроцессов();
		Для каждого Тип из ИспользуемыеТипы Цикл
			НовСтрока = Объект.ИспользуемыеОбъекты.Добавить();
			НовСтрока.ТипПредмета = Тип;
		КонецЦикла;
	КонецЕсли;
	
	СоздатьЭлементыФормыДляТипов();
	
	//ЗаполнитьСписокПравилРасчета(ЭтотОбъект);
	УстановитьВидимостьДоступность();
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(ТекущийОбъект.ПравилоРасчетаПроцентаВыполненияЗадач)
	  И Элементы.ПравилоРасчетаПроцентаВыполненияЗадач.СписокВыбора.НайтиПоЗначению(ТекущийОбъект.ПравилоРасчетаПроцентаВыполненияЗадач) = Неопределено Тогда
		ТекущийОбъект.ПравилоРасчетаПроцентаВыполненияЗадач = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ТекстОшибки = "";
	Для каждого СтрокаОбъекта из Объект.ИспользуемыеОбъекты Цикл
		Если СтрокаОбъекта.Использовать И НЕ ЗначениеЗаполнено(СтрокаОбъекта.ВариантЗаполнения) Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='Не выбран вариант заполнения для типа';en='No option for type filling is selected'")+ " " + Строка(СтрокаОбъекта.ТипПредмета)+Символы.ПС;
		КонецЕсли;
	КонецЦикла;
	Если ТекстОшибки<>"" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПараметрИспользованияПриИзменении(Элемент)
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры // ПараметрИспользованияПриИзменении()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()

	Для каждого СтрокаОбъекта из Объект.ИспользуемыеОбъекты Цикл 
		ТипНаименование = СтрокаОбъекта.ТипПредмета.Имя;
		Элементы["ВариантЗаполнения"+ТипНаименование].Доступность = СтрокаОбъекта.Использовать;
		Элементы["ВариантЗаполнения"+ТипНаименование].АвтоОтметкаНезаполненного = СтрокаОбъекта.Использовать;
	КонецЦикла;
	
КонецПроцедуры // УстановитьВидимостьДоступность()

&НаСервере
Процедура СоздатьЭлементыФормыДляТипов()
	
	ГруппаРазмещения = Элементы.ГруппаПараметры;
	Индекс = 0;
	Для каждого СтрокаОбъекта из Объект.ИспользуемыеОбъекты Цикл 
		
		ТипНаименование = СтрокаОбъекта.ТипПредмета.Имя;
		ТипСиноним = СтрокаОбъекта.ТипПредмета.Синоним;
		НоваяГруппа = Элементы.Добавить("ГруппаТипа"+ТипНаименование, Тип("ГруппаФормы"), ГруппаРазмещения);
		НоваяГруппа.Заголовок = ТипСиноним;
		НоваяГруппа.Вид                        = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.Поведение                  = ПоведениеОбычнойГруппы.Свертываемая;
		НоваяГруппа.ОтображениеУправления      = ОтображениеУправленияОбычнойГруппы.Картинка;
		НоваяГруппа.Группировка                = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		НоваяГруппа.РастягиватьПоГоризонтали   = Истина;
		НоваяГруппа.РастягиватьПоВертикали     = Ложь;
		
		НовыйЭлемент = Элементы.Добавить("Использовать"+ТипНаименование, Тип("ПолеФормы"), НоваяГруппа);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовыйЭлемент.ПутьКДанным = "Объект.ИспользуемыеОбъекты["+Строка(Индекс)+"].Использовать";
		НовыйЭлемент.УстановитьДействие("ПриИзменении", "ПараметрИспользованияПриИзменении");
		
		НовыйЭлемент = Элементы.Добавить("ВариантЗаполнения"+ТипНаименование, Тип("ПолеФормы"), НоваяГруппа);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовыйЭлемент.ПутьКДанным = "Объект.ИспользуемыеОбъекты["+Строка(Индекс)+"].ВариантЗаполнения";
		НовыйЭлемент.ПодсказкаВвода = НСтр("ru='Выберите вариант заполнения';en='Select the fill option'");
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	Элементы.Переместить(Элементы.ГруппаПрочее, ГруппаРазмещения);
	
КонецПроцедуры

#КонецОбласти
