#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) 
		 И ДанныеЗаполнения.Свойство("Родитель") 
		 И ТипЗнч(ДанныеЗаполнения.Родитель) = Тип("СправочникСсылка.CRM_ШаблоныЭтаповПроектов") Тогда
		 
		 ШаблонПроекта = ДанныеЗаполнения.Родитель.ШаблонПроекта;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Ссылка.Пустая() Тогда
		ОпределитьСмещенияШаблонаЭтапа();
		Если Родитель <> Ссылка.Родитель Тогда
			ДополнительныеСвойства.Вставить("ПредыдущийРодитель", Ссылка.Родитель);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПредыдущийРодитель") Тогда
		// Обновим смещения всех родителей этого шаблона этапа (если родитель был изменен).
		Если ЗначениеЗаполнено(Родитель) Тогда
			CRM_УправлениеПроектамиВызовСервера.ОбновитьСмещенияШаблонаЭтапаИВсехРодителей(Родитель);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДополнительныеСвойства.ПредыдущийРодитель) Тогда
			CRM_УправлениеПроектамиВызовСервера.ОбновитьСмещенияШаблонаЭтапаИВсехРодителей(ДополнительныеСвойства.ПредыдущийРодитель);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура ОпределитьСмещенияШаблонаЭтапа() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапПроекта.Ссылка КАК Ссылка,
		|	МИНИМУМ(ШаблонЭтапа.Смещение) КАК МинимальноеСмещение,
		|	МАКСИМУМ(ШаблонЭтапа.Смещение) КАК МаксимальноеСмещение,
		|	МАКСИМУМ(ШаблонЭтапа.Смещение + ВЫБОР
		|			КОГДА ШаблонЭтапа.ТипЭтапа = ЗНАЧЕНИЕ(Перечисление.CRM_ТипыЭтапов.КонтрольнаяТочка)
		|				ТОГДА 1
		|			ИНАЧЕ ШаблонЭтапа.ПродолжительностьДней
		|		КОНЕЦ) КАК Длительность
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	Документ.CRM_ШаблонЭтапаКалендарногоПлана КАК ШаблонЭтапа
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.CRM_ШаблоныЭтаповПроектов КАК ЭтапПроекта
		|		ПО ШаблонЭтапа.ШаблонЭтапаПроекта = ЭтапПроекта.Ссылка
		|			И (НЕ ШаблонЭтапа.ПометкаУдаления)
		|ГДЕ
		|	ЭтапПроекта.Ссылка В ИЕРАРХИИ(&ЭтаСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЭтапПроекта.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВременнаяТаблица.МинимальноеСмещение) КАК МинимальноеСмещение,
		|	МАКСИМУМ(ВременнаяТаблица.МаксимальноеСмещение) КАК МаксимальноеСмещение,
		|	МАКСИМУМ(ВременнаяТаблица.Длительность) КАК Длительность
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица";
	
	Запрос.УстановитьПараметр("ЭтаСсылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		МинимальноеСмещение = ВыборкаДетальныеЗаписи.МинимальноеСмещение;
		МаксимальноеСмещение = ВыборкаДетальныеЗаписи.МаксимальноеСмещение;
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Длительность) Тогда
			ПродолжительностьДней = ВыборкаДетальныеЗаписи.Длительность - МинимальноеСмещение;
		КонецЕсли;
	Иначе	
		МинимальноеСмещение = 0;
		МаксимальноеСмещение = 0;
		ПродолжительностьДней = 0;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли

