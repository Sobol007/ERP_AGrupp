
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = Справочники.РЭЙ_КлиринговыеСистемы.ПолучитьМакет("КлассификаторКлиринговыхСистем");

	Макет.Параметры.Расшифровка = Истина;

	ПолеТабличногоДокумента.Вывести(Макет);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ДОКУМЕНТА ФОРМЫ ПолеТабличногоДокумента

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	// Получение значений полей выбранной строки.

	ТабличныйДокумент 	= ПолеТабличногоДокумента;
	ТекущаяОбласть 		= ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКод 					= ТабличныйДокумент.Области.Код;
	ОбластьНаименованиеКраткое 	= ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное 	= ТабличныйДокумент.Области.НаименованиеПолное;
	ОбластьВалюта 				= ТабличныйДокумент.Области.Валюта;
	ОбластьСтрана 				= ТабличныйДокумент.Области.Страна;
	ОбластьКодАльфа2Страны 		= ТабличныйДокумент.Области.КодСтраны;
		
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		ПараметрыЗаполнения = Новый Структура;

		Код 				= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКод.Лево, ТекущаяОбласть.Низ, ОбластьКод.Право).Текст;
		НаименованиеКраткое = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
		НаименованиеПолное 	= ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное.Право).Текст;
	   	Валюта 				= НайтиЭлементСправочникаПоНаименованиюНаСервере("Валюты", СокрЛП(ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьВалюта.Лево, ТекущаяОбласть.Низ, ОбластьВалюта.Право).Текст));
		
		НаименованиеСтраны 	= СокрЛП(ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьСтрана.Лево, ТекущаяОбласть.Низ, ОбластьСтрана. Право).Текст);
		Страна 				= НайтиЭлементСправочникаПоНаименованиюНаСервере("СтраныМира", НаименованиеСтраны);
		КодАльфа2Страны 	= СокрЛП(ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодАльфа2Страны.Лево, ТекущаяОбласть.Низ, ОбластьКодАльфа2Страны. Право).Текст);
		
		
		ПараметрыЗаполнения.Вставить("Код", Код);
		ПараметрыЗаполнения.Вставить("НаименованиеКраткое", НаименованиеКраткое);
		ПараметрыЗаполнения.Вставить("НаименованиеПолное", НаименованиеПолное);
		ПараметрыЗаполнения.Вставить("Валюта", Валюта);
		ПараметрыЗаполнения.Вставить("НаименованиеСтраны", НаименованиеСтраны);
		ПараметрыЗаполнения.Вставить("Страна", Страна);
		ПараметрыЗаполнения.Вставить("КодАльфа2Страны", КодАльфа2Страны);
			 
		// Проверка наличия выбранного элемента.
		Ссылка = НайтиЭлементСправочникаПоКодуНаСервере("РЭЙ_КлиринговыеСистемы", Код);
		
		Если НЕ Ссылка.Пустая() Тогда
			
			ПараметрыЗаполнения.Вставить("Ссылка", Ссылка);
			
			ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ОткрытиеСуществующейКлиринговойСистемы", ЭтотОбъект, ПараметрыЗаполнения);
			
			ТекстВопроса = "В справочнике ""Клиринговые системы"" уже существует элемент с кодом """ + Код + """!
				|Нажмите ""Да"", чтобы открыть уже существующий элемент.
				|Нажмите ""Нет"", чтобы создать новый элемент.";
			
			ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена,, КодВозвратаДиалога.Отмена);
			
		Иначе	
			
			ОткрытиеНовойКлиринговойСистемы(ПараметрыЗаполнения)
			
		КонецЕсли;
		
	Иначе
		
		Сообщить("Действие не доступно! Выделенная область состоит из нескольких строк. Алгоритм подразумевает использование общей формы. По требованиям задачи не переводиться на упр. формы.");
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция НайтиЭлементСправочникаПоКодуНаСервере(СправочникНаименование, Код)
	
	Возврат Справочники[СправочникНаименование].НайтиПоКоду(Код);
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиЭлементСправочникаПоНаименованиюНаСервере(СправочникНаименование, Наименование)
	
	Возврат Справочники[СправочникНаименование].НайтиПоНаименованию(Наименование);
	
КонецФункции

&НаКлиенте
Процедура ОткрытиеНовойКлиринговойСистемы(ПараметрыЗаполнения)
	
	ФормаНовогоЭлемента = ПолучитьФорму("Справочник.РЭЙ_КлиринговыеСистемы.Форма.ФормаЭлемента",, ЭтаФорма.ВладелецФормы);
	
	Страна = ПараметрыЗаполнения.Страна; 
	Код = ПараметрыЗаполнения.Код;
	НаименованиеКраткое = ПараметрыЗаполнения.НаименованиеКраткое;
	Валюта = ПараметрыЗаполнения.Валюта;
	НаименованиеСтраны = ПараметрыЗаполнения.НаименованиеСтраны;
	КодАльфа2Страны = ПараметрыЗаполнения.КодАльфа2Страны;
	НаименованиеПолное = ПараметрыЗаполнения.НаименованиеПолное;
		
	Если Страна.Пустая() И СтрДлина(СокрЛП(НаименованиеСтраны)) <> 0 Тогда
		
		Сообщить("В справочнике ""Классификатор стран мира"" отсутствует страна данной клиринговой системы.
		|При добавлении клиринговой системы в справочник ""Классификатор стран мира""
		|одновременно будет добавлена страна " + НаименованиеСтраны + ".");
		
		ФормаНовогоЭлемента.НаименованиеНовойСтраны = СокрЛП(НаименованиеСтраны);
		
		ФормаНовогоЭлемента.КодАльфа2НовойСтраны = КодАльфа2Страны;
		
	КонецЕсли;
	
	ФормаНовогоЭлемента.Объект.Код 					= Код;
	ФормаНовогоЭлемента.Объект.НаименованиеКраткое 	= НаименованиеКраткое;
	ФормаНовогоЭлемента.Объект.Наименование 		= НаименованиеПолное;
	ФормаНовогоЭлемента.Объект.Валюта 				= Валюта;
	ФормаНовогоЭлемента.Объект.Страна 				= Страна;
	
	ФормаНовогоЭлемента.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытиеСуществующейКлиринговойСистемы(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОткрытьФорму("Справочник.РЭЙ_КлиринговыеСистемы.Форма.ФормаЭлемента", Новый Структура("Ключ", Параметры.Ссылка),  ЭтаФорма.ВладелецФормы); 
	ИначеЕсли Результат <> КодВозвратаДиалога.Отмена Тогда
		ОткрытиеНовойКлиринговойСистемы(Параметры);
	КонецЕсли;
	
КонецПроцедуры	


