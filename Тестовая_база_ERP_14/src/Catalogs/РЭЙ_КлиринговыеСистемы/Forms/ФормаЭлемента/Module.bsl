
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ПустаяСтрока(НаименованиеНовойСтраны) И Объект.Страна.Пустая() Тогда
		
		НужноОткрытьНовыйЭлемент = Ложь;
		НоваяСтрана = ПолучитьНовуюСтрану(НаименованиеНовойСтраны, КодАльфа2НовойСтраны, НужноОткрытьНовыйЭлемент, Отказ);
		
		Если НоваяСтрана <> Неопределено Тогда 
			
			Объект.Страна = НоваяСтрана;
			
			Если НужноОткрытьНовыйЭлемент Тогда 
				ПоказатьЗначение(, НоваяСтрана);
			КонецЕсли;	
			
		КонецЕсли;	
			
	КонецЕсли;	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


&НаСервереБезКонтекста
Функция ПолучитьНовуюСтрану(НаименованиеНовойСтраны, КОдАльфа2НовойСтраны, НужноОткрытьНовыйЭлемент,Отказ)
	
	НашлиСтрануВКлассификаторе = ЛОЖЬ;
	
	МакетСтран = Справочники.СтраныМира.ПолучитьМакет("РЭЙ_КлассификаторСтранМира");
	
	НККодЧисловой = МакетСтран.Области.КодЧисловой.Лево;
	НКНаименованиеКраткое = МакетСтран.Области.НаименованиеКраткое.Лево;
	НККодАльфа2 = МакетСтран.Области.КодАльфа2.Лево;		
	НККодАльфа3 = МакетСтран.Области.КодАльфа3.Лево;
	НКНаименованиеПолное = МакетСтран.Области.НаименованиеПолное.Лево;
	НКНаименованиеАнгл = МакетСтран.Области.НаименованиеАнгл.Лево;
	
	Нс = 3;
	Пока ИСТИНА Цикл
		
		НС = НС+1;
		текСтр = МакетСтран.ПолучитьОбласть("R"+НС+"C1:R"+НС+"C20");//с запасом
		
		ТекНаименованиеНовойСтраны = СокрЛП(ТекСтр.Область(1,НКНаименованиеКраткое,1,НКНаименованиеКраткое).Текст);
		ТекКодАльфа2 = СокрЛП(ТекСтр.Область(1,НККодАльфа2,1,НККодАльфа2).Текст);
		
		Если (НЕ ЗначениеЗаполнено(ТекНаименованиеНовойСтраны)) и (НЕ ЗначениеЗаполнено(ТекКодАльфа2)) Тогда
			Прервать;
		КонецЕсли;
		
		Если ВРЕГ(ТекНаименованиеНовойСтраны) = ВРЕГ(НаименованиеНовойСтраны) И ВРЕГ(ТекКодАльфа2) = ВРЕГ(КОдАльфа2НовойСтраны) Тогда
			НашлиСтрануВКлассификаторе = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не НашлиСтрануВКлассификаторе Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В справочнике ""Классификатор стран мира"" не найдена страна "+НаименованиеНовойСтраны+" с кодом Альфа-2 "+КодАльфа2НовойСтраны);
		Отказ = Истина;
		Возврат Неопределено;
		
	КонецЕсли;
	
	КодЧисловой         = СокрЛП(ТекСтр.Область(1,НККодЧисловой,1,НККодЧисловой).Текст);
	НаименованиеПолное  = СокрЛП(ТекСтр.Область(1, НКНаименованиеПолное,1,НКНаименованиеПолное).Текст);
	
	// Проверка наличия выбранного элемента.
	ТекСсылка = Справочники.СтраныМира.НайтиПоКоду(КодЧисловой);
	
	Если НЕ ТекСсылка.Пустая() Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("По коду " + КодЧисловой + " страна уже была создана ранее");
		Возврат ТекСсылка;
	КонецЕсли;
	
	//// Создание нового элемента справочника.
	НовыйЭлемент = Справочники.СтраныМира.СоздатьЭлемент();
	
	НовыйЭлемент.Код 				= КодЧисловой;
	НовыйЭлемент.Наименование 		= СокрЛП(ТекСтр.Область(1, НКНаименованиеКраткое).Текст);
	НовыйЭлемент.НаименованиеПолное = НаименованиеПолное;
	НовыйЭлемент.КодАльфа2 			= КодАльфа2НовойСтраны;
	НовыйЭлемент.КодАльфа3          = СокрЛП(ТекСтр.Область(1, НККодАльфа3).Текст);
	НовыйЭлемент.РЭЙ_НаименованиеАнгл = СокрЛП(ТекСтр.Область(1, НКНаименованиеАнгл).Текст);
	
	НовыйЭлемент.Записать();
	
	НужноОткрытьНовыйЭлемент = Истина;
	
	Возврат НовыйЭлемент.Ссылка;
	
КонецФункции	

