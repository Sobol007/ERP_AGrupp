
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ФайлИзменен = Ложь;
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		Если Объект.ТипХраненияФайла = ПредопределенноеЗначение("Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе") Тогда
			АдресФайла = Параметры.АдресВременногоХранилища;
			ФайлИзменен = Истина;
		Иначе
			Объект.ПутьКФайлу = Параметры.ПолноеИмяФайла;
		КонецЕсли;
		
		Объект.ИндексКартинки = РЭЙ_СлужебныйСервер.ПолучитьИндексПиктограммыФайла(Объект.Расширение);
		Объект.Автор = ПараметрыСеанса.ТекущийПользователь;
		Объект.ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.Наименование = Объект.ПолноеНаименование;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ФайлИзменен Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
		ТекущийОбъект.ФайлХранилище = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзмененСправочник_РЭЙ_Файлы");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФайл(Команда)
	Если Объект.ТипХраненияФайла = ПредопределенноеЗначение("Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе") Тогда
		Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
			текКаталог = КаталогВременныхФайлов() + ЭтаФорма.УникальныйИдентификатор ;
			СоздатьКаталог(текКаталог);
			текИмяФайла = текКаталог + "\" + Объект.ПолноеНаименование;
			ПолучитьФайл(ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ФайлХранилище"), текИмяФайла, Ложь);
			ЗапуститьПриложение(текИмяФайла);
		Иначе
			ПоказатьПредупреждение(, "Объект еще не записан.");
		КонецЕсли;
	Иначе
		ЗапуститьПриложение(Объект.ПутьКФайлу);
	КонецЕсли;
КонецПроцедуры
