#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ОбъектВладелец) Тогда
		ОбъектВладелец = Параметры.ОбъектВладелец;
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбъектВладелец");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ЭлементОтбора.ПравоеЗначение = ОбъектВладелец;
	Иначе
		Элементы.Список.ИзменятьСоставСтрок = Ложь;
		Элементы.ОбъектВладелец.Видимость = Ложь;
	КонецЕсли;
	Элементы.ФормаУдалить.ТолькоВоВсехДействиях = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущиеДанные.ТипХраненияФайла = ПредопределенноеЗначение("Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе") Тогда
		текКаталог = КаталогВременныхФайлов() + ЭтаФорма.УникальныйИдентификатор;
		СоздатьКаталог(текКаталог);
		текИмяФайла = текКаталог + "\" + Элемент.ТекущиеДанные.ПолноеНаименование;
		ПолучитьФайл(ПолучитьНавигационнуюСсылку(ВыбраннаяСтрока, "ФайлХранилище"), текИмяФайла, Ложь);
		ЗапуститьПриложение(текИмяФайла);
	Иначе
		ЗапуститьПриложение(Элемент.ТекущиеДанные.ПутьКФайлу);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ЗначенияЗаполнения = Новый Структура;
		ПараметрыОткрытия = Новый Структура;
		
		текФайл = Новый Файл(ДиалогВыбораФайла.ПолноеИмяФайла);
		
		текТипХраненияФайлов = ТипХраненияФайлов();
		
		ЗначенияЗаполнения.Вставить("ОбъектВладелец", ОбъектВладелец);
		ЗначенияЗаполнения.Вставить("ТипХраненияФайла", текТипХраненияФайлов);
		ЗначенияЗаполнения.Вставить("ПолноеНаименование", текФайл.Имя);
		ЗначенияЗаполнения.Вставить("Расширение", РасширениеБезТочки(текФайл.Расширение));
		ЗначенияЗаполнения.Вставить("Размер", текФайл.Размер());
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		Если текТипХраненияФайлов = ПредопределенноеЗначение("Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе") Тогда
			ДополнительныеПараметрыОповещения = Новый Структура("ПараметрыОткрытия", ПараметрыОткрытия);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ПомещениеФайла_Завершение", ЭтотОбъект, ДополнительныеПараметрыОповещения);
			НачатьПомещениеФайла(ОписаниеОповещения,, ДиалогВыбораФайла.ПолноеИмяФайла, Ложь, УникальныйИдентификатор); 
		Иначе
			ПараметрыОткрытия.Вставить("ПолноеИмяФайла", ДиалогВыбораФайла.ПолноеИмяФайла);
			ИдентификаторФормы = Новый УникальныйИдентификатор;
			ОткрытьФорму("Справочник.РЭЙ_Файлы.Форма.ФормаЭлемента", ПараметрыОткрытия,, ИдентификаторФормы);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПомещениеФайла_Завершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	ИдентификаторФормы = Новый УникальныйИдентификатор;
	ПараметрыОткрытия = ДополнительныеПараметры.ПараметрыОткрытия;
	ПараметрыОткрытия.Вставить("АдресВременногоХранилища", Адрес);
	
	ОткрытьФорму("Справочник.РЭЙ_Файлы.Форма.ФормаЭлемента", ПараметрыОткрытия,, ИдентификаторФормы);
КонецПроцедуры

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	Оповестить("ИзмененСправочник_РЭЙ_Файлы");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ТипХраненияФайлов()
	Возврат Константы.РЭЙ_ТипХраненияФайлов.Получить();
КонецФункции

&НаКлиенте
Функция РасширениеБезТочки(Знач Расширение)
	Расширение = НРег(СокрЛП(Расширение));
	Если Сред(Расширение, 1, 1) = "." Тогда
		Расширение = Сред(Расширение, 2);
	КонецЕсли;
	
	Возврат Расширение;
КонецФункции

#КонецОбласти
