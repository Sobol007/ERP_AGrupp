
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ДоступныеКалендари = ДоступныеПользователюКалендари().ВыгрузитьКолонку("Календарь");
	Параметры.Отбор.Вставить("Ссылка", ДоступныеКалендари);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Функция - Доступные пользователю календари
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи	 - пользователь.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - таблиц с доступными календарями.
// 
Функция ДоступныеПользователюКалендари(Пользователь = Неопределено) Экспорт
	
	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КалендариСотрудников.Ссылка КАК Календарь,
		|	КалендариСотрудников.Наименование КАК Наименование,
		|	ИСТИНА КАК ЯвляетсяВладельцем
		|ИЗ
		|	Справочник.CRM_КалендариСотрудников КАК КалендариСотрудников
		|ГДЕ
		|	КалендариСотрудников.ПометкаУдаления = ЛОЖЬ
		|	И КалендариСотрудников.Пользователь = &Пользователь
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Возврат Таблица;
	
КонецФункции

// Функция - Ссылка по идентификатору
//
// Параметры:
//  Идентификатор	 - Строка	 - идентификатор календаря.
//  Пользователь	 - СправочникСсылка.Пользователи	 - пользователь. 
// 
// Возвращаемое значение:
//  СправочникСсылка.CRM_КалендариСотрудников - календарь.
// 
Функция СсылкаПоИдентификатору(Идентификатор, Пользователь) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	КалендариСотрудников.Ссылка
	|ИЗ
	|	Справочник.CRM_КалендариСотрудников КАК КалендариСотрудников
	|ГДЕ
	|	КалендариСотрудников.Пользователь = &Пользователь
	|	И КалендариСотрудников.Ключ = &Ключ");
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	Запрос.УстановитьПараметр("Ключ",
		CRM_СинхронизацияКалендарей.КлючИзИдентификатора(Идентификатор, ТипЗнч(Справочники.CRM_КалендариСотрудников)));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.CRM_КалендариСотрудников.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли