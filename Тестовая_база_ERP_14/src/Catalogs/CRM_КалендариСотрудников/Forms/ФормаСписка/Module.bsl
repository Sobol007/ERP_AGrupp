
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьДоступныеКалендари();
	
	// Установим настройки формы для случая открытия в режиме выбора
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	Если Параметры.РежимВыбора Тогда
		КлючНазначенияИспользования = КлючНазначенияИспользования + "ВыборПодбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		КлючНазначенияИспользования = КлючНазначенияИспользования + "Список";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_КалендарьСотрудника" Тогда
		УстановитьДоступныеКалендари();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДоступныеКалендари()
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Или Параметры.ТолькоСвои Тогда
		ДоступныеКалендари = Справочники.CRM_КалендариСотрудников.ДоступныеПользователюКалендари().ВыгрузитьКолонку("Календарь");
		Список.Параметры.УстановитьЗначениеПараметра("ДоступныеКалендари", ДоступныеКалендари);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
