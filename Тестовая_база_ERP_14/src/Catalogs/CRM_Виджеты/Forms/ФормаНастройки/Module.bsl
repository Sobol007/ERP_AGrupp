
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Виджет") И Параметры.Свойство("ИндексВиджета") Тогда
		
		ТекущийПользователь 	= Параметры.ТекущийПользователь;
		ТекущееПодразделение	= ТекущийПользователь.Подразделение;
		Виджет 					= Параметры.Виджет;
		ИндексВиджета 			= Параметры.ИндексВиджета;
		
		// Получить настройки доступа к показателю.
		УстановитьПривилегированныйРежим(Истина);
		
		МенеджерЗаписи = РегистрыСведений.CRM_НастройкиВиджетов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Пользователь 	= ТекущийПользователь;
		МенеджерЗаписи.Виджет 			= Виджет;
		МенеджерЗаписи.ИндексВиджета 	= ИндексВиджета;
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			ПериодОбновления 		= МенеджерЗаписи.ПериодОбновления;
			ТипАналитики 			= МенеджерЗаписи.ТипАналитики;
			ЗначениеАналитики 		= МенеджерЗаписи.ЗначениеАналитики;
			
			Если ЗначениеЗаполнено(ЗначениеАналитики) Тогда
				Если ТипАналитики = Перечисления.CRM_ВидыРазверткиПоказателей.Подразделение Тогда
					ВыбранноеПодразделение = ЗначениеАналитики;
				ИначеЕсли ТипАналитики = Перечисления.CRM_ВидыРазверткиПоказателей.Аналитика1 Тогда
					ВыбранныйПользователь = ЗначениеАналитики;
				КонецЕсли;
			КонецЕсли;
		Иначе
			НайденПериод = Элементы.ПериодОбновления.СписокВыбора.НайтиПоЗначению(Виджет.ИсточникДанных.ПериодОбновления);
			Если Число(ИндексВиджета) > 4 ИЛИ НайденПериод = Неопределено Тогда
				ПериодОбновления 	= 3600;
			Иначе
				ПериодОбновления 	= НайденПериод.Значение;
			КонецЕсли;
			ТипАналитики 			= Перечисления.CRM_ВидыРазверткиПоказателей.Аналитика1;
			ЗначениеАналитики 		= ТекущийПользователь;
			ВыбранныйПользователь	= ТекущийПользователь;
		КонецЕсли;
		
		ПоказыватьВиджет = Истина;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Заголовок = Виджет.Наименование;
	Элементы.ПериодОбновления.Доступность = (Число(ИндексВиджета) <= 4);
	НастройкиИзменены = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкиИзменены Тогда
		Отказ = Истина;
		ОбратныйВызов = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОбратныйВызов, НСтр("ru='Все сделанные изменения будут отменены. Закрыть форму?';en='All changes made will be canceled. Close the form?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		НастройкиИзменены = Ложь;
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодОбновленияПриИзменении(Элемент)
	НастройкиИзменены = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТипАналитикиПриИзменении(Элемент)
	Если ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.НеИспользовать") Тогда
		ЗначениеАналитики = Неопределено;
	ИначеЕсли ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.Подразделение") Тогда
		ЗначениеАналитики = ВыбранноеПодразделение;
	ИначеЕсли ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.Аналитика1") Тогда
		ЗначениеАналитики = ?(ЗначениеЗаполнено(ВыбранныйПользователь), ВыбранныйПользователь, ТекущийПользователь);
	КонецЕсли;
	НастройкиИзменены = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеАналитикиПриИзменении(Элемент)
	Если ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.Подразделение") Тогда
		ВыбранноеПодразделение = ЗначениеАналитики;
	ИначеЕсли ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.Аналитика1") Тогда
		ВыбранныйПользователь = ЗначениеАналитики;
	КонецЕсли;
	НастройкиИзменены = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеАналитикиОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.НеИспользовать") Тогда
		Элементы.ЗначениеАналитики.Доступность = Ложь;
		ЗначениеАналитики = Неопределено;
	ИначеЕсли ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.Подразделение") Тогда
		ЗначениеАналитики = ВыбранноеПодразделение;
		Элементы.ЗначениеАналитики.Доступность = Истина;
	ИначеЕсли ТипАналитики = ПредопределенноеЗначение("Перечисление.CRM_ВидыРазверткиПоказателей.Аналитика1") Тогда
		ЗначениеАналитики = ТекущийПользователь;
		ВыбранныйПользователь = ЗначениеАналитики;
		Элементы.ЗначениеАналитики.Доступность = Истина;
	КонецЕсли;
	НастройкиИзменены = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеАналитикиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТолькоДоступные", Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,, Новый ОписаниеОповещения("ВыборЗначенияЗавершения", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗначенияЗавершения(Значение, ДополнительныеПараметры) Экспорт
	Если Значение <> Неопределено Тогда
		ЗначениеАналитики = Значение;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	НастройкиИзменены = Ложь;
	Если ПоказыватьВиджет Тогда
		ЗаписатьНовыеНастройкиНаСервере();
		Закрыть(ПериодОбновления);
	Иначе
		Закрыть(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	НастройкиИзменены = Ложь;
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНовыеНастройкиНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.CRM_НастройкиВиджетов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь 		= ТекущийПользователь;
	МенеджерЗаписи.Виджет 				= Виджет;
	МенеджерЗаписи.ИндексВиджета 		= ИндексВиджета;
	МенеджерЗаписи.ПериодОбновления 	= ПериодОбновления;
	МенеджерЗаписи.ТипАналитики 		= ТипАналитики;
	МенеджерЗаписи.ЗначениеАналитики 	= ЗначениеАналитики;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти


