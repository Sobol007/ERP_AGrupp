////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Область ОбработчикиСобытийФормы

#КонецОбласти // ОбработчикиСобытийФормы


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	РазрешеноИзменениеПроектов = РазрешеноГрупповоеИзменениеПроектов();
	
	Если РазрешеноИзменениеПроектов Тогда
	
		ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
	Иначе
		
		ПоказатьПредупреждение(,НСтр("ru = 'Команда ""Изменить выделенное"" доступна только пользователям, у которых есть одна из следующих ролей: 
		|- ""Полные права"", 
		|- ""Управление маркетингом"", 
		|- ""Добавление и изменение базовой нормативно-справочной информации"".'"))
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроект(Команда)
	
	Если ЗначениеЗаполнено(Элементы.Список.ТекущаяСтрока) Тогда
		СозданныйПроект = Неопределено;
		CRM_УправлениеПроектамиВызовСервера.СоздатьПроектПоШаблону(Элементы.Список.ТекущаяСтрока, СозданныйПроект);
		Если СозданныйПроект <> Неопределено Тогда
			ПодключитьОбработчикОжидания("ОткрытьСозданныйПроект", 0.1, Истина);
		КонецЕсли;
		
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбран шаблон проекта!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОтветственныйПриИзменении(Элемент)
	ОбновитьОтборыПриИзменении();
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеПриИзменении(Элемент)
	ОбновитьОтборыПриИзменении();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовФормы


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция РазрешеноГрупповоеИзменениеПроектов()

  Возврат Пользователи.РолиДоступны("CRM_УправлениеПроектами, CRM_ДобавлениеИзменениеБазовойНСИ, ПолныеПрава");

КонецФункции // РазрешеноГрупповоеИзменениеПроектов()

&НаКлиенте
Процедура ОткрытьСозданныйПроект()

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", СозданныйПроект);
	ОткрытьФорму("Справочник.Проекты.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОтборыПриИзменении()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	Список, 
	"Ответственный", 
	ОтборОтветственный, 
	ВидСравненияКомпоновкиДанных.Равно,
	, 
	ЗначениеЗаполнено(ОтборОтветственный));
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
	Список, 
	"CRM_Подразделение", 
	ОтборПодразделение, 
	ВидСравненияКомпоновкиДанных.Равно,
	, 
	ЗначениеЗаполнено(ОтборПодразделение));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ТекДанные = Элемент.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		Элементы.СоздатьПроект.Доступность = НЕ ТекДанные.ЭтоГруппа;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
