
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Подразделение", Подразделение)
			И НЕ Подразделение.Пустая() Тогда
			НастроитьПоПодразделению();
		Иначе
			ЗаполнитьПодразделение();
		КонецЕсли;
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
	// подсистема запрета редактирования ключевых реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Участок.Пустая() И НЕ ТолькоПросмотр Тогда
		// подсистема запрета редактирования ключевых реквизитов объектов
		ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(
			ЭтотОбъект,
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Участок"));
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("Запись_РабочиеЦентры");

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьПодразделение();
	
	ПриЧтенииСозданииНаСервере();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	УстановитьВидимостьРеквизитовЗагрузка();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	НастроитьПоПодразделению();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидРабочегоЦентраПриИзменении(Элемент)
	
	ВидРабочегоЦентраПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВидРабочегоЦентраПриИзмененииНаСервере()
	
	Если Подразделение.Пустая() Тогда
		
		ЗаполнитьПодразделение();
		
	КонецЕсли;
	
	УстановитьВидимостьРеквизитовЗагрузка();
	
КонецПроцедуры

&НаКлиенте
Процедура УчастокПриИзменении(Элемент)
	
	Если Подразделение.Пустая() Тогда
		
		ЗаполнитьПодразделение();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьРеквизитовЗагрузка()
	
	ПараллельнаяЗагрузка = Ложь;
	
	Если НЕ Объект.ВидРабочегоЦентра.Пустая() Тогда
		
		ПараллельнаяЗагрузка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.ВидРабочегоЦентра,
			"ПараллельнаяЗагрузка");
		
	КонецЕсли;
	
	Элементы.МаксимальнаяЗагрузка.Видимость = ПараллельнаяЗагрузка;
	Элементы.ВидРабочегоЦентраЕдиницаИзмеренияЗагрузки.Видимость = ПараллельнаяЗагрузка;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодразделение()
	
	Если Объект.ВидРабочегоЦентра.Пустая() И Объект.Участок.Пустая() Тогда
		
		Подразделение = Неопределено;
		
	Иначе
		
		Подразделение = ?(Объект.Участок.Пустая(),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидРабочегоЦентра, "Подразделение"),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Участок, "Владелец"));
		
	КонецЕсли;
	
	НастроитьПоПодразделению();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПоПодразделению()
	
	Если Подразделение.Пустая() Тогда
		
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Подразделение", Неопределено));
		
		Элементы.ВидРабочегоЦентра.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
		
		Элементы.Участок.СвязиПараметровВыбора = Новый ФиксированныйМассив(Новый Массив);
		
	Иначе
		
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Подразделение", Подразделение));
		
		Связи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Новый СвязьПараметраВыбора("Отбор.Подразделение", "Подразделение"));
		Элементы.ВидРабочегоЦентра.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		
		Связи = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Новый СвязьПараметраВыбора("Отбор.Владелец", "Подразделение"));
		Элементы.Участок.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()

	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
