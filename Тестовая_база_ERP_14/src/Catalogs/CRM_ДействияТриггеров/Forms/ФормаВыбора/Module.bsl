
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("УсловиеСрабатывания") И ЗначениеЗаполнено(Параметры.УсловиеСрабатывания) Тогда
		МассивДоступных = Новый Массив;
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	CRM_ДействияТриггеров.Ссылка КАК Ссылка,
		                      |	CRM_ДействияТриггеров.ОписаниеТипов КАК ОписаниеТипов
		                      |ИЗ
		                      |	Справочник.CRM_ДействияТриггеров КАК CRM_ДействияТриггеров
		                      |ГДЕ
		                      |	НЕ CRM_ДействияТриггеров.ПометкаУдаления");
		Выборка = Запрос.Выполнить().Выбрать();
		Если Параметры.УсловиеСрабатывания = Справочники.CRM_УсловияСрабатыванияТриггеров.ПоРасписанию Тогда
			Пока Выборка.Следующий() Цикл
				ОписаниеТипов = Выборка.ОписаниеТипов.Получить();
				Если ОписаниеТипов = Неопределено Тогда
					МассивДоступных.Добавить(Выборка.Ссылка);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ПроверяемыеТипы = Новый Массив;
			Если ЗначениеЗаполнено(Параметры.УсловиеСрабатывания.ОбъектДействия) Тогда
				ПроверяемыйТип = Тип(ОбщегоНазначения.ВидОбъектаПоТипу(ТипЗнч(Параметры.УсловиеСрабатывания.ОбъектДействия.ЗначениеПустойСсылки))
					+"Объект."+Параметры.УсловиеСрабатывания.ОбъектДействия.Имя);
				ПроверяемыеТипы.Добавить(ПроверяемыйТип);
			Иначе
				Для каждого Строка из Параметры.УсловиеСрабатывания.ОбъектыОбработки Цикл
					ПроверяемыйТип = Тип(ОбщегоНазначения.ВидОбъектаПоТипу(ТипЗнч(Строка.ОбъектОбработки.ЗначениеПустойСсылки))
						+"Объект."+Строка.ОбъектОбработки.Имя);
					ПроверяемыеТипы.Добавить(ПроверяемыйТип);
				КонецЦикла;
			КонецЕсли;
			Пока Выборка.Следующий() Цикл
				ОписаниеТипов = Выборка.ОписаниеТипов.Получить();
				Если ОписаниеТипов <> Неопределено И (ОписаниеТипов.Типы().Количество() = 0 ИЛИ CRM_ОбщегоНазначенияКлиентСервер.ПересечениеМножеств(ОписаниеТипов.Типы(), ПроверяемыеТипы).Количество() > 0) Тогда
					МассивДоступных.Добавить(Выборка.Ссылка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", МассивДоступных, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	Иначе
		МассивИсключаемых = Справочники.CRM_ДействияТриггеров.МассивИсключаемыхПоФОПредопределенных();
		Если МассивИсключаемых.Количество()>0 Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", МассивИсключаемых, ВидСравненияКомпоновкиДанных.НеВСписке,, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

