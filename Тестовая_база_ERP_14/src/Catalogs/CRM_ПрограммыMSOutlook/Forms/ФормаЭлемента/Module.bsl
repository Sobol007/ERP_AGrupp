
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДатаПоследнейСинхронизацииКонтактов	= Объект.ДатаПоследнейСинхронизацииКонтактов;
	ДатаПоследнейСинхронизацииСобытий	= Объект.ДатаПоследнейСинхронизацииСобытий;
	ДатаПоследнейСинхронизацииПисем		= Объект.ДатаПоследнейСинхронизацииПисем;
	Если  НЕ ЗначениеЗаполнено(ДатаПоследнейСинхронизацииКонтактов) 
		И НЕ ЗначениеЗаполнено(ДатаПоследнейСинхронизацииСобытий) Тогда
		
		Если Объект.Ссылка.Пустая() Тогда
			НадписьДатаСинхронизации = "";
		Иначе
			НадписьДатаСинхронизации = НСтр("ru='Обмен не выполнен';en='The exchange is not foundulfill'");
		КонецЕсли;
		
	Иначе
		НадписьДатаСинхронизации = "";
		
		Если ДатаПоследнейСинхронизацииКонтактов = ДатаПоследнейСинхронизацииСобытий Тогда
			НадписьДатаСинхронизации = НСтр("ru='Дата последнего сеанса обмена:';en='Date of the last session of an exchange:'") + " " + Строка(ДатаПоследнейСинхронизацииКонтактов)
		Иначе
			Если ЗначениеЗаполнено(ДатаПоследнейСинхронизацииКонтактов) Тогда
				НадписьДатаСинхронизации = НСтр("ru='Дата последней синхронизации контактов:';en='Date of the last synchronization of contacts:'") + " " + Строка(ДатаПоследнейСинхронизацииКонтактов);
			КонецЕсли;
			Если ЗначениеЗаполнено(ДатаПоследнейСинхронизацииСобытий) Тогда
				НадписьДатаСинхронизации = НадписьДатаСинхронизации + ?(ЗначениеЗаполнено(НадписьДатаСинхронизации), Символы.ПС, "");
				НадписьДатаСинхронизации = НадписьДатаСинхронизации + НСтр("ru='Дата последней синхронизации мероприятий:';en='Date of the last event synchronization:'") + " " + Строка(ДатаПоследнейСинхронизацииСобытий);
			КонецЕсли;
			Если ЗначениеЗаполнено(ДатаПоследнейСинхронизацииПисем) Тогда
				НадписьДатаСинхронизации = НадписьДатаСинхронизации + ?(ЗначениеЗаполнено(НадписьДатаСинхронизации), Символы.ПС, "");
				НадписьДатаСинхронизации = НадписьДатаСинхронизации + НСтр("ru='Дата последней синхронизации писем:';en='Date of the last synchronization of letters:'") + " " + Строка(ДатаПоследнейСинхронизацииПисем);
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
	Элементы.НадписьДатаСинхронизации.Заголовок = НадписьДатаСинхронизации;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Ссылка.Пустая() Тогда
		#Если НЕ Вебклиент Тогда
			Объект.Компьютер = ИмяКомпьютера();
			Объект.Наименование = Строка(Объект.Пользователь) +" (" + Строка(Объект.Компьютер) + ")";
		#Иначе
			ПолучитьИмяКомпьютераСформироватьНаименование();
		#КонецЕсли
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьИмяКомпьютераСформироватьНаименование()
	
	Объект.Компьютер = ИмяКомпьютера();
	Объект.Наименование = Строка(Объект.Пользователь) +" (" + Строка(Объект.Компьютер) + ")";
	
КонецПроцедуры

#КонецОбласти 
