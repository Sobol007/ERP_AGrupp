
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ДляПроекта;
	ДляПроекта = Ложь;
	
	Если Параметры.Свойство("РежимВыбора") Тогда
		Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	КонецЕсли;
	
	Если Параметры.Свойство("Основание") И НЕ Параметры.Основание = Неопределено Тогда
		Если ТипЗнч(Параметры.Основание) = Тип("СправочникСсылка.Проекты") Тогда
			ДляПроекта = Истина;
			ИмяОбъекта = Метаданные.Документы.УдалитьCRM_Событие.Имя;
		Иначе
			ИмяОбъекта = Параметры.Основание.Метаданные().Имя;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ИмяОбъекта", ИмяОбъекта, ВидСравненияКомпоновкиДанных.Равно,, Истина);
	КонецЕсли;
	
	МассивФильтра = ПолучитьСписокДляФильтра(ДляПроекта);
	Если МассивФильтра.Количество() > 0 Тогда
		СписокОтбора = Новый СписокЗначений;
		СписокОтбора.ЗагрузитьЗначения(МассивФильтра);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Ссылка", СписокОтбора, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
	КонецЕсли;
	
	ПоказатьПодсказку = CRM_ХранилищеНастроек.Загрузить(ЭтотОбъект.ИмяФормы, "ПоказыватьПодсказкуШаблоныДокументов");
	Элементы.Описание.Видимость = ?(ПоказатьПодсказку = Неопределено, Истина, ПоказатьПодсказку);	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	CRM_ХранилищеНастроек.Сохранить(ЭтотОбъект.ИмяФормы, "ПоказыватьПодсказкуШаблоныДокументов", Ложь);
	Элементы.Описание.Видимость = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = НЕ Копирование;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСписокДляФильтра(Знач ТолькоОбщие = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_ШаблоныОбъектов.Шаблон КАК Ссылка
	|ИЗ
	|	РегистрСведений.CRM_ШаблоныОбъектов КАК CRM_ШаблоныОбъектов
	|ГДЕ
	|	CRM_ШаблоныОбъектов.Владелец = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)";
	Если НЕ ТолькоОбщие Тогда
		Запрос.Текст = Запрос.Текст + "
		|	ИЛИ CRM_ШаблоныОбъектов.Владелец = &Владелец";
	КонецЕсли;
	Запрос.УстановитьПараметр("Владелец",Пользователи.ТекущийПользователь());
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти
