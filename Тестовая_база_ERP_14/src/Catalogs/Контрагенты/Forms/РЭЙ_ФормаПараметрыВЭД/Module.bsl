#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ПрочитатьРеквизитыИзВременногоХранилища();
	
	ЗаполнитьСтрануИФлагНеЯвляетсяРезидентом();
	ЗаполнитьКИО();
	
	УстановитьОтображение_Страна();
	УстановитьОтображение_НеЯвляетсяРезидентом();
	УстановитьОтображение_ОшибкаВКИО();
	УстановитьОтображение_ДатаРегистрации();
	УстановитьОтображение_РеквизитыНерезидента();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ОбновитьВысотуФормыПриОткрытии", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗавершениеРаботы Тогда 
		Если Модифицированность Тогда
			Отказ = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	ПараметрыЗакрытия = Новый Структура;

	Если Модифицированность Тогда 
		АдресХранилища = ПоместитьРеквизитыВоВременноеХранилище();
		ПараметрыЗакрытия.Вставить("АдресХранилища", АдресХранилища);
	КонецЕсли;
	
	ПараметрыЗакрытия.Вставить("Модифицированность", Модифицированность);

	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, ПараметрыЗакрытия); 
	ОписаниеОповещенияОЗакрытии = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура КИОПриИзменении(Элемент)
	УстановитьОтображение_ОшибкаВКИО();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСтрануИФлагНеЯвляетсяРезидентом()
	Если Сред(Объект.ИНН, 1, 2) = "99" Тогда
		Если Объект.РЭЙ_Страна = ПредопределенноеЗначение("Справочник.СтраныМира.Россия") Тогда
			Объект.РЭЙ_Страна = Неопределено;
			Модифицированность = Истина;
		КонецЕсли;
		Если Объект.РЭЙ_НеЯвляетсяРезидентом = Ложь Тогда
			Объект.РЭЙ_НеЯвляетсяРезидентом = Истина;
			Модифицированность = Истина;
		КонецЕсли;
	Иначе
		Если Объект.РЭЙ_Страна <> Объект.СтранаРегистрации Тогда
			Объект.РЭЙ_Страна = Объект.СтранаРегистрации;
			Если Объект.РЭЙ_Страна = ПредопределенноеЗначение("Справочник.СтраныМира.Россия") Тогда
				Объект.РЭЙ_НеЯвляетсяРезидентом = Ложь;
			Иначе
				Объект.РЭЙ_НеЯвляетсяРезидентом = Истина;
			КонецЕсли;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКИО()
	Если Сред(Объект.ИНН, 1, 2) = "99" И ПустаяСтрока(Объект.РЭЙ_КИО) Тогда
		Объект.РЭЙ_КИО = Сред(Объект.ИНН, 5, 5);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРеквизитыИзВременногоХранилища()
	Если Параметры.Свойство("АдресВременногоХранилища") Тогда 
		РЭЙ_СлужебныйСервер.ЗаполнитьСвойстваОбъектаИзВременногоХранилища(Объект, Параметры.АдресВременногоХранилища);
	КонецЕсли;	
КонецПроцедуры	

&НаСервере
Функция ПоместитьРеквизитыВоВременноеХранилище()
	МетаданныеОбъекта = РеквизитФормыВЗначение("Объект").Метаданные();
	АдресХранилища = РЭЙ_СлужебныйСервер.ВременноеХранилищеРеквизитов(Объект, МетаданныеОбъекта, УникальныйИдентификатор);
	
	Возврат АдресХранилища;
КонецФункции

#КонецОбласти

#Область Отображение

&НаКлиенте
Процедура ОбновитьВысотуФормыПриОткрытии()
	Высота = 15;
	ОбновитьВысотуФормы()
КонецПроцедуры

&НаСервере
Процедура ОбновитьВысотуФормы()
	Высота = 0;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображение_Страна()
	Если Сред(Объект.ИНН, 1, 2) = "99" Тогда
		Элементы.РЭЙ_Страна.Доступность = Истина;
	Иначе
		Элементы.РЭЙ_Страна.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображение_НеЯвляетсяРезидентом()
	Элементы.РЭЙ_НеЯвляетсяРезидентом.Доступность = Ложь;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображение_ОшибкаВКИО()
	Если Сред(Объект.ИНН, 1, 2) = "99" И Не ПустаяСтрока(Объект.РЭЙ_КИО) И Объект.РЭЙ_КИО <> Сред(Объект.ИНН, 5, 5) Тогда
		Элементы.ОшибкаВКИО.Видимость = Истина;
		ОбновитьВысотуФормы();
	Иначе
		Элементы.ОшибкаВКИО.Видимость = Ложь;
		ОбновитьВысотуФормы();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображение_ДатаРегистрации()
	Если Объект.РЭЙ_НеЯвляетсяРезидентом Тогда
		Элементы.РЭЙ_ДатаВнесенияВГосРеестр.Видимость = Ложь;
	Иначе
		Элементы.РЭЙ_ДатаВнесенияВГосРеестр.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображение_РеквизитыНерезидента()
	Элементы.Группа_ИдНомерНДС_СвифтБЕИ.Видимость = Объект.РЭЙ_НеЯвляетсяРезидентом;
	Элементы.Группа_ЭОРИ_ТИН.Видимость = Объект.РЭЙ_НеЯвляетсяРезидентом;
	Элементы.РЭЙ_КИО.Видимость = Объект.РЭЙ_НеЯвляетсяРезидентом;
	Элементы.РЭЙ_ЛЕИ.Видимость = Объект.РЭЙ_НеЯвляетсяРезидентом;
КонецПроцедуры

#КонецОбласти
