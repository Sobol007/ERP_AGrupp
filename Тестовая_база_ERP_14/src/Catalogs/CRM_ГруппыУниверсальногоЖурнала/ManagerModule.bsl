#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов
//  Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Процедура - Заполняет справочник по массиву типов.
//
// Параметры:
//  Объект		 - СправочникОбъект.CRM_ГруппыУниверсальногоЖурнала	 - Элемент справочника, который необходимо заполнить.
//  МассивТипов	 - Массив	 -   Массив типов.
//
Процедура ЗаполнитьОбъектПоМассивуТипов(Объект, МассивТипов) Экспорт
	Для Каждого Тип Из МассивТипов Цикл
		СсылкаПВХ = CRM_КлиентыСервер.ПолучитьСсылкуПВХОбъектыЖурналаПоТипу(Тип);
		Если ЗначениеЗаполнено(СсылкаПВХ) Тогда
			НоваяСтрока = Объект.Состав.Добавить();
			НоваяСтрока.Объект = СсылкаПВХ;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Процедура - Заполнить контакты
// Процедура заполняет предопределенный этемент справочника CRM_ГруппыУниверсальногоЖурнала - Контакты.
//
// Параметры:
//  Перезаполнять	 - 	Булево -  Признак необходимости перезаполнения.
//
Процедура ЗаполнитьКонтакты(Перезаполнять = Ложь) Экспорт
	Ссылка = Справочники.CRM_ГруппыУниверсальногоЖурнала.Контакты;
	Если Ссылка.Состав.Количество() > 0 И Не Перезаполнять Тогда Возврат; КонецЕсли;
	
	Объект = Ссылка.ПолучитьОбъект();
	
	Объект.Состав.Очистить();
	
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Тип("ДокументСсылка.CRM_Взаимодействие"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее"));
	МассивТипов.Добавить(Тип("ДокументСсылка.CRM_Телемаркетинг"));
	МассивТипов.Добавить(Тип("ДокументСсылка.CRM_РассылкаЭлектронныхПисем"));
	МассивТипов.Добавить(Тип("ДокументСсылка.СообщениеSMS"));
	МассивТипов.Добавить(Тип("ДокументСсылка.CRM_СообщениеМессенджера"));
	МассивТипов.Добавить(Тип("ДокументСсылка.Анкета"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ТелефонныйЗвонок"));
	
	Если НЕ CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "Встреча") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "Встреча"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ЗапланированноеВзаимодействие") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ЗапланированноеВзаимодействие"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Справочники", "СделкиСКлиентами") Тогда
			МассивТипов.Добавить(Тип("СправочникСсылка." + "СделкиСКлиентами"));
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьОбъектПоМассивуТипов(Объект, МассивТипов);
	
	Если Объект.Состав.Количество() > 0 Тогда
		Объект.Записать();
	КонецЕсли;
КонецПроцедуры

// Процедура - Заполнить продажи
// Процедура заполняет предопределенный этемент справочника CRM_ГруппыУниверсальногоЖурнала - Продажи.
//
// Параметры:
//  Перезаполнять	 - 	Булево -  Признак необходимости перезаполнения.
//
Процедура ЗаполнитьПродажи(Перезаполнять = Ложь) Экспорт
	Ссылка = Справочники.CRM_ГруппыУниверсальногоЖурнала.Продажи;
	Если Ссылка.Состав.Количество() > 0 И Не Перезаполнять Тогда Возврат; КонецЕсли;
	
	Объект = Ссылка.ПолучитьОбъект();
	
	Объект.Состав.Очистить();
	
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Тип("ДокументСсылка.CRM_Интерес"));
	Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		МассивТипов.Добавить(Тип("ДокументСсылка." + "CRM_СчетНаОплатуПокупателю"));
	КонецЕсли;
	МассивТипов.Добавить(Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту"));
	//МассивТипов.Добавить(Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс"));
	
	Если НЕ CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "АктВыполненныхРабот") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "АктВыполненныхРабот"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ВозвратТоваровОтКлиента") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ВозвратТоваровОтКлиента"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ЗаданиеТорговомуПредставителю") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ЗаданиеТорговомуПредставителю"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ЗаказКлиента") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ЗаказКлиента"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ЗаявкаНаВозвратТоваровОтКлиента") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ЗаявкаНаВозвратТоваровОтКлиента"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ОтчетКомиссионера") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ОтчетКомиссионера"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ОтчетКомиссионераОСписании") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ОтчетКомиссионераОСписании"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ОтчетОРозничныхПродажах") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ОтчетОРозничныхПродажах"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "РеализацияТоваровУслуг") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "РеализацияТоваровУслуг"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "СверкаВзаиморасчетов") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "СверкаВзаиморасчетов"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "СчетНаОплатуКлиенту") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "СчетНаОплатуКлиенту"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ЧекККМ") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ЧекККМ"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "ЧекККМВозврат") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "ЧекККМВозврат"));
		КонецЕсли;
		Если CRM_ОбщегоНазначенияСервер.НайтиВМетаданныхПоИмени("Документы", "КорректировкаРеализации") Тогда
			МассивТипов.Добавить(Тип("ДокументСсылка." + "КорректировкаРеализации"));
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьОбъектПоМассивуТипов(Объект, МассивТипов);
	
	Если Объект.Состав.Количество() > 0 Тогда
		Объект.Записать();
	КонецЕсли;
КонецПроцедуры

// Процедура - Заполнить процессы
// Процедура заполняет предопределенный этемент справочника CRM_ГруппыУниверсальногоЖурнала - Бизнес_процессы.
//
// Параметры:
//  Перезаполнять	 - 	Булево -  Признак необходимости перезаполнения.
//
Процедура ЗаполнитьПроцессы(Перезаполнять = Ложь) Экспорт
	Ссылка = Справочники.CRM_ГруппыУниверсальногоЖурнала.Бизнес_процессы;
	Если Ссылка.Состав.Количество() > 0 И Не Перезаполнять Тогда Возврат; КонецЕсли;
	
	Объект = Ссылка.ПолучитьОбъект();
	
	Объект.Состав.Очистить();
	
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс"));
	
	ЗаполнитьОбъектПоМассивуТипов(Объект, МассивТипов);
	
	Если Объект.Состав.Количество() > 0 Тогда
		Объект.Записать();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецЕсли