#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ПредставлениеПравила = Справочники.ПравилаРаспределенияРасходов.ПолучитьПредставлениеПравила(
		ОтборПоМатериалам, ОтборПоВидамРабот, ОтборПоПродукции, БазаРаспределенияПоПартиям);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("НазначениеПравила")
		И ДанныеЗаполнения.НазначениеПравила = Перечисления.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоПодразделениям
		И Не ДанныеЗаполнения.Свойство("БазаРаспределения") Тогда
		ДанныеЗаполнения.Вставить("БазаРаспределения", Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	Если Устаревшее Тогда
		
		НепроверяемыеРеквизиты.Добавить("БазаРаспределенияПоПартиям");
		НепроверяемыеРеквизиты.Добавить("БазаРаспределенияПоПодразделениям");
		НепроверяемыеРеквизиты.Добавить("ДоляСтоимостиНаПроизводство");
		НепроверяемыеРеквизиты.Добавить("Показатель");
				
	Иначе
		
		НепроверяемыеРеквизиты.Добавить("БазаРаспределения");
		
		Если Не (РаспределятьПоПартиям И РаспределятьНаСтатьи
			И Не (УточнятьПартииВДокументе Или ПодразделенияУказаныВручную)) Тогда
			НепроверяемыеРеквизиты.Добавить("ДоляСтоимостиНаПроизводство");
		КонецЕсли;
		
		Если Не РаспределятьПоПодразделениям Или ПодразделенияУказаныВручную Тогда
			НепроверяемыеРеквизиты.Добавить("БазаРаспределенияПоПодразделениям");
		КонецЕсли;
		
		Если Не РаспределятьПоПартиям Или УточнятьПартииВДокументе Тогда
			НепроверяемыеРеквизиты.Добавить("БазаРаспределенияПоПартиям");
		КонецЕсли;
		
		Если Не (БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно
			Или БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении) Тогда
			НепроверяемыеРеквизиты.Добавить("Показатель");
		КонецЕсли;
		
		РеквизитТЧСписание = Новый Массив;
		СтруктураТЧСписание = Новый Структура;
		СтруктураТЧСписание.Вставить("Списание", "СтатьяРасходов, АналитикаРасходов");
		РеквизитТЧСписание.Добавить(СтруктураТЧСписание);
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, РеквизитТЧСписание, НепроверяемыеРеквизиты, Отказ);
			
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
