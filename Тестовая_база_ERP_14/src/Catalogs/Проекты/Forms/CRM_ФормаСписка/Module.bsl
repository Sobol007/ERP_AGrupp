
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// +Этот блок обязательно должен быть в самом начале процедуры, чтобы при отказе не выполнять избыточный код.
	Если НЕ CRM_РежимФормЗакладкиСервер.ВосстановлениеФормыПриЗапускеСеанса(ЭтотОбъект, Отказ, СтандартнаяОбработка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	// -Этот блок обязательно должен быть в самом начале процедуры, чтобы при отказе не выполнять избыточный код.
	
	Если НЕ ПолучитьФункциональнуюОпцию("CRM_ИспользоватьБизнесПроцессы") Тогда
		Элементы.ПроектПроцесс.Видимость = Ложь;
		ПроектПроцесс = 1;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"CRM_ЭтоПроект",
		?(ПроектПроцесс=0, Ложь, Истина), ВидСравненияКомпоновкиДанных.Равно,, Истина);
	Иначе
		времПроектПроцесс = CRM_ХранилищеНастроек.Загрузить(ЭтотОбъект.ИмяФормы, "РежимПросмотраПроектов");
		ПроектПроцесс = ?(времПроектПроцесс = Неопределено, 0, времПроектПроцесс);
	КонецЕсли;
	
	ЗаполнитьПодменюСозданияПоШаблону();

	Элементы.ФормаСправочникПроектыCRM_СтартоватьБизнесПроцесс.Видимость = ?(ПроектПроцесс = 0, Истина, Ложь);
	Элементы.СоздатьШаблонНаОснованииПроекта.Видимость = ?(ПроектПроцесс = 1, Истина, Ложь);
	
	// +Рабочий стол
	CRM_СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	// -Рабочий стол
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроектПроцессПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда Возврат; КонецЕсли;
	ПриЗакрытииНаСервере();
КонецПроцедуры

#КонецОбласти //ОбработчикиСобытийФормы


#Область ПроцедурыДействияКомандныхПанелейФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	РазрешеноИзменениеПроектов = РазрешеноГрупповоеИзменениеПроектов();
	
	Если РазрешеноИзменениеПроектов Тогда
	
		ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);	
	
	Иначе
		
		ПоказатьПредупреждение(,НСтр("ru = 'Команда ""Изменить выделенное"" доступна только пользователям, у которых есть одна из следующих ролей: 
		|- ""Полные права"", 
		|- ""Управление маркетингом"", 
		|- ""Добавление и изменение базовой нормативно-справочной информации"".'"))
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстанавливатьФормуПриОткрытии(Команда)
	Элементы.КнопкаВосстанавливатьФормуПриОткрытии.Пометка = НЕ Элементы.КнопкаВосстанавливатьФормуПриОткрытии.Пометка;
	CRM_ХранилищеНастроек.Сохранить(ЭтотОбъект.ИмяФормы, "ВосстанавливатьФормуПриОткрытии", Элементы.КнопкаВосстанавливатьФормуПриОткрытии.Пометка);
КонецПроцедуры

&НаКлиенте
Процедура ПроектПроцессПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.Отбор,
		"CRM_ЭтоПроект",
		?(ПроектПроцесс=0,Ложь,Истина),ВидСравненияКомпоновкиДанных.Равно,,Истина);
	Элементы.СоздатьПоШаблону.Видимость = ?(ПроектПроцесс=0,Ложь,Истина);
	
	Элементы.ФормаСправочникПроектыCRM_СтартоватьБизнесПроцесс.Видимость = ?(ПроектПроцесс = 0, Истина, Ложь);
	Элементы.СоздатьШаблонНаОснованииПроекта.Видимость = ?(ПроектПроцесс = 1, Истина, Ложь);
	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
		Если НЕ Копирование И НЕ Группа Тогда
		Отказ = Истина;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("CRM_ЭтоПроект",?(ПроектПроцесс=0,Ложь,Истина));
		ОткрытьФорму("Справочник.Проекты.ФормаОбъекта",ПараметрыФормы,Элементы.Список);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПроектПоШаблону(Команда)
	ШаблонПроекта = ПолучитьШаблонПроектаСервер(СтрЗаменить(СтрЗаменить(Команда.Имя, "Шаблон", ""), "_", "-"));
	СозданныйПроект = Неопределено;
	CRM_УправлениеПроектамиВызовСервера.СоздатьПроектПоШаблону(ШаблонПроекта, СозданныйПроект);
	Если СозданныйПроект <> Неопределено Тогда
		ПодключитьОбработчикОжидания("ОткрытьСозданныйПроект", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокШаблонов()
	ОткрытьФорму("Справочник.CRM_ШаблоныПроектов.ФормаВыбора",, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьШаблонНаОснованииПроекта(Команда)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные<>Неопределено Тогда
		CRM_УправлениеПроектамиКлиент.СоздатьШаблонНаОснованииПроекта(ТекДанные.Ссылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти //ПроцедурыДействияКомандныхПанелейФормы


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПодменюСозданияПоШаблону()
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 10
	                      |	CRM_ШаблоныПроектов.Ссылка КАК Ссылка,
	                      |	CRM_ШаблоныПроектов.Наименование КАК Наименование
	                      |ИЗ
	                      |	Справочник.CRM_ШаблоныПроектов КАК CRM_ШаблоныПроектов
	                      |ГДЕ
	                      |	НЕ CRM_ШаблоныПроектов.ПометкаУдаления");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Команда = Команды.Добавить("Шаблон" + СтрЗаменить(Строка(Выборка.Ссылка.УникальныйИдентификатор()), "-", "_"));
		Команда.Действие = "СоздатьПроектПоШаблону";
		Команда.Заголовок = Выборка.Наименование;
		
		Элемент = Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Элементы.СоздатьПоШаблону);
		Элемент.ИмяКоманды = Команда.Имя;
	КонецЦикла;
	Команда = Команды.Добавить("ОткрытьСписокШаблонов");
	Команда.Действие = "ОткрытьСписокШаблонов";
	Команда.Заголовок = НСтр("ru = 'Показать все...'");
		
	Группа = Элементы.Добавить("Группа"+Команда.Имя, Тип("ГруппаФормы"), Элементы.СоздатьПоШаблону);
	Группа.Вид = ВидГруппыФормы.ГруппаКнопок;
	Элемент = Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Группа);
	Элемент.ИмяКоманды = Команда.Имя;
КонецПроцедуры
	
&НаСервере
Функция ПолучитьШаблонПроектаСервер(СтрУникальныйИдентификатор);
	Возврат Справочники.CRM_ШаблоныПроектов.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрУникальныйИдентификатор));
КонецФункции

&НаКлиенте
Процедура ОткрытьСозданныйПроект()

	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ключ", СозданныйПроект);
	ОткрытьФорму("Справочник.Проекты.ФормаОбъекта",ПараметрыФормы);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РазрешеноГрупповоеИзменениеПроектов()

  Возврат Пользователи.РолиДоступны("CRM_УправлениеПроектами, CRM_ДобавлениеИзменениеБазовойНСИ, ПолныеПрава");

КонецФункции // РазрешеноГрупповоеИзменениеПроектов()

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	CRM_РежимФормЗакладкиСервер.ПриЗакрытииНаСервере(ЭтотОбъект);
	
	CRM_ХранилищеНастроек.Сохранить(ЭтотОбъект.ИмяФормы, "РежимПросмотраПроектов", ПроектПроцесс);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора.ИмяФормы = "Справочник.CRM_ШаблоныПроектов.Форма.ФормаВыбора" Тогда
		СозданныйПроект = Неопределено;
		CRM_УправлениеПроектамиВызовСервера.СоздатьПроектПоШаблону(ВыбранноеЗначение, СозданныйПроект);
		Если СозданныйПроект <> Неопределено Тогда
			ПодключитьОбработчикОжидания("ОткрытьСозданныйПроект", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	CRM_СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти //СлужебныеПроцедурыИФункции

// +Рабочий стол
#Область Подключаемый_РабочийСтол

&НаКлиенте
Процедура Подключаемый_ПолеHTMLНапоминанийПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	CRM_РабочийСтолКлиент.ПолеHTMLНапоминанийПриНажатии(ЭтаФорма, Элемент, ДанныеСобытия, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_ПолеHTMLНапоминанийПриНажатии()

&НаКлиенте
Процедура Подключаемый_ПолеHTMLЗаметокПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	CRM_РабочийСтолКлиент.ПолеHTMLЗаметокПриНажатии(ЭтаФорма, Элемент, ДанныеСобытия, СтандартнаяОбработка);
КонецПроцедуры // Подключаемый_ПолеHTMLЗаметокПриНажатии()

#КонецОбласти
// -Рабочий стол
