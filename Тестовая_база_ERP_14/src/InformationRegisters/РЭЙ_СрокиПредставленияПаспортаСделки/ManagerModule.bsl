Функция ПолучитьИнициализационныеНастройки(ДатаНастроек)
	Макет = ПолучитьМакет("ИнициализационныеНастройки_" + Формат(ДатаНастроек, "ДФ=yyyyMMdd"));
	
	ИнициализационнаяТаблица = Новый ТаблицаЗначений;
	Для Каждого текКолонка Из Метаданные.РегистрыСведений.РЭЙ_СрокиПредставленияПаспортаСделки.Измерения Цикл
		ИнициализационнаяТаблица.Колонки.Добавить(текКолонка.Имя, текКолонка.Тип);
	КонецЦикла;
	Для Каждого текКолонка Из Метаданные.РегистрыСведений.РЭЙ_СрокиПредставленияПаспортаСделки.Ресурсы Цикл
		ИнициализационнаяТаблица.Колонки.Добавить(текКолонка.Имя, текКолонка.Тип);
	КонецЦикла;
	
	Для текСтрока = 2 По Макет.ВысотаТаблицы Цикл
		Если ПустаяСтрока(Макет.Область(текСтрока, 1).Текст) Тогда
			Прервать;
		КонецЕсли;
		
		НоваяСтрока = ИнициализационнаяТаблица.Добавить();
		НоваяСтрока.Процедура =  Макет.Область(текСтрока, 2).Текст;
		НоваяСтрока.Дни = РЭЙ_СлужебныйСервер.ПолучитьКлючСоответствия(РЭЙ_СлужебныйСервер.ПолучитьСоответствиеПредставлениеДни(), Макет.Область(текСтрока, 3).Текст);
		НоваяСтрока.КоличествоДней = Число(Макет.Область(текСтрока, 4).Текст);
		НоваяСтрока.ДатаСтартаТаймера = РЭЙ_СлужебныйСервер.ПолучитьКлючСоответствия(РЭЙ_СлужебныйСервер.ПолучитьСоответствиеПредставлениеДатаСтартаТаймера(), Макет.Область(текСтрока, 5).Текст);
	КонецЦикла;
	
	Возврат ИнициализационнаяТаблица;
КонецФункции

Процедура ОчиститьРегистр(Дата) Экспорт
    Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистр.Период
	|ИЗ
	|	РегистрСведений.РЭЙ_СрокиПредставленияПаспортаСделки КАК Регистр
	|ГДЕ
	|	Регистр.Период >= &Период";
	Запрос.УстановитьПараметр("Период", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.РЭЙ_СрокиПредставленияПаспортаСделки.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьИнициализационныеНастройки() Экспорт
	ДатаНастроек = Дата(2018,03,01);
	
	ОчиститьРегистр(ДатаНастроек);
	
	ИнициализационнаяТаблица = ПолучитьИнициализационныеНастройки(ДатаНастроек);

	НаборЗаписей = РегистрыСведений.РЭЙ_СрокиПредставленияПаспортаСделки.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаНастроек);
	Для Каждого СтрокаТаблицы Из ИнициализационнаяТаблица Цикл
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Запись.Период = ДатаНастроек;
	КонецЦикла;
	НаборЗаписей.Записать();
КонецПроцедуры
		
	