
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.РегистрыСведений.CRM_РасположениеРеквизитовКлиентов) Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для просмотра'");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	CRM_РасположениеРеквизитовКлиентов.ДопРеквизит
	|ПОМЕСТИТЬ тмпДопРеквизиты
	|ИЗ
	|	РегистрСведений.CRM_РасположениеРеквизитовКлиентов КАК CRM_РасположениеРеквизитовКлиентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Свойство,
	|	ВложенныйЗапрос.НаборСвойств
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДополнительныеРеквизиты.Свойство КАК Свойство,
	|		ДополнительныеРеквизиты.Ссылка КАК НаборСвойств
	|	ИЗ
	|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|	ГДЕ
	|		НЕ ДополнительныеРеквизиты.Ссылка.ЭтоГруппа
	|		И ДополнительныеРеквизиты.Ссылка В ИЕРАРХИИ(&ГруппаНабора)
	|		И НЕ ДополнительныеРеквизиты.Свойство В
	|					(ВЫБРАТЬ
	|						тмпДопРеквизиты.ДопРеквизит
	|					ИЗ
	|						тмпДопРеквизиты КАК тмпДопРеквизиты)) КАК ВложенныйЗапрос");
	
	Запрос.УстановитьПараметр("ГруппаНабора", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры);
	ТабНаборов = Запрос.Выполнить().Выгрузить();
	Для Каждого Строка Из ТабНаборов Цикл
		Если НЕ Строка.НаборСвойств.ПринадлежитЭлементу(Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры) И Строка.НаборСвойств <> Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Партнеры Тогда Продолжить КонецЕсли;
		Если Найти(Строка.НаборСвойств.Наименование, "не  используется") > 0 Тогда Продолжить конецЕсли;
		Реквизит = Строка.Свойство;
		Если Реквизит.Пустая() Тогда Продолжить КонецЕсли;
		Менеджер = РегистрыСведений.CRM_РасположениеРеквизитовКлиентов.СоздатьМенеджерЗаписи();
		ИмяУникальнаяЧасть = 
		СтрЗаменить(ВРег(Строка(Строка.НаборСвойств.УникальныйИдентификатор())), "-", "x")
		+ "_"
		+ СтрЗаменить(ВРег(Строка(Реквизит.УникальныйИдентификатор())), "-", "x");
		
		Менеджер.ИмяРеквизита =	"ДополнительныйРеквизитЗначение_" + ИмяУникальнаяЧасть;
		Менеджер.ДопРеквизит = Реквизит;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	МАКСИМУМ(CRM_РасположениеРеквизитовКлиентов.ПорядокРасположения) КАК ПорядокРасположения
		|ИЗ
		|	РегистрСведений.CRM_РасположениеРеквизитовКлиентов КАК CRM_РасположениеРеквизитовКлиентов
		|ГДЕ
		|	CRM_РасположениеРеквизитовКлиентов.Колонка = &ИмяКолонки";
		Запрос.УстановитьПараметр("ИмяКолонки", "Дополнительно");
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ПорядокЭлемента = ?(ЗначениеЗаполнено(Выборка.ПорядокРасположения), Выборка.ПорядокРасположения + 1, 1);
		Иначе
			ПорядокЭлемента = 1;
		КонецЕсли;
		
		Менеджер.ПредставлениеРеквизита = Реквизит.Наименование;
		Менеджер.Колонка = "Дополнительно";
		Менеджер.ПорядокРасположения = ПорядокЭлемента;
		Менеджер.Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Оповестить("Запись_НаборыДополнительныхРеквизитовИСведений", Новый Структура);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЛеваяПанельРеквизитов

&НаКлиенте
Процедура ЛеваяПанельРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЛеваяПанельРеквизитовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЛеваяПанельРеквизитовПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПраваяПанельРеквизитов

&НаКлиенте
Процедура ПраваяПанельРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДопРеквизитов

&НаКлиенте
Процедура СписокДопРеквизитовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДобавитьПравая(Команда)
	ТекСтрока = Элементы.СписокДопРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьЛевоПраво("Правая", ТекСтрока)
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьЛевая(Команда)
	ТекСтрока = Элементы.СписокДопРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьЛевоПраво("Левая", ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьЛевая(Команда)
	ТекСтрока = Элементы.ЛеваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьЛевоПраво("Дополнительно", ТекСтрока)
	КонецЕсли;;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьПравая(Команда)
	ТекСтрока = Элементы.ПраваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьЛевоПраво("Дополнительно", ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаВлево(Команда)
	ТекСтрока = Элементы.ПраваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьЛевоПраво("Левая", ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаВправо(Команда)
	ТекСтрока = Элементы.ЛеваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьЛевоПраво("Правая", ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПраваяВверх(Команда)
	ТекСтрока = Элементы.ПраваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьВверхВниз("Правая", -1, ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаЛеваяВверх(Команда)
	ТекСтрока = Элементы.ЛеваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьВверхВниз("Левая", -1, ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПраваяВниз(Команда)
	ТекСтрока = Элементы.ПраваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьВверхВниз("Правая", 1, ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаЛеваяВниз(Команда)
	ТекСтрока = Элементы.ЛеваяПанельРеквизитов.ТекущаяСтрока;
	Если ТекСтрока <> Неопределено Тогда
		ПереместитьВверхВниз("Левая", 1, ТекСтрока)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОтображениеПортрета(Команда)
	ОткрытьФорму("Справочник.Партнеры.ФормаОбъекта");
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьНастройку(Команда)
	
	//Если CRM_ОбщегоНазначенияПовтИсп.ЭтоCRM() Тогда
	//	ЗаписьВыполнена = CRM_НачалоРаботыСПрограммойСервер.ЗаписатьНастроенныйРазделРешения(ПредопределенноеЗначение("Перечисление.CRM_РазделыНастройкиРешения.ПортретКлиента"));
	//	Если ЗаписьВыполнена Тогда
	//		Оповестить("ОбновитьНастроенныеРазделы");
	//	КонецЕсли;
	//КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПорядокЭлемента(ИмяКолонки)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МАКСИМУМ(CRM_РасположениеРеквизитовКлиентов.ПорядокРасположения) КАК ПорядокРасположения
	|ИЗ
	|	РегистрСведений.CRM_РасположениеРеквизитовКлиентов КАК CRM_РасположениеРеквизитовКлиентов
	|ГДЕ
	|	CRM_РасположениеРеквизитовКлиентов.Колонка = &ИмяКолонки";
	Запрос.УстановитьПараметр("ИмяКолонки", ИмяКолонки);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат ?(ЗначениеЗаполнено(Выборка.ПорядокРасположения), Выборка.ПорядокРасположения + 1, 1);
	Иначе
		Возврат 1;
	КонецЕсли;	
КонецФункции
	
&НаСервере
Процедура ПереместитьЛевоПраво(ИмяКолонки, Реквизит)
	Менеджер = РегистрыСведений.CRM_РасположениеРеквизитовКлиентов.СоздатьМенеджерЗаписи();
	Менеджер.ИмяРеквизита =	Реквизит.ИмяРеквизита;
	Менеджер.ДопРеквизит = Реквизит.ДопРеквизит;
	Менеджер.Прочитать();
	ИмяКолонкиСтарое = Менеджер.Колонка;
	Менеджер.Колонка = ИмяКолонки;
	Менеджер.ПорядокРасположения = ПолучитьПорядокЭлемента(ИмяКолонки);
	Менеджер.Записать(Истина);
	ПересчитатьКолонки(ИмяКолонкиСтарое);
	Элементы["ЛеваяПанельРеквизитов"].Обновить();
	Элементы["ПраваяПанельРеквизитов"].Обновить();
	Элементы["СписокДопРеквизитов"].Обновить();
КонецПроцедуры

&НаСервере
Процедура ПереместитьВверхВниз(ИмяКолонки, Направление, Реквизит)
	Менеджер = РегистрыСведений.CRM_РасположениеРеквизитовКлиентов.СоздатьМенеджерЗаписи();
	Менеджер.ИмяРеквизита =	Реквизит.ИмяРеквизита;
	Менеджер.ДопРеквизит = Реквизит.ДопРеквизит;
	Менеджер.Прочитать();
	Если Менеджер.ПорядокРасположения = 1 И Направление = -1 Тогда
		Возврат;
	КонецЕсли;	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_РасположениеРеквизитовКлиентов.ПорядокРасположения КАК ПорядокРасположения,
	|	CRM_РасположениеРеквизитовКлиентов.ИмяРеквизита,
	|	CRM_РасположениеРеквизитовКлиентов.ДопРеквизит
	|ИЗ
	|	РегистрСведений.CRM_РасположениеРеквизитовКлиентов КАК CRM_РасположениеРеквизитовКлиентов
	|ГДЕ
	|	CRM_РасположениеРеквизитовКлиентов.Колонка = &ИмяКолонки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокРасположения";
	Запрос.УстановитьПараметр("ИмяКолонки", ИмяКолонки);
	ТабРеквизитов = Запрос.Выполнить().Выгрузить();
	ПоследняяСтрока = ТабРеквизитов[ТабРеквизитов.Количество()-1];
	Если Менеджер.ПорядокРасположения = ПоследняяСтрока.ПорядокРасположения И Направление = 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТекПозиция = Менеджер.ПорядокРасположения - 1;
	СтрокаЗамены = ТабРеквизитов[ТекПозиция+Направление];
	Менеджер.ПорядокРасположения = Менеджер.ПорядокРасположения + Направление;
	Менеджер.Записать(Истина);
	Менеджер = РегистрыСведений.CRM_РасположениеРеквизитовКлиентов.СоздатьМенеджерЗаписи();
	Менеджер.ИмяРеквизита =	СтрокаЗамены.ИмяРеквизита;
	Менеджер.ДопРеквизит = СтрокаЗамены.ДопРеквизит;
	Менеджер.Прочитать();
	Менеджер.ПорядокРасположения = Менеджер.ПорядокРасположения - Направление;
	Менеджер.Записать(Истина);
	Элементы[ИмяКолонки+"ПанельРеквизитов"].Обновить();
КонецПроцедуры

&НаСервере
Процедура ПересчитатьКолонки(ИмяКолонки)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	CRM_РасположениеРеквизитовКлиентов.ПорядокРасположения КАК ПорядокРасположения,
	|	CRM_РасположениеРеквизитовКлиентов.ИмяРеквизита,
	|	CRM_РасположениеРеквизитовКлиентов.ДопРеквизит,
	|	CRM_РасположениеРеквизитовКлиентов.Колонка КАК Колонка
	|ИЗ
	|	РегистрСведений.CRM_РасположениеРеквизитовКлиентов КАК CRM_РасположениеРеквизитовКлиентов
	|ГДЕ
	|	CRM_РасположениеРеквизитовКлиентов.Колонка = &ИмяКолонки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокРасположения";
	Запрос.УстановитьПараметр("ИмяКолонки", ИмяКолонки);
	ТабРеквизитов = Запрос.Выполнить().Выгрузить();
	Сч = 1;
	Для Каждого СтрокаРеквизит из ТабРеквизитов Цикл
		
		Менеджер = РегистрыСведений.CRM_РасположениеРеквизитовКлиентов.СоздатьМенеджерЗаписи();
		Менеджер.ИмяРеквизита =	СтрокаРеквизит.ИмяРеквизита;
		Менеджер.ДопРеквизит = СтрокаРеквизит.ДопРеквизит;
		Менеджер.Прочитать();
		Менеджер.ПорядокРасположения = Сч;
		Менеджер.Записать(Истина);
		Сч = Сч + 1;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти 









