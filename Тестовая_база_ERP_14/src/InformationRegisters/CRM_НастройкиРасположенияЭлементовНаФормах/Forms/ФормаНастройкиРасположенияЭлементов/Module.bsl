
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяНастраиваемойФормы = Параметры.ИмяФормы;
	Если НЕ CRM_УправлениеЭлементамиНаФормах.ОписаниеНастраиваемыхФорм().Свойство(СтрЗаменить(Параметры.ИмяФормы, ".", "_"), ОписаниеФормы) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИспользоватьДопРеквизиты = ОписаниеФормы.Свойство("СписокНаборовДопРеквизитов") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения");
	Если Не ИспользоватьДопРеквизиты Тогда
		Элементы.ФормаДобавитьРеквизит.Видимость = Ложь;
		Элементы.ФормаИзменитьРеквизит.Видимость = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_НастройкиРасположенияЭлементовНаФормах.ИмяЭлементаРазмещения КАК ИмяЭлементаРазмещения
	                      |ИЗ
	                      |	РегистрСведений.CRM_НастройкиРасположенияЭлементовНаФормах КАК CRM_НастройкиРасположенияЭлементовНаФормах
	                      |ГДЕ
	                      |	CRM_НастройкиРасположенияЭлементовНаФормах.ИмяФормы = &ИмяФормы
	                      |	И CRM_НастройкиРасположенияЭлементовНаФормах.ИмяЭлемента = CRM_НастройкиРасположенияЭлементовНаФормах.ИмяЭлементаРазмещения
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	CRM_НастройкиРасположенияЭлементовНаФормах.ПорядокРасположения");
	Запрос.УстановитьПараметр("ИмяФормы", ИмяНастраиваемойФормы);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		ОписаниеФормы.Вставить("ТекущийМассивОбластей", Результат.Выгрузить().ВыгрузитьКолонку("ИмяЭлементаРазмещения"));
	Иначе
		ОписаниеФормы.Вставить("ТекущийМассивОбластей", ОписаниеФормы.МассивОбластей);
	КонецЕсли;
	
	Для каждого ИмяГруппы из ОписаниеФормы.ТекущийМассивОбластей Цикл
		
		Команда = Команды.Добавить("ПеренестиВ"+ИмяГруппы);
		Команда.Действие = "Подключаемый_ПеренестиВГруппу";
		
		Кнопка = Элементы.Добавить("ПеренестиВ"+ИмяГруппы, Тип("КнопкаФормы"), Элементы.ДеревоЭлементовКонтекстноеМеню);
		Кнопка.ИмяКоманды = "ПеренестиВ"+ИмяГруппы;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Форма = ВладелецФормы;
	ЗаполнитьНастройки(Форма);
	ЭлементыДерева = ДеревоЭлементов.ПолучитьЭлементы();
	Для каждого ЭлементДерева из ЭлементыДерева Цикл
		Группа = Форма.Элементы.Найти(ЭлементДерева.Реквизит);
		ЭлементДерева.Заголовок = Группа.Заголовок;
		Кнопка = Элементы.Найти("ПеренестиВ"+ЭлементДерева.Реквизит);
		Кнопка.Заголовок = НСтр("ru = 'Переместить в'") + " " + Группа.Заголовок;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	ГотовоНаСервере();
	
	Если ДобавленыДопРеквизиты Тогда
		Оповестить("Запись_НаборыДополнительныхРеквизитовИСведений",
			Новый Структура("Ссылка", ОписаниеФормы.СписокНаборовДопРеквизитов[0]), ОписаниеФормы.СписокНаборовДопРеквизитов[0]);
	КонецЕсли;
			
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРеквизит(Команда)
	ДопПараметры = Новый Структура;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокНаборовДопРеквизитов", ОписаниеФормы.СписокНаборовДопРеквизитов);
	ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Элементы.ДеревоЭлементов.ТекущаяСтрока);
	Если ТекСтрока.ЭтоГруппа Тогда
		ДопПараметры.Вставить("Группа", Элементы.ДеревоЭлементов.ТекущаяСтрока);
	Иначе
		ДопПараметры.Вставить("Группа", ТекСтрока.ПолучитьРодителя().ПолучитьИдентификатор());
	КонецЕсли;
	ДобавленыДопРеквизиты = Истина;
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьРеквизитЗавершение", ЭтотОбъект, ДопПараметры);
	ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.CRM_ФормаЭлемента", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРеквизитЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Строки = ДеревоЭлементов.НайтиПоИдентификатору(ДопПараметры.Группа).ПолучитьЭлементы();
		НовСтрока = Строки.Добавить();
		НовСтрока.Реквизит = Результат.Ссылка;
		НовСтрока.Заголовок = Результат.Заголовок;
		НовСтрока.ЭтоДопРеквизит = Истина;
		Оповестить("Запись_НаборыДополнительныхРеквизитовИСведений",
			Новый Структура("Ссылка", Результат.НаборСвойств), Результат.НаборСвойств);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизит(Команда)
	ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Элементы.ДеревоЭлементов.ТекущаяСтрока);
	Если ТипЗнч(ТекСтрока.Реквизит) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекСтрока.Реквизит);
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьРеквизитЗавершение", ЭтотОбъект);
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.CRM_ФормаЭлемента", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ИначеЕсли ТекСтрока.ЭтоГруппа Тогда	
		ПараметрыРаздела = Новый Структура("ИмяРеквизита, Наименование", ТекСтрока.Реквизит, ТекСтрока.Заголовок);
		ОписаниеОповещения = Новый ОписаниеОповещения("ДействияСРазделомЗавершение", ЭтотОбъект);
		ОткрытьФорму("РегистрСведений.CRM_НастройкиРасположенияЭлементовНаФормах.Форма.ФормаРаздела", ПараметрыРаздела, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРеквизитЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Элементы.ДеревоЭлементов.ТекущаяСтрока);
		Если Результат.ПометкаУдаления Тогда
			Родитель = ТекСтрока.ПолучитьРодителя();
			СтрокиРодителя = Родитель.ПолучитьЭлементы();
			СтрокиРодителя.Удалить(СтрокиРодителя.Индекс(ТекСтрока));
		ИначеЕсли ТекСтрока.Заголовок <> Результат.Заголовок Тогда
			ТекСтрока.Заголовок = Результат.Заголовок;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтандартныеНастройки(Команда)
	ЗаполнитьСтандартныеНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьГруппу(Команда)
	ПараметрыРаздела = Новый Структура("НовыйРаздел", Истина);
	ОписаниеОповещения = Новый ОписаниеОповещения("ДействияСРазделомЗавершение", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.CRM_НастройкиРасположенияЭлементовНаФормах.Форма.ФормаРаздела", ПараметрыРаздела, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ДействияСРазделомЗавершение(СтруктураРаздела, ДопПараметры) Экспорт
	Если СтруктураРаздела = Неопределено Тогда
		Возврат;
	ИначеЕсли СтруктураРаздела.Свойство("НовыйРаздел") И СтруктураРаздела.НовыйРаздел Тогда
		СтрокиРазделов = ДеревоЭлементов.ПолучитьЭлементы();
		НовыйРаздел = СтрокиРазделов.Вставить(СтрокиРазделов.Количество()-1);
		НовыйРаздел.Реквизит = СтруктураРаздела.ИмяРеквизита;
		НовыйРаздел.Заголовок = СтруктураРаздела.Наименование;
		НовыйРаздел.ЭтоГруппа = Истина;
		ОписаниеФормы.ТекущийМассивОбластей.Добавить(НовыйРаздел.Реквизит);
		ДобавитьКомандуКонтекстногоМеню(НовыйРаздел.Реквизит, НовыйРаздел.Заголовок);
	ИначеЕсли СтруктураРаздела.Свойство("УдалениеРаздела") И СтруктураРаздела.УдалениеРаздела Тогда
		ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Элементы.ДеревоЭлементов.ТекущаяСтрока);
		СтрокиРазделов = ДеревоЭлементов.ПолучитьЭлементы();
		ЭлементыУдаляемогоРаздела = ТекСтрока.ПолучитьЭлементы();
		СтрокиДоп = СтрокиРазделов[СтрокиРазделов.Количество()-1].ПолучитьЭлементы();
		Для каждого ЭлементУдаляемогоРаздела из ЭлементыУдаляемогоРаздела Цикл
			НовСтр = СтрокиДоп.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ЭлементУдаляемогоРаздела);
		КонецЦикла;
		СтрокиРазделов.Удалить(ТекСтрока);
	Иначе
		ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Элементы.ДеревоЭлементов.ТекущаяСтрока);
		ТекСтрока.Заголовок = СтруктураРаздела.Наименование;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьКомандуКонтекстногоМеню(ИмяГруппы, Заголовок)
	Команда = Команды.Добавить("ПеренестиВ"+ИмяГруппы);
	Команда.Действие = "Подключаемый_ПеренестиВГруппу";
	Команда.Заголовок = НСтр("ru = 'Переместить в'") + " " + Заголовок;
	
	Кнопка = Элементы.Добавить("ПеренестиВ"+ИмяГруппы, Тип("КнопкаФормы"), Элементы.ДеревоЭлементовКонтекстноеМеню);
	Кнопка.ИмяКоманды = "ПеренестиВ"+ИмяГруппы;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыДеревоЭлементов

&НаКлиенте
Процедура ДеревоЭлементовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЭлементовПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЭлементовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	Если ДеревоЭлементов.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение).ЭтоГруппа Тогда
		Выполнение = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЭлементовПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ПеретащитьВ = ДеревоЭлементов.НайтиПоИдентификатору(Строка);
	Если НЕ ПеретащитьВ.ЭтоГруппа И ПеретащитьВ.ПолучитьРодителя() <> ДеревоЭлементов.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение).ПолучитьРодителя() Тогда
		СтандартнаяОбработка = Ложь;
		ПеренестиВГруппу(ПараметрыПеретаскивания.Значение, ПеретащитьВ.ПолучитьРодителя(), Строка)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЭлементовПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(Элемент.ТекущаяСтрока);
	Если ТекСтрока.ЭтоГруппа Тогда
		СтрокиДерева = ДеревоЭлементов.ПолучитьЭлементы();
		Индекс = ДеревоЭлементов.ПолучитьЭлементы().Индекс(ТекСтрока);
		Для каждого ТекЭлемент из Элементы.ДеревоЭлементовКонтекстноеМеню.ПодчиненныеЭлементы Цикл
			ТекЭлемент.Доступность = Ложь;
		КонецЦикла;
		Элементы.ДеревоЭлементовКонтекстноеМенюПереместить.Доступность = Истина;
		Элементы.ДеревоЭлементовПереместитьВверх.Доступность = (Индекс>1 И Индекс<>СтрокиДерева.Количество()-1);
		Элементы.ДеревоЭлементовКонтекстноеМенюПереместитьВверх.Доступность = Элементы.ДеревоЭлементовПереместитьВверх.Доступность;
		Элементы.ДеревоЭлементовПереместитьВниз.Доступность = (Индекс>0 И Индекс<СтрокиДерева.Количество()-2);
		Элементы.ДеревоЭлементовКонтекстноеМенюПереместитьВниз.Доступность = Элементы.ДеревоЭлементовПереместитьВниз.Доступность;
	Иначе
		Для каждого ТекЭлемент из Элементы.ДеревоЭлементовКонтекстноеМеню.ПодчиненныеЭлементы Цикл
			ТекЭлемент.Доступность = Истина;
		КонецЦикла;
		ТекЭлемент = Элементы.Найти("ПеренестиВ" + ТекСтрока.ПолучитьРодителя().Реквизит);
		Если ТекЭлемент <> Неопределено Тогда
			ТекЭлемент.Доступность = Ложь;
		КонецЕсли;
		Элементы.ДеревоЭлементовПереместитьВверх.Доступность = Истина;
		Элементы.ДеревоЭлементовКонтекстноеМенюПереместитьВверх.Доступность = Истина;
		Элементы.ДеревоЭлементовПереместитьВниз.Доступность = Истина;
		Элементы.ДеревоЭлементовКонтекстноеМенюПереместитьВниз.Доступность = Истина;
	КонецЕсли;
	ДоступностьРеквизита = ТекСтрока.ЭтоГруппа И ОписаниеФормы.МассивОбластей.Найти(ТекСтрока.Реквизит) = Неопределено 
		ИЛИ (ТипЗнч(ТекСтрока.Реквизит) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	Элементы.ФормаИзменитьРеквизит.Доступность = ДоступностьРеквизита; 
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЭлементовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекСтрока = ДеревоЭлементов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТипЗнч(ТекСтрока.Реквизит) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
		ПараметрыФормы = Новый Структура("Ключ", ТекСтрока.Реквизит);
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьРеквизитЗавершение", ЭтотОбъект);
		ОткрытьФорму("ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.Форма.CRM_ФормаЭлемента", ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоНаСервере()
	Дерево = РеквизитФормыВЗначение("ДеревоЭлементов");
	НаборЗаписей = РегистрыСведений.CRM_НастройкиРасположенияЭлементовНаФормах.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИмяФормы.Установить(ИмяНастраиваемойФормы);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	ПорядокГрупп = 0;
	Для каждого СтрокаГруппа из Дерево.Строки Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.ИмяФормы = ИмяНастраиваемойФормы;
		Запись.ЗаголовокЭлемента = СтрокаГруппа.Заголовок;
		Запись.ИмяЭлементаРазмещения = СтрокаГруппа.Реквизит;
		Запись.ИмяЭлемента = СтрокаГруппа.Реквизит;
		ПорядокГрупп = ПорядокГрупп + 1;
		Запись.ПорядокРасположения = ПорядокГрупп;
		
		Порядок = 0;
		Для каждого СтрокаЭлемент из СтрокаГруппа.Строки Цикл
			Запись = НаборЗаписей.Добавить();
			Запись.ИмяФормы = ИмяНастраиваемойФормы;
			Запись.ИмяЭлемента = СтрокаЭлемент.Реквизит;
			Запись.ЗаголовокЭлемента = СтрокаЭлемент.Заголовок;
			Запись.ИмяЭлементаРазмещения = СтрокаГруппа.Реквизит;
			Порядок = Порядок + 1;
			Запись.ПорядокРасположения = Порядок;
		КонецЦикла;
	КонецЦикла;
	НаборЗаписей.Записать();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПеренестиВГруппу(Команда)
	ИмяГруппы = СтрЗаменить(Команда.Имя, "ПеренестиВ", "");
	Для каждого Группа из ДеревоЭлементов.ПолучитьЭлементы() Цикл
		Если ИмяГруппы = Группа.Реквизит Тогда
			ПеренестиВГруппу(Элементы.ДеревоЭлементов.ТекущаяСтрока, Группа);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВГруппу(Строка, ПеренестиВ, Позиция = Неопределено)
	
	Перетаскиваемая = ДеревоЭлементов.НайтиПоИдентификатору(Строка);
	СтрокиГруппы = ПеренестиВ.ПолучитьЭлементы();
	Если Позиция = Неопределено Тогда
		НовСтрока = СтрокиГруппы.Добавить();
	Иначе
		НовСтрока = СтрокиГруппы.Вставить(СтрокиГруппы.Индекс(ДеревоЭлементов.НайтиПоИдентификатору(Позиция)));
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(НовСтрока, Перетаскиваемая);
	СтарыйРодитель = Перетаскиваемая.ПолучитьРодителя();
	СтрокиРодителя = СтарыйРодитель.ПолучитьЭлементы();
	СтрокиРодителя.Удалить(СтрокиРодителя.Индекс(Перетаскиваемая));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройки(Форма)
	
	ЭлементыДерева = ДеревоЭлементов.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	ДопРеквизиты = Новый Массив;
	Для каждого ИмяГруппы из ОписаниеФормы.ТекущийМассивОбластей Цикл
		Группа = Форма.Элементы.Найти(ИмяГруппы);
		Если Группа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НовГруппа = ЭлементыДерева.Добавить();
		НовГруппа.Реквизит = ИмяГруппы;
		НовГруппа.Заголовок = Группа.Заголовок;
		НовГруппа.ЭтоГруппа = Истина;
		ЭлементыГруппы = НовГруппа.ПолучитьЭлементы();
		Для каждого Элемент из Группа.ПодчиненныеЭлементы Цикл
			Если ОписаниеФормы.Свойство("МассивИсключаемыхЭлементов") И ОписаниеФормы.МассивИсключаемыхЭлементов.Найти(Элемент.Имя) <> Неопределено Тогда
				Продолжить;
			КонецЕсли; 
			Если ИспользоватьДопРеквизиты Тогда 
				СтрокиДопРек = Форма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизитаЗначение", Элемент.Имя));
				Если СтрокиДопРек.Количество() > 0 Тогда
					НовЭлемент = ЭлементыГруппы.Добавить();
					НовЭлемент.Реквизит = СтрокиДопРек[0].Свойство;
					НовЭлемент.Заголовок = СтрокиДопРек[0].Наименование;
					НовЭлемент.ЭтоДопРеквизит = Истина;
					ДопРеквизиты.Добавить(НовЭлемент.Реквизит);
				Иначе	
					НовЭлемент = ЭлементыГруппы.Добавить();
					НовЭлемент.Реквизит = Элемент.Имя;
					НовЭлемент.Заголовок = ?(ЗначениеЗаполнено(Элемент.Заголовок), Элемент.Заголовок, Элемент.Имя);
				КонецЕсли;
			Иначе
				НовЭлемент = ЭлементыГруппы.Добавить();
				НовЭлемент.Реквизит = Элемент.Имя;
				НовЭлемент.Заголовок = ?(ЗначениеЗаполнено(Элемент.Заголовок), Элемент.Заголовок, Элемент.Имя);
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	Если ИспользоватьДопРеквизиты Тогда
		Для каждого ДопРеквизит из Форма.Свойства_ОписаниеДополнительныхРеквизитов Цикл
			Если ДопРеквизиты.Найти(ДопРеквизит.Свойство) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Элемент = Форма.Элементы.Найти(ДопРеквизит.ИмяРеквизитаЗначение);
			РодительЭлемента = Элемент.Родитель;
			Пока ОписаниеФормы.ТекущийМассивОбластей.Найти(РодительЭлемента.Имя) = Неопределено Цикл
				РодительЭлемента = РодительЭлемента.Родитель;
			КонецЦикла;
			Для каждого ЭлементДерева из ЭлементыДерева Цикл
				Если ЭлементДерева.Реквизит = РодительЭлемента.Имя Тогда
					НовЭлемент = ЭлементДерева.ПолучитьЭлементы().Добавить();
					НовЭлемент.Реквизит = ДопРеквизит.Свойство;
					НовЭлемент.Заголовок = ДопРеквизит.Наименование;
					НовЭлемент.ЭтоДопРеквизит = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтандартныеНастройки()
	ПараметрыФормы = Новый Структура("РасположениеЭлементовПоУмолчанию", Истина);
	Форма = ПолучитьФорму(ИмяНастраиваемойФормы, ПараметрыФормы);
	ЗаполнитьНастройки(Форма);
КонецПроцедуры

#КонецОбласти
