#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ИмяРеквизита") Тогда
		ИмяРеквизита = Параметры.ИмяРеквизита;
	КонецЕсли;
	Если Параметры.Свойство("Наименование") Тогда
		Наименование = Параметры.Наименование;
	КонецЕсли;
	Если Параметры.Свойство("НовыйРаздел") Тогда
		НовыйРаздел = Параметры.НовыйРаздел;
		ИмяРеквизита = "ПользовательскийРаздел"+СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
		Наименование = НСтр("ru = 'Новый раздел'");
	Иначе
		НовыйРаздел = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	Если ПроверитьЗаполнение() Тогда
		Закрыть(Новый Структура("ИмяРеквизита, Наименование, НовыйРаздел", ИмяРеквизита, Наименование, НовыйРаздел));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отменить(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	ОписаниеОпопвещения = Новый ОписаниеОповещения("УдалитьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОпопвещения, НСтр("ru = 'Удалить раздел'")+" "+Наименование+"?", РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗавершение(Результат, ДопПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Закрыть(Новый Структура("ИмяРеквизита, УдалениеРаздела", ИмяРеквизита, Истина));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
