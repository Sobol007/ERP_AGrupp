#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Создает контрольную точку.
//
// Параметры:
//  Проект			 - СправочникСсылка.Проекты	 - Проект, измерение регистра.
//  Этап			 - СправочникСсылка.CRM_ЭтапыПроектов	 -  Этап, измерение регистра.
//  Исполнитель		 - СправочникСсылка.Пользователи	 -  Исполнитель, измерение регистра.
//  Подразделение	 - СправочникСсылка.СтруктураПредприятия	 - Подразделение, реквизит регистра.
//  ДатаНачала		 - Дата	 -  Дата контрольной точки.
//  ЭтоКТНачала		 - Булево	 -  Признак того, что контрольная точка является началом проекта или концом.
// 
// Возвращаемое значение:
//  СтрОшибка -   Строка описания ошибки выполнения функции.
//
Функция СоздатьКонтрольнуюТочку(Проект, Этап, Исполнитель, Подразделение, ДатаНачала, ЭтоКТНачала = Истина) Экспорт
	
	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("Проект", Проект);
	Если ЗначениеЗаполнено(Этап) Тогда
		ДанныеЗаполнения.Вставить("Этап", Этап);
	КонецЕсли;
	Если ЗначениеЗаполнено(Подразделение) Тогда
		ДанныеЗаполнения.Вставить("Подразделение", Подразделение);
	КонецЕсли;
	ДанныеЗаполнения.Вставить("ПлановаяДатаНачала", ДатаНачала);
	ДанныеЗаполнения.Вставить("ПлановаяДатаОкончания", ДатаНачала);
	ДанныеЗаполнения.Вставить("ПлановаяДлительность", 1);
	ДанныеЗаполнения.Вставить("ТипЭтапа", Перечисления.CRM_ТипыЭтапов.КонтрольнаяТочка);
	
	ДокументОбъект = Документы.CRM_ЭтапКалендарногоПлана.СоздатьДокумент();
	ДокументОбъект.Заполнить(ДанныеЗаполнения);
	
	Тема = Строка(Проект);
	Если ЗначениеЗаполнено(Этап) Тогда
		Тема = Тема + ?(ЗначениеЗаполнено(Тема), ", ", "") + Строка(Этап);
	КонецЕсли;
	Если ЭтоКТНачала Тогда
		Тема = НСтр("ru='Начало';en='Beginning'") + " """ + Тема + """";
	Иначе
		Тема = НСтр("ru='Окончание';en='Termination'") + " """ + Тема + """";
	КонецЕсли;
	
	ДокументОбъект.Тема = Тема;
	
	ДокументОбъект.Проект = Проект;
	
	бИсполнительОтветственный = Истина;
	Если ЗначениеЗаполнено(Проект.Ответственный) Тогда
		НоваяСтрока = ДокументОбъект.Участники.Добавить();
		НоваяСтрока.Пользователь	= Проект.Ответственный;
		КИПользователя = CRM_УправлениеПроектамиСервер.ПолучитьКИУчастника(НоваяСтрока.Пользователь);
		НоваяСтрока.Адрес = КИПользователя.Адрес;
		НоваяСтрока.Телефон = КИПользователя.Телефон;
		НоваяСтрока.Ответственный	= Истина;
		
		бИсполнительОтветственный = Ложь;
	Иначе
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Исполнитель) И Исполнитель <> Проект.Ответственный Тогда
		НоваяСтрока = ДокументОбъект.Участники.Добавить();
		НоваяСтрока.Пользователь	= Исполнитель;
		НоваяСтрока.Ответственный	= бИсполнительОтветственный;
		КИПользователя = CRM_УправлениеПроектамиСервер.ПолучитьКИУчастника(НоваяСтрока.Пользователь);
		НоваяСтрока.Адрес = КИПользователя.Адрес;
		НоваяСтрока.Телефон = КИПользователя.Телефон;
	КонецЕсли;
	
	Попытка
		ДокументОбъект.Записать();
		Возврат ДокументОбъект.Ссылка;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		СтрОшибка = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		Возврат СтрОшибка;
	КонецПопытки;
КонецФункции

#КонецОбласти


#КонецЕсли