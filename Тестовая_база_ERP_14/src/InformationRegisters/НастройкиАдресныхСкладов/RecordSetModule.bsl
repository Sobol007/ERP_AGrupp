#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	МетаданныеОбъекта = Метаданные();
	
	Для Каждого СтрМас Из ЭтотОбъект Цикл
		СтрМас.ГлубинаАнализа                     = МетаданныеОбъекта.Ресурсы.ГлубинаАнализа.ЗначениеЗаполнения;
		СтрМас.МинимальнаяВероятностьОтгрузки     = МетаданныеОбъекта.Ресурсы.МинимальнаяВероятностьОтгрузки.ЗначениеЗаполнения;
		СтрМас.УровеньОбслуживанияУпаковокКлассаX = МетаданныеОбъекта.Ресурсы.УровеньОбслуживанияУпаковокКлассаX.ЗначениеЗаполнения;
		СтрМас.УровеньОбслуживанияУпаковокКлассаZ = МетаданныеОбъекта.Ресурсы.УровеньОбслуживанияУпаковокКлассаZ.ЗначениеЗаполнения;
		СтрМас.ИспользоватьПрогнозРасходаУпаковок = МетаданныеОбъекта.Ресурсы.ИспользоватьПрогнозРасходаУпаковок.ЗначениеЗаполнения;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	ТекущийОбъект = ЭтотОбъект[0];
	
	МассивНепроверяемыхРеквизитов.Добавить("РабочийУчастокОтбор");
	МассивНепроверяемыхРеквизитов.Добавить("ОграничениеПоВесуОтбор");
	МассивНепроверяемыхРеквизитов.Добавить("ОграничениеПоОбъемуОтбор");
			
	Если ТекущийОбъект.НастройкаФормированияПоРабочимУчасткамОтбор = "ПоОдномуРабочемуУчастку" 
		И Не ЗначениеЗаполнено(ТекущийОбъект.РабочийУчастокОтбор) Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Рабочий участок"" для создания заданий на отбор не заполнено.';
								|en = 'The ""Work area"" field for picking job creation is not filled in.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "РабочийУчастокОтбор", "Запись", Отказ);
		
	КонецЕсли;
	
	Если ТекущийОбъект.ОграничиватьПоВесуОтбор
		И Не ЗначениеЗаполнено(ТекущийОбъект.ОграничениеПоВесуОтбор)
		И (Не ТекущийОбъект.НастройкаФормированияПоРабочимУчасткамОтбор = "СРазбиениемПоРабочимУчасткам") Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Ограничение по весу"" для создания заданий на отбор не заполнено.';
								|en = 'The ""Limitation by weight"" field for picking job creation is not filled in.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ОграничениеПоВесу", "Запись", Отказ);
		
	КонецЕсли;
	
	Если ТекущийОбъект.ОграничиватьПоОбъемуОтбор
		И Не ЗначениеЗаполнено(ТекущийОбъект.ОграничениеПоОбъемуОтбор)
		И (Не ТекущийОбъект.НастройкаФормированияПоРабочимУчасткамОтбор = "СРазбиениемПоРабочимУчасткам") Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Ограничение по объему"" для создания заданий на отбор не заполнено.';
								|en = 'The ""Limitation by volume"" field for picking job creation is not filled in. '");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ОграничениеПоОбъему", "Запись", Отказ);
		
	КонецЕсли;
	
 	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры
	
#КонецОбласти
#КонецЕсли