Функция ПолучитьИнициализационныеНастройки()
	Макет = ПолучитьМакет("ИнициализационныеНастройки_20180301");
	
	ИнициализационнаяТаблица = Новый ТаблицаЗначений;
	Для Каждого текКолонка Из Метаданные.РегистрыСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.Измерения Цикл
		ИнициализационнаяТаблица.Колонки.Добавить(текКолонка.Имя, текКолонка.Тип);
	КонецЦикла;
	Для Каждого текКолонка Из Метаданные.РегистрыСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.Ресурсы Цикл
		ИнициализационнаяТаблица.Колонки.Добавить(текКолонка.Имя, текКолонка.Тип);
	КонецЦикла;
	
	Для текСтрока = 2 По Макет.ВысотаТаблицы Цикл
		Если ПустаяСтрока(Макет.Область(текСтрока, 1).Текст) Тогда
			Прервать;
		КонецЕсли;
		
		НоваяСтрока = ИнициализационнаяТаблица.Добавить();
		НоваяСтрока.ВалютаЛимитов = Справочники.Валюты.НайтиПоКоду(Макет.Область(текСтрока, 3).Текст);
		НоваяСтрока.ЛимитБезПредоставленияДокументовВБанк = ПреобразоватьТекстВЧисло(Макет.Область(текСтрока, 4).Текст);
		НоваяСтрока.ЛимитБезПостановкиНаУчетКонтрактаИмпорт = ПреобразоватьТекстВЧисло(Макет.Область(текСтрока, 5).Текст);
		НоваяСтрока.ЛимитБезПостановкиНаУчетКонтрактаЭкспорт = ПреобразоватьТекстВЧисло(Макет.Область(текСтрока, 6).Текст);
		НоваяСтрока.ЛимитБезПостановкиНаУчетКредитногоДоговора = ПреобразоватьТекстВЧисло(Макет.Область(текСтрока, 7).Текст);
	КонецЦикла;
	
	Возврат ИнициализационнаяТаблица;
КонецФункции

Функция ПреобразоватьТекстВЧисло(Текст)
	Возврат Число(СтрЗаменить(Текст, " ", ""));
КонецФункции

Процедура ОчиститьРегистр(Дата) Экспорт
    Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистр.Период
	|ИЗ
	|	РегистрСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля КАК Регистр
	|ГДЕ
	|	Регистр.Период >= &Период";
	Запрос.УстановитьПараметр("Период", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Записать(Истина);
	КонецЦикла;
КонецПроцедуры

Процедура УстановитьИнициализационныеНастройки() Экспорт
	ДатаНастроек = Дата(2018,03,01);
	
	ОчиститьРегистр(ДатаНастроек);
	
	ИнициализационнаяТаблица = ПолучитьИнициализационныеНастройки();

	НаборЗаписей = РегистрыСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаНастроек);
	Для Каждого СтрокаТаблицы Из ИнициализационнаяТаблица Цикл
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
		Запись.Период = ДатаНастроек;
	КонецЦикла;
	НаборЗаписей.Записать();
КонецПроцедуры
		
Функция ПолучитьЛимитПоКонтракту(КонтрактВЭД, Дата) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Регистр.ВалютаЛимитов КАК ВалютаЛимитов,
	|	Регистр.ЛимитБезПостановкиНаУчетКонтрактаИмпорт КАК ЛимитБезПостановкиНаУчетКонтрактаИмпорт,
	|	Регистр.ЛимитБезПостановкиНаУчетКонтрактаЭкспорт КАК ЛимитБезПостановкиНаУчетКонтрактаЭкспорт,
	|	Регистр.ЛимитБезПостановкиНаУчетКредитногоДоговора КАК ЛимитБезПостановкиНаУчетКредитногоДоговора
	|ИЗ
	|	РегистрСведений.РЭЙ_НастройкиПараметровВалютногоКонтроля.СрезПоследних(&Период, ) КАК Регистр";
	Запрос.УстановитьПараметр("Период", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ВалютаЛимита = Справочники.Валюты.ПустаяСсылка();
	СуммаЛимита = 0;
	Если Выборка.Следующий() Тогда
		ВалютаЛимита = Выборка.ВалютаЛимитов;
		Если КонтрактВЭД.КредитныйДоговор Тогда
			СуммаЛимита = Выборка.ЛимитБезПостановкиНаУчетКредитногоДоговора;
		ИначеЕсли КонтрактВЭД.Импорт Тогда
			СуммаЛимита = Выборка.ЛимитБезПостановкиНаУчетКонтрактаИмпорт;
		ИначеЕсли КонтрактВЭД.Экспорт Тогда
			СуммаЛимита = Выборка.ЛимитБезПостановкиНаУчетКонтрактаЭкспорт;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Новый Структура("Сумма, Валюта", СуммаЛимита, ВалютаЛимита);
КонецФункции
