#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИсключаемыеИменаОтборов = "";
	
	Если ЗначениеЗаполнено(Параметры.КлючНазначенияИспользования) Тогда
		
		Список.КомпоновщикНастроек.Настройки.Отбор.Элементы.Очистить();
		Если Параметры.Отбор.Свойство("ФизическоеЛицо") Тогда
			ИсключаемыеИменаОтборов = "ФизическоеЛицо";
		КонецЕсли;
		
	КонецЕсли;
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список", , , , ИсключаемыеИменаОтборов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Для Каждого КлючЗаписи Из Элемент.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(КлючЗаписи);
		Если ДанныеСтроки <> Неопределено
			И ДанныеСтроки.ИспользоватьЭДСБанком
			И ЗначениеЗаполнено(ДанныеСтроки.ДокументОснование) Тогда
			
			ТекстОшибки = НСтр("ru = 'Нельзя изменять лицевые счета, введенные документом.';
								|en = 'Cannot change personal accounts entered by document.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДанныеСтроки.ДокументОснование, , , Отказ);
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
