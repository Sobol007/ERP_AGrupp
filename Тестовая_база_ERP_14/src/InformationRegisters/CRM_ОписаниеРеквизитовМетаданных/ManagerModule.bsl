#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Обновляет записи регистра сведений CRM_ОписаниеРеквизитовМетаданных.
//
Процедура ОбновитьЗаписи() Экспорт
	МассивМетаданных = Новый Массив();
	МассивМетаданных.Добавить(Метаданные.Справочники.Партнеры);
	МассивМетаданных.Добавить(Метаданные.Справочники.КонтактныеЛицаПартнеров);
	МассивМетаданных.Добавить(Метаданные.Документы.CRM_Интерес);
	
	МассивИтераторыРеквизитов = Новый Массив();
	МассивИтераторыРеквизитов.Добавить("СтандартныеРеквизиты");
	МассивИтераторыРеквизитов.Добавить("РеквизитыАдресации");
	МассивИтераторыРеквизитов.Добавить("Измерения");
	МассивИтераторыРеквизитов.Добавить("Ресурсы");
	МассивИтераторыРеквизитов.Добавить("Реквизиты");
	
	НаборЗаписей = РегистрыСведений.CRM_ОписаниеРеквизитовМетаданных.СоздатьНаборЗаписей();
	
	Для Каждого ОбъектМетаданных Из МассивМетаданных Цикл
		Для Каждого ИмяИтератора Из МассивИтераторыРеквизитов Цикл
			Попытка		Итератор = ОбъектМетаданных[ИмяИтератора];
			Исключение	Итератор = Неопределено;
			КонецПопытки;
			Если Итератор <> Неопределено Тогда
				Для Каждого Реквизит Из Итератор Цикл
					Имя = Реквизит.Имя;
					Представление = ?(ЗначениеЗаполнено(Реквизит.Представление()), Реквизит.Представление(), Реквизит.Имя);
					Если Не ЗначениеЗаполнено(Имя) Или Не ЗначениеЗаполнено(Представление) Тогда
						Продолжить;
					КонецЕсли;
					НоваяСтрока = НаборЗаписей.Добавить();
					НоваяСтрока.Объект = ОбъектМетаданных.ПолноеИмя();
					НоваяСтрока.Реквизит = Имя;
					НоваяСтрока.Представление = Представление;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	НаборЗаписей.Записать();
КонецПроцедуры

#КонецОбласти

#КонецЕсли