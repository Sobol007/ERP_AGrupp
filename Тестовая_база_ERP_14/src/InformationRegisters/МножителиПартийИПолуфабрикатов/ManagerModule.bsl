#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Процедура выполняет фоновое проведение
//
// Параметры:
//	Параметры - Структура - набор параметров для фонового проведения:
//		*ДокументСсылка - Ссылка на документ
//
Процедура ВыполнитьПроведение(ПараметрыРасчета, КалькуляцииКПроверке = Неопределено) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("РегистрСведений.МножителиПартийИПолуфабрикатов.РазузловатьПродукцию");
	
	Калькуляция = ПараметрыРасчета.ДокументСсылка;
	
	ОпределитьПараметрыРасчета(ПараметрыРасчета, Замер, Калькуляция);
	
	ЗаписатьСостояниеКалькуляции(Калькуляция, "НеРассчитана");
	
	ПровестиДокумент(Калькуляция, ПараметрыРасчета);
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных, "ПровестиДокумент");
	ПараметрыРасчета.КоличествоДанных = 0;
	
	РассчитатьПотребности(Калькуляция, ПараметрыРасчета);
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных, "РассчитатьПотребности");
	ПараметрыРасчета.КоличествоДанных = 0;
	
	ЗаполнитьСтоимости(ПараметрыРасчета);
	
	ДобавитьКалькуляциюКПроверке(Калькуляция, КалькуляцииКПроверке);
	
	Если БылаОтменаПроведения(Калькуляция) Тогда
		ОтменитьПроведениеДокумента(Калькуляция, ПараметрыРасчета);
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных);
	
	ДокументКПроведению = ДокументВОчереди();
	Если Не ДокументКПроведению = Неопределено Тогда
		ВыполнитьПроведение(Новый Структура("ДокументСсылка", ДокументКПроведению), КалькуляцииКПроверке);
	Иначе
		
		Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("РегистрСведений.МножителиПартийИПолуфабрикатов.ПроверитьСтоимость");
		ПроверитьСтоимость(КалькуляцииКПроверке);
		
		Если КалькуляцииКПроверке = Неопределено Тогда
			ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 1);
		Иначе
			ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, КалькуляцииКПроверке.Количество());
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область РасчетПотребностей

Процедура РассчитатьПотребности(Калькуляция, ПараметрыРасчета)
	
	Если БылаОтменаПроведения(Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибкиВРасчете = Ложь;
	НачатьТранзакцию();
	
	Попытка
		
		ЗаблокироватьДанные(Калькуляция);
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЕстьЗаписиВОчереди = ОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц);
				
		Если ЕстьЗаписиВОчереди Тогда
						
			ОбработатьОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц, ПараметрыРасчета);
		
			ОчиститьОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыРасчета.ОшибкаРазузлования) Тогда
			
			ЕстьОшибкиВРасчете = Истина;
			ЗаписатьСостояниеКалькуляции(Калькуляция, "ОшибкаРасчета");
			ЗаписатьВЖурналОшибкуРассчета(Калькуляция, ПараметрыРасчета.ОшибкаРазузлования);
			
		КонецЕсли;	
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЕстьОшибкиВРасчете = Истина;
		ЗаписатьСостояниеКалькуляции(Калькуляция, "ОшибкаРасчета");
		ОчиститьОчередьПоКалькуляции(Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(Калькуляция, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
	Если Не ЕстьОшибкиВРасчете И ЕстьЗаданияВОчереди(Калькуляция) Тогда
		РассчитатьПотребности(Калькуляция, ПараметрыРасчета);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьПродукциюПоЗаказу(Калькуляция, МенеджерВременныхТаблиц, Множители, Движения, ПараметрыРасчета)
	
	Множители.ЗаполнитьЗначения(Истина, "РасчетЗавершен");

	НаборВалют = Константы.СоздатьНабор("ВалютаРегламентированногоУчета, ВалютаУправленческогоУчета");
	НаборВалют.Прочитать();	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(Задания.Месяц) КАК Период
		|ПОМЕСТИТЬ НерассчитанныйПериод
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСебестоимости КАК Задания
		|ГДЕ
		|	Задания.Организация = &Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОчередьРасчета.ПартияСпецификацияПродукции КАК ПартияПродукции,
		|	ВЫБОР
		|		КОГДА Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				И Реквизиты.ФактическоеОкончаниеЭтапа < &ДатаКалькуляции
		|				И НерассчитанныйПериод.Период ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПартияВыпущена,
		|	Реквизиты.ПартияПроизводства
		|ПОМЕСТИТЬ ПартииПродукции
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
		|		ПО ОчередьРасчета.ПартияСпецификацияПродукции = Реквизиты.Ссылка
		|			И (Реквизиты.Проведен)
		|		ЛЕВОЕ СОЕДИНЕНИЕ НерассчитанныйПериод КАК НерассчитанныйПериод
		|		ПО НАЧАЛОПЕРИОДА(Реквизиты.ФактическоеОкончаниеЭтапа, МЕСЯЦ) > НерассчитанныйПериод.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииПродукции.ПартияПродукции 	КАК ПартияПродукции,
		|	Реквизиты.Ссылка 					КАК Этап,
		|	Реквизиты.Дата						КАК Дата,
		|	Реквизиты.Валюта					КАК Валюта,
		|	Реквизиты.ВидЦены					КАК ВидЦены,
		|	Реквизиты.Подразделение 			КАК Подразделение,
		|	Реквизиты.НеОтгружатьЧастями		КАК ОтгружатьОднойДатой,
		|	ВЫБОР
		|		КОГДА Реквизиты.ДатаОтгрузки = ДатаВремя(1, 1, 1)
		|			ТОГДА Реквизиты.Дата
		|		ИНАЧЕ 
		|			Реквизиты.ДатаОтгрузки 
		|	КОНЕЦ 								КАК ДатаОбеспечения,
		|	ВЫБОР
		|		КОГДА Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|			И Реквизиты.ФактическоеОкончаниеЭтапа < &ДатаКалькуляции
		|			И НерассчитанныйПериод.Период ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ 								КАК ЭтапЗавершен
		|ПОМЕСТИТЬ РеквизитыЭтаповПартий
		|ИЗ
		|	ПартииПродукции КАК ПартииПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
		|		ПО ПартииПродукции.ПартияПроизводства = Реквизиты.ПартияПроизводства
		|			И (Реквизиты.Проведен)
		|		ЛЕВОЕ СОЕДИНЕНИЕ НерассчитанныйПериод КАК НерассчитанныйПериод
		|		ПО НАЧАЛОПЕРИОДА(Реквизиты.ФактическоеОкончаниеЭтапа, МЕСЯЦ) > НерассчитанныйПериод.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартияПродукции,
		|	Этап
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(ПобочныеИзделия.ДатаПроизводства, ДЕНЬ) КАК Период,
		|	Реквизиты.Валюта КАК ВалютаДокумента
		|ПОМЕСТИТЬ втПериоды
		|ИЗ
		|	РеквизитыЭтаповПартий КАК Реквизиты
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ПобочныеИзделия
		|	ПО Реквизиты.Этап = ПобочныеИзделия.Ссылка
		|		И НЕ ПобочныеИзделия.Отменено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(КурсыВалют.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
		|	втПериоды.ВалютаДокумента,
		|	втПериоды.Период КАК Период,
		|	МАКСИМУМ(ЕСТЬNULL(КурсыВалют.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаПоследнегоКурса
		|ПОМЕСТИТЬ втДатыПоследнихКурсовВалют
		|ИЗ
		|	втПериоды КАК втПериоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО (КурсыВалют.Период <= втПериоды.Период)
		|			И (КурсыВалют.Валюта = &ВалютаРеглУчета
		|				ИЛИ КурсыВалют.Валюта = &ВалютаУпрУчета
		|				ИЛИ КурсыВалют.Валюта = втПериоды.ВалютаДокумента)
		|
		|СГРУППИРОВАТЬ ПО
		|	втПериоды.Период,
		|	втПериоды.ВалютаДокумента,
		|	ЕСТЬNULL(КурсыВалют.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ КурсыВалют.Валюта ЕСТЬ NULL
		|					И КурсыВалют.Валюта = &ВалютаРеглУчета
		|				ТОГДА КурсыВалют.Курс
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КурсРеглУчета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ КурсыВалют.Валюта ЕСТЬ NULL
		|					И КурсыВалют.Валюта = &ВалютаРеглУчета
		|				ТОГДА КурсыВалют.Кратность
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КратностьРеглУчета,
		|	втДатыПоследнихКурсовВалют.Период КАК Период,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ КурсыВалют.Валюта ЕСТЬ NULL
		|					И КурсыВалют.Валюта = &ВалютаУпрУчета
		|				ТОГДА КурсыВалют.Курс
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КурсУпрУчета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ КурсыВалют.Валюта ЕСТЬ NULL
		|					И КурсыВалют.Валюта = &ВалютаУпрУчета
		|				ТОГДА КурсыВалют.Кратность
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КратностьУпрУчета,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ КурсыВалют.Валюта ЕСТЬ NULL
		|					И КурсыВалют.Валюта = втДатыПоследнихКурсовВалют.ВалютаДокумента
		|				ТОГДА КурсыВалют.Курс
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КурсДокумента,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА НЕ КурсыВалют.Валюта ЕСТЬ NULL
		|					И КурсыВалют.Валюта = втДатыПоследнихКурсовВалют.ВалютаДокумента
		|				ТОГДА КурсыВалют.Кратность
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КратностьДокумента
		|ПОМЕСТИТЬ втКурсыВалютНаДату
		|ИЗ
		|	втДатыПоследнихКурсовВалют КАК втДатыПоследнихКурсовВалют
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО втДатыПоследнихКурсовВалют.ДатаПоследнегоКурса = КурсыВалют.Период
		|			И втДатыПоследнихКурсовВалют.Валюта = КурсыВалют.Валюта
		|
		|СГРУППИРОВАТЬ ПО
		|	втДатыПоследнихКурсовВалют.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКурсыВалютНаДату.Период,
		|	ВЫБОР
		|		КОГДА втКурсыВалютНаДату.КратностьДокумента * втКурсыВалютНаДату.КурсУпрУчета = 0
		|			ТОГДА 1
		|		ИНАЧЕ втКурсыВалютНаДату.КурсДокумента * втКурсыВалютНаДату.КратностьУпрУчета / (втКурсыВалютНаДату.КратностьДокумента * втКурсыВалютНаДату.КурсУпрУчета)
		|	КОНЕЦ КАК КоэффициентПересчетаВВалютуУпр,
		|	ВЫБОР
		|		КОГДА втКурсыВалютНаДату.КратностьДокумента * втКурсыВалютНаДату.КурсРеглУчета = 0
		|			ТОГДА 1
		|		ИНАЧЕ втКурсыВалютНаДату.КурсДокумента * втКурсыВалютНаДату.КратностьРеглУчета / (втКурсыВалютНаДату.КратностьДокумента * втКурсыВалютНаДату.КурсРеглУчета)
		|	КОНЕЦ КАК КоэффициентПересчетаВВалютуРегл
		|ПОМЕСТИТЬ КоэффициентыПересчетаВалют
		|ИЗ
		|	втКурсыВалютНаДату КАК втКурсыВалютНаДату
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Материалы.Номенклатура 			КАК Номенклатура,
		|	Материалы.Характеристика 		КАК Характеристика,
		|	Материалы.ДатаОтгрузки 			КАК ДатаПотребности,
		|	Материалы.Ссылка 				КАК Распоряжение,
		|	Материалы.Склад 				КАК Склад,
		|	Материалы.СтатьяКалькуляции 	КАК СтатьяКалькуляции,
		|	Материалы.Количество 			КАК Количество,
		|	Материалы.Спецификация 			КАК СпецификацияВПроцессе,
		|	Материалы.Назначение			КАК Назначение,
		|	Реквизиты.Подразделение 		КАК Подразделение,
		|	ВЫБОР
		|		КОГДА Материалы.Производится
		|				И НЕ Материалы.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ 							КАК ПроизводитсяВПроцессе,
		|	ВЫБОР
		|		КОГДА Реквизиты.ОтгружатьОднойДатой
		|			ТОГДА Реквизиты.ДатаОбеспечения
		|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА Реквизиты.Дата
		|		ИНАЧЕ
		|			Материалы.ДатаОтгрузки
		|	КОНЕЦ 							КАК ДатаОбеспечения,
		|	Реквизиты.ПартияПродукции 		КАК ПартияСпецификация
		|ПОМЕСТИТЬ МатериалыРаботы
		|ИЗ
		|	РеквизитыЭтаповПартий КАК Реквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Материалы
		|		ПО (Реквизиты.Этап = Материалы.Ссылка)
		|		И НЕ Материалы.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.РемонтируемыеИзделия)
		|ГДЕ
		|	НЕ Реквизиты.ЭтапЗавершен
		|;
		|
		// Материальные затраты, отнесенные на партию производства.
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Себестоимость.Период 						КАК Период,
		|	РеквизитыЭтаповПартий.ПартияПродукции		КАК Партия,
		|	Себестоимость.Партия						КАК ПартияМатериала,
		|	Себестоимость.АналитикаУчетаПартий 			КАК АналитикаУчетаПартий,
		|	Себестоимость.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
		|	Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	Себестоимость.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
		|	Себестоимость.СтатьяКалькуляции 			КАК СтатьяКалькуляции,
		|	Себестоимость.Количество 					КАК Количество
		|ПОМЕСТИТЬ ФактическиеДанные
		|ИЗ
		|	РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
		|		ПО Себестоимость.Регистратор = РеквизитыЭтаповПартий.Этап
		|			И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|
		|ГДЕ
		|	РеквизитыЭтаповПартий.ЭтапЗавершен
		|	И НЕ Себестоимость.КорАналитикаУчетаНоменклатуры.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|	И Себестоимость.Количество > 0
		|	И НЕ Себестоимость.Партия = НЕОПРЕДЕЛЕНО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОчередьРасчета.ПартияСпецификацияПродукции 	КАК ПартияСпецификацияПродукции,
		|	ОчередьРасчета.Продукция 					КАК Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции 		КАК ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение 					КАК НазначениеПродукции,
		|
		|	МатериалыРаботы.СпецификацияВПроцессе 		КАК Спецификация,
		|	МатериалыРаботы.Номенклатура 				КАК Номенклатура,
		|	МатериалыРаботы.Характеристика 				КАК Характеристика,
		|	МатериалыРаботы.Назначение 					КАК Назначение,
		|	МатериалыРаботы.ДатаОбеспечения 			КАК ДатаПотребности,
		|	СУММА(МатериалыРаботы.Количество 
		|		* Множители.Числитель 
		|		/ Множители.Знаменатель) 				КАК Количество,
		|	1 											КАК Порядок
		|ПОМЕСТИТЬ ПФПроизводимыеВПроцессе
		|ИЗ
		|	МатериалыРаботы КАК МатериалыРаботы
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОчередьРасчета КАК ОчередьРасчета
		|		ПО МатериалыРаботы.ПартияСпецификация = ОчередьРасчета.ПартияСпецификацияПродукции
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО (ОчередьРасчета.Калькуляция = Множители.Калькуляция)
		|			И (ОчередьРасчета.ПартияСпецификацияПродукции = Множители.ПартияСпецификацияПродукции)
		|			И (ОчередьРасчета.Продукция = Множители.Продукция)
		|			И (ОчередьРасчета.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции)
		|			И (ОчередьРасчета.Назначение = Множители.Назначение)
		|			И (ОчередьРасчета.ПартияСпецификацияПолуфабриката = Множители.ПартияСпецификацияПолуфабриката)
		|			И (ОчередьРасчета.Полуфабрикат = Множители.Полуфабрикат)
		|			И (ОчередьРасчета.ХарактеристикаПолуфабриката = Множители.ХарактеристикаПолуфабриката)
		|ГДЕ
		|	МатериалыРаботы.ПроизводитсяВПроцессе
		|
		|СГРУППИРОВАТЬ ПО
		|	ОчередьРасчета.Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПродукции,
		|	МатериалыРаботы.ДатаОбеспечения,
		|	МатериалыРаботы.Номенклатура,
		|	МатериалыРаботы.Характеристика,
		|	МатериалыРаботы.СпецификацияВПроцессе,
		|	МатериалыРаботы.Назначение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПФПроизводимыеВПроцессе.ДатаПотребности 			КАК Период,
		|	ПФПроизводимыеВПроцессе.Продукция 					КАК Продукция,
		|	ПФПроизводимыеВПроцессе.ХарактеристикаПродукции 	КАК ХарактеристикаПродукции,
		|	ПФПроизводимыеВПроцессе.НазначениеПродукции 		КАК Назначение,
		|	ПФПроизводимыеВПроцессе.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ПФПроизводимыеВПроцессе.Номенклатура 				КАК Полуфабрикат,
		|	ПФПроизводимыеВПроцессе.Характеристика 				КАК ХарактеристикаПолуфабриката,
		|	ПФПроизводимыеВПроцессе.Количество 					КАК КоличествоПолуфабриката,
		|	ЛОЖЬ 												КАК РасчетЗавершен,
		|	ЛОЖЬ 												КАК ПартияВыпущена,
		|	ПФПроизводимыеВПроцессе.Порядок 					КАК Порядок,
		|	""МножителиПФПроизводимыхВПроцессе"" 				КАК Комментарий
		|ИЗ
		|	ПФПроизводимыеВПроцессе КАК ПФПроизводимыеВПроцессе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МатериалыРаботы.ДатаОбеспечения 			КАК Период,
		
		|	ОчередьРасчета.Продукция 					КАК Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции 		КАК ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение 					КАК Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПродукции 	КАК ПартияСпецификацияПродукции,
		
		|	ОчередьРасчета.ПартияСпецификацияПродукции 	КАК ПартияПолуфабрикатаПродукции,
		|	МатериалыРаботы.Номенклатура 				КАК Полуфабрикат,
		|	МатериалыРаботы.Характеристика 				КАК ХарактеристикаПолуфабриката,
		|	МатериалыРаботы.Количество 					КАК КоличествоПолуфабриката,
		
		|	Множители.Числитель 						КАК Числитель,
		|	Множители.Знаменатель 						КАК Знаменатель,
		
		|	МатериалыРаботы.Склад 						КАК Склад,
		|	ЛОЖЬ 										КАК РасчетЗавершен,
		|	""МножителиМатериалов"" 					КАК Комментарий,
		|	1 											КАК Порядок
		|ИЗ
		|	МатериалыРаботы КАК МатериалыРаботы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОчередьРасчета КАК ОчередьРасчета
		|		ПО МатериалыРаботы.ПартияСпецификация = ОчередьРасчета.ПартияСпецификацияПродукции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|		ПО (ОчередьРасчета.Калькуляция = Множители.Калькуляция)
		|			И (ОчередьРасчета.ПартияСпецификацияПродукции = Множители.ПартияСпецификацияПродукции)
		|			И (ОчередьРасчета.Продукция = Множители.Продукция)
		|			И (ОчередьРасчета.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции)
		|			И (ОчередьРасчета.Назначение = Множители.Назначение)
		|			И (ОчередьРасчета.ПартияСпецификацияПолуфабриката = Множители.ПартияСпецификацияПолуфабриката)
		|			И (ОчередьРасчета.Полуфабрикат = Множители.Полуфабрикат)
		|			И (ОчередьРасчета.ХарактеристикаПолуфабриката = Множители.ХарактеристикаПолуфабриката)
		|ГДЕ
		|	НЕ МатериалыРаботы.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	И НЕ МатериалыРаботы.ПроизводитсяВПроцессе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МатериалыРаботы.Номенклатура 	КАК Номенклатура,
		|	МатериалыРаботы.Характеристика 	КАК Характеристика,
		|	МатериалыРаботы.Склад 			КАК Склад,
		|	ЛОЖЬ 							КАК ПроизводствоНаСтороне,
		|	""МатериалыДляРазузловки"" 		КАК Комментарий
		|ИЗ
		|	МатериалыРаботы КАК МатериалыРаботы
		|ГДЕ
		|	НЕ МатериалыРаботы.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	И НЕ МатериалыРаботы.ПроизводитсяВПроцессе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МатериалыРаботы.ДатаПотребности 	КАК Период,
		|	&Калькуляция 						КАК Калькуляция,
		|	МатериалыРаботы.ПартияСпецификация 	КАК ПартияСпецификация,
		|	МатериалыРаботы.Подразделение 		КАК Подразделение,
		|	МатериалыРаботы.СтатьяКалькуляции 	КАК СтатьяКалькуляции,
		|	МатериалыРаботы.Номенклатура 		КАК Номенклатура,
		|	МатериалыРаботы.Характеристика 		КАК Характеристика,
		
		|	МатериалыРаботы.Количество 			КАК Количество,
		|	0 									КАК Стоимость,
		|	0 									КАК СтоимостьБезНДС,
		|	0 									КАК СтоимостьРегл,
		|	0 									КАК СтоимостьЗабалансовая,
		|	0 									КАК СтоимостьЗабалансоваяРегл,
		
		|	""ПлановыеМатериальныеЗатраты"" 						КАК Комментарий,
		|	НАЧАЛОПЕРИОДА(МатериалыРаботы.ДатаПотребности, МЕСЯЦ) 	КАК ПериодЗатрат,
		|	МатериалыРаботы.ПроизводитсяВПроцессе 					КАК ЭтоПолуфабрикат
		|ИЗ
		|	МатериалыРаботы КАК МатериалыРаботы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПобочныеИзделия.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА Реквизиты.Дата
		|		ИНАЧЕ ПобочныеИзделия.ДатаПроизводства
		|	КОНЕЦ,
		|	&Калькуляция,
		|	Реквизиты.ПартияПродукции,
		|	Реквизиты.Подразделение,
		|	ПобочныеИзделия.СтатьяКалькуляции,
		|	ПобочныеИзделия.Номенклатура,
		|	ПобочныеИзделия.Характеристика,
		
		|	-ПобочныеИзделия.Количество,
		|	-ПобочныеИзделия.Сумма * Коэффициенты.КоэффициентПересчетаВВалютуУпр,
		|	-ПобочныеИзделия.Сумма * Коэффициенты.КоэффициентПересчетаВВалютуУпр,
		|	-ПобочныеИзделия.Сумма * Коэффициенты.КоэффициентПересчетаВВалютуРегл,
		|	0,
		|	0,
		
		|	""ПлановыеМатериальныеЗатратыПобочныеИзделия"",
		|	НАЧАЛОПЕРИОДА(ВЫБОР
		|			КОГДА ПобочныеИзделия.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА Реквизиты.Дата
		|			ИНАЧЕ ПобочныеИзделия.ДатаПроизводства
		|		КОНЕЦ, МЕСЯЦ),
		|	ЛОЖЬ
		|ИЗ
		|	РеквизитыЭтаповПартий КАК Реквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ПобочныеИзделия
		|		ПО (Реквизиты.Этап = ПобочныеИзделия.Ссылка)
		|			И НЕ ПобочныеИзделия.Отменено
		|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыПересчетаВалют КАК Коэффициенты
		|		ПО НАЧАЛОПЕРИОДА(ПобочныеИзделия.ДатаПроизводства, ДЕНЬ) = Коэффициенты.Период
		|ГДЕ
		|	НЕ Реквизиты.ЭтапЗавершен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА Трудозатраты.ДатаВыполнения <> ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА Трудозатраты.ДатаВыполнения 
		|		ИНАЧЕ 
		|			Реквизиты.Дата
		|	КОНЕЦ								КАК Период,
		|	Реквизиты.ПартияПродукции 			КАК ПартияСпецификация,
		|	Реквизиты.Подразделение 			КАК Подразделение,
		|	Трудозатраты.СтатьяКалькуляции 		КАК СтатьяКалькуляции,
		|	Трудозатраты.ВидРабот 				КАК ВидРабот,
		
		|	Трудозатраты.Количество 			КАК Количество,
		
		|	НАЧАЛОПЕРИОДА(
		|		ВЫБОР
		|			КОГДА Трудозатраты.ДатаВыполнения <> ДАТАВРЕМЯ(1,1,1) 
		|				ТОГДА Трудозатраты.ДатаВыполнения 
		|			ИНАЧЕ Реквизиты.Дата КОНЕЦ,
		|	МЕСЯЦ) 				КАК ПериодЗатрат,
		|	""Трудозатраты"" 	КАК Комментарий
		|ИЗ
		|	РеквизитыЭтаповПартий КАК Реквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.Трудозатраты КАК Трудозатраты
		|		ПО (Реквизиты.Этап = Трудозатраты.Ссылка)
		|ГДЕ
		|	НЕ Реквизиты.ЭтапЗавершен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииПродукции.ПартияПродукции КАК ПартияПродукции
		|ИЗ
		|	ПартииПродукции КАК ПартииПродукции
		|ГДЕ
		|	ПартииПродукции.ПартияВыпущена
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеДанные.Партия 						КАК ПартияПродукции,
		|	ФактическиеДанные.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
		|	ФактическиеДанные.Номенклатура 					КАК Номенклатура,
		|	ФактическиеДанные.Характеристика 				КАК Характеристика,
		|	ФактическиеДанные.Период 						КАК Период,
		|	ФактическиеДанные.ПартияМатериала				КАК Партия,
		|	ЛОЖЬ 											КАК ЭтоПолуфабрикат,
		|	СУММА(ФактическиеДанные.Количество) 			КАК Количество,
		|	ФактическиеДанные.АналитикаУчетаПартий 			КАК АналитикаУчетаПартий
		|ИЗ
		|	ФактическиеДанные КАК ФактическиеДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ФактическиеДанные.Номенклатура,
		|	ФактическиеДанные.Характеристика,
		|	ФактическиеДанные.Партия,
		|	ФактическиеДанные.АналитикаУчетаНоменклатуры,
		|	ФактическиеДанные.ПартияМатериала,
		|	ФактическиеДанные.Период,
		|	ФактическиеДанные.АналитикаУчетаПартий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФактическиеДанные.ПартияМатериала 				КАК Партия,
		|	ФактическиеДанные.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
		|	ФактическиеДанные.Номенклатура 					КАК Номенклатура,
		|	ФактическиеДанные.Характеристика 				КАК Характеристика,
		|	НЕОПРЕДЕЛЕНО 									КАК Назначение,
		|	ФактическиеДанные.АналитикаУчетаПартий 			КАК АналитикаУчетаПартий,
		|	СУММА(ФактическиеДанные.Количество) 			КАК Числитель,
		|	1 												КАК Знаменатель
		|ИЗ
		|	ФактическиеДанные КАК ФактическиеДанные
		|
		|СГРУППИРОВАТЬ ПО
		|	ФактическиеДанные.ПартияМатериала,
		|	ФактическиеДанные.Номенклатура,
		|	ФактическиеДанные.Характеристика,
		|	ФактическиеДанные.АналитикаУчетаНоменклатуры,
		|	ФактическиеДанные.АналитикаУчетаПартий
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПартииПродукции.ПартияПродукции,
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка),
		|	ОчередьРасчета.Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение,
		|	Себестоимость.АналитикаУчетаПартий,
		|	СУММА(Себестоимость.Количество),
		|	1
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииПродукции КАК ПартииПродукции
		|		ПО (ПартииПродукции.ПартияПродукции = ОчередьРасчета.ПартияСпецификацияПродукции)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
		|		ПО ПартииПродукции.ПартияПродукции = Себестоимость.Партия
		|			И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И Себестоимость.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции)
		|ГДЕ
		|	ПартииПродукции.ПартияВыпущена
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииПродукции.ПартияПродукции,
		|	ОчередьРасчета.Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение,
		|	Себестоимость.АналитикаУчетаПартий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ФактическиеДанные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РеквизитыЭтаповПартий";
		
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ДатаКалькуляции", НачалоМесяца(ПараметрыРасчета.ДатаКалькуляции));
	Запрос.УстановитьПараметр("ВалютаРеглУчета", НаборВалют.ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ВалютаУпрУчета", НаборВалют.ВалютаУправленческогоУчета);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Множители по полуфабрикатам производимым процессе.
	ВыборкаМножителей = МассивРезультатов[10].Выбрать();
	Пока ВыборкаМножителей.Следующий() Цикл
		
		НовыйМножитель = Множители.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйМножитель, ВыборкаМножителей);
		
	КонецЦикла;
	
	// Предварительные записи множителей возможных "честных" полуфабрикатов.
	МножителиМатериалов = МассивРезультатов[11].Выгрузить();
	
	// Материалы, по которым необходимо проверить признак "честного" полуфабриката.
	Материалы = МассивРезультатов[12].Выгрузить();
	
	ПлановыеМатериальныеЗатраты = МассивРезультатов[13].Выгрузить();
	Для Каждого ПлановаяМатериальнаяЗатрата Из ПлановыеМатериальныеЗатраты Цикл
		ЗаполнитьЗначенияСвойств(Движения.ПлановыеМатериальныеЗатраты.Добавить(), ПлановаяМатериальнаяЗатрата);
	КонецЦикла;
	
	Движения.ПлановыеТрудозатраты = МассивРезультатов[14].Выгрузить();
	
	ОтборПартийПродукции = Новый Структура("ПартияСпецификацияПродукции");
	ВыпущенныеПартии = МассивРезультатов[15].Выгрузить();
	Для Каждого ДанныеПартии Из ВыпущенныеПартии Цикл
		
		ОтборПартийПродукции.ПартияСпецификацияПродукции = ДанныеПартии.ПартияПродукции;
		МножителиВыпущенныхПартий = Множители.НайтиСтроки(ОтборПартийПродукции);
		
		Для Каждого МножительВыпущеннойПартии Из МножителиВыпущенныхПартий Цикл
			МножительВыпущеннойПартии.ПартияВыпущена = Истина;
		КонецЦикла;
		
	КонецЦикла;
	
	//Фактические данные.
	ПараметрыРазузлования = Новый Структура;
	ПараметрыРазузлования.Вставить("Множители", Множители);
	ПараметрыРазузлования.Вставить("МножителиПолуфабрикатов", МассивРезультатов[16].Выгрузить());
	ПараметрыРазузлования.Вставить("КРазузлованию", МассивРезультатов[17].Выгрузить());
	
	РассчитатьМножителиПоФактическимДанным(ПараметрыРазузлования, Калькуляция.Дата);

	КРазузлованию = Новый ТаблицаЗначений;
	КРазузлованию.Колонки.Добавить("Числитель");
	КРазузлованию.Колонки.Добавить("Знаменатель");
	КРазузлованию.Колонки.Добавить("Партия");
	КРазузлованию.Колонки.Добавить("Номенклатура");
	КРазузлованию.Колонки.Добавить("Характеристика");
	КРазузлованию.Колонки.Добавить("Назначение");
	КРазузлованию.Колонки.Добавить("АналитикаУчетаНоменклатуры");
	КРазузлованию.Колонки.Добавить("АналитикаУчетаПартий");

	// Разузловка полуфабрикатов в процессе.
	ПроверкаЦикла = 1; // Счетчик для проверки зацикливания.
	
	Пока Истина Цикл
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПФПроизводимыеВПроцессе.Продукция,
			|	ПФПроизводимыеВПроцессе.ХарактеристикаПродукции,
			|	ПФПроизводимыеВПроцессе.НазначениеПродукции,
			|	ПФПроизводимыеВПроцессе.ПартияСпецификацияПродукции,
			|	ПФПроизводимыеВПроцессе.Номенклатура,
			|	ПФПроизводимыеВПроцессе.Характеристика,
			|	ПФПроизводимыеВПроцессе.Количество,
			|	ПФПроизводимыеВПроцессе.Спецификация,
			|	ПФПроизводимыеВПроцессе.ДатаПотребности,
			|	ЕСТЬNULL(Изделия.Ссылка, ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)) КАК ПартияПФ,
			|	ЕСТЬNULL(Изделия.Ссылка.ПартияПроизводства, ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)) КАК ПартияПроизводства,			
			|	ВЫБОР
			|		КОГДА Изделия.Ссылка ЕСТЬ NULL
			|			ТОГДА ЛОЖЬ
			|		КОГДА Изделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
			|				И Изделия.Ссылка.ФактическоеОкончаниеЭтапа < &ДатаКалькуляции
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ПартияВыпущена,
			|	СУММА(ЕСТЬNULL(Изделия.Количество, 0)) КАК КоличествоВыпуск,
			|	СУММА(ВЫБОР
			|			КОГДА ЕСТЬNULL(Изделия.ДоляСтоимости, 1) = 0
			|				ТОГДА 1
			|			ИНАЧЕ ЕСТЬNULL(Изделия.ДоляСтоимости, 1)
			|		КОНЕЦ) КАК ДоляСтоимости
			|ПОМЕСТИТЬ ПартииПолуфабрикатов
			|ИЗ
			|	ПФПроизводимыеВПроцессе КАК ПФПроизводимыеВПроцессе
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Изделия
			|		ПО ПФПроизводимыеВПроцессе.Номенклатура = Изделия.Номенклатура
			|			И ПФПроизводимыеВПроцессе.Характеристика = Изделия.Характеристика
			|			И ПФПроизводимыеВПроцессе.Назначение = Изделия.Назначение
			|			И ПФПроизводимыеВПроцессе.Спецификация = Изделия.Ссылка.Спецификация
			|			И (Изделия.Ссылка.Проведен)
			|			И (НЕ Изделия.Отменено)
			|
			|СГРУППИРОВАТЬ ПО
			|	ПФПроизводимыеВПроцессе.Продукция,
			|	ПФПроизводимыеВПроцессе.ХарактеристикаПродукции,
			|	ПФПроизводимыеВПроцессе.НазначениеПродукции,
			|	ПФПроизводимыеВПроцессе.ПартияСпецификацияПродукции,
			|	ПФПроизводимыеВПроцессе.Спецификация,
			|	ПФПроизводимыеВПроцессе.Характеристика,
			|	ПФПроизводимыеВПроцессе.Номенклатура,
			|	ПФПроизводимыеВПроцессе.ДатаПотребности,
			|	ПФПроизводимыеВПроцессе.Количество,
			|	ПФПроизводимыеВПроцессе.Назначение,
			|	ВЫБОР
			|		КОГДА Изделия.Ссылка ЕСТЬ NULL
			|			ТОГДА ЛОЖЬ
			|		КОГДА Изделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
			|				И Изделия.Ссылка.ФактическоеОкончаниеЭтапа < &ДатаКалькуляции
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ,
			|	ЕСТЬNULL(Изделия.Ссылка.ПартияПроизводства, ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)),			
			|	ЕСТЬNULL(Изделия.Ссылка, ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПартииПолуфабрикатов.ПартияПФ,
			|	ПартииПолуфабрикатов.ПартияПроизводства,
			|	ПартииПолуфабрикатов.ПартияВыпущена
			|ПОМЕСТИТЬ ВозможныеПартии
			|ИЗ
			|	ПартииПолуфабрикатов КАК ПартииПолуфабрикатов
			|ГДЕ
			|	НЕ ПартииПолуфабрикатов.ПартияПФ ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВозможныеПартии.ПартияПФ		 	КАК ПартияПФ,
			|	Реквизиты.Ссылка 					КАК Этап,
			|	Реквизиты.Дата						КАК Дата,
			|	Реквизиты.Валюта					КАК Валюта,
			|	Реквизиты.Подразделение 			КАК Подразделение,
			|	Реквизиты.НеОтгружатьЧастями		КАК ОтгружатьОднойДатой,
			|	ВЫБОР
			|		КОГДА Реквизиты.ДатаОтгрузки = ДатаВремя(1, 1, 1)
			|			ТОГДА Реквизиты.Дата
			|		ИНАЧЕ 
			|			Реквизиты.ДатаОтгрузки 
			|	КОНЕЦ 								КАК ДатаОтгрузки,
			|	ВЫБОР
			|		КОГДА Реквизиты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
			|			И Реквизиты.ФактическоеОкончаниеЭтапа < &ДатаКалькуляции
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ 								КАК ЭтапЗавершен
			|ПОМЕСТИТЬ РеквизитыЭтаповПартий
			|ИЗ
			|	ВозможныеПартии КАК ВозможныеПартии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
			|		ПО ВозможныеПартии.ПартияПроизводства = Реквизиты.ПартияПроизводства
			|			И (Реквизиты.Проведен)
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ПартияПФ,
			|	Этап
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Себестоимость.Период 						КАК Период,
			|	РеквизитыЭтаповПартий.ПартияПФ				КАК Партия,
			|	Себестоимость.Партия						КАК ПартияМатериала,
			|	Себестоимость.АналитикаУчетаПартий 			КАК АналитикаУчетаПартий,
			|	Себестоимость.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
			|	Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	Себестоимость.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	Себестоимость.СтатьяКалькуляции 			КАК СтатьяКалькуляции,
			|	Себестоимость.Количество 					КАК Количество
			|ПОМЕСТИТЬ ФактическиеДанные
			|ИЗ
			|	РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
			|		ПО Себестоимость.Регистратор = РеквизитыЭтаповПартий.Этап
			|			И Себестоимость.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|ГДЕ
			|	РеквизитыЭтаповПартий.ЭтапЗавершен
			|	И НЕ Себестоимость.КорАналитикаУчетаНоменклатуры.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
			|	И Себестоимость.Количество <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Реквизиты.Ссылка КАК Спецификация,
			|	Изделия.Номенклатура,
			|	Изделия.Характеристика,
			|	МАКСИМУМ(&ДоляСтоимости) КАК ДоляСтоимости
			|ПОМЕСТИТЬ ДолиСтоимостиПоСпецификации
			|ИЗ
			|	ПФПроизводимыеВПроцессе КАК Полуфабрикаты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК Реквизиты
			|		ПО Полуфабрикаты.Спецификация = Реквизиты.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Изделия
			|		ПО Полуфабрикаты.Спецификация = Изделия.Ссылка
			|		И &СоединениеДоляСтоимости
			|
			|СГРУППИРОВАТЬ ПО
			|	Реквизиты.Ссылка,
			|	Изделия.Номенклатура,
			|	Изделия.Характеристика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПартииПолуфабрикатов.ПартияПФ,
			|	СУММА(ВЫБОР
			|			КОГДА ВыходныеИзделия.ДоляСтоимости = 0
			|				ТОГДА 1
			|			ИНАЧЕ ВыходныеИзделия.ДоляСтоимости
			|		КОНЕЦ) КАК ВсегоДолей
			|ПОМЕСТИТЬ ДолиСтоимостиПолуфабрикатов
			|ИЗ
			|	ВозможныеПартии КАК ПартииПолуфабрикатов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ВыходныеИзделия
			|		ПО ПартииПолуфабрикатов.ПартияПФ = ВыходныеИзделия.Ссылка
			|			И НЕ ВыходныеИзделия.Отменено
			|
			|СГРУППИРОВАТЬ ПО
			|	ПартииПолуфабрикатов.ПартияПФ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ПФПроизводимыеВПроцессе.Спецификация,
			|	СУММА(ВЫБОР
			|			КОГДА ЕСТЬNULL(ДолиСтоимости.ДоляСтоимости, 0) = 0
			|				ТОГДА Изделия.Количество
			|			ИНАЧЕ ДолиСтоимости.ДоляСтоимости
			|		КОНЕЦ)
			|ИЗ
			|	ПФПроизводимыеВПроцессе КАК ПФПроизводимыеВПроцессе
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК Реквизиты
			|			ПО ПФПроизводимыеВПроцессе.Спецификация = Реквизиты.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Изделия
			|			ПО ПФПроизводимыеВПроцессе.Спецификация = Изделия.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимостиПоСпецификации КАК ДолиСтоимости
			|			ПО Реквизиты.Ссылка = ДолиСтоимости.Спецификация
			|			И Изделия.Номенклатура = ДолиСтоимости.Номенклатура
			|			И Изделия.Характеристика = ДолиСтоимости.Характеристика
			|
			|СГРУППИРОВАТЬ ПО
			|	ПФПроизводимыеВПроцессе.Спецификация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПартииПолуфабрикатов.Продукция,
			|	ПартииПолуфабрикатов.ХарактеристикаПродукции,
			|	ПартииПолуфабрикатов.НазначениеПродукции,
			|	ПартииПолуфабрикатов.ПартияСпецификацияПродукции,
			|	Материалы.Номенклатура,
			|	Материалы.Характеристика,
			|	ВЫБОР
			|		КОГДА ПартииПолуфабрикатов.Количество / ПартииПолуфабрикатов.КоличествоВыпуск > 1
			|			ТОГДА (Материалы.Количество * ПартииПолуфабрикатов.ДоляСтоимости / ДолиСтоимостиПолуфабрикатов.ВсегоДолей)
			|		ИНАЧЕ ПартииПолуфабрикатов.Количество / ПартииПолуфабрикатов.КоличествоВыпуск * (Материалы.Количество * ПартииПолуфабрикатов.ДоляСтоимости / ДолиСтоимостиПолуфабрикатов.ВсегоДолей)
			|	КОНЕЦ КАК Количество,
			|	Материалы.ДатаОтгрузки КАК ДатаПотребности,
			|	Материалы.Спецификация,
			|	Материалы.Назначение КАК Назначение
			|ПОМЕСТИТЬ ПредварительныеПФПроизводимыеВПроцессе
			|ИЗ
			|	ПартииПолуфабрикатов КАК ПартииПолуфабрикатов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
			|		ПО ПартииПолуфабрикатов.ПартияПФ = РеквизитыЭтаповПартий.ПартияПФ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Материалы
			|		ПО РеквизитыЭтаповПартий.Этап = Материалы.Ссылка
			|			И (Материалы.Производится)
			|			И (НЕ Материалы.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка))
			|			И (НЕ ПартииПолуфабрикатов.ПартияВыпущена)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиСтоимостиПолуфабрикатов КАК ДолиСтоимостиПолуфабрикатов
			|		ПО ПартииПолуфабрикатов.ПартияПФ = ДолиСтоимостиПолуфабрикатов.ПартияПФ
			|ГДЕ
			|	НЕ ПартииПолуфабрикатов.ПартияПФ ЕСТЬ NULL
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПартииПолуфабрикатов.Номенклатура,
			|	ПартииПолуфабрикатов.Характеристика,
			|	СУММА(ПартииПолуфабрикатов.Количество) КАК Количество,
			|	НАЧАЛОПЕРИОДА(ПартииПолуфабрикатов.ДатаПотребности, МЕСЯЦ) КАК Период
			|ПОМЕСТИТЬ ПотребностиПолуфабрикатов
			|ИЗ
			|	ПартииПолуфабрикатов КАК ПартииПолуфабрикатов
			|
			|СГРУППИРОВАТЬ ПО
			|	НАЧАЛОПЕРИОДА(ПартииПолуфабрикатов.ДатаПотребности, МЕСЯЦ),
			|	ПартииПолуфабрикатов.Характеристика,
			|	ПартииПолуфабрикатов.Номенклатура
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	1 КАК Порядок,
			|	ПартииПолуфабрикатов.Продукция КАК Продукция,
			|	ПартииПолуфабрикатов.ХарактеристикаПродукции,
			|	ПартииПолуфабрикатов.НазначениеПродукции КАК Назначение,
			|	ПартииПолуфабрикатов.ПартияСпецификацияПродукции,
			|	ПартииПолуфабрикатов.Номенклатура КАК Полуфабрикат,
			|	ПартииПолуфабрикатов.Характеристика КАК ХарактеристикаПолуфабриката,
			|	ПартииПолуфабрикатов.ПартияПФ КАК ПартияСпецификацияПолуфабриката,
			|	""МножителиПартийИПолуфабрикатов"" КАК Пояснение,
			|	ПартииПолуфабрикатов.ДоляСтоимости КАК Числитель,
			|	ДолиСтоимостиПолуфабрикатов.ВсегоДолей * ПартииПолуфабрикатов.КоличествоВыпуск КАК Знаменатель,
			|	ПартииПолуфабрикатов.Количество КАК КоличествоПолуфабриката,
			|	ПартииПолуфабрикатов.КоличествоВыпуск КАК КоличествоВыпуск,
			|	ПартииПолуфабрикатов.ДатаПотребности КАК Период,
			|	ИСТИНА КАК РасчетЗавершен,
			|	КоличествоПолуфабрикатов.Количество КАК ВсегоПотребность,
			|	ПартииПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена
			|ИЗ
			|	ПартииПолуфабрикатов КАК ПартииПолуфабрикатов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиСтоимостиПолуфабрикатов КАК ДолиСтоимостиПолуфабрикатов
			|		ПО ПартииПолуфабрикатов.ПартияПФ = ДолиСтоимостиПолуфабрикатов.ПартияПФ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПотребностиПолуфабрикатов КАК КоличествоПолуфабрикатов
			|		ПО ПартииПолуфабрикатов.Номенклатура = КоличествоПолуфабрикатов.Номенклатура
			|			И ПартииПолуфабрикатов.Характеристика = КоличествоПолуфабрикатов.Характеристика
			|			И (НАЧАЛОПЕРИОДА(ПартииПолуфабрикатов.ДатаПотребности, МЕСЯЦ) = КоличествоПолуфабрикатов.Период)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	2,
			|	ПФПроизводимыеВПроцессе.Продукция,
			|	ПФПроизводимыеВПроцессе.ХарактеристикаПродукции,
			|	ПФПроизводимыеВПроцессе.НазначениеПродукции,
			|	ПФПроизводимыеВПроцессе.ПартияСпецификацияПродукции,
			|	ПФПроизводимыеВПроцессе.Номенклатура,
			|	ПФПроизводимыеВПроцессе.Характеристика,
			|	ПФПроизводимыеВПроцессе.Спецификация,
			|	""МножителиПартийИПолуфабрикатов"",
			|	СУММА(ВЫБОР
			|		КОГДА ЕСТЬNULL(Доли.ДоляСтоимости, 0) = 0 ТОГДА
			|			Изделия.Количество
			|		ИНАЧЕ 
			|			Доли.ДоляСтоимости
			|	КОНЕЦ) КАК ДоляСтоимости,
			|	ДолиСтоимостиПолуфабрикатов.ВсегоДолей * Изделия.Количество,
			|	ПФПроизводимыеВПроцессе.Количество,
			|	9999999999999,
			|	ПФПроизводимыеВПроцессе.ДатаПотребности,
			|	ЛОЖЬ,
			|	ПФПроизводимыеВПроцессе.Количество,
			|	ЛОЖЬ
			|ИЗ
			|	ПФПроизводимыеВПроцессе КАК ПФПроизводимыеВПроцессе
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиСтоимостиПолуфабрикатов КАК ДолиСтоимостиПолуфабрикатов
			|			ПО ПФПроизводимыеВПроцессе.Спецификация = ДолиСтоимостиПолуфабрикатов.ПартияПФ
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК Реквизиты
			|			ПО ПФПроизводимыеВПроцессе.Спецификация = Реквизиты.Ссылка
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК Изделия
			|			ПО ПФПроизводимыеВПроцессе.Спецификация = Изделия.Ссылка
			|			И ПФПроизводимыеВПроцессе.Номенклатура = Изделия.Номенклатура
			|			И ПФПроизводимыеВПроцессе.Характеристика = Изделия.Характеристика
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимостиПоСпецификации КАК Доли
			|			ПО Доли.Спецификация = Реквизиты.Ссылка
			|			И Доли.Номенклатура = Изделия.Номенклатура
			|			И Доли.Характеристика = Изделия.Характеристика
			|
			|СГРУППИРОВАТЬ ПО
			|	ПФПроизводимыеВПроцессе.Продукция,
			|	ПФПроизводимыеВПроцессе.ХарактеристикаПродукции,
			|	ПФПроизводимыеВПроцессе.НазначениеПродукции,
			|	ПФПроизводимыеВПроцессе.ПартияСпецификацияПродукции,
			|	ПФПроизводимыеВПроцессе.Количество,
			|	ПФПроизводимыеВПроцессе.Характеристика,
			|	ПФПроизводимыеВПроцессе.Спецификация,
			|	ПФПроизводимыеВПроцессе.Номенклатура,
			|	ДолиСтоимостиПолуфабрикатов.ВсегоДолей * Изделия.Количество,
			|	ПФПроизводимыеВПроцессе.ДатаПотребности
			|
			|УПОРЯДОЧИТЬ ПО
			|	Порядок
			|ИТОГИ
			|	МАКСИМУМ(КоличествоВыпуск),
			|	МАКСИМУМ(ВсегоПотребность)
			|ПО
			|	Полуфабрикат,
			|	ПартияСпецификацияПолуфабриката
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РеквизитыЭтаповПартий.ПартияПФ 		КАК ПартияСпецификация,
			|	РеквизитыЭтаповПартий.Подразделение КАК Подразделение,
			|	Материалы.СтатьяКалькуляции			КАК СтатьяКалькуляции,
			|	Материалы.Номенклатура				КАК Номенклатура,
			|	Материалы.Характеристика			КАК Характеристика,
			|	ВЫБОР
			|		КОГДА РеквизитыЭтаповПартий.ОтгружатьОднойДатой
			|			ТОГДА РеквизитыЭтаповПартий.ДатаОтгрузки
			|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА РеквизитыЭтаповПартий.Дата
			|		ИНАЧЕ Материалы.ДатаОтгрузки
			|	КОНЕЦ 							КАК Период,
			|	НАЧАЛОПЕРИОДА(ВЫБОР
			|		КОГДА РеквизитыЭтаповПартий.ОтгружатьОднойДатой
			|			ТОГДА РеквизитыЭтаповПартий.ДатаОтгрузки
			|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА РеквизитыЭтаповПартий.Дата
			|		ИНАЧЕ
			|			Материалы.ДатаОтгрузки
			|	КОНЕЦ, МЕСЯЦ) 					КАК ПериодЗатрат,
			|	Материалы.Производится 			КАК ЭтоПолуфабрикат,
			|	0 								КАК Стоимость,
			|	СУММА(Материалы.Количество) 	КАК Количество,
			|	""ПлановыеМатериальныеЗатраты"" КАК Пояснение
			|ИЗ
			|	ВозможныеПартии КАК Партии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
			|		ПО Партии.ПартияПФ = РеквизитыЭтаповПартий.ПартияПФ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Материалы
			|		ПО РеквизитыЭтаповПартий.Этап = Материалы.Ссылка
			|
			|ГДЕ
			|	НЕ Партии.ПартияВыпущена
			|
			|СГРУППИРОВАТЬ ПО
			|	Материалы.Характеристика,
			|	Материалы.СтатьяКалькуляции,
			|	РеквизитыЭтаповПартий.ПартияПФ,
			|	Материалы.Номенклатура,
			|	РеквизитыЭтаповПартий.Подразделение,
			|	ВЫБОР
			|		КОГДА РеквизитыЭтаповПартий.ОтгружатьОднойДатой
			|			ТОГДА РеквизитыЭтаповПартий.ДатаОтгрузки
			|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА РеквизитыЭтаповПартий.Дата
			|		ИНАЧЕ Материалы.ДатаОтгрузки
			|	КОНЕЦ,
			|	Материалы.Производится,
			|	НАЧАЛОПЕРИОДА(ВЫБОР
			|		КОГДА РеквизитыЭтаповПартий.ОтгружатьОднойДатой
			|			ТОГДА РеквизитыЭтаповПартий.ДатаОтгрузки
			|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА РеквизитыЭтаповПартий.Дата
			|		ИНАЧЕ Материалы.ДатаОтгрузки
			|	КОНЕЦ, МЕСЯЦ)			
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Партии.ПартияПФ,
			|	РеквизитыЭтаповПартий.Подразделение,
			|	ПобочныеИзделия.СтатьяКалькуляции,
			|	ПобочныеИзделия.Номенклатура,
			|	ПобочныеИзделия.Характеристика,
			|	РеквизитыЭтаповПартий.Дата,
			|	НАЧАЛОПЕРИОДА(РеквизитыЭтаповПартий.Дата, МЕСЯЦ),
			|	ЛОЖЬ,
			|	СУММА(-1 * ПобочныеИзделия.Сумма),
			|	СУММА(-1 * ПобочныеИзделия.Количество),
			|	""ПобочныеИзделия""
			|ИЗ
			|	ВозможныеПартии КАК Партии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
			|		ПО Партии.ПартияПФ = РеквизитыЭтаповПартий.ПартияПФ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ПобочныеИзделия
			|		ПО РеквизитыЭтаповПартий.Этап = ПобочныеИзделия.Ссылка
			|ГДЕ
			|	НЕ Партии.ПартияВыпущена
			|	И НЕ ПобочныеИзделия.Отменено
			|
			|СГРУППИРОВАТЬ ПО
			|	ПобочныеИзделия.Характеристика,
			|	ПобочныеИзделия.СтатьяКалькуляции,
			|	Партии.ПартияПФ,
			|	ПобочныеИзделия.Номенклатура,
			|	РеквизитыЭтаповПартий.Подразделение,
			|	РеквизитыЭтаповПартий.Дата,
			|	НАЧАЛОПЕРИОДА(РеквизитыЭтаповПартий.Дата, МЕСЯЦ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА ТаблицаТрудозатрат.ДатаВыполнения <> ДАТАВРЕМЯ(1,1,1)
			|			ТОГДА ТаблицаТрудозатрат.ДатаВыполнения
			|		ИНАЧЕ РеквизитыЭтаповПартий.Дата
			|	КОНЕЦ 									КАК Период,
			|	Партии.ПартияПФ 						КАК ПартияСпецификация,
			|	РеквизитыЭтаповПартий.Подразделение 	КАК Подразделение,
			|	ТаблицаТрудозатрат.ВидРабот 			КАК ВидРабот,
			|	ТаблицаТрудозатрат.СтатьяКалькуляции	КАК СтатьяКалькуляции,
			
			|	СУММА(ТаблицаТрудозатрат.Количество) 	КАК Количество,
			
			|	НАЧАЛОПЕРИОДА(
			|		ВЫБОР
			|			КОГДА ТаблицаТрудозатрат.ДатаВыполнения <> ДАТАВРЕМЯ(1,1,1) 
			|				ТОГДА ТаблицаТрудозатрат.ДатаВыполнения 
			|			ИНАЧЕ РеквизитыЭтаповПартий.Дата 
			|		КОНЕЦ,
			|	МЕСЯЦ) 									КАК ПериодЗатрат,
			|	""Трудозатраты"" 						КАК Пояснение
			|ИЗ
			|	ВозможныеПартии КАК Партии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
			|		ПО Партии.ПартияПФ = РеквизитыЭтаповПартий.ПартияПФ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.Трудозатраты КАК ТаблицаТрудозатрат
			|		ПО РеквизитыЭтаповПартий.Этап = ТаблицаТрудозатрат.Ссылка
			|ГДЕ
			|	НЕ Партии.ПартияВыпущена
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТрудозатрат.СтатьяКалькуляции,
			|	Партии.ПартияПФ,
			|	РеквизитыЭтаповПартий.Подразделение,
			|	ТаблицаТрудозатрат.ВидРабот,
			|	ВЫБОР
			|		КОГДА ТаблицаТрудозатрат.ДатаВыполнения <> ДАТАВРЕМЯ(1,1,1)
			|			ТОГДА ТаблицаТрудозатрат.ДатаВыполнения
			|		ИНАЧЕ РеквизитыЭтаповПартий.Дата
			|	КОНЕЦ,
			|	НАЧАЛОПЕРИОДА(
			|		ВЫБОР
			|			КОГДА ТаблицаТрудозатрат.ДатаВыполнения <> ДАТАВРЕМЯ(1,1,1) 
			|				ТОГДА ТаблицаТрудозатрат.ДатаВыполнения 
			|			ИНАЧЕ РеквизитыЭтаповПартий.Дата 
			|		КОНЕЦ,
			|	МЕСЯЦ)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА РеквизитыЭтаповПартий.ОтгружатьОднойДатой
			|			ТОГДА РеквизитыЭтаповПартий.ДатаОтгрузки
			|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА РеквизитыЭтаповПартий.Дата
			|		ИНАЧЕ Материалы.ДатаОтгрузки
			|	КОНЕЦ 							КАК Период,
			|	ПартииПолуфабрикатов.Продукция,
			|	ПартииПолуфабрикатов.ХарактеристикаПродукции,
			|	ПартииПолуфабрикатов.НазначениеПродукции КАК Назначение,
			|	ПартииПолуфабрикатов.ПартияСпецификацияПродукции,
			|	ПартииПолуфабрикатов.ПартияПФ КАК ПартияПолуфабрикатаПродукции,
			|	Материалы.Номенклатура КАК Полуфабрикат,
			|	Материалы.Характеристика КАК ХарактеристикаПолуфабриката,
			|	Материалы.Склад,
			|	Материалы.Производится,
			|	СУММА(Материалы.Количество) КАК Числитель,
			|	1 КАК Знаменатель,
			|	СУММА(Материалы.Количество) КАК КоличествоПолуфабриката,
			|	ЛОЖЬ КАК РасчетЗавершен,
			|	ЛОЖЬ КАК ПартияВыпущена
			|ИЗ
			|	ПартииПолуфабрикатов КАК ПартииПолуфабрикатов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК РеквизитыЭтаповПартий
			|		ПО ПартииПолуфабрикатов.ПартияПФ = РеквизитыЭтаповПартий.ПартияПФ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Материалы
			|		ПО РеквизитыЭтаповПартий.Этап = Материалы.Ссылка
			|ГДЕ
			|	НЕ ПартииПолуфабрикатов.ПартияВыпущена
			|
			|СГРУППИРОВАТЬ ПО
			|	Материалы.Характеристика,
			|	ПартииПолуфабрикатов.ХарактеристикаПродукции,
			|	ПартииПолуфабрикатов.ПартияСпецификацияПродукции,
			|	ПартииПолуфабрикатов.ПартияПФ,
			|	Материалы.Номенклатура,
			|	Материалы.Склад,
			|	Материалы.Производится,
			|	ВЫБОР
			|		КОГДА РеквизитыЭтаповПартий.ОтгружатьОднойДатой
			|			ТОГДА РеквизитыЭтаповПартий.ДатаОтгрузки
			|		КОГДА Материалы.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА РеквизитыЭтаповПартий.Дата
			|		ИНАЧЕ Материалы.ДатаОтгрузки
			|	КОНЕЦ,
			|	ПартииПолуфабрикатов.Продукция,
			|	ПартииПолуфабрикатов.НазначениеПродукции
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ФактическиеДанные.Партия КАК ПартияПродукции,
			|	ФактическиеДанные.АналитикаУчетаНоменклатуры,
			|	ФактическиеДанные.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ФактическиеДанные.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ФактическиеДанные.Период,
			|	ФактическиеДанные.ПартияМатериала КАК Партия,
			|	ИСТИНА КАК ЭтоПолуфабрикат,
			|	СУММА(ФактическиеДанные.Количество) КАК Количество
			|ИЗ
			|	ФактическиеДанные КАК ФактическиеДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ФактическиеДанные.АналитикаУчетаНоменклатуры.Номенклатура,
			|	ФактическиеДанные.АналитикаУчетаНоменклатуры.Характеристика,
			|	ФактическиеДанные.Партия,
			|	ФактическиеДанные.АналитикаУчетаНоменклатуры,
			|	ФактическиеДанные.ПартияМатериала,
			|	ФактическиеДанные.Период
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПФПроизводимыеВПроцессе
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПартииПолуфабрикатов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ДолиСтоимостиПолуфабрикатов
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПредварительныеПФПроизводимыеВПроцессе.Продукция,
			|	ПредварительныеПФПроизводимыеВПроцессе.ХарактеристикаПродукции,
			|	ПредварительныеПФПроизводимыеВПроцессе.НазначениеПродукции,
			|	ПредварительныеПФПроизводимыеВПроцессе.ПартияСпецификацияПродукции,
			|	ПредварительныеПФПроизводимыеВПроцессе.Номенклатура,
			|	ПредварительныеПФПроизводимыеВПроцессе.Характеристика,
			|	ПредварительныеПФПроизводимыеВПроцессе.Количество,
			|	ПредварительныеПФПроизводимыеВПроцессе.ДатаПотребности,
			|	ПредварительныеПФПроизводимыеВПроцессе.Спецификация,
			|	ПредварительныеПФПроизводимыеВПроцессе.Назначение
			|ПОМЕСТИТЬ ПФПроизводимыеВПроцессе
			|ИЗ
			|	ПредварительныеПФПроизводимыеВПроцессе КАК ПредварительныеПФПроизводимыеВПроцессе
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ДолиСтоимостиПоСпецификации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПредварительныеПФПроизводимыеВПроцессе
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВозможныеПартии
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ РеквизитыЭтаповПартий
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ФактическиеДанные
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПотребностиПолуфабрикатов";
		
		ПараметрыПодстановки = ПроизводствоСервер.ПараметрыПодстановкиАлгоритмаРасчетаДолейСтоимости("Изделия", "Реквизиты.СпособРаспределенияЗатратНаВыходныеИзделия");
		ПараметрыПодстановки.ДоляСтоимости = "&ДоляСтоимости";
		ПараметрыПодстановки.СоединениеДоляСтоимости = "И &СоединениеДоляСтоимости";
		
		ПроизводствоСервер.ВыполнитьПодстановкуАлгоритмаРасчетаДолейСтоимости(Запрос.Текст, ПараметрыПодстановки);
		
		Запрос.УстановитьПараметр("ВидЦены", Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ());
		Запрос.УстановитьПараметр("ДатаКалькуляции", НачалоМесяца(Калькуляция.Дата));
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ВыборкаМножителей = МассивРезультатов[8].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВыборкаМатериалов = МассивРезультатов[9].Выбрать();
		ВыборкаТрудозатрат = МассивРезультатов[10].Выбрать();
		ВыборкаМатериаловРазузловка = МассивРезультатов[11].Выбрать();
		
		Пока ВыборкаМножителей.Следующий() Цикл
			
			ТребуемоеКоличество = ВыборкаМножителей.ВсегоПотребность;
			
			ВыборкаПоПартиям = ВыборкаМножителей.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоПартиям.Следующий() И ТребуемоеКоличество > 0 Цикл
				
				КоличествоПартии = ВыборкаПоПартиям.КоличествоВыпуск;
				
				ДетальныеЗаписи = ВыборкаПоПартиям.Выбрать();
				Пока ДетальныеЗаписи.Следующий() И ТребуемоеКоличество > 0 Цикл
					
					КоличествоКСписанию = Мин(КоличествоПартии, Мин(ТребуемоеКоличество, ДетальныеЗаписи.КоличествоПолуфабриката));
					
					Если КоличествоПартии = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					ОтборПозицииПФ = Новый Структура("ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, Полуфабрикат, ХарактеристикаПолуфабриката");
					ЗаполнитьЗначенияСвойств(ОтборПозицииПФ, ДетальныеЗаписи);
					
					СтрокиМножителей = Множители.НайтиСтроки(ОтборПозицииПФ);
					
					Если СтрокиМножителей.Количество() = 1 И СтрокиМножителей[0].РасчетЗавершен = Ложь Тогда
						НовыйМножитель = СтрокиМножителей[0];
					Иначе
						НовыйМножитель = Множители.Добавить();
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(НовыйМножитель, ДетальныеЗаписи);
					НовыйМножитель.Порядок = ПроверкаЦикла;
					НовыйМножитель.Числитель = ДетальныеЗаписи.Числитель * КоличествоКСписанию;
					НовыйМножитель.КоличествоПолуфабриката = КоличествоКСписанию;
					
					ПривестиЧислительИЗнаменатель(НовыйМножитель);
					
					КоличествоПартии = КоличествоПартии - КоличествоКСписанию;
					ТребуемоеКоличество = ТребуемоеКоличество - КоличествоКСписанию;
					
					Если НовыйМножитель.ПартияВыпущена Тогда
						
						СтрокаДляРазузлования = КРазузлованию.Добавить();
						СтрокаДляРазузлования.Числитель = НовыйМножитель.Числитель;
						СтрокаДляРазузлования.Знаменатель = НовыйМножитель.Знаменатель;
						СтрокаДляРазузлования.Партия = НовыйМножитель.ПартияСпецификацияПолуфабриката;
						СтрокаДляРазузлования.Номенклатура = НовыйМножитель.Полуфабрикат;
						СтрокаДляРазузлования.Характеристика = НовыйМножитель.ХарактеристикаПолуфабриката;
						СтрокаДляРазузлования.Назначение = Неопределено;
						СтрокаДляРазузлования.АналитикаУчетаНоменклатуры = Справочники.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка();
						СтрокаДляРазузлования.АналитикаУчетаПартий = Неопределено;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ПараметрыРазузлования = Новый Структура;
		ПараметрыРазузлования.Вставить("МножителиПолуфабрикатов", МассивРезультатов[12].Выгрузить());
		ПараметрыРазузлования.Вставить("КРазузлованию", КРазузлованию);
		ПараметрыРазузлования.Вставить("Множители", Множители);
		
		РассчитатьМножителиПоФактическимДанным(ПараметрыРазузлования, Калькуляция.Дата);
		
		КРазузлованию.Очистить();
		
		Если МассивРезультатов[8].Пустой() Тогда
			Прервать; // Конец цепочки.
		КонецЕсли;
		
		Пока ВыборкаМатериалов.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Движения.ПлановыеМатериальныеЗатраты.Добавить(), ВыборкаМатериалов);
		КонецЦикла;
		
		Пока ВыборкаТрудозатрат.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Движения.ПлановыеТрудозатраты.Добавить(), ВыборкаТрудозатрат);
		КонецЦикла;

		Пока ВыборкаМатериаловРазузловка.Следующий() Цикл
			
			// Подкладываем к множителям ПФ производимые в процессе.
			Если ВыборкаМатериаловРазузловка.Производится Тогда
				
				НовыйМножитель = Множители.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйМножитель, ВыборкаМатериаловРазузловка);
				НовыйМножитель.Порядок = ПроверкаЦикла + 1;
				
			КонецЕсли;
			
			Если ВыборкаМатериаловРазузловка.Производится
				Или Не ЗначениеЗаполнено(ВыборкаМатериаловРазузловка.Склад) Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйМножитель = МножителиМатериалов.Добавить();

			ЗаполнитьЗначенияСвойств(НовыйМножитель, ВыборкаМатериаловРазузловка);
			НовыйМножитель.Порядок = ПроверкаЦикла + 1;
			НовыйМножитель.Числитель = 1;
			
			НовыйМатериал = Материалы.Добавить();
			НовыйМатериал.Номенклатура = ВыборкаМатериаловРазузловка.Полуфабрикат;
			НовыйМатериал.Характеристика = ВыборкаМатериаловРазузловка.ХарактеристикаПолуфабриката;
			НовыйМатериал.Склад = ВыборкаМатериаловРазузловка.Склад;
			НовыйМатериал.ПроизводствоНаСтороне = Ложь;
			
		КонецЦикла;
		
		ПроверкаЦикла = ПроверкаЦикла + 1;
		Если ПроверкаЦикла > 10000 Тогда
			Прервать; // Зациклилось.
		КонецЕсли;
		
	КонецЦикла;

	ЗаполнитьМножителиПоМатериалам(Материалы, Калькуляция, МножителиМатериалов, Множители, Движения);

	Множители.ЗаполнитьЗначения(Калькуляция, "Калькуляция");
	Множители.Свернуть("Калькуляция, ПартияСпецификацияПродукции, Продукция, Период, ХарактеристикаПродукции, Назначение, 
		|ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, Порядок, РасчетЗавершен, ПартияВыпущена, Знаменатель", 
		"Числитель, КоличествоПолуфабриката, Стоимость, СтоимостьБезНДС, СтоимостьРегл, Трудозатраты, ТрудозатратыРегл, ПостатейныеПостоянные, ПостатейныеПостоянныеБезНДС, 
		|ПостатейныеПостоянныеРегл, ПостатейныеПеременные, ПостатейныеПеременныеБезНДС, ПостатейныеПеременныеРегл, СтоимостьЗабалансовая, СтоимостьЗабалансоваяРегл");
	
	Множители.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	Множители.ЗаполнитьЗначения(Калькуляция.ПодразделениеДиспетчер, "Подразделение");
	
КонецПроцедуры

Процедура ОбработатьОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц, ПараметрыРасчета)
	
	Движения = ИнициализироватьТаблицыДвижений();
	
	РазузловатьПродукцию(Калькуляция, МенеджерВременныхТаблиц, Движения, ПараметрыРасчета);
	
	ЗаписатьДвижения(Движения, Калькуляция, МенеджерВременныхТаблиц, ПараметрыРасчета);
		
КонецПроцедуры

Функция ИнициализироватьТаблицыДвижений()
	
	Регистры = Новый Массив;
	Регистры.Добавить("ПлановыеМатериальныеЗатраты");
	Регистры.Добавить("ПлановыеТрудозатраты");
	Регистры.Добавить("ПлановыеПрочиеЗатраты");
	Регистры.Добавить("МножителиПартийИПолуфабрикатов");
	
	Результат = Новый Структура;
	
	Для каждого Регистр Из Регистры Цикл
		
		Если Регистр = "МножителиПартийИПолуфабрикатов" Тогда
			Набор = РегистрыСведений[Регистр].СоздатьНаборЗаписей();
		Иначе	
			Набор = РегистрыНакопления[Регистр].СоздатьНаборЗаписей();
		КонецЕсли;
		
		Таблица = Набор.ВыгрузитьКолонки();
		Результат.Вставить(Регистр, Таблица);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Проведение

Процедура ПровестиДокумент(Калькуляция, ПараметрыРасчета)
	
	Если БылаОтменаПроведения(Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		ЗаблокироватьДанные(Калькуляция);
		
		Очередь = ОчередьПроведения(Калькуляция);
		
		Если Очередь.ЕстьЗаписиВОчереди Тогда
			
			ДокументОбъект = Калькуляция.ПолучитьОбъект();
			
			ОчиститьДвижения(ДокументОбъект, ПараметрыРасчета);
			
			СформироватьДвижения(ДокументОбъект);
			
			РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Очередь.Записи, Калькуляция);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписатьСостояниеКалькуляции(Калькуляция, "ОшибкаРасчета");
		ОчиститьОчередьПоКалькуляции(Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(Калькуляция, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
КонецПроцедуры

Функция ОчередьПроведения(Калькуляция)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОчередьРасчетаПлановыхКалькуляций.Калькуляция КАК Калькуляция,
		|	ОчередьРасчетаПлановыхКалькуляций.Разделитель КАК Разделитель
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК ОчередьРасчетаПлановыхКалькуляций
		|ГДЕ
		|	ОчередьРасчетаПлановыхКалькуляций.Калькуляция = &Калькуляция";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Структура("ЕстьЗаписиВОчереди, Записи");
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Результат.ЕстьЗаписиВОчереди = Истина;
		Результат.Записи = РезультатЗапроса.Выгрузить();
		
	Иначе
		Результат.ЕстьЗаписиВОчереди = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ОтменитьПроведениеДокумента(Калькуляция, ПараметрыРасчета)
	
	ДокументОбъект = Калькуляция.ПолучитьОбъект();
	ОчиститьДвижения(ДокументОбъект, ПараметрыРасчета);
	ОчиститьОчередьПоКалькуляции(Калькуляция);
	ЗаписатьСостояниеКалькуляции(Калькуляция, "НеРассчитана");
	
КонецПроцедуры

#КонецОбласти

#Область Разузлование

Процедура РазузловатьПродукцию(Калькуляция, МенеджерВременныхТаблиц, Движения, ПараметрыРасчета)
	
	Запрос = Новый Запрос(ТекстЗапросаОчередьРазузлованияПродукции());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	УстановитьПривилегированныйРежим(Истина);
	Множители = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТребуетсяРазузловатьЗаказ(МенеджерВременныхТаблиц) Тогда
		
		// Заказы на производство уже разузлованы.
		// К разузловке могут быть только честные полуфабрикаты, которые указаны в качестве материалов.
		ОбработатьПродукциюПоЗаказу(Калькуляция, МенеджерВременныхТаблиц, Множители, Движения, ПараметрыРасчета);
		
	КонецЕсли;
		
	Если Множители.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	РазузловатьСпецификациюИзделий(Калькуляция, Множители, Движения, ПараметрыРасчета);

КонецПроцедуры

Процедура ЗаполнитьМножителиПоМатериалам(Материалы, Калькуляция, МножителиМатериалов, Множители, Движения)
	
	Если Материалы.Количество() > 0 Тогда
		
		Движения.ПлановыеМатериальныеЗатраты.Индексы.Добавить("Номенклатура, Характеристика, ПартияСпецификация, ЭтоПолуфабрикат");
		ОтборПолуфабрикатов = Новый Структура("Номенклатура, Характеристика, ПартияСпецификация, ЭтоПолуфабрикат");		
		
		Материалы.Свернуть("Номенклатура, Характеристика, Склад, ПроизводствоНаСтороне");
		Материалы.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
		Материалы.ЗаполнитьЗначения(Калькуляция.ПодразделениеДиспетчер, "Подразделение");
		
		ДанныеЗаказа = Новый Структура("ПодразделениеДиспетчер", Материалы[0].Подразделение);
		СпособыПолученияМатериалов = ОбеспечениеПроизводства.СпособыПолученияМатериаловПоУмолчанию(
			ДанныеЗаказа,Материалы,,Перечисления.ВариантыЗаполненияОбеспеченияПроизводства.ПоСхемамОбеспечения);
		СпособыПолученияМатериалов.Колонки.Добавить("НачалоПроизводства", Новый ОписаниеТипов("Дата"));
		СпособыПолученияМатериалов.ЗаполнитьЗначения(ТекущаяДатаСеанса(), "НачалоПроизводства");
		СпособыПолученияМатериалов.Индексы.Добавить("Запланировать");
		
		ОтборМножителей = Новый Структура("Полуфабрикат, ХарактеристикаПолуфабриката, Склад");
		МножителиМатериалов.Индексы.Добавить("Полуфабрикат, ХарактеристикаПолуфабриката, Склад");
		
		ВсеСпецификацииПФ = ПолучитьОсновныеСпецификации(
			СпособыПолученияМатериалов.Скопировать(Новый Структура("Запланировать", Истина)));
		ОтборСпецификаций = Новый Структура("Номенклатура, Характеристика, Подразделение");
		ВсеСпецификацииПФ.Индексы.Добавить("Номенклатура, Характеристика, Подразделение");
		
		Для Каждого СпособПолученияМатериала Из СпособыПолученияМатериалов Цикл
			
			ОтборМножителей.Полуфабрикат = СпособПолученияМатериала.Номенклатура;
			ОтборМножителей.ХарактеристикаПолуфабриката = СпособПолученияМатериала.Характеристика;
			ОтборМножителей.Склад = СпособПолученияМатериала.Склад;
			НаборМножителей = МножителиМатериалов.НайтиСтроки(ОтборМножителей);
			
			ЗаполнитьЗначенияСвойств(ОтборСпецификаций, СпособПолученияМатериала);
			СпецификацииПФ = ВсеСпецификацииПФ.НайтиСтроки(ОтборСпецификаций);
			
			Если СпецификацииПФ.Количество() = 0 Тогда
				
				ИндексЭлемента = НаборМножителей.Количество() - 1;
				Пока ИндексЭлемента >= 0 Цикл
					
					МножителиМатериалов.Удалить(НаборМножителей[ИндексЭлемента]);
					ИндексЭлемента = ИндексЭлемента - 1;
					
				КонецЦикла;
				
			Иначе
				
				Для Каждого МножительМатериала Из НаборМножителей Цикл
					
					НовыйМножитель = Множители.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйМножитель, МножительМатериала);
					НовыйМножитель.ПартияСпецификацияПолуфабриката = СпецификацииПФ[0].Спецификация;
					ПривестиЧислительИЗнаменатель(НовыйМножитель);
					
					ЗаполнитьЗначенияСвойств(ОтборПолуфабрикатов, МножительМатериала);
					ОтборПолуфабрикатов.Номенклатура = МножительМатериала.Полуфабрикат;
					ОтборПолуфабрикатов.Характеристика = МножительМатериала.ХарактеристикаПолуфабриката;
					Если ЗначениеЗаполнено(МножительМатериала.ПартияПолуфабрикатаПродукции) Тогда
						ОтборПолуфабрикатов.ПартияСпецификация = МножительМатериала.ПартияПолуфабрикатаПродукции;
					Иначе
						ОтборПолуфабрикатов.ПартияСпецификация = МножительМатериала.ПартияСпецификацияПродукции;
					КонецЕсли;
					
					ОтборПолуфабрикатов.ЭтоПолуфабрикат = Ложь;
					
					МатериалыПолуфабрикаты = Движения.ПлановыеМатериальныеЗатраты.НайтиСтроки(ОтборПолуфабрикатов);
					Для Каждого Материал Из МатериалыПолуфабрикаты Цикл
						Материал.ЭтоПолуфабрикат = Истина;
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры

Функция ТекстЗапросаОчередьРазузлованияПродукции()
	
	Возврат
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОчередьРасчета.Калькуляция КАК Калькуляция,
		|	ОчередьРасчета.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ОчередьРасчета.Продукция КАК Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение КАК Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	ОчередьРасчета.Полуфабрикат КАК Полуфабрикат,
		|	ОчередьРасчета.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ОчередьРасчета.Разделитель КАК Разделитель,
		|	ОчередьРасчета.ДатаПотребности КАК Период,
		|	ОчередьРасчета.Порядок КАК Порядок,
		|	ЛОЖЬ КАК ПартияВыпущена,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.РасчетЗавершен, ЛОЖЬ) КАК РасчетЗавершен,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката, 0) КАК КоличествоПолуфабриката,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Калькуляция.ПодразделениеДиспетчер, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Стоимость, 0) КАК Стоимость,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьБезНДС, 0) КАК СтоимостьБезНДС,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьРегл, 0) КАК СтоимостьРегл,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Трудозатраты, 0) КАК Трудозатраты,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ТрудозатратыРегл, 0) КАК ТрудозатратыРегл,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные, 0) КАК ПостатейныеПостоянные,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС, 0) КАК ПостатейныеПостоянныеБезНДС,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл, 0) КАК ПостатейныеПостоянныеРегл,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПеременные, 0) КАК ПостатейныеПеременные,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС, 0) КАК ПостатейныеПеременныеБезНДС,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл, 0) КАК ПостатейныеПеременныеРегл,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Числитель, 0) КАК Числитель,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Знаменатель, 0) КАК Знаменатель,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая, 0) КАК СтоимостьЗабалансовая,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл, 0) КАК СтоимостьЗабалансоваяРегл,
		|	ВЫБОР
		|		КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ПО ОчередьРасчета.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
		|			И ОчередьРасчета.ПартияСпецификацияПродукции = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции
		|			И ОчередьРасчета.Продукция = МножителиПартийИПолуфабрикатов.Продукция
		|			И ОчередьРасчета.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			И ОчередьРасчета.Назначение = МножителиПартийИПолуфабрикатов.Назначение
		|			И ОчередьРасчета.ПартияСпецификацияПолуфабриката = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката
		|			И ОчередьРасчета.Полуфабрикат = МножителиПартийИПолуфабрикатов.Полуфабрикат
		|			И ОчередьРасчета.ХарактеристикаПолуфабриката = МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
		|			И (ОчередьРасчета.ДатаПотребности = НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, ДЕНЬ))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
КонецФункции

Процедура РазузловатьСпецификациюИзделий(Калькуляция, Множители, Движения, ПараметрыРасчета)
	
	ПотребностиКРазузлованию = ПотребностиДляПолученияДанныхСпецификаций(Множители, ПараметрыРасчета);
	
	Если ПотребностиКРазузлованию.Количество() = 0 Тогда
		
		Движения.МножителиПартийИПолуфабрикатов = Множители;
		Возврат;
		
	КонецЕсли;
	
	// Разузлование спецификаций.
	ПараметрыВыборкиДанных = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных();
	ПараметрыВыборкиДанных.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
	
	Потребности = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(
		ПотребностиКРазузлованию,
		ПараметрыВыборкиДанных);
	
	// Заполнение дат производства в зависимости от длительности этапов.
	// Одновременное выполнение этапов не поддерживается.
	ПериодКалькуляции = Калькуляция.Дата;
	ЗаполнитьДатыПроизводства(Потребности, ПериодКалькуляции);

	РазузлованныеОбъекты = Новый Массив;
	КоличествоПолуфабрикатов = Потребности.Количество();
	Индекс = 0;
	
	Множители.Индексы.Добавить("ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, РасчетЗавершен");
	Множители.Индексы.Добавить("Полуфабрикат, ХарактеристикаПолуфабриката, ПартияСпецификацияПолуфабриката, РасчетЗавершен");
	Множители.Индексы.Добавить("ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, РасчетЗавершен");
	
	// Обработка результата разузлования.
	// Расчет множителей полуфабрикатов.
	Пока Индекс < КоличествоПолуфабрикатов Цикл
		
		ДанныеСпецификации = Потребности[Индекс];
		
		ДолиСтоимости = ДанныеСпецификации.ВыходныеИзделия.Итог("ДоляСтоимости");
		
		Если ДолиСтоимости = 0 Тогда
			ДолиСтоимости = 1;
		КонецЕсли;
		
		Для Каждого ВыходноеИзделие Из ДанныеСпецификации.ВыходныеИзделия Цикл
				
			ДоляСтоимости = ?(ВыходноеИзделие.ДоляСтоимости = 0, 1, ВыходноеИзделие.ДоляСтоимости);
			
			ОтборПродукции = Новый Структура;
			ОтборПродукции.Вставить("ПартияСпецификацияПродукции", 	ДанныеСпецификации.Спецификация);
			ОтборПродукции.Вставить("Продукция", 					ВыходноеИзделие.Номенклатура);
			ОтборПродукции.Вставить("ХарактеристикаПродукции", 		ВыходноеИзделие.Характеристика);
			ОтборПродукции.Вставить("РасчетЗавершен", 				Ложь);
				
			СтрокиМножителей = Множители.НайтиСтроки(ОтборПродукции);
			
			Если СтрокиМножителей.Количество() = 0 Тогда
				
				// Выходное изделие является полуфабрикатом.
				ОтборПолуфабриката = Новый Структура;
				ОтборПолуфабриката.Вставить("Полуфабрикат", 					ВыходноеИзделие.Номенклатура);
				ОтборПолуфабриката.Вставить("ХарактеристикаПолуфабриката", 		ВыходноеИзделие.Характеристика);
				ОтборПолуфабриката.Вставить("ПартияСпецификацияПолуфабриката", 	ДанныеСпецификации.Спецификация);
				ОтборПолуфабриката.Вставить("РасчетЗавершен", 					Ложь);
				
				СтрокиМножителей = Множители.НайтиСтроки(ОтборПолуфабриката);
				
				Для Каждого СтрокаСПФ Из СтрокиМножителей Цикл
					
					СтрокаСПФ.Числитель = СтрокаСПФ.Числитель * ДоляСтоимости;
					
					Если СтрокаСПФ.Знаменатель = Неопределено Тогда
						СтрокаСПФ.Знаменатель = 1;
					КонецЕсли;
					
					СтрокаСПФ.Знаменатель = СтрокаСПФ.Знаменатель * ДолиСтоимости * ВыходноеИзделие.Количество;
					ПривестиЧислительИЗнаменатель(СтрокаСПФ);
					
				КонецЦикла;
				
			Иначе
				
				Для Каждого СтрокаПродукции Из СтрокиМножителей Цикл
					СтрокаПродукции.Период = ДанныеСпецификации.Окончание;
				КонецЦикла;
				
			КонецЕсли;
			
			Для Каждого Множитель Из СтрокиМножителей Цикл
				
				Если Множитель.Знаменатель = Неопределено Тогда
					
					// Спецификация в этапах была не указана.
					Множитель.Числитель = Множитель.КоличествоПолуфабриката * ДоляСтоимости;
					Множитель.Знаменатель = ДолиСтоимости;
					
					ПривестиЧислительИЗнаменатель(Множитель);
					
				КонецЕсли;
					
				Множитель.РасчетЗавершен = Истина;
				
				Для Каждого СтрокаМатериала Из ДанныеСпецификации.МатериалыИУслуги Цикл
					
					Если Не (СтрокаМатериала.Запланировать 
						Или СтрокаМатериала.СпособПолученияПолуфабриката = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации) Тогда
						Продолжить;
					КонецЕсли;
					
					НовыйМножитель = Множители.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйМножитель, Множитель, "ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, Калькуляция");
					
					Если СтрокаМатериала.СпособПолученияПолуфабриката = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации Тогда
						НовыйМножитель.ПартияСпецификацияПолуфабриката = СтрокаМатериала.Спецификация;
					Иначе
						НовыйМножитель.ПартияСпецификацияПолуфабриката = Неопределено;
					КонецЕсли;
					
					НовыйМножитель.Период = ДанныеСпецификации.Начало;
					НовыйМножитель.Полуфабрикат = СтрокаМатериала.Номенклатура;
					НовыйМножитель.ХарактеристикаПолуфабриката = СтрокаМатериала.Характеристика;
					НовыйМножитель.Порядок = Множитель.Порядок + 1;
					
					НовыйМножитель.Числитель = Множитель.Числитель * СтрокаМатериала.Количество;
					НовыйМножитель.Знаменатель = Множитель.Знаменатель;
					
					ПривестиЧислительИЗнаменатель(НовыйМножитель);
					
					НовыйМножитель.Стоимость = 0;
					НовыйМножитель.СтоимостьБезНДС = 0;
					НовыйМножитель.СтоимостьРегл = 0;
					НовыйМножитель.Трудозатраты = 0;
					НовыйМножитель.ТрудозатратыРегл = 0;
					НовыйМножитель.ПостатейныеПостоянные = 0;
					НовыйМножитель.ПостатейныеПостоянныеБезНДС = 0;
					НовыйМножитель.ПостатейныеПостоянныеРегл = 0;
					НовыйМножитель.ПостатейныеПеременные = 0;
					НовыйМножитель.ПостатейныеПеременныеБезНДС = 0;
					НовыйМножитель.ПостатейныеПеременныеРегл = 0;
					
					НовыйМножитель.КоличествоПолуфабриката = НовыйМножитель.Числитель / НовыйМножитель.Знаменатель;
					НовыйМножитель.РасчетЗавершен = Ложь;
					НовыйМножитель.ПартияВыпущена = Ложь;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
					
		Если Не РазузлованныеОбъекты.Найти(ДанныеСпецификации.Спецификация) = Неопределено Тогда
			
			Индекс = Индекс + 1;
			Продолжить;
			
		КонецЕсли;
		
		ПараметрыРасчетаПериода = ПараметрыРасчетаПериода();
		ПараметрыРасчетаПериода.Период = НачалоДня(ДанныеСпецификации.Начало);
		ПараметрыРасчетаПериода.Этапы = ДанныеСпецификации.Этапы;
		ПараметрыРасчетаПериода.ДлительностиЭтапов = ДанныеСпецификации.Этапы.ВыгрузитьКолонку("ДлительностьЭтапа");
		
		ЭтапыНаСтороне = ДанныеСпецификации.Этапы.НайтиСтроки(Новый Структура("ПроизводствоНаСтороне", Истина));
		Для Каждого ЭтапНаСтороне Из ЭтапыНаСтороне Цикл
			
			НоваяУслуга = Движения.ПлановыеМатериальныеЗатраты.Добавить();
			НоваяУслуга.Период 				= ПериодКалькуляции;
			НоваяУслуга.ПартияСпецификация 	= ДанныеСпецификации.Спецификация;
			НоваяУслуга.Подразделение 		= ЭтапНаСтороне.Подразделение;
			НоваяУслуга.СтатьяКалькуляции 	= ЭтапНаСтороне.СтатьяКалькуляции;
			НоваяУслуга.Номенклатура 		= ЭтапНаСтороне.УслугаПереработчика;
			НоваяУслуга.Характеристика 		= ЭтапНаСтороне.ХарактеристикаУслугиПереработчика;
			НоваяУслуга.Количество 			= ЭтапНаСтороне.Количество;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = ЭтапНаСтороне.Этап;
			НоваяУслуга.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
		КонецЦикла;
		
		Для Каждого СтрокаМатериала Из ДанныеСпецификации.МатериалыИУслуги Цикл
			
			НовыйМатериал = Движения.ПлановыеМатериальныеЗатраты.Добавить();
			НовыйМатериал.Период = ПериодКалькуляции;
			НовыйМатериал.ПартияСпецификация = ДанныеСпецификации.Спецификация;
			НовыйМатериал.Подразделение = СтрокаМатериала.ПодразделениеЭтапа;
			НовыйМатериал.СтатьяКалькуляции = СтрокаМатериала.СтатьяКалькуляции;
			НовыйМатериал.Номенклатура = СтрокаМатериала.Номенклатура;
			НовыйМатериал.Характеристика = СтрокаМатериала.Характеристика;
			НовыйМатериал.Количество = СтрокаМатериала.Количество;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = СтрокаМатериала.Этап;
			НовыйМатериал.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
			Если СтрокаМатериала.Запланировать 
				Или СтрокаМатериала.СпособПолученияПолуфабриката = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации Тогда
				НовыйМатериал.ЭтоПолуфабрикат = Истина;
			КонецЕсли;
			
			Если Не СтрокаМатериала.СпособПолученияПолуфабриката = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиНаЭтапе Тогда
				Продолжить;
			КонецЕсли;
			
			НовыйМатериал.Стоимость			= -1;
			НовыйМатериал.СтоимостьБезНДС	= -1;
			НовыйМатериал.СтоимостьРегл		= -1;
			
		КонецЦикла;
		
		Для Каждого СтрокаТрудозатрат Из ДанныеСпецификации.Трудозатраты Цикл
			
			НоваяТрудозатрата = Движения.ПлановыеТрудозатраты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяТрудозатрата, СтрокаТрудозатрат);
			НоваяТрудозатрата.Период = ПериодКалькуляции;
			НоваяТрудозатрата.ПартияСпецификация = ДанныеСпецификации.Спецификация;			
			НоваяТрудозатрата.Количество = СтрокаТрудозатрат.Количество;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = СтрокаТрудозатрат.Этап;
			НоваяТрудозатрата.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
		КонецЦикла;
		
		Для Каждого СтрокаМатериала Из ДанныеСпецификации.ВозвратныеОтходы Цикл
			
			НоваяСтрока = Движения.ПлановыеМатериальныеЗатраты.Добавить();
			НоваяСтрока.Период = ПериодКалькуляции;
			НоваяСтрока.ПартияСпецификация = ДанныеСпецификации.Спецификация;
			НоваяСтрока.Подразделение = СтрокаМатериала.ПодразделениеЭтапа;
			НоваяСтрока.СтатьяКалькуляции = СтрокаМатериала.СтатьяКалькуляции;
			НоваяСтрока.Номенклатура = СтрокаМатериала.Номенклатура;
			НоваяСтрока.Характеристика = СтрокаМатериала.Характеристика;
			НоваяСтрока.Количество = -СтрокаМатериала.Количество;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = СтрокаМатериала.Этап;
			НоваяСтрока.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
		КонецЦикла;		
		
		РазузлованныеОбъекты.Добавить(ДанныеСпецификации.Спецификация);
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
	Множители.Индексы.Очистить();
	ПривестиМножителиКОбщемуЗнаменателю(Множители);
	
	Движения.МножителиПартийИПолуфабрикатов = Множители;
		
КонецПроцедуры

Функция ПотребностиДляПолученияДанныхСпецификаций(Изделия, ПараметрыРасчета)
	
	Результат = Новый Массив;
	
	Полуфабрикаты = Изделия.Скопировать(
		Новый Структура("ПартияСпецификацияПолуфабриката", Неопределено), 
		"Полуфабрикат, ХарактеристикаПолуфабриката, Подразделение");
	Полуфабрикаты.Колонки.Полуфабрикат.Имя = "Номенклатура";
	Полуфабрикаты.Колонки.ХарактеристикаПолуфабриката.Имя = "Характеристика";
	Полуфабрикаты.Колонки.Добавить("НачалоПроизводства", Новый ОписаниеТипов("Дата"));
	Полуфабрикаты.ЗаполнитьЗначения(ПараметрыРасчета.Калькуляция.Дата, "НачалоПроизводства");
	
	Индекс = Полуфабрикаты.Количество() - 1;
	Пока Индекс > 0 Цикл
		
		Если Не ЗначениеЗаполнено(Полуфабрикаты[Индекс].Номенклатура) Тогда
			Полуфабрикаты.Удалить(Индекс);
		КонецЕсли;
		
		Индекс = Индекс - 1;
		
	КонецЦикла;
	
	СпецификацииИзделий = ПолучитьОсновныеСпецификации(Полуфабрикаты);
	СпецификацииИзделий.Индексы.Добавить("Номенклатура, Характеристика, Подразделение");
	ОтборСпецификаций = Новый Структура("Номенклатура, Характеристика, Подразделение");
	
	Изделия.Индексы.Добавить("Продукция, ХарактеристикаПродукции");
	
	Для Каждого Изделие Из Изделия Цикл
		
		Если Изделие.РасчетЗавершен Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяПотребность = ОписаниеДанныхКРазузлованию();
		
		Если ЗначениеЗаполнено(Изделие.Полуфабрикат) Тогда
			
			НоваяПотребность.Номенклатура = Изделие.Полуфабрикат;
			НоваяПотребность.Характеристика = Изделие.ХарактеристикаПолуфабриката;
			НоваяПотребность.Спецификация = Изделие.ПартияСпецификацияПолуфабриката;
			НоваяПотребность.ДатаВыпуска = Изделие.Период;
			НоваяПотребность.РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию;
			
			Если Не ЗначениеЗаполнено(НоваяПотребность.Спецификация) Тогда
				
				ПФВИсходныхДанных = Изделия.НайтиСтроки(Новый Структура("Продукция, ХарактеристикаПродукции", Изделие.Полуфабрикат, Изделие.ХарактеристикаПолуфабриката));
				
				Если ПФВИсходныхДанных.Количество() > 0 Тогда
					НоваяПотребность.Спецификация = ПФВИсходныхДанных[0].ПартияСпецификацияПродукции;
				Иначе
					
					ЗаполнитьЗначенияСвойств(ОтборСпецификаций, НоваяПотребность);
					ОтборСпецификаций.Подразделение = Изделие.Подразделение;
					
					СпецификацииИзделия = СпецификацииИзделий.НайтиСтроки(ОтборСпецификаций);
					
					Если СпецификацииИзделия.Количество() > 0 Тогда
						НоваяПотребность.Спецификация = СпецификацииИзделия[0].Спецификация;
					КонецЕсли;
					
				КонецЕсли;
					
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(НоваяПотребность.Спецификация) Тогда
				
				Изделие.РасчетЗавершен = Истина;
				Продолжить;
				
			Иначе
				Изделие.ПартияСпецификацияПолуфабриката = НоваяПотребность.Спецификация;
			КонецЕсли;
			
		Иначе
			
			НоваяПотребность.Номенклатура = Изделие.Продукция;
			НоваяПотребность.Характеристика = Изделие.ХарактеристикаПродукции;
			НоваяПотребность.Спецификация = Изделие.ПартияСпецификацияПродукции;
			НоваяПотребность.ДатаВыпуска = Изделие.Период;
			НоваяПотребность.РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КНачалу;
			
		КонецЕсли;
		
		НоваяПотребность.ПодразделениеДиспетчер = Изделие.Подразделение;
		
		ДанныеОсновногоИзделия = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(НоваяПотребность.Спецификация,
			НоваяПотребность.Номенклатура,
			НоваяПотребность.Характеристика);
			
		НоваяПотребность.Количество = ДанныеОсновногоИзделия.КоличествоНаЕдиницуПартииВыпуска;
		Результат.Добавить(НоваяПотребность);
		
	КонецЦикла;
	
	Возврат Результат;
	
Конецфункции

Процедура ЗаполнитьДатыПроизводства(Потребности, ПериодКалькуляции)
	
	Индекс = Потребности.Количество() - 1;
	
	ОбщаяДлительность = 0;
	Пока Индекс >= 0 Цикл
		
		ДанныеСпецификации = Потребности[Индекс];
		ДанныеСпецификации.Вставить("ДатаВыпуска", ПериодКалькуляции);
		ДанныеСпецификации.Вставить("Начало");
		ДанныеСпецификации.Вставить("Окончание");
		ДанныеСпецификации.Вставить("РазмещениеВыпуска", Перечисления.СпособыПривязкиОперацийПроизводства.ПустаяСсылка());
		
		ТекущаяДлительность = 0;
		Для Каждого Этап Из ДанныеСпецификации.Этапы Цикл
			ТекущаяДлительность = ТекущаяДлительность + Этап.ДлительностьЭтапаВДнях;
		КонецЦикла;
		
		Если ДанныеСпецификации.РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КНачалу Тогда
			
			ДанныеСпецификации.Начало = ДанныеСпецификации.ДатаВыпуска + ОбщаяДлительность;
			ДанныеСпецификации.Окончание = ДанныеСпецификации.Начало + ТекущаяДлительность * 86400;
			
		Иначе
			
			ДанныеСпецификации.Окончание = ПериодКалькуляции;
			ДанныеСпецификации.Начало = ПериодКалькуляции;
			
		КонецЕсли;
		
		ОбщаяДлительность = ОбщаяДлительность + ТекущаяДлительность * 86400;
		
		Индекс = Индекс - 1;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОписаниеДанныхКРазузлованию()
	
	Описание = Новый Структура;
	
	Описание.Вставить("Спецификация", Справочники.РесурсныеСпецификации.ПустаяСсылка());
	Описание.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	Описание.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	Описание.Вставить("ПодразделениеДиспетчер", Справочники.СтруктураПредприятия.ПустаяСсылка());
	Описание.Вставить("Количество", 0);
	Описание.Вставить("ДатаВыпуска", Дата(1, 1, 1));
	Описание.Вставить("РазмещениеВыпуска", Перечисления.СпособыПривязкиОперацийПроизводства.ПустаяСсылка());
		
	Возврат Описание;
	
КонецФункции

#КонецОбласти

#Область РаботаСДвижениями

Процедура СформироватьДвижения(ДокументОбъект)
	
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	ДополнительныеСвойства = ДокументОбъект.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("ЭтоНовый",    Ложь);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойства);
	
	Документы.ПлановаяКалькуляция2_2.ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства);
	
	ЗаписатьМножителиДокумента(ДокументСсылка, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОчиститьДвижения(ДокументОбъект, ПараметрыРасчета)
	
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	ДополнительныеСвойства = ДокументОбъект.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("ЭтоНовый",    Ложь);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения);
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(ДокументСсылка, ДополнительныеСвойства);
	
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ДокументОбъект);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(*) КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|	ГДЕ
		|		ПлановыеМатериальныеЗатраты.Регистратор = &Регистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КОЛИЧЕСТВО(*)
		|	ИЗ
		|		РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|	ГДЕ
		|		ПлановыеТрудозатраты.Регистратор = &Регистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КОЛИЧЕСТВО(*)
		|	ИЗ
		|		РегистрНакопления.ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|	ГДЕ
		|		ПлановыеПрочиеЗатраты.Регистратор = &Регистратор) КАК ВложенныйЗапрос";
	
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПараметрыРасчета.КоличествоДанных = ПараметрыРасчета.КоличествоДанных + Выборка.Количество;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого НаборЗаписей Из ДокументОбъект.Движения Цикл
		НаборЗаписей.Записать();
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	ОчиститьМножители(ДокументСсылка);
	
КонецПроцедуры

Процедура ЗаписатьМножители(Калькуляция, ДополнительныеСвойства, ПараметрыРасчета)
	
	Множители = Неопределено;
	
	Если Не ДополнительныеСвойства.Свойство("МножителиПартийИПолуфабрикатов", Множители)
		И Не ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаМножителиПартийИПолуфабрикатов", Множители) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период КАК Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок КАК Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Стоимость КАК Стоимость,
		|	МножителиПартийИПолуфабрикатов.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	МножителиПартийИПолуфабрикатов.СтоимостьРегл КАК СтоимостьРегл,
		|	МножителиПартийИПолуфабрикатов.Трудозатраты КАК Трудозатраты,
		|	МножителиПартийИПолуфабрикатов.ТрудозатратыРегл КАК ТрудозатратыРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен КАК РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена
		|	ИЗ
		|		РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|	ГДЕ
		|		МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|		И МножителиПартийИПолуфабрикатов.РасчетЗавершен";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Множители.Индексы.Очистить();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Множители.Добавить(), Выборка);
	КонецЦикла;
	
	Множители.Свернуть("Период, Калькуляция, ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, Знаменатель, РасчетЗавершен, ПартияВыпущена", 
		"Порядок, Числитель, КоличествоПолуфабриката, Стоимость, СтоимостьБезНДС, СтоимостьРегл, Трудозатраты, ТрудозатратыРегл, ПостатейныеПостоянные, ПостатейныеПостоянныеБезНДС, ПостатейныеПостоянныеРегл, ПостатейныеПеременные, ПостатейныеПеременныеБезНДС, ПостатейныеПеременныеРегл, СтоимостьЗабалансовая, СтоимостьЗабалансоваяРегл");
	
	ПривестиМножителиКОбщемуЗнаменателю(Множители);
	
	НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Калькуляция.Установить(Калькуляция);
	
	Множители.Свернуть("Период, Калькуляция, ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, Порядок, Знаменатель, РасчетЗавершен, ПартияВыпущена", 
		"Числитель, КоличествоПолуфабриката, Стоимость, СтоимостьБезНДС, СтоимостьРегл, Трудозатраты, ТрудозатратыРегл, ПостатейныеПостоянные, ПостатейныеПостоянныеБезНДС, ПостатейныеПостоянныеРегл, ПостатейныеПеременные, ПостатейныеПеременныеБезНДС, ПостатейныеПеременныеРегл, СтоимостьЗабалансовая, СтоимостьЗабалансоваяРегл");
	НаборЗаписей.Загрузить(Множители);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПараметрыРасчета.КоличествоДанных = ПараметрыРасчета.КоличествоДанных + Множители.Количество();
	
КонецПроцедуры

Процедура ЗаписатьМножителиДокумента(Калькуляция, ДополнительныеСвойства)
	
	Множители = Неопределено;
	
	Если Не ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаМножителиПартийИПолуфабрикатов", Множители) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Калькуляция.Установить(Калькуляция);
	
	НаборЗаписей.Загрузить(Множители);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОчиститьМножители(Калькуляция)
	
	НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Калькуляция.Установить(Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗаписатьДвижения(Движения, Калькуляция, МенеджерВременныхТаблиц, ПараметрыРасчета)
	
	АктуальныеДвижения = АктуальныеДвиженияОчереди(Калькуляция, МенеджерВременныхТаблиц);
	
	ДокументОбъект = Калькуляция.ПолучитьОбъект();
	
	ДополнительныеСвойства = ДокументОбъект.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("ЭтоНовый",    Ложь);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Калькуляция, ДополнительныеСвойства);
	
	Для Каждого КлючИЗначение Из Движения Цикл
			
		ИмяРегистра = КлючИЗначение.Ключ;
		
		Если ИмяРегистра = "МножителиПартийИПолуфабрикатов" Тогда
			Продолжить;
		КонецЕсли;
		
		ДвиженияПоРегистру = ДокументОбъект.Движения[ИмяРегистра];
		
		ТаблицаДвижений = АктуальныеДвижения[ИмяРегистра];
		ДвиженияПоРегистру.Загрузить(ТаблицаДвижений);
		
		ТаблицаДвижений.Свернуть("ПартияСпецификация");
		ПартииСпецификации = ТаблицаДвижений.ВыгрузитьКолонку("ПартияСпецификация");
		
		Для Каждого Строка Из КлючИЗначение.Значение Цикл
			
			Если Не ПартииСпецификации.Найти(Строка.ПартияСпецификация) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Запись = ДвиженияПоРегистру.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			Запись.Активность = Истина;
			Запись.Калькуляция = Калькуляция;
			
		КонецЦикла;
		
		ДвиженияПоРегистру.Записывать = Истина;
		
	КонецЦикла;
	
	ЗаписатьМножители(Калькуляция, Движения, ПараметрыРасчета);

	УстановитьПривилегированныйРежим(Истина);
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ДокументОбъект);
	УстановитьПривилегированныйРежим(Ложь);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Функция АктуальныеДвиженияОчереди(Калькуляция, МенеджерВременныхТаблиц)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Период,
		|	ПлановыеМатериальныеЗатраты.Регистратор,
		|	ПлановыеМатериальныеЗатраты.НомерСтроки,
		|	ПлановыеМатериальныеЗатраты.Активность,
		|	ПлановыеМатериальныеЗатраты.Калькуляция,
		|	ПлановыеМатериальныеЗатраты.ПартияСпецификация,
		|	ПлановыеМатериальныеЗатраты.Подразделение,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции,
		|	ПлановыеМатериальныеЗатраты.Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика,
		|	ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат,
		|	ПлановыеМатериальныеЗатраты.ПериодЗатрат,
		|	ПлановыеМатериальныеЗатраты.Количество,
		|	ПлановыеМатериальныеЗатраты.Стоимость,
		|	ПлановыеМатериальныеЗатраты.СтоимостьБезНДС,
		|	ПлановыеМатериальныеЗатраты.СтоимостьРегл		
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|ГДЕ
		|	ПлановыеМатериальныеЗатраты.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеТрудозатраты.Период,
		|	ПлановыеТрудозатраты.Регистратор,
		|	ПлановыеТрудозатраты.НомерСтроки,
		|	ПлановыеТрудозатраты.Активность,
		|	ПлановыеТрудозатраты.Калькуляция,
		|	ПлановыеТрудозатраты.ПартияСпецификация,
		|	ПлановыеТрудозатраты.Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции,
		|	ПлановыеТрудозатраты.ВидРабот,
		|	ПлановыеТрудозатраты.ПериодЗатрат,
		|	ПлановыеТрудозатраты.Количество,
		|	ПлановыеТрудозатраты.Стоимость,
		|	ПлановыеТрудозатраты.СтоимостьРегл
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|ГДЕ
		|	ПлановыеТрудозатраты.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеПрочиеЗатраты.Период,
		|	ПлановыеПрочиеЗатраты.Регистратор,
		|	ПлановыеПрочиеЗатраты.НомерСтроки,
		|	ПлановыеПрочиеЗатраты.Активность,
		|	ПлановыеПрочиеЗатраты.Калькуляция,
		|	ПлановыеПрочиеЗатраты.ПартияСпецификация,
		|	ПлановыеПрочиеЗатраты.Подразделение,
		|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
		|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
		|	ПлановыеПрочиеЗатраты.АналитикаРасходов,
		|	ПлановыеПрочиеЗатраты.Стоимость,
		|	ПлановыеПрочиеЗатраты.СтоимостьБезНДС,
		|	ПлановыеПрочиеЗатраты.СтоимостьРегл
		|ИЗ
		|	РегистрНакопления.ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|ГДЕ
		|	ПлановыеПрочиеЗатраты.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Калькуляция);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПлановыеМатериальныеЗатраты", МассивРезультатов[0].Выгрузить());
		
	Результат.Вставить("ПлановыеТрудозатраты", МассивРезультатов[1].Выгрузить());
	
	Результат.Вставить("ПлановыеПрочиеЗатраты", МассивРезультатов[2].Выгрузить());
		
	Возврат Результат;
	
КонецФункции

Процедура ОчиститьОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОчередьРасчета.Калькуляция,
		|	ОчередьРасчета.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ОчередьРасчета.Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение КАК Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	ОчередьРасчета.Полуфабрикат,
		|	ОчередьРасчета.ХарактеристикаПолуфабриката,
		|	ОчередьРасчета.Разделитель,
		|	ОчередьРасчета.Порядок,
		|	ОчередьРасчета.ДатаПотребности
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК ОчередьРасчетаПлановыхКалькуляций
		|		ПО ОчередьРасчета.Калькуляция = ОчередьРасчетаПлановыхКалькуляций.Калькуляция
		|			И ОчередьРасчета.ПартияСпецификацияПродукции = ОчередьРасчетаПлановыхКалькуляций.ПартияСпецификацияПродукции
		|			И ОчередьРасчета.Продукция = ОчередьРасчетаПлановыхКалькуляций.Продукция
		|			И ОчередьРасчета.ХарактеристикаПродукции = ОчередьРасчетаПлановыхКалькуляций.ХарактеристикаПродукции
		|			И ОчередьРасчета.Назначение = ОчередьРасчетаПлановыхКалькуляций.Назначение
		|			И ОчередьРасчета.ПартияСпецификацияПолуфабриката = ОчередьРасчетаПлановыхКалькуляций.ПартияСпецификацияПолуфабриката
		|			И ОчередьРасчета.Полуфабрикат = ОчередьРасчетаПлановыхКалькуляций.Полуфабрикат
		|			И ОчередьРасчета.ХарактеристикаПолуфабриката = ОчередьРасчетаПлановыхКалькуляций.ХарактеристикаПолуфабриката
		|			И ОчередьРасчета.Разделитель = ОчередьРасчетаПлановыхКалькуляций.Разделитель
		|			И ОчередьРасчета.ДатаПотребности = ОчередьРасчетаПлановыхКалькуляций.ДатаПотребности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОчередьРасчета";
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Очередь = МассивРезультатов[0].Выгрузить();
	
	РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Очередь, Калькуляция);
	
КонецПроцедуры

Процедура ОчиститьОчередьПоКалькуляции(Калькуляция)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Очередь.Калькуляция КАК Калькуляция,
		|	Очередь.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	Очередь.Продукция КАК Продукция,
		|	Очередь.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Очередь.Назначение КАК Назначение,
		|	Очередь.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Очередь.Полуфабрикат КАК Полуфабрикат,
		|	Очередь.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Очередь.Разделитель КАК Разделитель,
		|	Очередь.ДатаПотребности КАК ДатаПотребности,
		|	Очередь.Порядок КАК Порядок
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Калькуляция = &Калькуляция";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Запрос.Выполнить().Выгрузить(), Калькуляция);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаблокироватьДанные(Калькуляция)
	
	Блокировка = Новый БлокировкаДанных();
	
	ДобавитьБлокировкуРегистрМножителиПартийИПолуфабрикатов(Блокировка, Калькуляция);
	ДобавитьБлокировкуРегистрПлановыеМатериальныеЗатраты(Блокировка, Калькуляция);
	ДобавитьБлокировкуРегистрПлановыеТрудозатраты(Блокировка, Калькуляция);
	ДобавитьБлокировкуРегистрПлановыеПрочиеЗатраты(Блокировка, Калькуляция);
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрМножителиПартийИПолуфабрикатов(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.МножителиПартийИПолуфабрикатов");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрПлановыеМатериальныеЗатраты(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПлановыеМатериальныеЗатраты");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрПлановыеТрудозатраты(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПлановыеТрудозатраты");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрПлановыеПрочиеЗатраты(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПлановыеПрочиеЗатраты");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Функция ОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Очередь.Калькуляция КАК Калькуляция,
		|	Очередь.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	Очередь.Продукция КАК Продукция,
		|	Очередь.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Очередь.Назначение КАК Назначение,
		|	Очередь.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Очередь.Полуфабрикат КАК Полуфабрикат,
		|	Очередь.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Очередь.ДатаПотребности КАК ДатаПотребности,
		|	Очередь.Порядок КАК Порядок,
		|	МАКСИМУМ(Очередь.Разделитель) КАК Разделитель
		|ПОМЕСТИТЬ ОчередьРасчета
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Калькуляция = &Калькуляция
		|	И НЕ Очередь.ОтменаПроведения
		|
		|СГРУППИРОВАТЬ ПО
		|	Очередь.ДатаПотребности,
		|	Очередь.ХарактеристикаПолуфабриката,
		|	Очередь.Полуфабрикат,
		|	Очередь.ПартияСпецификацияПолуфабриката,
		|	Очередь.Назначение,
		|	Очередь.ХарактеристикаПродукции,
		|	Очередь.Продукция,
		|	Очередь.ПартияСпецификацияПродукции,
		|	Очередь.Порядок,
		|	Очередь.Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОчередьРасчета.Калькуляция КАК Калькуляция
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета";
		
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЕстьЗаписиВОчереди = НЕ МассивРезультатов[1].Пустой();
	
	Возврат ЕстьЗаписиВОчереди;
	
КонецФункции

Функция ДокументВОчереди()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Очередь.Калькуляция КАК Калькуляция,
		|	МАКСИМУМ(Очередь.ОтменаПроведения) КАК ОтменаПроведения
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Очередь.Калькуляция
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(Очередь.ОтменаПроведения) = ЛОЖЬ";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Калькуляция;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЕстьЗаданияВОчереди(Калькуляция)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Очередь.Разделитель КАК Разделитель
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Калькуляция = &Калькуляция
		|	И НЕ Очередь.ОтменаПроведения");
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат (НЕ Запрос.Выполнить().Пустой());
	
КонецФункции

Функция БылаОтменаПроведения(Калькуляция)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Очередь.Калькуляция КАК Калькуляция
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.ОтменаПроведения
		|	И Очередь.Калькуляция = &Калькуляция";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ТребуетсяРазузловатьЗаказ(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОчередьРасчета.Продукция
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ОчередьРасчета.ПартияСпецификацияПродукции) = ТИП(Документ.ЭтапПроизводства2_2)
		|	И ОчередьРасчета.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура ЗаписатьСостояниеКалькуляции(Калькуляция, Состояние)
	
	РегистрыСведений.СостоянияПлановыхКалькуляций.УстановитьСостояние(Калькуляция, Состояние);
	
КонецПроцедуры

Процедура АктуализироватьСостояниеКалькуляций(Калькуляции)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияПлановыхКалькуляций.Калькуляция КАК Калькуляция
		|ИЗ
		|	РегистрСведений.СостоянияПлановыхКалькуляций КАК СостоянияПлановыхКалькуляций
		|ГДЕ
		|	СостоянияПлановыхКалькуляций.Калькуляция В(&Калькуляции)
		|	И НЕ СостоянияПлановыхКалькуляций.Состояние = ""ОшибкаРасчета""";
	
	Запрос.УстановитьПараметр("Калькуляции", Калькуляции);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаписатьСостояниеКалькуляции(Выборка.Калькуляция, "РассчитанаСОшибками");
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеВМассив(Значение)
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(Значение);
	
	Возврат МассивЭлементов;
	
КонецФункции

Процедура ЗаписатьВЖурналОшибкуРассчета(Калькуляция, Причина)
	
	ТекстСообщения = НСтр("ru = 'Не удалось выполнить проведение документа %Ссылка%  по причине: %Причина%';
							|en = 'Cannot post the %Ссылка% document due to: %Причина%'");
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Калькуляция);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", Причина);
		
	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрацииОшибкаРасчета(),
		УровеньЖурналаРегистрации.Ошибка,
		Калькуляция.Метаданные(),
		Калькуляция,
		ТекстСообщения);
	
КонецПроцедуры

Функция СобытиеЖурналаРегистрацииОшибкаРасчета() Экспорт
	
	Возврат НСтр("ru = 'Фоновое проведение калькуляции продукции.';
				|en = 'Background posting of product costing.'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

Функция ПолучитьОсновныеСпецификации(СпособыПолученияМатериалов)
	
	ПараметрыЗапроса = Новый Структура("ДляСпискаНоменклатуры", Истина);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.Подразделение                           КАК Подразделение,
		|	Т.Номенклатура                            КАК Номенклатура,
		|	Т.Характеристика                          КАК Характеристика,
		|	НАЧАЛОПЕРИОДА(Т.НачалоПроизводства, ДЕНЬ) КАК НачалоПроизводства
		|ПОМЕСТИТЬ СписокНоменклатуры
		|ИЗ
		|	&Материалы КАК Т";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВПакете();
	ТекстЗапроса = ТекстЗапроса + УправлениеДаннымиОбИзделиях.ПолучитьТекстЗапросаОсновнойСпецификации(ПараметрыЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Материалы", СпособыПолученияМатериалов);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ОпределитьПараметрыРасчета(ПараметрыРасчета, Замер, Калькуляция)
	
	РеквизитыКалькуляции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Калькуляция, "Дата, Организация");
	
	ПараметрыРасчета.Вставить("Калькуляция", Калькуляция);
	ПараметрыРасчета.Вставить("ДатаКалькуляции", РеквизитыКалькуляции.Дата);
	ПараметрыРасчета.Вставить("Организация", РеквизитыКалькуляции.Организация);	
	ПараметрыРасчета.Вставить("Замер", Замер);
	ПараметрыРасчета.Вставить("КоличествоДанных", 0);
	ПараметрыРасчета.Вставить("ОшибкаРазузлования", "");
	
КонецПроцедуры

#КонецОбласти

#Область Стоимости

Процедура ЗаполнитьСтоимости(ПараметрыРасчета)
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	
	Попытка
		
		ЗаблокироватьДанные(ПараметрыРасчета.Калькуляция);
		
		Движения = ИнициализироватьТаблицыДвижений();
		МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
		ПодготовитьСлужебныеВременныеТаблицы(МенеджерВТ, ПараметрыРасчета.Калькуляция);
		
		// Расчет стоимости материалов.
		ЗаполнитьСтоимостьМатериалов(ПараметрыРасчета.Калькуляция, МенеджерВТ);
		
		// Расчет стоимости трудозатрат.
		ЗаполнитьСтоимостьТрудозатрат(ПараметрыРасчета.Калькуляция, МенеджерВТ);
		
		РассчитатьПредварительнуюСтоимость(ПараметрыРасчета.Калькуляция, МенеджерВТ);
		
		// Расчет постатейных расходов.
		ЗаполнитьСтоимостьПостатейных(ПараметрыРасчета.Калькуляция, МенеджерВТ);
		
		РассчитатьПредварительнуюСтоимость(ПараметрыРасчета.Калькуляция, МенеджерВТ);

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Период,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.ПериодЗатрат,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Калькуляция,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.ПартияСпецификация,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Подразделение,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.СтатьяКалькуляции,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Номенклатура,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Характеристика,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.ЭтоПолуфабрикат,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Количество,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.Стоимость,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьБезНДС,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьРегл,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансовая,
			|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансоваяРегл
			|ИЗ
			|	МатериальныеЗатратыВключаяПолуфабрикаты КАК МатериальныеЗатратыВключаяПолуфабрикаты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПлановыеТрудозатраты.Период,
			|	ПлановыеТрудозатраты.ПериодЗатрат,
			|	ПлановыеТрудозатраты.Калькуляция,
			|	ПлановыеТрудозатраты.ПартияСпецификация,
			|	ПлановыеТрудозатраты.Подразделение,
			|	ПлановыеТрудозатраты.СтатьяКалькуляции,
			|	ПлановыеТрудозатраты.ВидРабот,
			|	ПлановыеТрудозатраты.Количество,
			|	ПлановыеТрудозатраты.Стоимость,
			|	ПлановыеТрудозатраты.СтоимостьРегл
			|ИЗ
			|	ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПлановыеПрочиеЗатраты.Период,
			|	ПлановыеПрочиеЗатраты.Калькуляция,
			|	ПлановыеПрочиеЗатраты.ПартияСпецификация,
			|	ПлановыеПрочиеЗатраты.Подразделение,
			|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
			|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
			|	ПлановыеПрочиеЗатраты.Стоимость,
			|	ПлановыеПрочиеЗатраты.СтоимостьБезНДС,
			|	ПлановыеПрочиеЗатраты.СтоимостьРегл
			|ИЗ
			|	ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Множители.Период,
			|	Множители.Продукция,
			|	Множители.ПартияСпецификацияПродукции,
			|	Множители.Калькуляция,
			|	Множители.ХарактеристикаПродукции,
			|	Множители.Назначение,
			|	Множители.Стоимость,
			|	Множители.СтоимостьБезНДС,
			|	Множители.СтоимостьРегл,
			|	Множители.СтоимостьЗабалансовая,
			|	Множители.СтоимостьЗабалансоваяРегл,
			|	Множители.Трудозатраты,
			|	Множители.ТрудозатратыРегл,
			|	Множители.ПостатейныеПеременные,
			|	Множители.ПостатейныеПостоянныеБезНДС,
			|	Множители.ПостатейныеПостоянныеРегл,
			|	Множители.ПостатейныеПостоянные,
			|	Множители.ПостатейныеПеременныеБезНДС,
			|	Множители.ПостатейныеПеременныеРегл,
			|	Множители.ПартияСпецификацияПолуфабриката,
			|	Множители.Полуфабрикат,
			|	Множители.ХарактеристикаПолуфабриката,
			|	Множители.Порядок,
			|	Множители.Числитель,
			|	Множители.Знаменатель,
			|	Множители.КоличествоПолуфабриката,
			|	Множители.РасчетЗавершен,
			|	Множители.ПартияВыпущена
			|ИЗ
			|	МножителиПартийИПолуфабрикатов КАК Множители";
		
		Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ДокументОбъект = ПараметрыРасчета.Калькуляция.ПолучитьОбъект();
		
		ДополнительныеСвойства = ДокументОбъект.ДополнительныеСвойства;
		ДополнительныеСвойства.Вставить("ЭтоНовый",    Ложь);
		ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		
		ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(ПараметрыРасчета.Калькуляция, ДополнительныеСвойства);
		
		ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаМножителиПартийИПолуфабрикатов", МассивРезультатов[3].Выгрузить());	
		
		ДокументОбъект.Движения.ПлановыеМатериальныеЗатраты.Загрузить(МассивРезультатов[0].Выгрузить());
		ДокументОбъект.Движения.ПлановыеТрудозатраты.Загрузить(МассивРезультатов[1].Выгрузить());
		ДокументОбъект.Движения.ПлановыеПрочиеЗатраты.Загрузить(МассивРезультатов[2].Выгрузить());
		
		ДокументОбъект.Движения.ПлановыеМатериальныеЗатраты.Записывать = Истина;
		ДокументОбъект.Движения.ПлановыеТрудозатраты.Записывать = Истина;
		ДокументОбъект.Движения.ПлановыеПрочиеЗатраты.Записывать = Истина;
		
		УстановитьПривилегированныйРежим(Истина);
		ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ДокументОбъект);
		УстановитьПривилегированныйРежим(Ложь);
		ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
		
		ЗаписатьМножителиДокумента(ПараметрыРасчета.Калькуляция, ДополнительныеСвойства);
		
		Для Каждого НаборЗаписей Из ДокументОбъект.Движения Цикл
			ПараметрыРасчета.КоличествоДанных = ПараметрыРасчета.КоличествоДанных + НаборЗаписей.Количество();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписатьСостояниеКалькуляции(ПараметрыРасчета.Калькуляция, "ОшибкаРасчета");
		ОчиститьОчередьПоКалькуляции(ПараметрыРасчета.Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(ПараметрыРасчета.Калькуляция, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьСтоимостьМатериалов(Калькуляция, МенеджерВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.УстановитьПараметр("ВидЦены", Калькуляция.ВидЦены);
	Запрос.УстановитьПараметр("Период", Калькуляция.Дата);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика КАК Характеристика,
		|	ПлановыеМатериальныеЗатраты.Период КАК Период
		|ПОМЕСТИТЬ НоменклатураДляЦен
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|ГДЕ
		|	ПлановыеМатериальныеЗатраты.Калькуляция = &Калькуляция
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеМатериальныеЗатраты.Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика,
		|	ПлановыеМатериальныеЗатраты.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НоменклатураДляЦен.Номенклатура КАК Номенклатура,
		|	НоменклатураДляЦен.Характеристика КАК Характеристика,
		|	НоменклатураДляЦен.Период КАК Период,
		|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК ПериодЦены
		|ПОМЕСТИТЬ ДатыЦенВРазрезе
		|ИЗ
		|	НоменклатураДляЦен КАК НоменклатураДляЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО (ЦеныНоменклатуры.Номенклатура = НоменклатураДляЦен.Номенклатура)
		|			И (ЦеныНоменклатуры.Характеристика = НоменклатураДляЦен.Характеристика)
		|			И (ЦеныНоменклатуры.ВидЦены = &ВидЦены)
		|			И НоменклатураДляЦен.Период >= ЦеныНоменклатуры.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураДляЦен.Период,
		|	НоменклатураДляЦен.Характеристика,
		|	НоменклатураДляЦен.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыЦенВРазрезе.Номенклатура КАК Номенклатура,
		|	ДатыЦенВРазрезе.Характеристика КАК Характеристика,
		|	ДатыЦенВРазрезе.Период КАК Период,
		|	ЕСТЬNULL(ЦеныНоменклатуры.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка,
		|	ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) КАК Цена,
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ИЗ
		|	ДатыЦенВРазрезе КАК ДатыЦенВРазрезе
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ДатыЦенВРазрезе.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ДатыЦенВРазрезе.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И (ЦеныНоменклатуры.ВидЦены = &ВидЦены)
		|			И ДатыЦенВРазрезе.ПериодЦены = ЦеныНоменклатуры.Период";
	
	УстановитьПривилегированныйРежим(Истина);
	Материалы = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	КоэффициентыУпаковок = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентыУпаковок(Материалы);
	
	Для Каждого Материал Из Материалы Цикл
		
		УпаковкиПоМатериалу = КоэффициентыУпаковок.Получить(Материал.Номенклатура);
		Если УпаковкиПоМатериалу = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		КоэффициентУпаковки = УпаковкиПоМатериалу.Получить(Материал.Упаковка);
		Если Не ЗначениеЗаполнено(КоэффициентУпаковки) Тогда
			Продолжить;
		КонецЕсли;
		
		Материал.Цена = Материал.Цена / КоэффициентУпаковки;
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Период КАК Период,
		|	Т.Упаковка КАК Упаковка,
		|	Т.Цена КАК Цена,
		|	Т.Валюта КАК Валюта
		|ПОМЕСТИТЬ ЦеныНоменклатурыПредварительная
		|ИЗ
		|	&Материалы КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Период КАК Период,
		|	Т.Упаковка КАК Упаковка,
		|	Т.Цена КАК Цена,
		|	Т.Валюта КАК Валюта
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	ЦеныНоменклатурыПредварительная КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ПОМЕСТИТЬ ВалютыНоменклатуры
		|ИЗ
		|	ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВалютыНоменклатуры.Валюта КАК Валюта,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) КАК Кратность
		|ПОМЕСТИТЬ КурсыВалютНоменклатуры
		|ИЗ
		|	ВалютыНоменклатуры КАК ВалютыНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						Валюты.Валюта КАК Валюта
		|					ИЗ
		|						ВалютыНоменклатуры КАК Валюты)) КАК КурсыВалютСрезПоследних
		|		ПО ВалютыНоменклатуры.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Калькуляция КАК Калькуляция,
		|	ПлановыеМатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика КАК Характеристика,
		|	ПлановыеМатериальныеЗатраты.Количество КАК Количество,
		|	ПлановыеМатериальныеЗатраты.Период КАК Период,
		|	ВЫБОР
		|		КОГДА НЕ ПлановыеМатериальныеЗатраты.Стоимость = 0
		|			И НЕ (ПлановыеМатериальныеЗатраты.Количество > 0 И ПлановыеМатериальныеЗатраты.Стоимость = -1)
		|			ТОГДА ПлановыеМатериальныеЗатраты.Стоимость
		|		ИНАЧЕ ПлановыеМатериальныеЗатраты.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) * ВЫБОР
		|				КОГДА ЦеныНоменклатуры.Валюта = КурсыВалютУУ.Валюта
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.Кратность, 0) * КурсыВалютУУ.Курс = 0
		|							ТОГДА 1
		|						ИНАЧЕ КурсыВалютНоменклатуры.Курс * КурсыВалютУУ.Кратность / (КурсыВалютНоменклатуры.Кратность * КурсыВалютУУ.Курс)
		|					КОНЕЦ
		|			КОНЕЦ * ВЫБОР
		|				КОГДА Количество > 0
		|					И ПлановыеМатериальныеЗатраты.Стоимость = -1
		|					ТОГДА 1
		|				КОГДА ПлановыеМатериальныеЗатраты.Количество < 0
		|					ТОГДА 1
		|				КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|					ТОГДА 1
		|				КОГДА ВидыЦен.ЦенаВключаетНДС
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
		|							ТОГДА 1.1
		|						КОГДА ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|								ИЛИ ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|							ТОГДА 1
		|						ИНАЧЕ 1.18
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Стоимость,
		|	ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ПлановыеМатериальныеЗатраты.ПериодЗатрат КАК ПериодЗатрат,
		|	ВЫБОР
		|		КОГДА НЕ ПлановыеМатериальныеЗатраты.СтоимостьРегл = 0
		|			И НЕ (ПлановыеМатериальныеЗатраты.Количество > 0 И ПлановыеМатериальныеЗатраты.СтоимостьРегл = -1)
		|			ТОГДА ПлановыеМатериальныеЗатраты.СтоимостьРегл
		|		ИНАЧЕ ПлановыеМатериальныеЗатраты.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ * ВЫБОР
		|		КОГДА ЦеныНоменклатуры.Валюта = КурсыВалютБУ.Валюта
		|			ТОГДА 1
		|		КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.Кратность, 0) * КурсыВалютБУ.Курс = 0
		|			ТОГДА 1
		|		ИНАЧЕ КурсыВалютНоменклатуры.Курс * КурсыВалютБУ.Кратность / (КурсыВалютНоменклатуры.Кратность * КурсыВалютБУ.Курс)
		|	КОНЕЦ / ВЫБОР
		|		КОГДА Количество > 0
		|			И ПлановыеМатериальныеЗатраты.СтоимостьРегл = -1
		|			ТОГДА 1
		|		КОГДА ПлановыеМатериальныеЗатраты.Количество < 0
		|			ТОГДА 1
		|		КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|			ТОГДА 1
		|		КОГДА НЕ ВидыЦен.ЦенаВключаетНДС
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
		|					ТОГДА 1.1
		|				КОГДА ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|						ИЛИ ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|					ТОГДА 1
		|				ИНАЧЕ 1.18
		|			КОНЕЦ
		|	КОНЕЦ КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА НЕ ПлановыеМатериальныеЗатраты.СтоимостьБезНДС = 0
		|			И НЕ (ПлановыеМатериальныеЗатраты.Количество > 0 И ПлановыеМатериальныеЗатраты.СтоимостьБезНДС = -1)
		|			ТОГДА ПлановыеМатериальныеЗатраты.СтоимостьБезНДС
		|		ИНАЧЕ ПлановыеМатериальныеЗатраты.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ * ВЫБОР
		|		КОГДА ЦеныНоменклатуры.Валюта = КурсыВалютУУ.Валюта
		|			ТОГДА 1
		|		КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.Кратность, 0) * КурсыВалютУУ.Курс = 0
		|			ТОГДА 1
		|		ИНАЧЕ КурсыВалютНоменклатуры.Курс * КурсыВалютУУ.Кратность / (КурсыВалютНоменклатуры.Кратность * КурсыВалютУУ.Курс)
		|	КОНЕЦ / ВЫБОР
		|		КОГДА Количество > 0
		|			И ПлановыеМатериальныеЗатраты.СтоимостьБезНДС = -1
		|			ТОГДА 1
		|		КОГДА ПлановыеМатериальныеЗатраты.Количество < 0
		|			ТОГДА 1
		|		КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|			ТОГДА 1
		|		КОГДА НЕ ВидыЦен.ЦенаВключаетНДС
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
		|					ТОГДА 1.1
		|				КОГДА ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|						ИЛИ ПлановыеМатериальныеЗатраты.Номенклатура.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|					ТОГДА 1
		|				ИНАЧЕ 1.18
		|			КОНЕЦ
		|	КОНЕЦ КАК СтоимостьБезНДС,
		|	ПлановыеМатериальныеЗатраты.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	ПлановыеМатериальныеЗатраты.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ МатериальныеЗатраты
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ПлановыеМатериальныеЗатраты.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ПлановыеМатериальныеЗатраты.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И ПлановыеМатериальныеЗатраты.Период = ЦеныНоменклатуры.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалютНоменклатуры КАК КурсыВалютНоменклатуры
		|		ПО (ЦеныНоменклатуры.Валюта = КурсыВалютНоменклатуры.Валюта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютБУ
		|		ПО (КурсыВалютБУ.ВидВалюты = ""ВалютаРеглУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО (ВидыЦен.Ссылка = &ВидЦены)
		|ГДЕ
		|	ПлановыеМатериальныеЗатраты.Калькуляция = &Калькуляция
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Калькуляция,
		|	ПартияСпецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НоменклатураДляЦен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыЦенВРазрезе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныНоменклатурыПредварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВалютыНоменклатуры";
	
	Запрос.УстановитьПараметр("Материалы", Материалы);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

Процедура ЗаполнитьСтоимостьТрудозатрат(Калькуляция, МенеджерВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПлановыеТрудозатраты.ВидРабот КАК ВидРабот,
		|	ПлановыеТрудозатраты.Период КАК Период
		|ПОМЕСТИТЬ ВидыРабот
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|ГДЕ
		|	ПлановыеТрудозатраты.Калькуляция = &Калькуляция
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыеТрудозатраты.Период,
		|	ПлановыеТрудозатраты.ВидРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыРабот.ВидРабот КАК ВидРабот,
		|	ВидыРабот.Период КАК Период,
		|	МАКСИМУМ(РасценкиРаботСотрудников.Период) КАК ПериодРасценки
		|ПОМЕСТИТЬ ДатыРасценок
		|ИЗ
		|	ВидыРабот КАК ВидыРабот
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников КАК РасценкиРаботСотрудников
		|		ПО (РасценкиРаботСотрудников.ВидРабот = ВидыРабот.ВидРабот)
		|			И (РасценкиРаботСотрудников.Период <= ВидыРабот.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыРасценок.ВидРабот КАК ВидРабот,
		|	ДатыРасценок.Период КАК Период,
		|	ЕСТЬNULL(РасценкиРаботСотрудников.Расценка, 0) КАК Расценка
		|ПОМЕСТИТЬ Расценки
		|ИЗ
		|	ДатыРасценок КАК ДатыРасценок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников КАК РасценкиРаботСотрудников
		|		ПО ДатыРасценок.ВидРабот = РасценкиРаботСотрудников.ВидРабот
		|			И ДатыРасценок.ПериодРасценки = РасценкиРаботСотрудников.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРабот,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеТрудозатраты.Калькуляция КАК Калькуляция,
		|	ПлановыеТрудозатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеТрудозатраты.Подразделение КАК Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыеТрудозатраты.ВидРабот КАК ВидРабот,
		|	ПлановыеТрудозатраты.Период КАК Период,
		|	ПлановыеТрудозатраты.Количество КАК Количество,
		|	ПлановыеТрудозатраты.Количество * Расценки.Расценка * ВЫБОР
		|		КОГДА КурсыВалютРасценок.Валюта = КурсыВалютУУ.Валюта
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|				КОГДА КурсыВалютРасценок.Кратность * КурсыВалютУУ.Курс = 0
		|					ТОГДА 1
		|				ИНАЧЕ КурсыВалютРасценок.Курс * КурсыВалютУУ.Кратность / (КурсыВалютРасценок.Кратность * КурсыВалютУУ.Курс)
		|			КОНЕЦ
		|	КОНЕЦ КАК Стоимость,
		|	ПлановыеТрудозатраты.ПериодЗатрат КАК ПериодЗатрат,
		|	ПлановыеТрудозатраты.Количество * Расценки.Расценка * ВЫБОР
		|		КОГДА КурсыВалютРасценок.Валюта = КурсыВалютБУ.Валюта
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|				КОГДА КурсыВалютРасценок.Кратность * КурсыВалютБУ.Курс = 0
		|					ТОГДА 1
		|				ИНАЧЕ КурсыВалютРасценок.Курс * КурсыВалютБУ.Кратность / (КурсыВалютРасценок.Кратность * КурсыВалютБУ.Курс)
		|			КОНЕЦ
		|	КОНЕЦ КАК СтоимостьРегл
		|ПОМЕСТИТЬ ПлановыеТрудозатратыБезСтраховыхВзносов
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Расценки КАК Расценки
		|		ПО (Расценки.ВидРабот = ПлановыеТрудозатраты.ВидРабот)
		|			И (Расценки.Период = ПлановыеТрудозатраты.Период)
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРасценок
		|		ПО (КурсыВалютРасценок.ВидВалюты = ""ВалютаРасценокВидовРабот"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютБУ
		|		ПО (КурсыВалютБУ.ВидВалюты = ""ВалютаРеглУчета"")
		|ГДЕ
		|	ПлановыеТрудозатраты.Калькуляция = &Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ГодЗатрат КАК ГодЗатрат,
		|	ВложенныйЗапрос.Организация КАК Организация
		|ПОМЕСТИТЬ ПериодыЗатратПоОрганизации
		|ИЗ
		|	(ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ПлановыеТрудозатраты.ПериодЗатрат, ГОД) КАК ГодЗатрат,
		|		ПлановыеТрудозатраты.Калькуляция.Организация КАК Организация
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатраты
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НАЧАЛОПЕРИОДА(ПлановыеТрудозатраты.ПериодЗатрат, ГОД),
		|		ПлановыеТрудозатраты.Калькуляция.Организация) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ГодЗатрат КАК ГодЗатрат,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа КАК ВидТарифа
		|ПОМЕСТИТЬ ПрименяемыеТарифыПоПериодам
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыЗатратПоОрганизации.ГодЗатрат КАК ГодЗатрат,
		|		ПериодыЗатратПоОрганизации.Организация КАК Организация,
		|		МАКСИМУМ(ПрименяемыеТарифыСтраховыхВзносов.Период) КАК Период,
		|		ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|	ИЗ
		|		РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыЗатратПоОрганизации КАК ПериодыЗатратПоОрганизации
		|			ПО (ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация = ПериодыЗатратПоОрганизации.Организация
		|					ИЛИ ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация = ПериодыЗатратПоОрганизации.Организация.ГоловнаяОрганизация)
		|				И ПрименяемыеТарифыСтраховыхВзносов.Период <= ПериодыЗатратПоОрганизации.ГодЗатрат
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыЗатратПоОрганизации.Организация,
		|		ПериодыЗатратПоОрганизации.ГодЗатрат,
		|		ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|		ПО ВложенныйЗапрос.Период = ПрименяемыеТарифыСтраховыхВзносов.Период
		|			И ВложенныйЗапрос.ГоловнаяОрганизация = ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ГодЗатрат КАК ГодЗатрат,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ТарифыСтраховыхВзносов.ПФР КАК ПФР,
		|	ТарифыСтраховыхВзносов.ФСС КАК ФСС,
		|	ТарифыСтраховыхВзносов.ФФОМС + ТарифыСтраховыхВзносов.ТФОМС КАК ФОМС
		|ПОМЕСТИТЬ ПрименяемыеТарифыСтраховыхВзносов
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПрименяемыеТарифыПоПериодам.ГодЗатрат КАК ГодЗатрат,
		|		ПрименяемыеТарифыПоПериодам.ВидТарифа КАК ВидТарифа,
		|		МАКСИМУМ(ТарифыСтраховыхВзносов.Период) КАК Период,
		|		ПрименяемыеТарифыПоПериодам.Организация КАК Организация
		|	ИЗ
		|		ПрименяемыеТарифыПоПериодам КАК ПрименяемыеТарифыПоПериодам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыСтраховыхВзносов КАК ТарифыСтраховыхВзносов
		|			ПО ПрименяемыеТарифыПоПериодам.ГодЗатрат >= ТарифыСтраховыхВзносов.Период
		|				И ПрименяемыеТарифыПоПериодам.ВидТарифа = ТарифыСтраховыхВзносов.ВидТарифа
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПрименяемыеТарифыПоПериодам.ГодЗатрат,
		|		ПрименяемыеТарифыПоПериодам.ВидТарифа,
		|		ПрименяемыеТарифыПоПериодам.Организация) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыСтраховыхВзносов КАК ТарифыСтраховыхВзносов
		|		ПО ВложенныйЗапрос.ВидТарифа = ТарифыСтраховыхВзносов.ВидТарифа
		|			И ВложенныйЗапрос.Период = ТарифыСтраховыхВзносов.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетСтраховыхВзносов.Калькуляция КАК Калькуляция,
		|	РасчетСтраховыхВзносов.ПартияСпецификация КАК ПартияСпецификация,
		|	РасчетСтраховыхВзносов.Подразделение КАК Подразделение,
		|	РасчетСтраховыхВзносов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	РасчетСтраховыхВзносов.ВидРабот КАК ВидРабот,
		|	РасчетСтраховыхВзносов.Период КАК Период,
		|	СУММА(РасчетСтраховыхВзносов.Стоимость) КАК Стоимость,
		|	РасчетСтраховыхВзносов.ПериодЗатрат КАК ПериодЗатрат,
		|	СУММА(РасчетСтраховыхВзносов.Количество) КАК Количество,
		|	СУММА(РасчетСтраховыхВзносов.СтоимостьРегл) КАК СтоимостьРегл
		|ПОМЕСТИТЬ ПлановыеТрудозатраты
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция КАК Калькуляция,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПартияСпецификация КАК ПартияСпецификация,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Подразделение КАК Подразделение,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ВидРабот КАК ВидРабот,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Период КАК Период,
		|		ВЫРАЗИТЬ((ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФОМС, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ПФР, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФСС, 0))/ 100 КАК ЧИСЛО(31,2)) КАК Стоимость,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат КАК ПериодЗатрат,
		|		0 КАК Количество,
		|		ВЫРАЗИТЬ((ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФОМС, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ПФР, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФСС, 0))/ 100 КАК ЧИСЛО(31,2)) КАК СтоимостьРегл
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатратыБезСтраховыхВзносов
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|			ПО (НАЧАЛОПЕРИОДА(ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат, ГОД) = ПрименяемыеТарифыСтраховыхВзносов.ГодЗатрат)
		|				И ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция.Организация = ПрименяемыеТарифыСтраховыхВзносов.Организация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПартияСпецификация,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Подразделение,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтатьяКалькуляции,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ВидРабот,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Период,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Количество,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатратыБезСтраховыхВзносов) КАК РасчетСтраховыхВзносов
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетСтраховыхВзносов.Период,
		|	РасчетСтраховыхВзносов.Подразделение,
		|	РасчетСтраховыхВзносов.Калькуляция,
		|	РасчетСтраховыхВзносов.ВидРабот,
		|	РасчетСтраховыхВзносов.СтатьяКалькуляции,
		|	РасчетСтраховыхВзносов.ПериодЗатрат,
		|	РасчетСтраховыхВзносов.ПартияСпецификация
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетСтраховыхВзносов.Количество) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Калькуляция,
		|	ПартияСпецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВидыРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыРасценок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Расценки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПлановыеТрудозатратыБезСтраховыхВзносов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПериодыЗатратПоОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПрименяемыеТарифыПоПериодам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПрименяемыеТарифыСтраховыхВзносов";
		
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

Процедура ЗаполнитьСтоимостьПостатейных(Калькуляция, МенеджерВТ)
	
	ТрудИМатериалы = Новый Массив;
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов);
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов);
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов);
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов);
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда);
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда);
	ТрудИМатериалы.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат);
	
	ТипыБазПродукция = Новый Массив;
	ТипыБазПродукция.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции);
	ТипыБазПродукция.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции);
	ТипыБазПродукция.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции);
	ТипыБазПродукция.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("ВидЦены", Калькуляция.ВидЦены);
	Запрос.УстановитьПараметр("Организация", Калькуляция.Организация);
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Партия КАК Партия,
		|	ВложенныйЗапрос.Период КАК Период
		|ПОМЕСТИТЬ ПартииИзделий
		|ИЗ
		|	(ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, МЕСЯЦ) КАК Период,
		|		МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК Партия
		|	ИЗ
		|		РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|	ГДЕ
		|		МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, МЕСЯЦ),
		|		МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката
		|	ИЗ
		|		РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|	ГДЕ
		|		МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ) КАК Период,
		|	&Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	""Продукция"" КАК ТипБазы,
		|	ВложенныйЗапрос.Номенклатура КАК Продукция,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Ссылка КАК ПартияСпецификация,
		|	ВложенныйЗапрос.Количество КАК Количество
		|ПОМЕСТИТЬ ПродукцияПредварительная
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПартииИзделий.Период КАК Период,
		|		Реквизиты.Ссылка КАК Ссылка,
		|		Реквизиты.Подразделение КАК Подразделение,
		|		ТЧВыходныеИзделия.Номенклатура КАК Номенклатура,
		|		ТЧВыходныеИзделия.Характеристика КАК Характеристика,
		|		ТЧВыходныеИзделия.Количество КАК Количество
		|	ИЗ
		|		ПартииИзделий КАК ПартииИзделий
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
		|			ПО ПартииИзделий.Партия = Реквизиты.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТЧВыходныеИзделия
		|			ПО ПартииИзделий.Партия = ТЧВыходныеИзделия.Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПартииИзделий.Период,
		|		ВыходныеИзделия.Ссылка,
		|		ВыходныеИзделия.Этап.Подразделение,
		|		ВыходныеИзделия.Номенклатура,
		|		ВыходныеИзделия.Характеристика,
		|		ВыходныеИзделия.Количество
		|	ИЗ
		|		ПартииИзделий КАК ПартииИзделий
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
		|			ПО ПартииИзделий.Партия = ВыходныеИзделия.Ссылка) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродукцияПредварительная.Продукция КАК Продукция,
		|	ПродукцияПредварительная.Характеристика КАК Характеристика,
		|	ПродукцияПредварительная.Период КАК Период,
		|	МАКСИМУМ(ЕСТЬNULL(ЦеныНоменклатуры.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодЦены,
		|	&ВидЦены КАК ВидЦены
		|ПОМЕСТИТЬ АктуальныеПериодыУстановкиЦен
		|ИЗ
		|	ПродукцияПредварительная КАК ПродукцияПредварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ПродукцияПредварительная.Период >= ЦеныНоменклатуры.Период
		|			И ПродукцияПредварительная.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И ПродукцияПредварительная.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И (ЦеныНоменклатуры.ВидЦены = &ВидЦены)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродукцияПредварительная.Продукция,
		|	ПродукцияПредварительная.Характеристика,
		|	ПродукцияПредварительная.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктуальныеПериодыУстановкиЦен.Продукция КАК Номенклатура,
		|	АктуальныеПериодыУстановкиЦен.Характеристика КАК Характеристика,
		|	АктуальныеПериодыУстановкиЦен.Период КАК Период,
		|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ИЗ
		|	АктуальныеПериодыУстановкиЦен КАК АктуальныеПериодыУстановкиЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО АктуальныеПериодыУстановкиЦен.ПериодЦены = ЦеныНоменклатуры.Период
		|			И АктуальныеПериодыУстановкиЦен.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И АктуальныеПериодыУстановкиЦен.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И АктуальныеПериодыУстановкиЦен.ВидЦены = ЦеныНоменклатуры.ВидЦены
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	АктуальныеПериодыУстановкиЦен.Продукция,
		|	АктуальныеПериодыУстановкиЦен.Характеристика,
		|	АктуальныеПериодыУстановкиЦен.Период,
		|	ЦеныНоменклатуры.Упаковка,
		|	ЦеныНоменклатуры.Цена,
		|	ЦеныНоменклатуры.Валюта
		|ИЗ
		|	АктуальныеПериодыУстановкиЦен КАК АктуальныеПериодыУстановкиЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО АктуальныеПериодыУстановкиЦен.ПериодЦены = ЦеныНоменклатуры.Период
		|			И АктуальныеПериодыУстановкиЦен.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И (ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) = ЦеныНоменклатуры.Характеристика)
		|			И АктуальныеПериодыУстановкиЦен.ВидЦены = ЦеныНоменклатуры.ВидЦены";

	УстановитьПривилегированныйРежим(Истина);
	Материалы = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	КоэффициентыУпаковок = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентыУпаковок(Материалы);
	
	Для Каждого Материал Из Материалы Цикл
		
		УпаковкиПоМатериалу = КоэффициентыУпаковок.Получить(Материал.Номенклатура);
		Если УпаковкиПоМатериалу = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		КоэффициентУпаковки = УпаковкиПоМатериалу.Получить(Материал.Упаковка);
		Если Не ЗначениеЗаполнено(КоэффициентУпаковки) Тогда
			Продолжить;
		КонецЕсли;
		
		Материал.Цена = Материал.Цена / КоэффициентУпаковки;
		
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Период КАК Период,
		|	Т.Упаковка КАК Упаковка,
		|	Т.Цена КАК Цена,
		|	Т.Валюта КАК Валюта
		|ПОМЕСТИТЬ ЦеныНоменклатурыПредварительная
		|ИЗ
		|	&Материалы КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Период КАК Период,
		|	Т.Упаковка КАК Упаковка,
		|	Т.Цена КАК Цена,
		|	Т.Валюта КАК Валюта
		|ПОМЕСТИТЬ ЦеныПродукции
		|ИЗ
		|	ЦеныНоменклатурыПредварительная КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Т.Номенклатура,
		|	Т.Характеристика,
		|	Т.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ПлановыеМатериальныеЗатраты.ПериодЗатрат, МЕСЯЦ) КАК Период,
		|	&Организация КАК Организация,
		|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
		|	""Материалы"" КАК ТипБазы,
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Затрата,
		|	ПлановыеМатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеМатериальныеЗатраты.Количество КАК Количество,
		|	ПлановыеМатериальныеЗатраты.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	ПлановыеМатериальныеЗатраты.СтоимостьРегл КАК СтоимостьРегл,
		|	ПлановыеМатериальныеЗатраты.Стоимость КАК Стоимость		
		|ПОМЕСТИТЬ БазыРаспределенияПредварительная
		|ИЗ
		|	МатериальныеЗатратыВключаяПолуфабрикаты КАК ПлановыеМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО ПлановыеМатериальныеЗатраты.Номенклатура = СН.Ссылка
		|ГДЕ
		|	НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ПлановыеТрудозатраты.ПериодЗатрат, МЕСЯЦ),
		|	&Организация,
		|	ПлановыеТрудозатраты.Подразделение,
		|	""Трудозатраты"",
		|	ПлановыеТрудозатраты.ВидРабот,
		|	ПлановыеТрудозатраты.ПартияСпецификация,
		|	ПлановыеТрудозатраты.Количество,
		|	ПлановыеТрудозатраты.Стоимость,
		|	ПлановыеТрудозатраты.СтоимостьРегл,
		|	ПлановыеТрудозатраты.Стоимость
		|ИЗ
		|	ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Предварительная.Период,
		|	Предварительная.Организация,
		|	Предварительная.Подразделение,
		|	Предварительная.ТипБазы,
		|	Предварительная.Продукция,
		|	Предварительная.ПартияСпецификация,
		|	Предварительная.Количество,
		|	Предварительная.Количество * ЕСТЬNULL(ЦеныПродукции.Цена, 0) * ВЫБОР
		|				КОГДА ЦеныПродукции.Валюта = КурсыВалютУУ.Валюта
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.Кратность, 0) * КурсыВалютУУ.Курс = 0
		|							ТОГДА 1
		|						ИНАЧЕ КурсыВалютНоменклатуры.Курс * КурсыВалютУУ.Кратность / (КурсыВалютНоменклатуры.Кратность * КурсыВалютУУ.Курс)
		|					КОНЕЦ
		|			КОНЕЦ * ВЫБОР
		|				КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|					ТОГДА 1
		|				КОГДА ВидыЦен.ЦенаВключаетНДС
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
		|							ТОГДА 1.1
		|						КОГДА Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|								ИЛИ Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|							ТОГДА 0
		|						ИНАЧЕ 1.18
		|					КОНЕЦ
		|			КОНЕЦ КАК Стоимость,
		|	Предварительная.Количество * ЕСТЬNULL(ЦеныПродукции.Цена, 0) 
		|		* ВЫБОР
		|			КОГДА ЦеныПродукции.Валюта = КурсыВалютБУ.Валюта
		|				ТОГДА 1
		|			КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.Кратность, 0) * КурсыВалютБУ.Курс = 0
		|				ТОГДА 1
		|			ИНАЧЕ 
		|				КурсыВалютНоменклатуры.Курс * КурсыВалютБУ.Кратность / (КурсыВалютНоменклатуры.Кратность * КурсыВалютБУ.Курс)
		|		КОНЕЦ / ВЫБОР
		|			КОГДА Предварительная.Количество < 0
		|				ИЛИ ВидыЦен.Ссылка ЕСТЬ NULL
		|				ИЛИ НЕ ВидыЦен.ЦенаВключаетНДС
		|				ТОГДА 1
		|			ИНАЧЕ 
		|				ВЫБОР
		|					КОГДА Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
		|						ТОГДА 1.1
		|					КОГДА Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|						ИЛИ Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|						ТОГДА 1
		|					ИНАЧЕ 1.18
		|				КОНЕЦ
		|	КОНЕЦ КАК СтоимостьРегл,
		|	Предварительная.Количество * ЕСТЬNULL(ЦеныПродукции.Цена, 0) 
		|		* ВЫБОР
		|			КОГДА ЦеныПродукции.Валюта = КурсыВалютУУ.Валюта
		|				ИЛИ ЕСТЬNULL(КурсыВалютНоменклатуры.Кратность, 0) * КурсыВалютУУ.Курс = 0
		|				ТОГДА 1
		|			ИНАЧЕ КурсыВалютНоменклатуры.Курс * КурсыВалютУУ.Кратность / (КурсыВалютНоменклатуры.Кратность * КурсыВалютУУ.Курс)
		|		КОНЕЦ 
		|			/ ВЫБОР
		|				КОГДА Предварительная.Количество < 0
		|					ИЛИ ВидыЦен.Ссылка ЕСТЬ NULL
		|					ИЛИ НЕ ВидыЦен.ЦенаВключаетНДС
		|					ТОГДА 1
		|				ИНАЧЕ 
		|					ВЫБОР
		|						КОГДА Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
		|							ТОГДА 1.1
		|						КОГДА Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0)
		|							ИЛИ Предварительная.Продукция.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)
		|							ТОГДА 1
		|						ИНАЧЕ 1.18
		|					КОНЕЦ
		|			КОНЕЦ КАК СтоимостьБезНДС
		|ИЗ
		|	ПродукцияПредварительная КАК Предварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПродукции КАК ЦеныПродукции
		|		ПО Предварительная.Период = ЦеныПродукции.Период
		|			И Предварительная.Продукция = ЦеныПродукции.Номенклатура
		|			И Предварительная.Характеристика = ЦеныПродукции.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалютНоменклатуры КАК КурсыВалютНоменклатуры
		|		ПО (ЦеныПродукции.Валюта = КурсыВалютНоменклатуры.Валюта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютБУ
		|		ПО (КурсыВалютБУ.ВидВалюты = ""ВалютаРеглУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО (ВидыЦен.Ссылка = &ВидЦены)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БазыРаспределенияПредварительная.Период КАК Период,
		|	БазыРаспределенияПредварительная.Организация КАК Организация,
		|	БазыРаспределенияПредварительная.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА НЕ &АналитическийУчетПоГруппамПродукции 
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ
		|			ЕСТЬNULL(РеквизитыПартии.ПартияПроизводства.ГруппаПродукции, ЕСТЬNULL(ИзделияСпецификации.Номенклатура.ГруппаАналитическогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)))
		|	КОНЕЦ КАК ГруппаПродукции,
		|	БазыРаспределенияПредварительная.ТипБазы КАК ТипБазы,
		|	БазыРаспределенияПредварительная.Затрата КАК Затрата,
		|	БазыРаспределенияПредварительная.ПартияСпецификация КАК ПартияСпецификация,
		|	БазыРаспределенияПредварительная.Количество КАК Количество,
		|	БазыРаспределенияПредварительная.Стоимость КАК Стоимость,
		|	БазыРаспределенияПредварительная.СтоимостьРегл КАК СтоимостьРегл,
		|	БазыРаспределенияПредварительная.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	ВЫБОР
		|		КОГДА БазыРаспределенияПредварительная.ТипБазы = ""Трудозатраты""
		|			ТОГДА 0
		|		ИНАЧЕ &Объем * БазыРаспределенияПредварительная.Количество
		|	КОНЕЦ КАК Объем,
		|	ВЫБОР
		|		КОГДА БазыРаспределенияПредварительная.ТипБазы = ""Трудозатраты""
		|			ТОГДА 0
		|		ИНАЧЕ &Вес * БазыРаспределенияПредварительная.Количество
		|	КОНЕЦ КАК Вес
		|ПОМЕСТИТЬ БазыРаспределения
		|ИЗ
		|	БазыРаспределенияПредварительная КАК БазыРаспределенияПредварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО БазыРаспределенияПредварительная.Затрата = СН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ИзделияСпецификации
		|			ПО БазыРаспределенияПредварительная.ПартияСпецификация = ИзделияСпецификации.Ссылка
		|			И ИзделияСпецификации.НомерСтроки = 1
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыПартии
		|			ПО БазыРаспределенияПредварительная.ПартияСпецификация = РеквизитыПартии.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	Подразделение,
		|	Затрата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(БазыРаспределения.Период, МЕСЯЦ) КАК Период
		|ПОМЕСТИТЬ ПериодыПоМесяцам
		|ИЗ
		|	БазыРаспределения КАК БазыРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	МАКСИМУМ(Нормативы.Ссылка) КАК Ссылка
		|ПОМЕСТИТЬ АктуальныеДокументыПоПериодам
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(Нормативы.Дата) КАК Дата,
		|		Нормативы.Организация КАК Организация,
		|		Нормативы.Подразделение КАК Подразделение,
		|		ПериодыПоМесяцам.Период КАК Период
		|	ИЗ
		|		ПериодыПоМесяцам КАК ПериодыПоМесяцам
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов КАК Нормативы
		|			ПО ПериодыПоМесяцам.Период >= Нормативы.ДатаНачалаДействия
		|				И (НЕ Нормативы.ПометкаУдаления)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыПоМесяцам.Период,
		|		Нормативы.Организация,
		|		Нормативы.Подразделение) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов КАК Нормативы
		|		ПО ВложенныйЗапрос.Дата = Нормативы.Дата
		|			И ВложенныйЗапрос.Организация = Нормативы.Организация
		|			И ВложенныйЗапрос.Подразделение = Нормативы.Подразделение
		|			И (НЕ Нормативы.ПометкаУдаления)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПравилаРаспределенияРасходовОтборПоМатериалам.Ссылка КАК Ссылка,
		|	ПравилаРаспределенияРасходовОтборПоМатериалам.Материал КАК ПозицияОтбора
		|ПОМЕСТИТЬ ПозицииОтборовПоЗатратам
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоМатериалам КАК ПравилаРаспределенияРасходовОтборПоМатериалам
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПравилаРаспределенияРасходовОтборПоВидамРабот.Ссылка,
		|	ПравилаРаспределенияРасходовОтборПоВидамРабот.ВидРабот
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоВидамРабот КАК ПравилаРаспределенияРасходовОтборПоВидамРабот
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПравилаРаспределенияРасходовОтборПоПродукции.Ссылка,
		|	ПравилаРаспределенияРасходовОтборПоПродукции.Продукция
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоПродукции КАК ПравилаРаспределенияРасходовОтборПоПродукции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПравилаРаспределенияРасходовОтборПоГруппамПродукции.Ссылка КАК Ссылка,
		|	ПравилаРаспределенияРасходовОтборПоГруппамПродукции.ГруппаПродукции КАК ПозицияОтбора
		|ПОМЕСТИТЬ ПозицииОтборовПоГруппамПродукции
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоГруппамПродукции КАК ПравилаРаспределенияРасходовОтборПоГруппамПродукции
		|ГДЕ
		|	&АналитическийУчетПоГруппамПродукции
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктуальныеДокументыПоПериодам.Период КАК Период,
		|	АктуальныеДокументыПоПериодам.Организация КАК Организация,
		|	АктуальныеДокументыПоПериодам.Подразделение КАК Подразделение,
		|	НормыПостоянныхРасходов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	НормыПостоянныхРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	НормыПостоянныхРасходов.Норматив КАК Норматив,
		|	НормыПостоянныхРасходов.Кратность КАК Кратность,
		|	ПравилаРаспределенияРасходов.БазаРаспределенияПоПартиям КАК БазаРаспределения,
		|	ПозицииОтборовПоЗатратам.ПозицияОтбора КАК ПозицияОтбораПоЗатратам,
		|	ПозицииОтборовПоГруппамПродукции.ПозицияОтбора КАК ПозицияОтбораПоГруппамПродукции
		|ПОМЕСТИТЬ АктуальныеНормативыПоПериодам
		|ИЗ
		|	АктуальныеДокументыПоПериодам КАК АктуальныеДокументыПоПериодам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов.НормыПостоянныхРасходов КАК НормыПостоянныхРасходов
		|		ПО АктуальныеДокументыПоПериодам.Ссылка = НормыПостоянныхРасходов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределенияРасходов
		|		ПО (НормыПостоянныхРасходов.СтатьяРасходов.ПравилоРаспределенияРасходов = ПравилаРаспределенияРасходов.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПозицииОтборовПоЗатратам КАК ПозицииОтборовПоЗатратам
		|		ПО (ПравилаРаспределенияРасходов.Ссылка = ПозицииОтборовПоЗатратам.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПозицииОтборовПоГруппамПродукции КАК ПозицииОтборовПоГруппамПродукции
		|		ПО (ПравилаРаспределенияРасходов.Ссылка = ПозицииОтборовПоГруппамПродукции.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктуальныеДокументыПоПериодам.Период,
		|	АктуальныеДокументыПоПериодам.Организация,
		|	АктуальныеДокументыПоПериодам.Подразделение,
		|	НормыПеременныхРасходов.СтатьяКалькуляции,
		|	НормыПеременныхРасходов.СтатьяРасходов,
		|	НормыПеременныхРасходов.Норматив,
		|	НормыПеременныхРасходов.Кратность,
		|	ПравилаРаспределенияРасходов.БазаРаспределенияПоПартиям,
		|	ПозицииОтборовПоЗатратам.ПозицияОтбора,
		|	ПозицииОтборовПоГруппамПродукции.ПозицияОтбора
		|ИЗ
		|	АктуальныеДокументыПоПериодам КАК АктуальныеДокументыПоПериодам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов.НормыПеременныхРасходов КАК НормыПеременныхРасходов
		|		ПО АктуальныеДокументыПоПериодам.Ссылка = НормыПеременныхРасходов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределенияРасходов
		|		ПО (НормыПеременныхРасходов.СтатьяРасходов.ПравилоРаспределенияРасходов = ПравилаРаспределенияРасходов.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПозицииОтборовПоЗатратам КАК ПозицииОтборовПоЗатратам
		|		ПО (ПравилаРаспределенияРасходов.Ссылка = ПозицииОтборовПоЗатратам.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПозицииОтборовПоГруппамПродукции КАК ПозицииОтборовПоГруппамПродукции
		|		ПО (ПравилаРаспределенияРасходов.Ссылка = ПозицииОтборовПоГруппамПродукции.Ссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	БазаРаспределения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеПрочиеЗатратыПредварительная.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеПрочиеЗатратыПредварительная.Калькуляция КАК Калькуляция,
		|	ПлановыеПрочиеЗатратыПредварительная.Подразделение КАК Подразделение,
		|	ПлановыеПрочиеЗатратыПредварительная.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыеПрочиеЗатратыПредварительная.СтатьяРасходов КАК СтатьяРасходов,
		|	ПлановыеПрочиеЗатратыПредварительная.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	ПлановыеПрочиеЗатратыПредварительная.Период КАК Период,
		|	ПлановыеПрочиеЗатратыПредварительная.Норматив 
		|		/ ПлановыеПрочиеЗатратыПредварительная.Кратность 
		|		* ВЫБОР
		|		КОГДА КурсыВалютРегл.Валюта = КурсыВалютУУ.Валюта
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|				КОГДА КурсыВалютУУ.Кратность * КурсыВалютРегл.Курс = 0
		|					ТОГДА 1
		|				ИНАЧЕ КурсыВалютУУ.Курс * КурсыВалютРегл.Кратность / (КурсыВалютУУ.Кратность * КурсыВалютРегл.Курс)
		|			КОНЕЦ
		|	КОНЕЦ * ПлановыеПрочиеЗатратыПредварительная.СтоимостьРегл КАК СтоимостьРегл,
		|	ПлановыеПрочиеЗатратыПредварительная.Стоимость КАК Стоимость
		|ПОМЕСТИТЬ ПлановыеПрочиеЗатраты
		|ИЗ
		|	(ВЫБРАТЬ
		|		БазыРаспределения.ПартияСпецификация,
		|		&Калькуляция КАК Калькуляция,
		|		БазыРаспределения.Подразделение,
		|		АктуальныеНормативыПоПериодам.СтатьяКалькуляции,
		|		АктуальныеНормативыПоПериодам.СтатьяРасходов,
		|		ВЫБОР
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
		|				ТОГДА БазыРаспределения.Стоимость
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции))
		|				ТОГДА БазыРаспределения.Количество
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции))
		|				ТОГДА БазыРаспределения.Объем
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции))
		|				ТОГДА БазыРаспределения.Вес
		|			ИНАЧЕ 0
		|		КОНЕЦ * АктуальныеНормативыПоПериодам.Норматив / АктуальныеНормативыПоПериодам.Кратность КАК Стоимость,
		|		ВЫБОР
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
		|				ТОГДА БазыРаспределения.СтоимостьБезНДС
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции))
		|				ТОГДА БазыРаспределения.Количество
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции))
		|				ТОГДА БазыРаспределения.Объем
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции))
		|				ТОГДА БазыРаспределения.Вес
		|			ИНАЧЕ 0
		|		КОНЕЦ * АктуальныеНормативыПоПериодам.Норматив / АктуальныеНормативыПоПериодам.Кратность КАК СтоимостьБезНДС,
		|		ВЫБОР
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
		|				ТОГДА БазыРаспределения.СтоимостьРегл
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции))
		|				ТОГДА БазыРаспределения.Количество
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции))
		|				ТОГДА БазыРаспределения.Объем
		|			КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов),
		|																	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции))
		|				ТОГДА БазыРаспределения.Вес
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СтоимостьРегл,
		|		АктуальныеНормативыПоПериодам.Норматив,
		|		АктуальныеНормативыПоПериодам.Кратность,
		|		БазыРаспределения.Период
		|	ИЗ
		|		АктуальныеНормативыПоПериодам КАК АктуальныеНормативыПоПериодам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазыРаспределения КАК БазыРаспределения
		|			ПО АктуальныеНормативыПоПериодам.Период = БазыРаспределения.Период
		|				И АктуальныеНормативыПоПериодам.Организация = БазыРаспределения.Организация
		|				И АктуальныеНормативыПоПериодам.Подразделение = БазыРаспределения.Подразделение
		|				И ВЫБОР 
		|					КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов))
		|					ТОГДА БазыРаспределения.ТипБазы = ""Трудозатраты""
		|					КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов))
		|					ТОГДА БазыРаспределения.ТипБазы = ""Материалы""
		|					КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
		|					ТОГДА БазыРаспределения.ТипБазы В (""Материалы"", ""Трудозатраты"")
		|					КОГДА АктуальныеНормативыПоПериодам.БазаРаспределения В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции),
		|																				ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции))
		|					ТОГДА БазыРаспределения.ТипБазы = ""Продукция""
		|					КОНЕЦ
		|				И (АктуальныеНормативыПоПериодам.ПозицияОтбораПоЗатратам = БазыРаспределения.Затрата
		|				ИЛИ АктуальныеНормативыПоПериодам.ПозицияОтбораПоЗатратам ЕСТЬ NULL)
		|				И (АктуальныеНормативыПоПериодам.ПозицияОтбораПоГруппамПродукции = БазыРаспределения.ГруппаПродукции
		|				ИЛИ АктуальныеНормативыПоПериодам.ПозицияОтбораПоГруппамПродукции ЕСТЬ NULL)) КАК ПлановыеПрочиеЗатратыПредварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРегл
		|		ПО (КурсыВалютРегл.ВидВалюты = ""ВалютаРеглУчета"")
		|;
		|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Объем", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("СН.ЕдиницаИзмерения", "СН"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Вес", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("СН.ЕдиницаИзмерения", "СН"));
	
	Запрос.УстановитьПараметр("ТрудИМатериалы", ТрудИМатериалы);
	Запрос.УстановитьПараметр("ТипыБазПродукция", ТипыБазПродукция);
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции", ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	Запрос.УстановитьПараметр("Материалы", Материалы);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	УниверсальныеМеханизмыПартийИСебестоимости.УничтожитьВременныеТаблицы(МенеджерВТ,
		"МножителиПартийИПолуфабрикатов,МатериальныеЗатратыВключаяПолуфабрикаты,БазыРаспределенияПредварительная,БазыРаспределения,
		|ПериодыПоМесяцам,АктуальныеДокументыПоПериодам,ПозицииОтборовПоЗатратам,ПозицииОтборовПоГруппамПродукции,АктуальныеНормативыПоПериодам,ПартииИзделий,
		|ПродукцияПредварительная, АктуальныеПериодыУстановкиЦен, ЦеныНоменклатурыПредварительная, ЦеныПродукции");
	
КонецПроцедуры

Процедура РассчитатьПредварительнуюСтоимость(Калькуляция, МенеджерВТ)
	
	УдалитьТаблицуПрочихЗатрат = Ложь;
	
	Если МенеджерВТ.Таблицы.Найти("ПлановыеПрочиеЗатраты") = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	*
			|ПОМЕСТИТЬ ПлановыеПрочиеЗатраты
			|ИЗ
			|	РегистрНакопления.ПлановыеПрочиеЗатраты";
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		УдалитьТаблицуПрочихЗатрат = Истина;
		
	КонецЕсли;
	
	РассчитатьЦеныМатериалов(Калькуляция, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МатериальныеЗатраты.Калькуляция КАК Калькуляция,
		|	МатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	МатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	МатериальныеЗатраты.Характеристика КАК Характеристика,
		|	МатериальныеЗатраты.Подразделение КАК Подразделение,
		|	МатериальныеЗатраты.Период КАК Период,
		|	МатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	МатериальныеЗатраты.Количество КАК Количество,
		|	МатериальныеЗатраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.Цена
		|		ИНАЧЕ МатериальныеЗатраты.Стоимость
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаБезНДС
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьБезНДС
		|	КОНЕЦ КАК СтоимостьБезНДС,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаРегл
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьРегл
		|	КОНЕЦ КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаЗабаланс
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьЗабалансовая
		|	КОНЕЦ КАК СтоимостьЗабалансовая,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаЗабалансРегл
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьЗабалансоваяРегл
		|	КОНЕЦ КАК СтоимостьЗабалансоваяРегл,
		|	МатериальныеЗатраты.ПериодЗатрат КАК ПериодЗатрат
		|ПОМЕСТИТЬ МатериальныеЗатратыВключаяПолуфабрикаты
		|ИЗ
		|	МатериальныеЗатраты КАК МатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныМатериалов КАК ЦеныМатериалов
		|		ПО МатериальныеЗатраты.Калькуляция = ЦеныМатериалов.Калькуляция
		|			И МатериальныеЗатраты.Номенклатура = ЦеныМатериалов.Номенклатура
		|			И МатериальныеЗатраты.Характеристика = ЦеныМатериалов.Характеристика
		|			И (МатериальныеЗатраты.ЭтоПолуфабрикат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МатериальныеЗатраты.ПартияСпецификация,
		|	МатериальныеЗатраты.Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МножителиПартийИПолуфабрикатов.Период КАК Период,
		|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок КАК Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен КАК РасчетЗавершен
		|ПОМЕСТИТЬ ВТПродукция
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период КАК Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок КАК Порядок,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен КАК РасчетЗавершен,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК Материальные,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеБезНДС,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеРегл,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансовая * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеЗабаланс,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансоваяРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеЗабалансРегл,
		|	0 КАК Трудозатраты,
		|	0 КАК ТрудозатратыРегл,
		|	0 КАК ПостатейныеПостоянные,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременные,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель
		|ПОМЕСТИТЬ Затраты
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериальныеЗатратыВключаяПолуфабрикаты КАК МатериальныеЗатратыВключаяПолуфабрикаты
		|		ПО МножителиПартийИПолуфабрикатов.Калькуляция = МатериальныеЗатратыВключаяПолуфабрикаты.Калькуляция
		|			И МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката = МатериальныеЗатратыВключаяПолуфабрикаты.ПартияСпецификация
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ПлановыеТрудозатраты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель),
		|	ПлановыеТрудозатраты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель),
		|	0 КАК ПостатейныеПостоянные,
		|	0 КАК ПостатейныеПостоянныеБезНДС,
		|	0 КАК ПостатейныеПостоянныеРегл,
		|	0 КАК ПостатейныеПеременные,
		|	0 КАК ПостатейныеПеременныеБезНДС,
		|	0 КАК ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ПО МножителиПартийИПолуфабрикатов.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|			И МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеТрудозатраты.ПартияСпецификация
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|		ПО МножителиПартийИПолуфабрикатов.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|			И МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Период,
		|	ВТПродукция.Калькуляция,
		|	ВТПродукция.ПартияСпецификацияПродукции,
		|	ВТПродукция.Продукция,
		|	ВТПродукция.ХарактеристикаПродукции,
		|	ВТПродукция.Назначение,
		|	ВТПродукция.ПартияСпецификацияПолуфабриката,
		|	ВТПродукция.Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПолуфабриката,
		|	ВТПродукция.Порядок,
		|	ВТПродукция.КоличествоПолуфабриката,
		|	ВТПродукция.РасчетЗавершен,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьБезНДС * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансовая * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансоваяРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВТПродукция.Числитель,
		|	ВТПродукция.Знаменатель
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериальныеЗатратыВключаяПолуфабрикаты КАК МатериальныеЗатратыВключаяПолуфабрикаты
		|		ПО ВТПродукция.Калькуляция = МатериальныеЗатратыВключаяПолуфабрикаты.Калькуляция
		|			И ВТПродукция.ПартияСпецификацияПродукции = МатериальныеЗатратыВключаяПолуфабрикаты.ПартияСпецификация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Период,
		|	ВТПродукция.Калькуляция,
		|	ВТПродукция.ПартияСпецификацияПродукции,
		|	ВТПродукция.Продукция,
		|	ВТПродукция.ХарактеристикаПродукции,
		|	ВТПродукция.Назначение,
		|	ВТПродукция.ПартияСпецификацияПолуфабриката,
		|	ВТПродукция.Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПолуфабриката,
		|	ВТПродукция.Порядок,
		|	ВТПродукция.КоличествоПолуфабриката,
		|	ВТПродукция.РасчетЗавершен,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ПлановыеТрудозатраты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	ПлановыеТрудозатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВТПродукция.Числитель,
		|	ВТПродукция.Знаменатель
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ПО ВТПродукция.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|			И ВТПродукция.ПартияСпецификацияПродукции = ПлановыеТрудозатраты.ПартияСпецификация
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Период,
		|	ВТПродукция.Калькуляция,
		|	ВТПродукция.ПартияСпецификацияПродукции,
		|	ВТПродукция.Продукция,
		|	ВТПродукция.ХарактеристикаПродукции,
		|	ВТПродукция.Назначение,
		|	ВТПродукция.ПартияСпецификацияПолуфабриката,
		|	ВТПродукция.Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПолуфабриката,
		|	ВТПродукция.Порядок,
		|	ВТПродукция.КоличествоПолуфабриката,
		|	ВТПродукция.РасчетЗавершен,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|	КОНЕЦ,
		|	ВТПродукция.Числитель,
		|	ВТПродукция.Знаменатель
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|		ПО ВТПродукция.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|			И ВТПродукция.ПартияСпецификацияПродукции = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Затраты.Период КАК Период,
		|	Затраты.Продукция КАК Продукция,
		|	Затраты.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Затраты.Назначение КАК Назначение,
		|	СУММА(Затраты.Трудозатраты) КАК Трудозатраты,
		|	СУММА(Затраты.ТрудозатратыРегл) КАК ТрудозатратыРегл,
		|	СУММА(Затраты.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(Затраты.ПостатейныеПеременныеБезНДС) КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(Затраты.ПостатейныеПеременныеРегл) КАК ПостатейныеПеременныеРегл,
		|	СУММА(Затраты.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(Затраты.ПостатейныеПостоянныеБезНДС) КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(Затраты.ПостатейныеПостоянныеРегл) КАК ПостатейныеПостоянныеРегл,
		|	СУММА(Затраты.Материальные + Затраты.Трудозатраты + Затраты.ПостатейныеПеременные + Затраты.ПостатейныеПостоянные) КАК Стоимость,
		|	СУММА(Затраты.МатериальныеБезНДС + Затраты.Трудозатраты + Затраты.ПостатейныеПеременныеБезНДС + Затраты.ПостатейныеПостоянныеБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(Затраты.МатериальныеРегл + Затраты.ТрудозатратыРегл + Затраты.ПостатейныеПеременныеРегл + Затраты.ПостатейныеПостоянныеРегл) КАК СтоимостьРегл,
		|	СУММА(Затраты.МатериальныеЗабаланс) КАК СтоимостьЗабалансовая,
		|	СУММА(Затраты.МатериальныеЗабалансРегл) КАК СтоимостьЗабалансоваяРегл,
		|	Затраты.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Затраты.Полуфабрикат КАК Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Затраты.Порядок КАК Порядок,
		|	Затраты.Числитель КАК Числитель,
		|	Затраты.Знаменатель КАК Знаменатель,
		|	Затраты.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	Затраты.РасчетЗавершен КАК РасчетЗавершен,
		|	ЛОЖЬ КАК ПартияВыпущена
		|ПОМЕСТИТЬ МножителиПартийИПолуфабрикатов
		|ИЗ
		|	Затраты КАК Затраты
		|
		|СГРУППИРОВАТЬ ПО
		|	Затраты.Период,
		|	Затраты.Продукция,
		|	Затраты.ПартияСпецификацияПродукции,
		|	Затраты.Калькуляция,
		|	Затраты.ХарактеристикаПродукции,
		|	Затраты.Назначение,
		|	Затраты.ПартияСпецификацияПолуфабриката,
		|	Затраты.Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката,
		|	Затраты.Порядок,
		|	Затраты.Знаменатель,
		|	Затраты.Числитель,
		|	Затраты.КоличествоПолуфабриката,
		|	Затраты.РасчетЗавершен
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.Трудозатраты,
		|	МножителиПартийИПолуфабрикатов.ТрудозатратыРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл,
		|	МножителиПартийИПолуфабрикатов.Стоимость,
		|	МножителиПартийИПолуфабрикатов.СтоимостьБезНДС,
		|	МножителиПартийИПолуфабрикатов.СтоимостьРегл,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|	И МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныМатериалов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Затраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПродукция";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если УдалитьТаблицуПрочихЗатрат Тогда
		
		Запрос.Текст =
			"УНИЧТОЖИТЬ ПлановыеПрочиеЗатраты";
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Функция РассчитатьЦеныМатериалов(Калькуляция, МенеджерВТ)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	       
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Множители.Калькуляция,
		|	Множители.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Множители.Полуфабрикат КАК Номенклатура,
		|	Множители.ХарактеристикаПолуфабриката КАК Характеристика,
		|	Множители.Знаменатель КАК Знаменатель,
		|	МАКСИМУМ(Множители.Порядок) КАК Порядок,
		|	СУММА(Множители.КоличествоПолуфабриката) КАК КоличествоПолуфабриката,
		|	СУММА(Множители.Числитель) КАК Числитель,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.Стоимость / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьБезНДС / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьБезНДС,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьРегл,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьЗабалансовая / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьЗабалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьЗабалансоваяРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ НаборПолуфабрикатов
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|ГДЕ
		|	Множители.Калькуляция = &Калькуляция
		|	И НЕ Множители.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Множители.ХарактеристикаПолуфабриката,
		|	Множители.ПартияСпецификацияПолуфабриката,
		|	Множители.Калькуляция,
		|	Множители.Полуфабрикат,
		|	Множители.Знаменатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	НаборПолуфабрикатов.Номенклатура КАК Номенклатура,
		|	НаборПолуфабрикатов.Характеристика КАК Характеристика,
		|	НаборПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	НаборПолуфабрикатов.Порядок КАК Порядок,
		|	НаборПолуфабрикатов.КоличествоПолуфабриката КАК Количество,
		|	НаборПолуфабрикатов.Числитель КАК Числитель,
		|	НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияПолуфабриката
		|ИЗ
		|	НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаборПолуфабрикатов.Порядок УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	НаборПолуфабрикатов.Номенклатура КАК Номенклатура,
		|	НаборПолуфабрикатов.Характеристика КАК Характеристика,
		|	СУММА(НаборПолуфабрикатов.Стоимость) КАК Стоимость,
		|	СУММА(НаборПолуфабрикатов.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(НаборПолуфабрикатов.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(НаборПолуфабрикатов.СтоимостьЗабалансовая) КАК СтоимостьЗабалансовая,
		|	СУММА(НаборПолуфабрикатов.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ПолуфабрикатыСводно
		|ИЗ
		|	НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|
		|СГРУППИРОВАТЬ ПО
		|	НаборПолуфабрикатов.Характеристика,
		|	НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	НаборПолуфабрикатов.Калькуляция,
		|	НаборПолуфабрикатов.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.Номенклатура КАК Номенклатура,
		|	Затраты.Характеристика КАК Характеристика,
		|	Затраты.ПартияСпецификацияПолуфабриката КАК ПартияПолуфабриката,
		|	Затраты.Материал КАК Материал,
		|	Затраты.ХарактеристикаМатериала КАК ХарактеристикаМатериала,
		|	СУММА(Затраты.КоличествоМатериала) КАК КоличествоМатериала,
		|	СУММА(Затраты.Стоимость) КАК Стоимость,
		|	СУММА(Затраты.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(Затраты.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(Затраты.СтоимостьЗабалансовая) КАК СтоимостьЗабалансовая,
		|	СУММА(Затраты.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	(ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция КАК Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура КАК Номенклатура,
		|		НаборПолуфабрикатов.Характеристика КАК Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА ПМЗ.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КоличествоМатериала,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА 0
		|			ИНАЧЕ ПМЗ.Стоимость
		|		КОНЕЦ КАК Стоимость,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА 0
		|			ИНАЧЕ ПМЗ.СтоимостьБезНДС
		|		КОНЕЦ КАК СтоимостьБезНДС,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА 0
		|			ИНАЧЕ ПМЗ.СтоимостьРегл
		|		КОНЕЦ КАК СтоимостьРегл,
		|		0 КАК СтоимостьЗабалансовая,
		|		0 КАК СтоимостьЗабалансоваяРегл,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА ПМЗ.Номенклатура
		|			ИНАЧЕ НЕОПРЕДЕЛЕНО
		|		КОНЕЦ КАК Материал,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА ПМЗ.Характеристика
		|			ИНАЧЕ НЕОПРЕДЕЛЕНО
		|		КОНЕЦ КАК ХарактеристикаМатериала
		|	ИЗ
		|		ПолуфабрикатыСводно КАК НаборПолуфабрикатов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериальныеЗатраты КАК ПМЗ
		|			ПО НаборПолуфабрикатов.Калькуляция = ПМЗ.Калькуляция
		|				И НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПМЗ.ПартияСпецификация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура,
		|		НаборПолуфабрикатов.Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		0,
		|		Трудозатраты.Стоимость,
		|		0,
		|		Трудозатраты.СтоимостьРегл,
		|		0,
		|		0,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО
		|	ИЗ
		|		ПолуфабрикатыСводно КАК НаборПолуфабрикатов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеТрудозатраты КАК Трудозатраты
		|			ПО НаборПолуфабрикатов.Калькуляция = Трудозатраты.Калькуляция
		|				И НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката = Трудозатраты.ПартияСпецификация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура,
		|		НаборПолуфабрикатов.Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		0,
		|		ПлановыеПрочиеЗатраты.Стоимость,
		|		ПлановыеПрочиеЗатраты.СтоимостьБезНДС,
		|		ПлановыеПрочиеЗатраты.СтоимостьРегл,
		|		0,
		|		0,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО
		|	ИЗ
		|		НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|			ПО НаборПолуфабрикатов.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|				И НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура,
		|		НаборПолуфабрикатов.Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		0,
		|		НаборПолуфабрикатов.Стоимость,
		|		НаборПолуфабрикатов.СтоимостьБезНДС,
		|		НаборПолуфабрикатов.СтоимостьРегл,
		|		НаборПолуфабрикатов.СтоимостьЗабалансовая,
		|		НаборПолуфабрикатов.СтоимостьЗабалансоваяРегл,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО
		|	ИЗ
		|		НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|	ГДЕ
		|		НЕ(НаборПолуфабрикатов.Стоимость = 0
		|					И НаборПолуфабрикатов.СтоимостьБезНДС = 0
		|					И НаборПолуфабрикатов.СтоимостьРегл = 0
		|					И НаборПолуфабрикатов.СтоимостьЗабалансовая = 0
		|					И НаборПолуфабрикатов.СтоимостьЗабалансоваяРегл = 0)) КАК Затраты
		|
		|СГРУППИРОВАТЬ ПО
		|	Затраты.Номенклатура,
		|	Затраты.ХарактеристикаМатериала,
		|	Затраты.ПартияСпецификацияПолуфабриката,
		|	Затраты.Материал,
		|	Затраты.Калькуляция,
		|	Затраты.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПолуфабрикатыСводно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НаборПолуфабрикатов";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаПолуфабрикатов = МассивРезультатов[1].Выбрать();
	
	Затраты = МассивРезультатов[3].Выгрузить();
	Затраты.Индексы.Добавить("Калькуляция, Номенклатура, Характеристика, ПартияПолуфабриката");
	
	ОтборЗатрат = Новый Структура("Калькуляция, Номенклатура, Характеристика, ПартияПолуфабриката");
	
	КЧ = Новый КвалификаторыЧисла(15,2);
	
	ЦеныМатериалов = Новый ТаблицаЗначений;
	ЦеныМатериалов.Колонки.Добавить("Калькуляция", Новый ОписаниеТипов("ДокументСсылка.ПлановаяКалькуляция2_2"));
	ЦеныМатериалов.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ЦеныМатериалов.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ЦеныМатериалов.Колонки.Добавить("Цена", Новый ОписаниеТипов("Число", КЧ));
	ЦеныМатериалов.Колонки.Добавить("ЦенаБезНДС", Новый ОписаниеТипов("Число", КЧ));
	ЦеныМатериалов.Колонки.Добавить("ЦенаРегл", Новый ОписаниеТипов("Число", КЧ));
	ЦеныМатериалов.Колонки.Добавить("ЦенаЗабаланс", Новый ОписаниеТипов("Число", КЧ));
	ЦеныМатериалов.Колонки.Добавить("ЦенаЗабалансРегл", Новый ОписаниеТипов("Число", КЧ));
	
	ЦеныМатериалов.Индексы.Добавить("Калькуляция, Номенклатура, Характеристика");
	
	ОтборЦен = Новый Структура("Калькуляция, Номенклатура, Характеристика");
	
	Пока ВыборкаПолуфабрикатов.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборЗатрат, ВыборкаПолуфабрикатов);
		
		СтоимостьЗатрат = 0;
		СтоимостьЗатратБезНДС = 0;
		СтоимостьЗатратРегл = 0;
		СтоимостьЗатратЗабаланс = 0;
		СтоимостьЗатратЗабалансРегл = 0;
		
		ЗатратыПолуфабриката = Затраты.НайтиСтроки(ОтборЗатрат);
		Для Каждого Затрата Из ЗатратыПолуфабриката Цикл
			
			Если ЗначениеЗаполнено(Затрата.Материал) Тогда
				
				ЗаполнитьЗначенияСвойств(ОтборЦен, Затрата);
				ОтборЦен.Номенклатура = Затрата.Материал;
				ОтборЦен.Характеристика = Затрата.ХарактеристикаМатериала;
				
				СредняяЦена = 0;
				СредняяЦенаБезНДС = 0;
				СредняяЦенаРегл = 0;
				СредняяЦенаЗабаланс = 0;
				СредняяЦенаЗабалансРегл = 0;
				
				СтрокиСЦенами = ЦеныМатериалов.НайтиСтроки(ОтборЦен);
				
				Если СтрокиСЦенами.Количество() > 0 Тогда
					
					Для Каждого СтрокаЦены Из СтрокиСЦенами Цикл
						
						СредняяЦена = СредняяЦена + СтрокаЦены.Цена;
						СредняяЦенаБезНДС = СредняяЦенаБезНДС + СтрокаЦены.ЦенаБезНДС;
						СредняяЦенаРегл = СредняяЦенаРегл + СтрокаЦены.ЦенаРегл;
						СредняяЦенаЗабаланс = СредняяЦенаЗабаланс + СтрокаЦены.ЦенаЗабаланс;
						СредняяЦенаЗабалансРегл = СредняяЦенаЗабалансРегл + СтрокаЦены.ЦенаЗабалансРегл;
						
					КонецЦикла;
					
					СредняяЦена = СредняяЦена / СтрокиСЦенами.Количество();
					СредняяЦенаБезНДС = СредняяЦенаБезНДС / СтрокиСЦенами.Количество();
					СредняяЦенаРегл = СредняяЦенаРегл / СтрокиСЦенами.Количество();
					СредняяЦенаЗабаланс = СредняяЦенаЗабаланс / СтрокиСЦенами.Количество();
					СредняяЦенаЗабалансРегл = СредняяЦенаЗабалансРегл / СтрокиСЦенами.Количество();
					
				КонецЕсли;
				
				СтоимостьЗатрат = СтоимостьЗатрат + СредняяЦена * Затрата.КоличествоМатериала;
				СтоимостьЗатратБезНДС = СтоимостьЗатратБезНДС + СредняяЦенаБезНДС * Затрата.КоличествоМатериала;
				СтоимостьЗатратРегл = СтоимостьЗатратРегл + СредняяЦенаРегл * Затрата.КоличествоМатериала;
				СтоимостьЗатратЗабаланс = СтоимостьЗатратЗабаланс + СредняяЦенаЗабаланс * Затрата.КоличествоМатериала;
				СтоимостьЗатратЗабалансРегл = СтоимостьЗатратЗабалансРегл + СредняяЦенаЗабалансРегл * Затрата.КоличествоМатериала;
				
			Иначе
				
				СтоимостьЗатрат = СтоимостьЗатрат + Затрата.Стоимость;
				СтоимостьЗатратБезНДС = СтоимостьЗатратБезНДС + Затрата.СтоимостьБезНДС;
				СтоимостьЗатратРегл = СтоимостьЗатратРегл + Затрата.СтоимостьРегл;
				СтоимостьЗатратЗабаланс = СтоимостьЗатратЗабаланс + Затрата.СтоимостьЗабалансовая;
				СтоимостьЗатратЗабалансРегл = СтоимостьЗатратЗабалансРегл + Затрата.СтоимостьЗабалансоваяРегл;
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЦенаМатериала = ЦеныМатериалов.Добавить();
		ЗаполнитьЗначенияСвойств(ЦенаМатериала, ВыборкаПолуфабрикатов);
		
		Если Не ВыборкаПолуфабрикатов.Количество = 0 Тогда
			
			ЦенаМатериала.Цена = СтоимостьЗатрат * ВыборкаПолуфабрикатов.Числитель / ВыборкаПолуфабрикатов.Знаменатель / ВыборкаПолуфабрикатов.Количество;
			ЦенаМатериала.ЦенаБезНДС = СтоимостьЗатратБезНДС * ВыборкаПолуфабрикатов.Числитель / ВыборкаПолуфабрикатов.Знаменатель / ВыборкаПолуфабрикатов.Количество;
			ЦенаМатериала.ЦенаРегл = СтоимостьЗатратРегл * ВыборкаПолуфабрикатов.Числитель / ВыборкаПолуфабрикатов.Знаменатель / ВыборкаПолуфабрикатов.Количество;
			ЦенаМатериала.ЦенаЗабаланс = СтоимостьЗатратЗабаланс * ВыборкаПолуфабрикатов.Числитель / ВыборкаПолуфабрикатов.Знаменатель / ВыборкаПолуфабрикатов.Количество;
			ЦенаМатериала.ЦенаЗабалансРегл = СтоимостьЗатратЗабалансРегл * ВыборкаПолуфабрикатов.Числитель / ВыборкаПолуфабрикатов.Знаменатель / ВыборкаПолуфабрикатов.Количество;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Калькуляция КАК Калькуляция,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Цена КАК Цена,
		|	Т.ЦенаБезНДС КАК ЦенаБезНДС,
		|	Т.ЦенаРегл КАК ЦенаРегл,
		|	Т.ЦенаЗабаланс КАК ЦенаЗабаланс,
		|	Т.ЦенаЗабалансРегл КАК ЦенаЗабалансРегл
		|ПОМЕСТИТЬ ПредварительныеЦеныМатериалов
		|ИЗ
		|	&ЦеныМатериалов КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПредварительныеЦеныМатериалов.Калькуляция КАК Калькуляция,
		|	ПредварительныеЦеныМатериалов.Номенклатура КАК Номенклатура,
		|	ПредварительныеЦеныМатериалов.Характеристика КАК Характеристика,
		|	СРЕДНЕЕ(ПредварительныеЦеныМатериалов.Цена) КАК Цена,
		|	СРЕДНЕЕ(ПредварительныеЦеныМатериалов.ЦенаБезНДС) КАК ЦенаБезНДС,
		|	СРЕДНЕЕ(ПредварительныеЦеныМатериалов.ЦенаРегл) КАК ЦенаРегл,
		|	СРЕДНЕЕ(ПредварительныеЦеныМатериалов.ЦенаЗабаланс) КАК ЦенаЗабаланс,
		|	СРЕДНЕЕ(ПредварительныеЦеныМатериалов.ЦенаЗабалансРегл) КАК ЦенаЗабалансРегл
		|ПОМЕСТИТЬ ЦеныМатериалов
		|ИЗ
		|	ПредварительныеЦеныМатериалов КАК ПредварительныеЦеныМатериалов
		|
		|СГРУППИРОВАТЬ ПО
		|	ПредварительныеЦеныМатериалов.Калькуляция,
		|	ПредварительныеЦеныМатериалов.Номенклатура,
		|	ПредварительныеЦеныМатериалов.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПредварительныеЦеныМатериалов";
	
	Запрос.УстановитьПараметр("ЦеныМатериалов", ЦеныМатериалов);

	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецФункции

Процедура ПривестиЧислительИЗнаменатель(Множитель)
	
	Если Множитель.Числитель % Множитель.Знаменатель = 0 Тогда
		
		Множитель.Числитель = Множитель.Числитель / Множитель.Знаменатель;
		Множитель.Знаменатель = 1;
		
		Возврат;
		
	КонецЕсли;
	
	Если Множитель.Знаменатель % Множитель.Числитель = 0 Тогда
		
		Множитель.Знаменатель = Множитель.Знаменатель / Множитель.Числитель;
		Множитель.Числитель = 1;
		
		Возврат;
		
	КонецЕсли;
	
	Число1 = Множитель.Числитель;
	Число2 = Множитель.Знаменатель;
	
	Пока Не (Число1 = 0 Или Число2 = 0) Цикл
		Если Число1 > Число2 Тогда
			Число1 = Число1 % Число2;
		Иначе
			Число2 = Число2 % Число1;
		КонецЕсли;
	КонецЦикла;
	
	Множитель.Числитель = Множитель.Числитель / (Число1 + Число2);
	Множитель.Знаменатель = Множитель.Знаменатель / (Число1 + Число2);
	
КонецПроцедуры

Процедура ПривестиМножителиКОбщемуЗнаменателю(Множители)
	
	КолонкиУникальности = "Период, ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, Порядок, РасчетЗавершен, ПартияВыпущена";	
	КопияМножителей = Множители.Скопировать(, КолонкиУникальности);
	
	КопияМножителей.Колонки.Добавить("Дубль");
	КопияМножителей.ЗаполнитьЗначения(1, "Дубль");
	
	КопияМножителей.Свернуть(КолонкиУникальности, "Дубль");
	
	Множители.Индексы.Добавить(КолонкиУникальности);
	ОтборМножителей = Новый Структура(КолонкиУникальности);
	
	Для Каждого Множитель Из КопияМножителей Цикл
		
		Если Множитель.Дубль <= 1 Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОтборМножителей, Множитель);
		МножителиДляПриведения = Множители.НайтиСтроки(ОтборМножителей);
		
		Индекс = 0;
		ОбщийЗнаменатель = 1;
		КоличествоСлагаемых = МножителиДляПриведения.Количество();
		РасчетЗнаменателя = Истина;
		
		Пока Индекс < КоличествоСлагаемых Цикл
			
			МножительДляПриведения = МножителиДляПриведения[Индекс];
			Если РасчетЗнаменателя Тогда
				ОбщийЗнаменатель = ОбщийЗнаменатель * МножительДляПриведения.Знаменатель;
			Иначе
				
				МножительДляПриведения.Числитель = ОбщийЗнаменатель / МножительДляПриведения.Знаменатель * МножительДляПриведения.Числитель;
				МножительДляПриведения.Знаменатель = ОбщийЗнаменатель;
				
			КонецЕсли;
			
			Индекс = Индекс + 1;
			Если Индекс >= КоличествоСлагаемых И РасчетЗнаменателя Тогда
				
				// Сброс цикла на начало и расчет числителей.
				РасчетЗнаменателя = Ложь;
				Индекс = 0;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьСлужебныеВременныеТаблицы(МенеджерВТ, Калькуляция)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВалютаУправленческогоУчета.Значение КАК Валюта,
		|	""ВалютаУпрУчета"" КАК ВидВалюты
		|ПОМЕСТИТЬ Валюты
		|ИЗ
		|	Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВалютаРегламентированногоУчета.Значение,
		|	""ВалютаРеглУчета""
		|ИЗ
		|	Константа.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВалютаРасценокВидовРабот.Значение,
		|	""ВалютаРасценокВидовРабот""
		|ИЗ
		|	Константа.ВалютаРасценокВидовРабот КАК ВалютаРасценокВидовРабот
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидВалюты,
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Валюты.Валюта КАК Валюта,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) КАК Кратность,
		|	Валюты.ВидВалюты КАК ВидВалюты
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	Валюты КАК Валюты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						Валюты.Валюта КАК Валюта
		|					ИЗ
		|						Валюты КАК Валюты)) КАК КурсыВалютСрезПоследних
		|		ПО Валюты.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидВалюты,
		|	Валюта";
	
	Запрос.УстановитьПараметр("Период", Калькуляция.Дата);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ФактическиеДанные

Процедура РассчитатьМножителиПоФактическимДанным(ПараметрыРазузлования, ДатаКалькуляции)

	ОтборПолуфабрикатов = Новый Структура("ПартияПродукции, Продукция, ХарактеристикаПродукции, ТребуетсяРазузлование");
	ОтборПолуфабрикатов.ТребуетсяРазузлование = Истина;
	
	// Фактические затраты.
	ПараметрыДереваСебестоимости = СтруктураСебестоимости.ПараметрыДереваСебестоимости();
	ПараметрыДереваСебестоимости.ДинамическоеСчитывание = Ложь;
	ПараметрыДереваСебестоимости.ТипРезультата = "ТаблицаЗначений";
	
	ПараметрыУзлаДереваСебестоимости = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
	
	Для Каждого ВыборкаПолуфабрикатов Из ПараметрыРазузлования.КРазузлованию Цикл
		
		Отборы = ПараметрыУзлаДереваСебестоимости.Отборы;
		Отборы.Продукция = ЗначениеВМассив(ВыборкаПолуфабрикатов.Номенклатура);
		Отборы.ХарактеристикиПродукции = ЗначениеВМассив(ВыборкаПолуфабрикатов.Характеристика);
		Отборы.ПартииПродукции = ЗначениеВМассив(ВыборкаПолуфабрикатов.Партия);
		Если Не ВыборкаПолуфабрикатов.АналитикаУчетаПартий = Неопределено Тогда
			Отборы.АналитикиУчетаПартийПродукции = ЗначениеВМассив(ВыборкаПолуфабрикатов.АналитикаУчетаПартий);
		КонецЕсли;
		Отборы.Числитель = ВыборкаПолуфабрикатов.Числитель;
		Отборы.Знаменатель = 0;
		
		СтруктураСебестоимости.ПостроитьДеревоСебестоимости(ПараметрыДереваСебестоимости, ПараметрыУзлаДереваСебестоимости);
		
		ОтборФактическихДанных = Новый Структура;
		ОтборФактическихДанных.Вставить("Партия", ВыборкаПолуфабрикатов.Партия);
		ОтборФактическихДанных.Вставить("АналитикаУчетаНоменклатуры", ВыборкаПолуфабрикатов.АналитикаУчетаНоменклатуры);
		НаборМножителей = ПараметрыРазузлования.МножителиПолуфабрикатов.НайтиСтроки(ОтборФактическихДанных);
		
		Для Каждого МножительПФ Из НаборМножителей Цикл
			
			ОтборМножителей = Новый Структура();
			Если МножительПФ.ЭтоПолуфабрикат Тогда
				ОтборМножителей.Вставить("ПартияСпецификацияПродукции", МножительПФ.Партия);
			Иначе
				ОтборМножителей.Вставить("ПартияСпецификацияПолуфабриката", МножительПФ.Партия);
			КонецЕсли;
			
			ЗначенияМножителей = ПараметрыРазузлования.Множители.НайтиСтроки(ОтборМножителей);
			
			Для Каждого ЗначениеМножителя Из ЗначенияМножителей Цикл
				
				НовыйМножитель = ПараметрыРазузлования.Множители.Добавить();
				НовыйМножитель.Период = МножительПФ.Период;
				НовыйМножитель.ПартияСпецификацияПродукции = МножительПФ.ПартияПродукции;
				НовыйМножитель.Продукция = ЗначениеМножителя.Продукция;
				НовыйМножитель.ХарактеристикаПродукции = ЗначениеМножителя.ХарактеристикаПродукции;
				НовыйМножитель.Назначение = ЗначениеМножителя.Назначение;
				
				НовыйМножитель.ПартияСпецификацияПолуфабриката = ВыборкаПолуфабрикатов.Партия;
				НовыйМножитель.Полуфабрикат = ВыборкаПолуфабрикатов.Номенклатура;
				НовыйМножитель.ХарактеристикаПолуфабриката = ВыборкаПолуфабрикатов.Характеристика;
				НовыйМножитель.Числитель = ЗначениеМножителя.Числитель * ВыборкаПолуфабрикатов.Числитель;
				НовыйМножитель.Знаменатель = ЗначениеМножителя.Знаменатель * ВыборкаПолуфабрикатов.Знаменатель;
				
				ПривестиЧислительИЗнаменатель(НовыйМножитель);
				
				НовыйМножитель.РасчетЗавершен = Истина;
				НовыйМножитель.ПартияВыпущена = Истина;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если Не ПараметрыДереваСебестоимости.Результат = Неопределено Тогда
		
		// Есть фактические затраты.
		ПараметрыДереваСебестоимости.Результат.Колонки.Добавить("Продукция", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ПараметрыДереваСебестоимости.Результат.Колонки.Добавить("ХарактеристикаПродукции", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		
		Для Каждого Полуфабрикат Из ПараметрыДереваСебестоимости.Результат Цикл
			
			Если Не Полуфабрикат.ТребуетсяРазузлование Тогда
				Продолжить;
			КонецЕсли;
			
			Полуфабрикат.Продукция = Полуфабрикат.АналитикаУчетаПродукции.Номенклатура;
			Полуфабрикат.ХарактеристикаПродукции = Полуфабрикат.АналитикаУчетаПродукции.Характеристика;
			
		КонецЦикла;
		
		ПараметрыДереваСебестоимости.Результат.Индексы.Добавить("ПартияПродукции, Продукция, ХарактеристикаПродукции");
		
		Для Каждого Множитель Из ПараметрыРазузлования.Множители Цикл
			
			Если Не Множитель.РасчетЗавершен И Не Множитель.ПартияВыпущена Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Множитель.Полуфабрикат) Тогда
				
				ОтборПолуфабрикатов.ПартияПродукции = Множитель.ПартияСпецификацияПродукции;
				ОтборПолуфабрикатов.Продукция = Множитель.Продукция;
				ОтборПолуфабрикатов.ХарактеристикаПродукции = Множитель.ХарактеристикаПродукции;
				
			Иначе
				
				ОтборПолуфабрикатов.ПартияПродукции = Множитель.ПартияСпецификацияПолуфабриката;
				ОтборПолуфабрикатов.Продукция = Множитель.Полуфабрикат;
				ОтборПолуфабрикатов.ХарактеристикаПродукции = Множитель.ХарактеристикаПолуфабриката;
				
			КонецЕсли;
			
			Полуфабрикаты = ПараметрыДереваСебестоимости.Результат.НайтиСтроки(ОтборПолуфабрикатов);
			Если Не Полуфабрикаты.Количество() > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ПФ Из Полуфабрикаты Цикл
				
				Если ПФ.ТипЗатрат = "Продукция" Тогда
					
					Множитель.Стоимость = ПФ.Сумма;
					Множитель.СтоимостьБезНДС = ПФ.ПостатейныеПостоянныеБезНДС + ПФ.ПостатейныеПеременныеБезНДС + ПФ.МатериальныеБезНДС + ПФ.ДопРасходыБезНДС + ПФ.Трудозатраты;
					Множитель.СтоимостьРегл = ПФ.ПостатейныеПостоянныеРегл + ПФ.ПостатейныеПеременныеРегл + ПФ.МатериальныеРегл + ПФ.ДопРасходыРегл + ПФ.ТрудозатратыРегл;
					Множитель.Трудозатраты = ПФ.Трудозатраты;
					Множитель.ТрудозатратыРегл = ПФ.ТрудозатратыРегл;
					Множитель.ПостатейныеПостоянные = ПФ.ПостатейныеПостоянные;
					Множитель.ПостатейныеПостоянныеБезНДС = ПФ.ПостатейныеПостоянныеБезНДС;
					Множитель.ПостатейныеПостоянныеРегл = ПФ.ПостатейныеПостоянныеРегл;
					Множитель.ПостатейныеПеременные = ПФ.ПостатейныеПеременные;
					Множитель.ПостатейныеПеременныеБезНДС = ПФ.ПостатейныеПеременныеБезНДС;
					Множитель.ПостатейныеПеременныеРегл = ПФ.ПостатейныеПеременныеРегл;
					// Забаланс.
					Множитель.СтоимостьЗабалансовая = ПФ.СуммаЗабалансовая;
					Множитель.СтоимостьЗабалансоваяРегл = ПФ.СтоимостьЗабалансоваяРегл;
					
					Продолжить;
					
				КонецЕсли;
				
				НовыйМножитель = ПараметрыРазузлования.Множители.Добавить();
				
				НовыйМножитель.Период = ПФ.Партия.ФактическоеОкончаниеЭтапа;
				НовыйМножитель.Калькуляция = Множитель.Калькуляция;
				НовыйМножитель.ПартияСпецификацияПродукции = Множитель.ПартияСпецификацияПродукции;
				НовыйМножитель.Продукция = Множитель.Продукция;
				НовыйМножитель.ХарактеристикаПродукции = Множитель.ХарактеристикаПродукции;
				НовыйМножитель.Назначение = Множитель.Назначение;
				
				НовыйМножитель.ПартияСпецификацияПолуфабриката = ПФ.Партия;
				НовыйМножитель.Полуфабрикат = ПФ.Номенклатура;
				НовыйМножитель.ХарактеристикаПолуфабриката = ПФ.Характеристика;
				НовыйМножитель.Числитель = ПФ.Числитель;
				НовыйМножитель.Знаменатель = ПФ.Знаменатель;
				НовыйМножитель.КоличествоПолуфабриката = ПФ.Количество;
				
				ПривестиЧислительИЗнаменатель(НовыйМножитель);
				
				НовыйМножитель.Стоимость = ПФ.Сумма;
				НовыйМножитель.СтоимостьБезНДС = ПФ.ПостатейныеПостоянныеБезНДС + ПФ.ПостатейныеПеременныеБезНДС + ПФ.МатериальныеБезНДС + ПФ.ДопРасходыБезНДС + ПФ.Трудозатраты;
				НовыйМножитель.СтоимостьРегл = ПФ.ПостатейныеПостоянныеРегл + ПФ.ПостатейныеПеременныеРегл + ПФ.МатериальныеРегл + ПФ.ДопРасходыРегл + ПФ.ТрудозатратыРегл;
				НовыйМножитель.Трудозатраты = ПФ.Трудозатраты;
				НовыйМножитель.ТрудозатратыРегл = ПФ.ТрудозатратыРегл;
				НовыйМножитель.ПостатейныеПостоянные = ПФ.ПостатейныеПостоянные;
				НовыйМножитель.ПостатейныеПостоянныеБезНДС = ПФ.ПостатейныеПостоянныеБезНДС;
				НовыйМножитель.ПостатейныеПостоянныеРегл = ПФ.ПостатейныеПостоянныеРегл;
				НовыйМножитель.ПостатейныеПеременные = ПФ.ПостатейныеПеременные;
				НовыйМножитель.ПостатейныеПеременныеБезНДС = ПФ.ПостатейныеПеременныеБезНДС;
				НовыйМножитель.ПостатейныеПеременныеРегл = ПФ.ПостатейныеПеременныеРегл;
				// Забаланс.
				НовыйМножитель.СтоимостьЗабалансовая = ПФ.СуммаЗабалансовая;
				НовыйМножитель.СтоимостьЗабалансоваяРегл = ПФ.СтоимостьЗабалансоваяРегл;
				
				НовыйМножитель.ПартияВыпущена = Истина;
				НовыйМножитель.РасчетЗавершен = Истина;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;	
	
	ПривестиМножителиКОбщемуЗнаменателю(ПараметрыРазузлования.Множители);
	
КонецПроцедуры

#КонецОбласти

#Область Проверка

Процедура ДобавитьКалькуляциюКПроверке(Калькуляция, МассивКалькуляций)
	
	Если БылаОтменаПроведения(Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивКалькуляций = Неопределено Тогда
		МассивКалькуляций = Новый Массив;
	КонецЕсли;
	
	Если МассивКалькуляций.Найти(Калькуляция) = Неопределено Тогда
		МассивКалькуляций.Добавить(Калькуляция);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСтоимость(Калькуляции)
	
	Проверка = АудитСостоянияСистемы.НайтиПроверкуПоКлючевымПолям(Документы.ПлановаяКалькуляция2_2.ПустаяСсылка(), "ДвиженияСНулевойСтоимостью");
	
	Если НЕ ЗначениеЗаполнено(Проверка) Тогда
		ВызватьИсключение НСтр("ru = 'Не найдена служебная контекстная проверка для операции ""Калькуляция продукциии""';
								|en = 'Service context check for the ""Product costing"" operation was not found'");
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Калькуляции", Калькуляции);
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыПроверки = АудитСостоянияСистемы.ВыполнитьПроверкуСостоянияСистемы(Проверка, ДопПараметры);
	УстановитьПривилегированныйРежим(Ложь);
	
	КалькуляцииСОшибкамиРасчета = ПараметрыПроверки.ДополнительныеПараметры.КалькуляцииСОшибкамиРасчета;
	
	Для Каждого Калькуляция Из Калькуляции Цикл
		
		Если Не КалькуляцииСОшибкамиРасчета.Найти(Калькуляция) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписатьСостояниеКалькуляции(Калькуляция, "Рассчитана");
		
	КонецЦикла;
	
	Если КалькуляцииСОшибкамиРасчета.Количество() > 0 Тогда
		АктуализироватьСостояниеКалькуляций(КалькуляцииСОшибкамиРасчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПолучитьПериодЗатрат(ПараметрыРасчетаПериода)
	
	ПериодЗатрат = НачалоДня(ПараметрыРасчетаПериода.Период);
	ПозицияЭтапа = ПараметрыРасчетаПериода.Этапы.Индекс(
		ПараметрыРасчетаПериода.Этапы.Найти(ПараметрыРасчетаПериода.ТекущийЭтап, "Этап"));
	Счетчик = 0;
	Пока Счетчик < ПозицияЭтапа Цикл
		
		ПериодЗатрат = ПериодЗатрат + ПараметрыРасчетаПериода.ДлительностиЭтапов[Счетчик] * 86400;
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	Возврат НачалоМесяца(ПериодЗатрат);
	
КонецФункции

Функция ПараметрыРасчетаПериода()
	
	Параметры = Новый Структура;
	Параметры.Вставить("Период", Дата(1, 1, 1));
	Параметры.Вставить("Этапы");
	Параметры.Вставить("ТекущийЭтап");
	Параметры.Вставить("ДлительностиЭтапов");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецЕсли
