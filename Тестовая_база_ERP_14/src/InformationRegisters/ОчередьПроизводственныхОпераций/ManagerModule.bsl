#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Рассчитывает и записывает очередь производственных операций
//  (вызывается при обработки очереди заданий к расчету очереди производственных операций).
//
// Параметры:
//  Задание				 - Структура				 - задание к обработке
//  	* ОбъектРасчета - ДокументСсылка.ЭтапПроизводства2_2 - обрабатываемый документ
//  ПропуститьЗадание	 - Булево					 - признак, требуется пропустить выполнение задания (возвращаемое значение).
//
Процедура РассчитатьОчередьОтложенно(Задание, ПропуститьЗадание = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбъектРасчета = Задание.ОбъектРасчета;
	
	РассчитатьОчередь(ОбъектРасчета, Истина);
	
КонецПроцедуры

// Рассчитывает и записывает очередь производственных операций
//
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2 - обрабатываемый документ
//  РассчитыватьСменныеЗадания - Булево	- признак необходимости расчета
//		данных регистра "Операции к созданию сменных заданий"
//
Процедура РассчитатьОчередь(Этап, РассчитыватьСменныеЗадания = Истина) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.ЭтапПроизводства2_2");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Этап);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		БлокировкаДанных.Заблокировать();
		
		ДанныеЭтапа = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этап);
		
		Если ДанныеЭтапа.ТребуетсяРассчитать Тогда
			
			Очередь = ОчередьОпераций(Этап, ДанныеЭтапа);
			ЗаписатьОчередь(Этап, Очередь);
			
			Если ДанныеЭтапа.ИспользоватьСменныеЗадания
				И РассчитыватьСменныеЗадания
				И НЕ ДанныеЭтапа.ВАрхиве Тогда
				
				РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.РассчитатьОперации(Этап);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

// Добавляет в очередь заданий к расчету данных регистра новое задание.
//
// Параметры:
//  Этап				 - ДокументСсылка.ЭтапПроизводство2_2	 - этап производства.
//  УдалениеПроведения	 - Булево								 - признак обработки удаления проведения.
//  Отказ				 - Булево								 - признак прерывания обработки проведения.
//
Процедура ДобавитьЗаданиеКРасчетуОчереди(Этап, УдалениеПроведения = Ложь, Отказ = Ложь) Экспорт
	
	ЗаблокироватьДанныеДляРасчетаОчереди(Этап, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЭтапа = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этап);
	
	ВыполнитьКонтрольПередРасчетомОчереди(ДанныеЭтапа, УдалениеПроведения, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьОчередь(Этап);
	
	Если ДанныеЭтапа.ИспользоватьСменныеЗадания Тогда
		РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.УдалитьОперацииЭтапа(Этап);
	КонецЕсли;
	
	Если ДанныеЭтапа.ТребуетсяРассчитать И НЕ УдалениеПроведения Тогда
		РегистрыСведений.ЗаданияКРасчетуОчередиПроизводственныхОпераций.ДобавитьЗадание(Этап);
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает очередь по указанной технологической операции.
//
// Параметры:
//  КлючОперации - Структура - см. УправлениеПроизводствомКлиентСервер.КлючПроизводственнойОперации()
//  Отказ		 - Булево	 - признак прерывания обработки проведения.
//
Процедура ПересчитатьОчередь(КлючОперации, Отказ = Ложь) Экспорт
	
	ЗаблокироватьОчередьДляЗаписиПоКлючу(КлючОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОкружениеОперации = ОкружениеОперацииВОчереди(КлючОперации);
	
	ДанныеОперации = НайтиОперациюПоКлючу(ОкружениеОперации, КлючОперации);
	Если ДанныеОперации = Неопределено Тогда
		СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ);
		Возврат;
	КонецЕсли;
	
	Этап = КлючОперации.Этап;
	
	ЗаблокироватьДанныеДляРасчетаОчередиПоОперации(Этап, ДанныеОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПересчитатьОчередьПоОперации(Этап, ДанныеОперации, Отказ);
	
КонецПроцедуры

// Возвращает данные операции из очереди
//
// Параметры:
//  КлючОперации - Структура - см. УправлениеПроизводствомКлиентСервер.КлючПроизводственнойОперации().
// 
// Возвращаемое значение:
//   - Структура - данные операции из очереди.
//
Функция ДанныеОперацииИзОчереди(КлючОперации) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Очередь.Подразделение             КАК Подразделение,
	|	Очередь.МаршрутнаяКарта           КАК МаршрутнаяКарта,
	//
	|	Очередь.Операция                  КАК Операция,
	|	Очередь.ИдентификаторОперации     КАК ИдентификаторОперации,
	//
	|	ВЫБОР
	|		КОГДА Очередь.Операция.РабочийЦентр ССЫЛКА Справочник.ВидыРабочихЦентров
	|			ТОГДА Очередь.Операция.РабочийЦентр
	|		ИНАЧЕ Очередь.Операция.РабочийЦентр.ВидРабочегоЦентра
	|	КОНЕЦ КАК ВидРабочегоЦентра,
	|	
	|	ВЫБОР
	|		КОГДА Очередь.Операция.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|			ТОГДА Очередь.Операция.РабочийЦентр
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.РабочиеЦентры.ПустаяСсылка)
	|	КОНЕЦ КАК РабочийЦентр,
	|
	|	Очередь.Операция.ЕдиницаИзмерения                КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(Очередь.Операция.МожноПропустить, ЛОЖЬ) КАК МожноПропустить,
	//
	|	Очередь.Этап                   КАК Этап,
	//
	|	Очередь.Запланировано          КАК Запланировано,
	|	ВЫБОР
	|		КОГДА Очередь.Запланировано + Очередь.ТребуетПовторения > Очередь.Создано
	|			ТОГДА Очередь.Запланировано + Очередь.ТребуетПовторения - Очередь.Создано
	|		ИНАЧЕ 0
	|	КОНЕЦ                          КАК ОжиданиеСоздания,
	|	Очередь.ОжиданиеПредшествующих КАК ОжиданиеПредшествующих,
	|	Очередь.НачатыПредшествующие   КАК НачатыПредшествующие,
	|	Очередь.МожноВыполнять         КАК МожноВыполнять,
	|	Очередь.Выполняется            КАК Выполняется,
	|	Очередь.Выполнено              КАК Выполнено,
	|	Очередь.ТребуетПовторения      КАК ТребуетПовторения,
	|	Очередь.Пропущено              КАК Пропущено,
	//
	|	Очередь.ВАрхиве                КАК ВАрхиве
	|ИЗ
	|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
	|ГДЕ
	|	Очередь.Этап = &Этап
	|	И Очередь.Операция = &Операция
	|	И Очередь.ИдентификаторОперации = &ИдентификаторОперации");

	Запрос.УстановитьПараметр("Этап", КлючОперации.Этап);
	Запрос.УстановитьПараметр("Операция", КлючОперации.Операция);
	Запрос.УстановитьПараметр("ИдентификаторОперации", КлючОперации.ИдентификаторОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеОперации = Новый Структура();
	
	Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеОперации.Вставить(Колонка.Имя);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеОперации, Выборка);
	
	Возврат ДанныеОперации;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Конструкторы

Функция ОчередьОперацийКонструктор()
	
	НаборЗаписей = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	
	Возврат НаборЗаписей.ВыгрузитьКолонки();
	
КонецФункции

#КонецОбласти

#Область ВыполнитьКонтроль

Функция ВыполнитьКонтрольПередРасчетомОчереди(ДанныеЭтапа, УдалениеПроведения = Ложь, Отказ = Ложь)
	
	ЕстьОшибка = Ложь;
	
	Если ДанныеЭтапа.ТребуетсяРассчитать И НЕ УдалениеПроведения Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СУММА(Очередь.Создано) > 0 КАК ОшибкаВОперациях
		|ИЗ
		|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК НазначенныеОперации
		|	ПО Очередь.Подразделение = НазначенныеОперации.Подразделение
		|		И Очередь.Этап = НазначенныеОперации.Этап
		|		И Очередь.Операция = НазначенныеОперации.Операция
		|		И Очередь.ИдентификаторОперации = НазначенныеОперации.ИдентификаторОперации
		|		И НазначенныеОперации.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|ГДЕ
		|	Очередь.Этап = &Этап
		|
		|ИМЕЮЩИЕ
		|	(СУММА(Очередь.Создано) > 0 
		|		ИЛИ МАКСИМУМ(НазначенныеОперации.Этап ЕСТЬ НЕ NULL) = ИСТИНА)
		|	И (МАКСИМУМ(ВЫБОР
		|				КОГДА Очередь.МаршрутнаяКарта <> &МаршрутнаяКарта
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ) = ИСТИНА
		|		ИЛИ МАКСИМУМ(ВЫБОР
		|				КОГДА Очередь.КоэффициентМаршрутнойКарты <> &КоэффициентМаршрутнойКарты
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ) = ИСТИНА)");
		Запрос.УстановитьПараметр("Этап", ДанныеЭтапа.Ссылка);
		Запрос.УстановитьПараметр("МаршрутнаяКарта", ДанныеЭтапа.МаршрутнаяКарта);
		Запрос.УстановитьПараметр("КоэффициентМаршрутнойКарты", ДанныеЭтапа.КоэффициентМаршрутнойКарты);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЕстьОшибка = Истина;
			
			Если Выборка.ОшибкаВОперациях Тогда
				ТекстСообщения = НСтр("ru = 'Запрещено изменять маршрутную карту и коэффициент после создания производственных операций.';
										|en = 'You cannot change operations sheet and factor after creating production operations.'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Запрещено изменять маршрутную карту и коэффициент после создания сменных заданий.';
										|en = 'You cannot change operations sheet and factor after creating shift jobs.'");
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ОшибкаВОперациях
		|ИЗ
		|	Документ.ПроизводственнаяОперация2_2 КАК Операции
		|ГДЕ
		|	Операции.Этап = &Этап
		|	И НЕ Операции.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЛОЖЬ
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК НазначенныеОперации
		|ГДЕ
		|	НазначенныеОперации.Этап = &Этап
		|	И НазначенныеОперации.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)");
		Запрос.УстановитьПараметр("Этап", ДанныеЭтапа.Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЕстьОшибка = Истина;
			
			Если Выборка.ОшибкаВОперациях Тогда
				ТекстСообщения = НСтр("ru = 'По этапу созданы операции.';
										|en = 'Operations are created for the stage. '");
			Иначе
				ТекстСообщения = НСтр("ru = 'По этапу созданы сменные задания.';
										|en = 'Shift jobs are created by the stage.'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьОшибка Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка расчета очереди производственных операций. %1';
										|en = 'An error occurred while calculating production operation queue. %1'"), ТекстСообщения);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				,
				, 
				Отказ);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область РассчитатьПоЭтапу

Процедура ЗаблокироватьДанныеДляРасчетаОчереди(Этап, Отказ)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.ПроизводственнаяОперация2_2");
	ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьПроизводственныхОпераций");
	ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПооперационноеПланирование") Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ПооперационноеРасписание2_2");
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСменныеЗадания") Тогда
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОперацииКСозданиюСменныхЗаданий");
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	КонецЕсли;
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось заблокировать очередь производственных операций: %Причина%';
								|en = 'Cannot lock queue of production operations: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				,
				, 
				Отказ);
			
	КонецПопытки;
	
КонецПроцедуры

Функция ОчередьОпераций(Этап, ДанныеЭтапа)
	
	КлючеваяОперация = "МежцеховоеУправление2_2.РасчетОчередиПроизводственныхОпераций";
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	
	ОчередьОпераций = ОчередьОперацийВнутриЗамераВремени(Этап, ДанныеЭтапа);
	
	КоличествоДанных = ОчередьОпераций.Количество();
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоДанных);
	
	Возврат ОчередьОпераций;
	
КонецФункции

Функция ОчередьОперацийВнутриЗамераВремени(Этап, ДанныеЭтапа)
	
	ОчередьОпераций = ОчередьОперацийКонструктор();
	
	ДанныеМаршрутнойКарты = Справочники.МаршрутныеКарты.ДанныеМаршрутнойКарты(
		ДанныеЭтапа.МаршрутнаяКарта, 
		ДанныеЭтапа.КоэффициентМаршрутнойКарты,
		,
		,
		"Операции");

	СписокОпераций = СписокОперацийДляРасчета(Этап, ДанныеМаршрутнойКарты);
	
	Для каждого ТекущаяСтрока Из СписокОпераций Цикл
		
		НоваяОперация = ОчередьОпераций.Добавить();
		
		НоваяОперация.Этап = Этап;
		
		ЗаполнитьЗначенияСвойств(
			НоваяОперация,
			ТекущаяСтрока,
			"Операция,
			|ИдентификаторОперации,
			|НомерОперации,
			|НомерСледующейОперации");

		ЗаполнитьЗначенияСвойств(
			НоваяОперация,
			ДанныеЭтапа,
			"Распоряжение,
			|Подразделение,
			|МаршрутнаяКарта,
			|КоэффициентМаршрутнойКарты,
			|ВАрхиве");
		
		НоваяОперация.Запланировано = ТекущаяСтрока.КоличествоНаПартию;
		РассчитатьПоказателиВыполненияОперации(НоваяОперация, ТекущаяСтрока);
		
	КонецЦикла;
	
	Возврат ОчередьОпераций;
	
КонецФункции

Процедура ЗаписатьОчередь(Этап, Очередь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	Набор.Отбор.Этап.Установить(Этап);
	
	Набор.Загрузить(Очередь);
	
	Набор.Записать(Истина);

КонецПроцедуры

Процедура ОчиститьОчередь(Этап)

	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	Набор.Отбор.Этап.Установить(Этап);
	
	Набор.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область РассчитатьПоОперации

Процедура ЗаблокироватьОчередьДляЗаписиПоКлючу(КлючОперации, Отказ = Ложь) Экспорт
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьПроизводственныхОпераций");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки.УстановитьЗначение("Этап", КлючОперации.Этап);
	ЭлементБлокировки.УстановитьЗначение("Операция", КлючОперации.Операция);
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторОперации", КлючОперации.ИдентификаторОперации);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось заблокировать очередь производственных операций: %Причина%';
								|en = 'Cannot lock queue of production operations: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				,
				, 
				Отказ); 
			
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаблокироватьДанныеДляРасчетаОчередиПоОперации(Этап, ДанныеОперации, Отказ)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Последовательность = ДанныеОперации.Владелец();
	
	Для каждого ТекущаяСтрока Из Последовательность Цикл
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.ПроизводственнаяОперация2_2");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		
		ЭлементБлокировки.УстановитьЗначение("Операция", ТекущаяСтрока.Операция);
		ЭлементБлокировки.УстановитьЗначение("ИдентификаторОперации", ТекущаяСтрока.ИдентификаторОперации);
		
		Если ТекущаяСтрока.НомерОперации = ДанныеОперации.НомерСледующейОперации 
			ИЛИ ТекущаяСтрока = ДанныеОперации Тогда
		
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьПроизводственныхОпераций");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
			
			ЭлементБлокировки.УстановитьЗначение("Операция", ТекущаяСтрока.Операция);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторОперации", ТекущаяСтрока.ИдентификаторОперации);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось заблокировать очередь производственных операций: %Причина%';
								|en = 'Cannot lock queue of production operations: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщения,
				,
				,
				, 
				Отказ); 
			
	КонецПопытки;
	
КонецПроцедуры

Процедура ПересчитатьОчередьПоОперации(Этап, ДанныеОперации, Отказ)
	
	ДополнитьОперацииРезультатомВыполнения(Этап, ДанныеОперации.Владелец());
	
	Последовательность = СледующиеОперации(ДанныеОперации);
	Последовательность.Вставить(0, ДанныеОперации);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	
	Для каждого ТекущаяСтрока Из Последовательность Цикл
		
		Набор.Отбор.Этап.Установить(Этап);
		Набор.Отбор.Операция.Установить(ТекущаяСтрока.Операция);
		Набор.Отбор.ИдентификаторОперации.Установить(ТекущаяСтрока.ИдентификаторОперации);
		
		Набор.Прочитать();
		
		Если Набор.Количество() = 0 Тогда
			СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ);
			Возврат;
		КонецЕсли;
		
		РассчитатьПоказателиВыполненияОперации(Набор[0], ТекущаяСтрока);
		
		Попытка
			Набор.Записать(Истина);
		Исключение
			СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ);
			Возврат;
		КонецПопытки;
		
	КонецЦикла;
	
	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(
		ДанныеОперации.Подразделение);
	Если ПараметрыПодразделения.ИспользоватьСменныеЗадания Тогда
		
		РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.РассчитатьОперации(Этап);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

#Область Последовательность

Функция ПредыдущиеОперации(Курсор)
	
	Отбор = Новый Структура("НомерСледующейОперации", Курсор.НомерОперации);
	
	Возврат Курсор.Владелец().НайтиСтроки(Отбор);
	
КонецФункции

Функция СледующиеОперации(Курсор)
	
	Отбор = Новый Структура("НомерОперации", Курсор.НомерСледующейОперации);
	
	Возврат Курсор.Владелец().НайтиСтроки(Отбор);
	
КонецФункции

Функция ОкружениеОперацииВОчереди(КлючОперации)
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ТекущаяОперация.Подразделение КАК Подразделение,
		|	ТекущаяОперация.Операция КАК Операция,
		|	ТекущаяОперация.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ТекущаяОперация.НомерОперации КАК НомерОперации,
		|	ТекущаяОперация.НомерСледующейОперации КАК НомерСледующейОперации,
		|	ТекущаяОперация.Запланировано КАК КоличествоНаПартию,
		|	ТекущаяОперация.ВремяВыполненияЕдИзм КАК ВремяВыполненияЕдИзм
		|ПОМЕСТИТЬ ВТТекущаяОперация
		|ИЗ
		|	РегистрСведений.ОчередьПроизводственныхОпераций КАК ТекущаяОперация
		|ГДЕ
		|	ТекущаяОперация.Этап = &Этап
		|	И ТекущаяОперация.Операция = &Операция
		|	И ТекущаяОперация.ИдентификаторОперации = &ИдентификаторОперации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СледующиеОперации.Подразделение КАК Подразделение,
		|	СледующиеОперации.Операция КАК Операция,
		|	СледующиеОперации.ИдентификаторОперации КАК ИдентификаторОперации,
		|	СледующиеОперации.НомерОперации КАК НомерОперации,
		|	СледующиеОперации.НомерСледующейОперации КАК НомерСледующейОперации,
		|	СледующиеОперации.Запланировано КАК КоличествоНаПартию,
		|	СледующиеОперации.ВремяВыполненияЕдИзм КАК ВремяВыполненияЕдИзм
		|ПОМЕСТИТЬ ВТСледующиеОперации
		|ИЗ
		|	РегистрСведений.ОчередьПроизводственныхОпераций КАК СледующиеОперации
		|ГДЕ
		|	СледующиеОперации.Этап = &Этап
		|	И СледующиеОперации.НомерОперации В
		|			(ВЫБРАТЬ
		|				ТекущаяОперация.НомерСледующейОперации
		|			ИЗ
		|				ВТТекущаяОперация КАК ТекущаяОперация)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Операции.Подразделение              КАК Подразделение,
		|	Операции.Операция                   КАК Операция,
		|	Операции.ИдентификаторОперации      КАК ИдентификаторОперации,
		|	Операции.НомерОперации              КАК НомерОперации,
		|	Операции.НомерСледующейОперации     КАК НомерСледующейОперации,
		|	Операции.КоличествоНаПартию         КАК КоличествоНаПартию,
		|	Операции.Операция.Количество        КАК Количество,
		|	Операции.ВремяВыполненияЕдИзм       КАК ВремяВыполненияЕдИзм,
		|
		|	&ТекстРеквизитыИсполнителяОперации,
		|	&ТекстРеквизитыРасчетаВремениВыполненияОперации
		|ИЗ
		|	(ВЫБРАТЬ
		|		СледующиеОперации.Подразделение КАК Подразделение,
		|		СледующиеОперации.Операция КАК Операция,
		|		СледующиеОперации.ИдентификаторОперации КАК ИдентификаторОперации,
		|		СледующиеОперации.НомерОперации КАК НомерОперации,
		|		СледующиеОперации.НомерСледующейОперации КАК НомерСледующейОперации,
		|		СледующиеОперации.КоличествоНаПартию КАК КоличествоНаПартию,
		|		СледующиеОперации.ВремяВыполненияЕдИзм КАК ВремяВыполненияЕдИзм
		|	ИЗ
		|		ВТСледующиеОперации КАК СледующиеОперации
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПредыдущиеОперации.Подразделение,
		|		ПредыдущиеОперации.Операция,
		|		ПредыдущиеОперации.ИдентификаторОперации,
		|		ПредыдущиеОперации.НомерОперации,
		|		ПредыдущиеОперации.НомерСледующейОперации,
		|		ПредыдущиеОперации.Запланировано,
		|		ПредыдущиеОперации.ВремяВыполненияЕдИзм
		|	ИЗ
		|		РегистрСведений.ОчередьПроизводственныхОпераций КАК ПредыдущиеОперации
		|	ГДЕ
		|		ПредыдущиеОперации.Этап = &Этап
		|		И ПредыдущиеОперации.НомерСледующейОперации В
		|				(ВЫБРАТЬ
		|					ТекущаяОперация.НомерОперации
		|				ИЗ
		|					ВТТекущаяОперация КАК ТекущаяОперация
		|			
		|				ОБЪЕДИНИТЬ ВСЕ
		|			
		|				ВЫБРАТЬ
		|					СледующиеОперации.НомерОперации
		|				ИЗ
		|					ВТСледующиеОперации КАК СледующиеОперации)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТекущаяОперация.Подразделение,
		|		ТекущаяОперация.Операция,
		|		ТекущаяОперация.ИдентификаторОперации,
		|		ТекущаяОперация.НомерОперации,
		|		ТекущаяОперация.НомерСледующейОперации,
		|		ТекущаяОперация.КоличествоНаПартию,
		|		ТекущаяОперация.ВремяВыполненияЕдИзм
		|	ИЗ
		|		ВТТекущаяОперация КАК ТекущаяОперация) КАК Операции");
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстРеквизитыИсполнителяОперации",
		Справочники.ТехнологическиеОперации.ТекстЗапросаРеквизитыИсполнителяОперации("Операции.Операция"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстРеквизитыРасчетаВремениВыполненияОперации",
		Справочники.ТехнологическиеОперации.ТекстЗапросаРеквизитыРасчетаВремениВыполненияОперации("Операции.Операция"));
		
	Запрос.УстановитьПараметр("Этап", КлючОперации.Этап);
	Запрос.УстановитьПараметр("Операция", КлючОперации.Операция);
	Запрос.УстановитьПараметр("ИдентификаторОперации", КлючОперации.ИдентификаторОперации);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция НайтиОперациюПоКлючу(СписокОпераций, КлючОперации)
	
	ОтборОперация = Новый Структура("Операция,ИдентификаторОперации");
	ЗаполнитьЗначенияСвойств(ОтборОперация, КлючОперации);
	
	НайденныеСтроки = СписокОпераций.НайтиСтроки(ОтборОперация);
	Если НайденныеСтроки.ВГраница() <> -1 Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

Функция СписокОперацийДляРасчета(Этап, ДанныеМаршрутнойКарты)
	
	ДополнитьОперацииРезультатомВыполнения(Этап, ДанныеМаршрутнойКарты.Операции);
	Возврат ДанныеМаршрутнойКарты.Операции;
	
КонецФункции

#КонецОбласти

#Область ВыполнениеОпераций

Процедура ДополнитьОперацииРезультатомВыполнения(Этап, Операции)
	
	ВыполнениеОпераций = РезультатВыполненияОпераций(
		Этап, 
		Операции.Скопировать(,"Операция,ИдентификаторОперации"));
	
	ОписаниеТипов = Новый ОписаниеТипов(
		"Число", 
		Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный));
	
	Операции.Колонки.Добавить("Создано",           ОписаниеТипов);
	Операции.Колонки.Добавить("Выполняется",       ОписаниеТипов);
	Операции.Колонки.Добавить("Выполнено",         ОписаниеТипов);
	Операции.Колонки.Добавить("ТребуетПовторения", ОписаниеТипов);
	Операции.Колонки.Добавить("Пропущено",         ОписаниеТипов);
	
	ОписаниеТипов = Новый ОписаниеТипов(
		"Число", 
		Новый КвалификаторыЧисла(10, 1, ДопустимыйЗнак.Неотрицательный));
	
	Операции.Колонки.Добавить("ВремяВыполненияСозданныхОпераций", ОписаниеТипов);
	
	ОтборОперация = Новый Структура("Операция,ИдентификаторОперации");
	
	Для каждого ДанныеОперации Из Операции Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборОперация, ДанныеОперации);
		НайденныеСтроки = ВыполнениеОпераций.НайтиСтроки(ОтборОперация);
		
		Если НайденныеСтроки.ВГраница() <> -1 Тогда
			
			ЗаполнитьЗначенияСвойств(
				ДанныеОперации,
				НайденныеСтроки[0],
				"Создано,
				|Выполняется,
				|Выполнено,
				|ТребуетПовторения,
				|Пропущено");
			
			ДанныеОперации.ВремяВыполненияСозданныхОпераций = ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВЕдиницеИзмерения(
				НайденныеСтроки[0].ВремяВыполнения, 
				ДанныеОперации.ВремяВыполненияЕдИзм); 
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РезультатВыполненияОпераций(Этап, Операции = Неопределено)
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить("
		|ВЫБРАТЬ
		|	ПроизводственнаяОперация.Операция КАК Операция,
		|	ПроизводственнаяОперация.ИдентификаторОперации КАК ИдентификаторОперации,
		|	СУММА(ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус В (&СтатусСоздана, &СтатусВыполняется, &СтатусПропущена)
		|				ТОГДА ПроизводственнаяОперация.Количество
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполнена
		|				ТОГДА ПроизводственнаяОперация.КоличествоФакт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Создано,
		|	СУММА(ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполняется
		|				ТОГДА ПроизводственнаяОперация.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Выполняется,
		|	СУММА(ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполнена
		|				ТОГДА ПроизводственнаяОперация.КоличествоФакт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Выполнено,
		|	СУММА(ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполнена
		|					И ПроизводственнаяОперация.ТребуетПовторения
		|				ТОГДА ПроизводственнаяОперация.КоличествоФакт
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ТребуетПовторения,
		|	СУММА(ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусПропущена
		|				ТОГДА ПроизводственнаяОперация.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Пропущено,
		|	СУММА(ВЫБОР ПроизводственнаяОперация.ВремяВыполненияЕдИзм
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|				ТОГДА 60
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|				ТОГДА 3600
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|				ТОГДА 86400
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|				ТОГДА 86400
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПроизводственнаяОперация.ВремяВыполнения) КАК ВремяВыполнения
		|ИЗ
		|	Документ.ПроизводственнаяОперация2_2 КАК ПроизводственнаяОперация
		|ГДЕ
		|	ПроизводственнаяОперация.Этап = &Этап
		|	И &ТекстОтборОперации
		|	И ПроизводственнаяОперация.Статус <> &СтатусНеВыполнена
		|	И ПроизводственнаяОперация.Проведен
		|
		|СГРУППИРОВАТЬ ПО
		|	ПроизводственнаяОперация.Операция,ПроизводственнаяОперация.ИдентификаторОперации");
	
	Если Операции <> Неопределено Тогда
		ТекстыЗапроса.Вставить(0,
			"ВЫБРАТЬ
			|	ОтборОперации.Операция,
			|	ОтборОперации.ИдентификаторОперации
			|ПОМЕСТИТЬ ВтОтборОперации
			|ИЗ
			|	&ОтборОперации КАК ОтборОперации");
	КонецЕсли;
	
	ТекстЗапроса = УправлениеПроизводством.ОбъединитьТекстыЗапросаВПакет(ТекстыЗапроса);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Этап", Этап);
	
	Запрос.УстановитьПараметр("СтатусСоздана", Перечисления.СтатусыПроизводственныхОпераций.Создана);
	Запрос.УстановитьПараметр("СтатусВыполняется", Перечисления.СтатусыПроизводственныхОпераций.Выполняется);
	Запрос.УстановитьПараметр("СтатусВыполнена", Перечисления.СтатусыПроизводственныхОпераций.Выполнена);
	Запрос.УстановитьПараметр("СтатусПропущена", Перечисления.СтатусыПроизводственныхОпераций.Пропущена);
	Запрос.УстановитьПараметр("СтатусНеВыполнена", Перечисления.СтатусыПроизводственныхОпераций.НеВыполнена);
	
	Если Операции <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстОтборОперации", "
			|(ПроизводственнаяОперация.Операция,ПроизводственнаяОперация.ИдентификаторОперации) 
			|	В (ВЫБРАТЬ Т.Операция,Т.ИдентификаторОперации Из ВтОтборОперации КАК Т)");
		Запрос.УстановитьПараметр("ОтборОперации", Операции);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстОтборОперации", "Истина");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Операция,ИдентификаторОперации");
	
	Возврат Результат;
	
КонецФункции

Функция РассчитатьПоказателиВыполненияОперации(Операция, ДанныеОперации)
	
	ЗаполнитьЗначенияСвойств(
		Операция,
		ДанныеОперации,
		"Создано,
		|Выполняется,
		|Выполнено,
		|ТребуетПовторения,
		|Пропущено");
	
	Операция.ОжиданиеПредшествующих = 0;
	Операция.НачатыПредшествующие   = 0;
	Операция.МожноВыполнять         = 0;
	
	ПредыдущиеОперации = ПредыдущиеОперации(ДанныеОперации);
	
	Если ПредыдущиеОперации.ВГраница() <> -1 Тогда
		
		МожноВыполнятьРасчетное       = -1;
		НачатыПредшествующиеРасчетное = -1;
		
		Если Операция.Запланировано > 0 Тогда
		
			Для каждого ПредыдущаяОперация Из ПредыдущиеОперации Цикл
				
				РасчетноеКоличество = (ПредыдущаяОперация.Выполнено - ПредыдущаяОперация.ТребуетПовторения + ПредыдущаяОперация.Пропущено)
										/(ПредыдущаяОперация.КоличествоНаПартию/Операция.Запланировано);
				
				МожноВыполнятьРасчетное = ?(МожноВыполнятьРасчетное > -1,
													Мин(МожноВыполнятьРасчетное,
														РасчетноеКоличество),
													РасчетноеКоличество);
			КонецЦикла;
			
			Для каждого ПредыдущаяОперация Из ПредыдущиеОперации Цикл
				
				РасчетноеКоличество = ПредыдущаяОперация.Выполняется
										/(ПредыдущаяОперация.КоличествоНаПартию/Операция.Запланировано)
									+ Макс((ПредыдущаяОперация.Выполнено - ПредыдущаяОперация.ТребуетПовторения + ПредыдущаяОперация.Пропущено)
											/(ПредыдущаяОперация.КоличествоНаПартию/Операция.Запланировано) 
										- МожноВыполнятьРасчетное, 0);
				
				НачатыПредшествующиеРасчетное = ?(НачатыПредшествующиеРасчетное > -1,
														Мин(НачатыПредшествующиеРасчетное,
															РасчетноеКоличество), 
														РасчетноеКоличество);
				
			КонецЦикла;
		
		КонецЕсли;
		
		НачатыПредшествующиеРасчетное = Макс(НачатыПредшествующиеРасчетное, 0);
		МожноВыполнятьРасчетное       = Макс(МожноВыполнятьРасчетное, 0);
		
		ОжидаетВыполнения = Макс(Операция.Запланировано + Операция.ТребуетПовторения, Операция.Создано)
			- (Операция.Выполняется + Операция.Выполнено + Операция.Пропущено);
		
		Если МожноВыполнятьРасчетное < Макс(Операция.Запланировано + Операция.ТребуетПовторения, Операция.Создано) Тогда
			Операция.МожноВыполнять = МожноВыполнятьРасчетное
				- (Операция.Выполняется + Операция.Выполнено - Операция.ТребуетПовторения + Операция.Пропущено);
		Иначе
			Операция.МожноВыполнять = ОжидаетВыполнения;
		КонецЕсли;
		
		Операция.НачатыПредшествующие = Мин(НачатыПредшествующиеРасчетное, ОжидаетВыполнения);
		Операция.ОжиданиеПредшествующих = ОжидаетВыполнения - Операция.МожноВыполнять - Операция.НачатыПредшествующие;
		
	Иначе
		
		Операция.МожноВыполнять = Макс(Операция.Запланировано + Операция.ТребуетПовторения, Операция.Создано)
			- (Операция.Выполняется + Операция.Выполнено + Операция.Пропущено);
		
	КонецЕсли;
	
	ОбщееВремяВыполнения = ОперативныйУчетПроизводстваКлиентСервер.СтруктураРасчетаОбщегоВремениВыполнения();
	ЗаполнитьЗначенияСвойств(ОбщееВремяВыполнения, ДанныеОперации);
		
	ОперативныйУчетПроизводстваКлиентСервер.РассчитатьОбщееВремяВыполненияОперации(
		ОбщееВремяВыполнения, 
		Операция.КоэффициентМаршрутнойКарты);
		
	Операция.ВремяВыполненияНормативное = ОбщееВремяВыполнения.ВремяВыполнения;
	Операция.ВремяВыполненияЕдИзм       = ОбщееВремяВыполнения.ВремяВыполненияЕдИзм;
		
	Если ДанныеОперации.ВремяВыполненияСозданныхОпераций > 0 Тогда
		
		Если (Операция.Запланировано + Операция.ТребуетПовторения) > Операция.Создано
			И ДанныеОперации.Количество > 0 Тогда
		    
			ОперативныйУчетПроизводстваКлиентСервер.РассчитатьОбщееВремяВыполненияОперации(
					ОбщееВремяВыполнения,
					(Операция.Запланировано + Операция.ТребуетПовторения - Операция.Создано) / ДанныеОперации.Количество);
			
			Операция.ВремяВыполнения = ОбщееВремяВыполнения.ВремяВыполнения + ДанныеОперации.ВремяВыполненияСозданныхОпераций;
			
		Иначе
			
			Операция.ВремяВыполнения = ДанныеОперации.ВремяВыполненияСозданныхОпераций;
			
		КонецЕсли;
		
	Иначе
		
		Операция.ВремяВыполнения = Операция.ВремяВыполненияНормативное;
		
	КонецЕсли;
	
	Операция.Порядок = ?(Операция.Выполнено > 0 ИЛИ Операция.Пропущено > 0, 5, 
						?(Операция.Выполняется > 0, 4,
						?(Операция.МожноВыполнять > 0, 3,
						?(Операция.НачатыПредшествующие > 0, 2,
						?(Операция.ОжиданиеПредшествующих > 0, 1, 0)))));
	
КонецФункции

#КонецОбласти

Процедура СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ)
	
	ТекстСообщения = НСтр("ru = 'Не удалось рассчитать очередь производственных операций.';
							|en = 'Cannot calculate the production operation queue.'");
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			,
			, 
			Отказ);

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
