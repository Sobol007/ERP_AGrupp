
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТолькоПросмотр = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьФоновоеЗадание(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Запись не выделена.';
										|en = 'Record is not highlighted.'"));
		Возврат;
	КонецЕсли;
	
	ТекстРезультата = "";
	ОтменитьФоновоеЗаданиеНаСервере(Элементы.Список.ТекущиеДанные.ИдентификаторПотока, ТекстРезультата);
	
	ПоказатьПредупреждение(, ТекстРезультата);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ОтменитьФоновоеЗаданиеНаСервере(ИдентификаторЗадания, ТекстРезультата)
	
	ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторЗадания);
	
	Если ФоновоеЗадание = Неопределено Тогда
		ТекстРезультата = НСтр("ru = 'Не удалость найти фоновое задание по идентификатору.';
								|en = 'Could not find background job by id.'");
		Возврат;
	КонецЕсли;
	
	Попытка
		ФоновоеЗадание.Отменить();
		ТекстРезультата = НСтр("ru = 'Фоновое задание отменено.';
								|en = 'Background task is canceled'");
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстРезультата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось отменить фоновое задание по причине:
			           |%1';
			           |en = 'Failed to cancel the background job due to: 
			           |%1'"), КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти


