#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Отчет.Организация = РЭЙ_Универсализация.ПолучитьОсновнуюОрганизацию();
	
	Если Не ЗначениеЗаполнено(Отчет.ВариантОтчета) Тогда
		Отчет.ВариантОтчета = Элементы.ВариантОтчета.СписокВыбора[0];
	КонецЕсли;
	
	УстановитьПараметрыВыбора_КонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	Если ПроверитьЗаполнение() Тогда
		СформироватьОтчетНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()
	Результат.Очистить();

	ТекущийОбъект = РеквизитФормыВЗначение("Отчет");
	ТекущийОбъект.СформироватьОтчет(Результат);
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Отчет");
	
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	Элементы.Результат.ОтображениеСостояния.Видимость = Ложь;
КонецПроцедуры	

&НаКлиенте
Процедура ВыводитьЗаголовок(Команда)
	Элементы.ВыводитьЗаголовок.Пометка = Не Элементы.ВыводитьЗаголовок.Пометка;
	ВыводЗаголовка();
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеПериодаПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	ПараметрыОткрытия = Новый Структура("НачалоПериода,КонецПериода", Отчет.НачалоПериода, Отчет.ОкончаниеПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериод_Завершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыОткрытия, Элементы.ВыбратьПериод,,,, ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод_Завершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отчет.НачалоПериода = РезультатВыбора.НачалоПериода;
	Отчет.ОкончаниеПериода = РезультатВыбора.КонецПериода;
	
	ОтобразитьСостояниеНесформированногоОтчета();
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтчетаПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	УстановитьПараметрыВыбора_КонтрактВЭД();
	ЗаполнитьКонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	УстановитьПараметрыВыбора_КонтрактВЭД();
	ЗаполнитьКонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура КонтрактВЭДПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	КонтрактВЭДПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтрактВЭДПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Отчет.КонтрактВЭД) Тогда
		Отчет.Контрагент = Отчет.КонтрактВЭД.Контрагент;
		Отчет.Организация = Отчет.КонтрактВЭД.Организация;
		УстановитьПараметрыВыбора_КонтрактВЭД();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Отбражение

&НаСервере
Процедура УстановитьПараметрыВыбора_КонтрактВЭД()
	МассивПараметровВыбора = Новый Массив;
	
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Импорт", Истина));
	
	Элементы.КонтрактВЭД.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	СвязиПараметровВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		СвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Отчет.Организация"));
	КонецЕсли;
	Если ЗначениеЗаполнено(Отчет.Контрагент) Тогда
		СвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Контрагент", "Отчет.Контрагент"));
	КонецЕсли;
	
	Элементы.КонтрактВЭД.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыводЗаголовка()
	Если Отчет.ВысотаЗаголовка <> 0 Тогда
		Результат.Область(1,, Отчет.ВысотаЗаголовка).Видимость = Элементы.ВыводитьЗаголовок.Пометка;		
	КонецЕсли;	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьКонтрактВЭД()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РЭЙ_КонтрактыВЭД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РЭЙ_КонтрактыВЭД КАК РЭЙ_КонтрактыВЭД
	|ГДЕ
	|	РЭЙ_КонтрактыВЭД.Контрагент = &Контрагент
	|	И РЭЙ_КонтрактыВЭД.Организация = &Организация
	|	И НЕ РЭЙ_КонтрактыВЭД.ПометкаУдаления
	|	И РЭЙ_КонтрактыВЭД.Импорт";

	Запрос.УстановитьПараметр("Контрагент", Отчет.Контрагент);
	Запрос.УстановитьПараметр("Организация", Отчет.Организация);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Отчет.КонтрактВЭД = Выборка.Ссылка;
		КонтрактВЭДПриИзмененииНаСервере();
	Иначе
		Отчет.КонтрактВЭД = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеНесформированногоОтчета()
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
	Отчет.ВысотаЗаголовка = 0;
КонецПроцедуры
#КонецОбласти
