Процедура СформироватьОтчет(ТабДок) Экспорт
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Макет = ПолучитьМакет("Макет");
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	Область.Параметры.ПредставлениеПериода = ПредставлениеПериода(НачалоПериода, КонецДня(ОкончаниеПериода));
	Область.Параметры.Организация = ?(ЗначениеЗаполнено(Организация), Организация, "Все");
	Область.Параметры.Контрагент = ?(ЗначениеЗаполнено(Контрагент), Контрагент, "Все");
	Область.Параметры.КонтрактВЭД = ?(ЗначениеЗаполнено(КонтрактВЭД), КонтрактВЭД, "Все");
	ТабДок.Вывести(Область);
	ВысотаЗаголовка = ТабДок.ВысотаТаблицы;
		
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);	
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);	
	Запрос.УстановитьПараметр("Организация", Организация);	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);	
	Запрос.УстановитьПараметр("КонтрактВЭД", КонтрактВЭД);	
	
	Запрос.Текст =  
	"ВЫБРАТЬ
	|	РЭЙ_КонтрактыВЭД.Ссылка КАК КонтрактВЭД,
	|	РЭЙ_КонтрактыВЭД.ДоговорКонтрагента
	|ПОМЕСТИТЬ СоответствиеДоговоровИКонтрактов
	|ИЗ
	|	Справочник.РЭЙ_КонтрактыВЭД КАК РЭЙ_КонтрактыВЭД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РЭЙ_КонтрактыВЭДДополнительныеСоглашения.Ссылка,
	|	РЭЙ_КонтрактыВЭДДополнительныеСоглашения.Договор
	|ИЗ
	|	Справочник.РЭЙ_КонтрактыВЭД.ДополнительныеСоглашения КАК РЭЙ_КонтрактыВЭДДополнительныеСоглашения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГТДИмпортТовары.Ссылка КАК ГТД,
	|	ВЫРАЗИТЬ(ГТДИмпортТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг) КАК ПТУ,
	|	ВЫРАЗИТЬ(ГТДИмпортТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг).Контрагент КАК Контрагент,
	|	СоответствиеДоговоровИКонтрактов.КонтрактВЭД,
	|	СУММА(ГТДИмпортТовары.ТаможеннаяСтоимость) КАК СуммаГТД
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ГТДИмпортТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СоответствиеДоговоровИКонтрактов КАК СоответствиеДоговоровИКонтрактов
	|		ПО (ВЫРАЗИТЬ(ГТДИмпортТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг).Договор = СоответствиеДоговоровИКонтрактов.ДоговорКонтрагента)
	|ГДЕ
	|	(&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ ГТДИмпортТовары.ДокументПоступления.Организация = &Организация)
	|	И (&Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ИЛИ ГТДИмпортТовары.ДокументПоступления.Контрагент = &Контрагент)
	|	И (&КонтрактВЭД = ЗНАЧЕНИЕ(Справочник.РЭЙ_КонтрактыВЭД.ПустаяСсылка)
	|			ИЛИ СоответствиеДоговоровИКонтрактов.КонтрактВЭД = &КонтрактВЭД)
	|	И (&ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И ГТДИмпортТовары.Ссылка.Дата > &НачалоПериода
	|			ИЛИ ГТДИмпортТовары.Ссылка.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&ОкончаниеПериода, ДЕНЬ))
	|	И (&ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И ГТДИмпортТовары.ДокументПоступления.Дата > &НачалоПериода
	|			ИЛИ ГТДИмпортТовары.ДокументПоступления.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&ОкончаниеПериода, ДЕНЬ))
	|	И ГТДИмпортТовары.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ГТДИмпортТовары.Ссылка,
	|	ВЫРАЗИТЬ(ГТДИмпортТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг).Контрагент,
	|	ВЫРАЗИТЬ(ГТДИмпортТовары.ДокументПоступления КАК Документ.ПриобретениеТоваровУслуг),
	|	СоответствиеДоговоровИКонтрактов.КонтрактВЭД";
	
	Запрос.Выполнить();
	
	
	Если ВариантОтчета = "ГТД сгруппированные по ПТУ" Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Данные.Контрагент КАК Контрагент,
		|	Данные.КонтрактВЭД КАК КонтрактВЭД,
		|	Данные.ПТУ КАК Документ1,
		|	Данные.ПТУ.СуммаДокумента КАК Сумма1,
		|	Данные.ПТУ.Дата КАК Дата1,
		|	Данные.ПТУ.Валюта КАК Валюта1,
		|	Данные.ГТД КАК Документ2,
		|	Данные.СуммаГТД КАК Сумма2,
		|	Данные.ГТД.Дата КАК Дата2,
		|	Данные.ГТД.Валюта КАК Валюта2,
		|	Данные.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК Валюта
		|ИЗ
		|	Данные КАК Данные
		|
		|УПОРЯДОЧИТЬ ПО
		|	Данные.Контрагент.Наименование,
		|	Данные.КонтрактВЭД.КонтрактДатаПодписания,
		|	Данные.КонтрактВЭД.КонтрактНомер,
		|	Данные.КонтрактВЭД.Наименование,
		|	Данные.ПТУ.Дата,
		|	Данные.ГТД.Дата
		|ИТОГИ
		|	СУММА(Сумма2)
		|ПО
		|	Контрагент,
		|	КонтрактВЭД,
		|	Документ1,
		|	Документ2";
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Данные.Контрагент КАК Контрагент,
		|	Данные.КонтрактВЭД КАК КонтрактВЭД,
		|	Данные.ГТД КАК Документ1,
		|	Данные.СуммаГТД КАК Сумма1,
		|	Данные.ГТД.Дата КАК Дата1,
		|	Данные.ГТД.Валюта КАК Валюта1,
		|	Данные.ПТУ КАК Документ2,
		|	Данные.ПТУ.СуммаДокумента КАК Сумма2,
		|	Данные.ПТУ.Дата КАК Дата2,
		|	Данные.ПТУ.Валюта КАК Валюта2,
		|	Данные.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК Валюта
		|ИЗ
		|	Данные КАК Данные
		|
		|УПОРЯДОЧИТЬ ПО
		|	Данные.Контрагент.Наименование,
		|	Данные.КонтрактВЭД.КонтрактДатаПодписания,
		|	Данные.КонтрактВЭД.КонтрактНомер,
		|	Данные.КонтрактВЭД.Наименование,
		|	Данные.ГТД.Дата,
		|	Данные.ПТУ.Дата
		|ИТОГИ
		|	СУММА(Сумма1)
		|ПО
		|	Контрагент,
		|	КонтрактВЭД,
		|	Документ1,
		|	Документ2";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если ВариантОтчета = "ГТД сгруппированные по ПТУ" Тогда
		Область = Макет.ПолучитьОбласть("Шапка_ГТДпоПТУ");
	Иначе
		Область = Макет.ПолучитьОбласть("Шапка_ПТУпоГТД");
	КонецЕсли;
	ТабДок.Вывести(Область);
	ТабДок.ФиксацияСверху = ТабДок.ВысотаТаблицы;
	
	ВыборкаКонтрагент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКонтрагент.Следующий() Цикл
		Область = Макет.ПолучитьОбласть("Контрагент");
		Область.Параметры.Заполнить(ВыборкаКонтрагент);
		ТабДок.Вывести(Область);
		
		ВыборкаКонтрактВЭД = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрактВЭД.Следующий() Цикл
			ВывестиВалюту = Истина;
			ВывестиПаспортСделки = Истина;
			
			Область = Макет.ПолучитьОбласть("КонтрактВЭД");
			Область.Параметры.Заполнить(ВыборкаКонтрактВЭД);
			ТабДок.Вывести(Область);
			текПаспортСделки = РЭЙ_СлужебныйСервер.ПолучитьПаспортСделкиПоКонтрактуВЭД(ВыборкаКонтрактВЭД.КонтрактВЭД);
			
			ВыборкаДокумент1 = ВыборкаКонтрактВЭД.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДокумент1.Следующий() Цикл
				Область = Макет.ПолучитьОбласть("Документ1");
				Область.Параметры.Заполнить(ВыборкаДокумент1);
				Область.Параметры.Документ1Представление = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(ВыборкаДокумент1.Документ1);
				Область.Параметры.СуммаВалютаКонтракта = РЭЙ_СлужебныйСервер.ПересчитатьИзВалютыВВалюту(ВыборкаДокумент1.Сумма1, ВыборкаДокумент1.Дата1, ВыборкаДокумент1.Валюта1, ВыборкаКонтрактВЭД.Валюта);
				Область.Параметры.СуммаВалютаРегл = РЭЙ_СлужебныйСервер.ПересчитатьИзВалютыВВалюту(ВыборкаДокумент1.Сумма1, ВыборкаДокумент1.Дата1, ВыборкаДокумент1.Валюта1, ВалютаРегл);
				Если ВывестиВалюту Тогда
					Область.Параметры.ВалютаПредставление = ВыборкаКонтрактВЭД.Валюта;
					ВывестиВалюту = Ложь;
				КонецЕсли;
				ТабДок.Вывести(Область);
				
				ВыборкаДокумент2 = ВыборкаДокумент1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаДокумент2.Следующий() Цикл
					Область = Макет.ПолучитьОбласть("Документ2");
					Область.Параметры.Заполнить(ВыборкаДокумент2);
					Область.Параметры.Документ2Представление = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(ВыборкаДокумент2.Документ2);
					Область.Параметры.СуммаВалютаКонтракта = РЭЙ_СлужебныйСервер.ПересчитатьИзВалютыВВалюту(ВыборкаДокумент2.Сумма2, ВыборкаДокумент2.Дата2, ВыборкаДокумент2.Валюта2, ВыборкаКонтрактВЭД.Валюта);
					Область.Параметры.СуммаВалютаРегл = РЭЙ_СлужебныйСервер.ПересчитатьИзВалютыВВалюту(ВыборкаДокумент2.Сумма2, ВыборкаДокумент2.Дата2, ВыборкаДокумент2.Валюта2, ВалютаРегл);
					Если ВывестиПаспортСделки Тогда
						Область.Параметры.НомерПаспортаСделкиПредставление = текПаспортСделки.НомерПаспортаСделки;
						Область.Параметры.ПаспортСделкиРасшифровка = текПаспортСделки;
						ВывестиПаспортСделки = Ложь;
					КонецЕсли;
					ТабДок.Вывести(Область);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		
	КонецЦикла;
	
	Область = Макет.ПолучитьОбласть("Дно");
	ТабДок.Вывести(Область);
	
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры

