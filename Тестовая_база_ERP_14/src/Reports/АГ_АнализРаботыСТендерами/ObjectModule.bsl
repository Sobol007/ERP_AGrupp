Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ВыбранныеКолонки	= Неопределено;
	
	Для Каждого ЭлементНастроек ИЗ ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастроек)=Тип("ЗначениеПараметраНастроекКомпоновкиДанных") 
			И Строка(ЭлементНастроек.Параметр)="СписокРеквизитов" И ЭлементНастроек.Использование Тогда
			
			ВыбранныеКолонки	= ЭлементНастроек.Значение; 	
		КонецЕсли;	
	КонецЦикла;
	
	Если ВыбранныеКолонки=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого ЭлементВыбраннаяКолонка ИЗ ВыбранныеКолонки Цикл
		
		ЗаголовокКолонки	= ЭлементВыбраннаяКолонка.Значение;
		Для Каждого ПолеГруппировок Из ЭтотОбъект.КомпоновщикНастроек.Настройки.ДоступныеПоляГруппировок.Элементы Цикл
			Если Строка(ПолеГруппировок.Поле)=ЭлементВыбраннаяКолонка.Значение Тогда
				ЗаголовокКолонки	= ПолеГруппировок.Заголовок;
			КонецЕсли;	
		КонецЦикла;
		
		ЭлементВыбраннаяКолонка.Представление	= ЗаголовокКолонки;
		
	КонецЦикла;	
		
		
	СКД    = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	знКомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	знКомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД));
	знКомпоновщикНастроек.ЗагрузитьНастройки(ЭтотОбъект.КомпоновщикНастроек.Настройки);
	знКомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ЭтотОбъект.КомпоновщикНастроек.ПользовательскиеНастройки);
	
	Настройки = знКомпоновщикНастроек.Настройки;
	
	// Удалим все группировки
	
	Настройки.Структура.Очистить();
	
	// Добавим группировку
	
	ДетальныеЗаписи = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	// Добавим выбранные колонки
	знКомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();	
	Для Каждого ЭлементВыбраннаяКолонка ИЗ ВыбранныеКолонки Цикл
		ВыбранныеПоля = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранныеПоля.Поле      = Новый ПолеКомпоновкиДанных(ЭлементВыбраннаяКолонка.Значение);
		ВыбранныеПоля.Заголовок = ЭлементВыбраннаяКолонка.Представление;	
	КонецЦикла;	
	
	// Уберем итоги
	Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, знКомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки,, Тип("ГенераторМакетаКомпоновкиДанных"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры	