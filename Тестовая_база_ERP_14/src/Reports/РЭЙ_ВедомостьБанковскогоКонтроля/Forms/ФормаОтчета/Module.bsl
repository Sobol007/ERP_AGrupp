#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьДатуОтчета();
	Отчет.Организация = РЭЙ_Универсализация.ПолучитьОсновнуюОрганизацию();
	
	Отчет.НеПечататьРазделы456 = Истина;	
	
	Если ЗначениеЗаполнено(Отчет.КонтрактВЭД) Тогда
		СформироватьОтчетНаСервере();
	КонецЕсли;
	
	УстановитьПараметрыВыбора_КонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	Если ПроверитьЗаполнение() Тогда
		Если Не ЗначениеЗаполнено(РЭЙ_СлужебныйСервер.ПолучитьЗначениеРеквизита(Отчет.КонтрактВЭД, "ПаспортСделки")) Тогда
			ПоказатьПредупреждение(, "Не удалось найти документ ""Заявление о постановке на учет контракта"" по выбранному контракту.");
			Возврат;
		КонецЕсли;
		СформироватьОтчетНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()
	ЗаполнитьДатуОтчета();
	
	Результат.Очистить();
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	
	Если Отчет.ДатаОтчета >= Дата(2018,03,01) Тогда
		Результат.Вывести(ОтчетОбъект.СформироватьВБК_2018());
	Иначе
		Результат.Вывести(ОтчетОбъект.СформироватьВБК_2015());
	КонецЕсли;
	
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.НеИспользовать;
	Элементы.Результат.ОтображениеСостояния.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтчетаПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	Отчет.ДатаОтчетаВручную = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтчетаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОтобразитьСостояниеНесформированногоОтчета();
	Отчет.ДатаОтчетаВручную = Ложь;
	ЗаполнитьДатуОтчета();
КонецПроцедуры

&НаКлиенте
Процедура ВидКонтрактаВЭДПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	УстановитьПараметрыВыбора_КонтрактВЭД();
	ЗаполнитьКонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	УстановитьПараметрыВыбора_КонтрактВЭД();
	ЗаполнитьКонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	УстановитьПараметрыВыбора_КонтрактВЭД();
	ЗаполнитьКонтрактВЭД();
КонецПроцедуры

&НаКлиенте
Процедура КонтрактВЭДПриИзменении(Элемент)
	ОтобразитьСостояниеНесформированногоОтчета();
	КонтрактВЭДПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КонтрактВЭДПриИзмененииНаСервере()
	Если ЗначениеЗаполнено(Отчет.КонтрактВЭД) Тогда
		Отчет.Контрагент = Отчет.КонтрактВЭД.Контрагент;
		Отчет.Организация = Отчет.КонтрактВЭД.Организация;
		
		Если Отчет.КонтрактВЭД.КредитныйДоговор = Ложь Тогда
			Отчет.ВидКонтрактаВЭД = "Контракт";
		КонецЕсли; 
		Если Отчет.КонтрактВЭД.КредитныйДоговор Тогда
			Отчет.ВидКонтрактаВЭД = "Кредитный договор";
		КонецЕсли;
		
		УстановитьПараметрыВыбора_КонтрактВЭД();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Отображение

&НаСервере

Процедура УстановитьПараметрыВыбора_КонтрактВЭД()
	МассивПараметровВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(Отчет.ВидКонтрактаВЭД) Тогда
		Если Отчет.ВидКонтрактаВЭД = "Контракт" Тогда
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.КредитныйДоговор", Ложь));
		КонецЕсли;
		Если Отчет.ВидКонтрактаВЭД = "Кредитный договор" Тогда
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.КредитныйДоговор", Истина));
		КонецЕсли;
	КонецЕсли;
	
	Элементы.КонтрактВЭД.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	СвязиПараметровВыбора = Новый Массив;
	
	Если ЗначениеЗаполнено(Отчет.Организация) Тогда
		СвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Отчет.Организация"));
	КонецЕсли;
	Если ЗначениеЗаполнено(Отчет.Контрагент) Тогда
		СвязиПараметровВыбора.Добавить(Новый СвязьПараметраВыбора("Отбор.Контрагент", "Отчет.Контрагент"));
	КонецЕсли;
	
	Элементы.КонтрактВЭД.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДатуОтчета()
	Если Не Отчет.ДатаОтчетаВручную Тогда
		Отчет.ДатаОтчета = ТекущаяДатаСеанса();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКонтрактВЭД()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РЭЙ_КонтрактыВЭД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.РЭЙ_КонтрактыВЭД КАК РЭЙ_КонтрактыВЭД
	|ГДЕ
	|	РЭЙ_КонтрактыВЭД.Контрагент = &Контрагент
	|	И РЭЙ_КонтрактыВЭД.Организация = &Организация
	|	И НЕ РЭЙ_КонтрактыВЭД.ПометкаУдаления
	|	И (&ВидКонтрактаВЭД = """"
	|			ИЛИ &ВидКонтрактаВЭД = ""Контракт""
	|				И РЭЙ_КонтрактыВЭД.КредитныйДоговор = ЛОЖЬ
	|			ИЛИ &ВидКонтрактаВЭД = ""Кредитный договор""
	|				И РЭЙ_КонтрактыВЭД.КредитныйДоговор = ИСТИНА)";

	Запрос.УстановитьПараметр("Контрагент", Отчет.Контрагент);
	Запрос.УстановитьПараметр("Организация", Отчет.Организация);
	Запрос.УстановитьПараметр("ВидКонтрактаВЭД", Отчет.ВидКонтрактаВЭД);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Отчет.КонтрактВЭД = Выборка.Ссылка;
		КонтрактВЭДПриИзмененииНаСервере();
	Иначе
		Отчет.КонтрактВЭД = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеНесформированногоОтчета()
	Элементы.Результат.ОтображениеСостояния.ДополнительныйРежимОтображения = ДополнительныйРежимОтображения.Неактуальность;
	Элементы.Результат.ОтображениеСостояния.Видимость = Истина;
КонецПроцедуры

#КонецОбласти
