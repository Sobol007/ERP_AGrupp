#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.РазрешеноИзменятьВарианты = Ложь;
	
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
		ЗначениеПоиска = Новый ПараметрКомпоновкиДанных("Номенклатура");
		Для каждого ЭлементПараметр Из КомпоновщикНастроекФормы.Настройки.ПараметрыДанных.Элементы Цикл
			Если ЭлементПараметр.Параметр = ЗначениеПоиска Тогда
				ЭлементПараметр.ПредставлениеПользовательскойНастройки = НСтр("ru = 'Комплект';
																				|en = 'Kit'");
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ОтборНоменклатура = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Номенклатура").Значение;
	ОтборСерия = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Серия").Значение;
	
	Если НЕ ЗначениеЗаполнено(ОтборНоменклатура) Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Продукция или полуфабрикат"" не заполнено.';
									|en = 'The ""Products or semi-finished product"" field is not filled in.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Поле ""Комплект"" не заполнено.';
									|en = 'Kit is not filled in. '");
		КонецЕсли; 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ); 
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ОтборСерия) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Серия"" не заполнено.';
								|en = 'The Series field is not filled in.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ); 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументРезультат.Очистить();
	
	#Область Инициализация
	ИспользоватьПроизводство = ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство");
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Номенклатура", КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Номенклатура").Значение);
	ПараметрыОтчета.Вставить("Характеристика", КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Характеристика").Значение);
	ПараметрыОтчета.Вставить("Серия", КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Серия").Значение);
	
	СтруктураСерииДерево = СтруктураСерииНоменклатуры(ПараметрыОтчета);
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.СтруктураСерииНоменклатуры.ПФ_MXL_СтруктураСерииНоменклатуры");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьПараметры = Макет.ПолучитьОбласть("Параметры");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПустаяСтрока = Макет.ПолучитьОбласть("ПустаяСтрока");
	#КонецОбласти
	
	// Вывод области Заголовок
	#Область ОбластьЗаголовок
	ПараметрыОбласти = Новый Структура("ТекстЗаголовка", НСтр("ru = 'Структура серии номенклатуры';
																|en = 'Product series structure'"));
	ОбластьЗаголовок.Параметры.Заполнить(ПараметрыОбласти);
	ДокументРезультат.Вывести(ОбластьЗаголовок);
	#КонецОбласти
	
	// Вывод области Параметры
	#Область ОбластьПараметры
	ПараметрыОбласти = Новый Структура;
	ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
										Строка(ПараметрыОтчета.Номенклатура), 
										Строка(ПараметрыОтчета.Характеристика));
										
	ПараметрыОбласти.Вставить("ОтборНоменклатура", ПредставлениеНоменклатуры);
	ПараметрыОбласти.Вставить("ОтборСерия", Строка(ПараметрыОтчета.Серия));
	Если ИспользоватьПроизводство Тогда
		ПараметрыОбласти.Вставить("ТекстПродукция", НСтр("ru = 'Продукция или полуфабрикат';
														|en = 'Products or semi-finished product'"));
	Иначе
		ПараметрыОбласти.Вставить("ТекстПродукция", НСтр("ru = 'Комплект';
														|en = 'Kit'"));
	КонецЕсли;
	ОбластьПараметры.Параметры.Заполнить(ПараметрыОбласти);
	ОбластьПараметры.Параметры.Заполнить(ПараметрыОбласти);
	ДокументРезультат.НачатьАвтогруппировкуСтрок();
	ДокументРезультат.Вывести(ОбластьПустаяСтрока, 1,, Истина);
	ДокументРезультат.Вывести(ОбластьПараметры, 2,, Истина);
	ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	ДокументРезультат.Вывести(ОбластьПустаяСтрока);
	#КонецОбласти
	
	// Вывод области Шапка
	#Область ОбластьШапка
	ПараметрыОбласти = Новый Структура;
	Если ИспользоватьПроизводство Тогда
		ПараметрыОбласти.Вставить("ТекстМатериал", НСтр("ru = 'Полуфабрикат или материал';
														|en = 'Semi-finished product or material'"));
		ПараметрыОбласти.Вставить("ТекстИзрасходовано", НСтр("ru = 'Израсходовано';
															|en = 'Spent'"));
	Иначе
		ПараметрыОбласти.Вставить("ТекстМатериал", НСтр("ru = 'Комплектующая';
														|en = 'Component'"));
		ПараметрыОбласти.Вставить("ТекстИзрасходовано", НСтр("ru = 'Израсходовано';
															|en = 'Spent'"));
	КонецЕсли;
	ОбластьШапка.Параметры.Заполнить(ПараметрыОбласти);
	ДокументРезультат.Вывести(ОбластьШапка);
	#КонецОбласти
	
	ДокументРезультат.НачатьАвтогруппировкуСтрок();
	ЗаполнитьСтрокиРекурсивно(СтруктураСерииДерево, ОбластьСтрока, 0, ДокументРезультат);
	ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураСерииНоменклатуры(Параметры)
	
	СтруктураСерииНоменклатурыДерево = Новый ДеревоЗначений;
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("Израсходовано", Новый ОписаниеТипов("Число"));
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("Строка"));
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("НоменклатураПредставление", Новый ОписаниеТипов("Строка"));
	СтруктураСерииНоменклатурыДерево.Колонки.Добавить("СерияПредставление", Новый ОписаниеТипов("Строка"));
	
	СтруктураСерииКоллекция = СтруктураСерииНоменклатурыДерево.Строки;
	
	СписокНоменклатуры = Новый ТаблицаЗначений;
	СписокНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СписокНоменклатуры.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СписокНоменклатуры.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	СписокНоменклатуры.Колонки.Добавить("Строки");
	
	НоваяСтрока = СписокНоменклатуры.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
	НоваяСтрока.Строки = СтруктураСерииКоллекция;
	
	// Для предотвращения зацикливания
	ОтработаннаяНоменклатура = Новый ТаблицаЗначений;
	ОтработаннаяНоменклатура.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ОтработаннаяНоменклатура.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ОтработаннаяНоменклатура.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	ЗаполнитьЗначенияСвойств(ОтработаннаяНоменклатура.Добавить(), Параметры);
	
	ОтборПоХарактеристике = ЗначениеЗаполнено(Параметры.Характеристика);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока СписокНоменклатуры.Количество() <> 0 Цикл
		
		НовыйСписокНоменклатуры = СписокНоменклатуры.СкопироватьКолонки();
		
		ИспользованныеСерии = ИспользованныеСерии(СписокНоменклатуры, ОтборПоХарактеристике);
		
		Если НЕ ОтборПоХарактеристике Тогда
			
			ОтборПоХарактеристике = Истина;
			
			СписокНоменклатуры.Очистить();
			
			Колонки = "НоменклатураСписка, ХарактеристикаСписка, СерияСписка";
			ИспользованныеСерииКопия = ИспользованныеСерии.Скопировать(, Колонки);
			ИспользованныеСерииКопия.Свернуть(Колонки);
			
			Для каждого Строка Из ИспользованныеСерииКопия Цикл
				
				НоваяСтрока = СписокНоменклатуры.Добавить();
				НоваяСтрока.Номенклатура = Строка.НоменклатураСписка;
				НоваяСтрока.Характеристика = Строка.ХарактеристикаСписка;
				НоваяСтрока.Серия = Строка.СерияСписка;
				НоваяСтрока.Строки = СтруктураСерииКоллекция;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Для каждого СтрокаНоменклатура Из СписокНоменклатуры Цикл
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("НоменклатураСписка", СтрокаНоменклатура.Номенклатура);
			СтруктураПоиска.Вставить("ХарактеристикаСписка", СтрокаНоменклатура.Характеристика);
			СтруктураПоиска.Вставить("СерияСписка", СтрокаНоменклатура.Серия);
			
	  		СписокСтрок = ИспользованныеСерии.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаСерияМатериала Из СписокСтрок Цикл
				
				НоваяСерия = СтрокаНоменклатура.Строки.Добавить();
				
				ЗаполнитьЗначенияСвойств(НоваяСерия, СтрокаСерияМатериала);
				
				НоваяСерия.НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаСерияМатериала.НоменклатураПредставление,
					СтрокаСерияМатериала.ХарактеристикаПредставление);
					
				Если ЗначениеЗаполнено(НоваяСерия.Серия) Тогда
					
					СтруктураПоиска = Новый Структура("Номенклатура, Характеристика, Серия");
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, НоваяСерия);
					
			 		СписокСтрок = ОтработаннаяНоменклатура.НайтиСтроки(СтруктураПоиска);
					
					Если СписокСтрок.Количество() = 0 Тогда
						
						НоваяНоменклатура = НовыйСписокНоменклатуры.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяНоменклатура, НоваяСерия);
						НоваяНоменклатура.Строки = НоваяСерия.Строки;
						
						ЗаполнитьЗначенияСвойств(ОтработаннаяНоменклатура.Добавить(), НоваяСерия);
						
					КонецЕсли; 
					
				КонецЕсли;
				
			КонецЦикла; 
			
		КонецЦикла;
		
		СписокНоменклатуры = НовыйСписокНоменклатуры.Скопировать();
		
	КонецЦикла; 
	
	Возврат СтруктураСерииНоменклатурыДерево;
	
КонецФункции

Функция ИспользованныеСерии(СписокНоменклатуры, ОтборПоХарактеристике)

	ТекстыЗапроса = Новый Массив;
	
	#Область СписокНоменклатуры
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Серия КАК Справочник.СерииНоменклатуры) КАК Серия
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&СписокНоменклатуры КАК СписокНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия");
	
	#КонецОбласти
	
	//++ НЕ УТ
	#Область ПоступлениеСерийПриПроизводстве21
	
	//++ НЕ УТКА
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства) КАК Документ,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).Распоряжение КАК Распоряжение,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).КодСтроки КАК КодСтроки,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).КодСтрокиЭтапыГрафик КАК КодСтрокиЭтапыГрафик,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).Этап КАК Этап,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).НоменклатураПолуфабриката КАК НоменклатураПолуфабриката,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).НомерПартии КАК НомерПартии,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ ПоступлениеСерииПоГрафикуПроизводства
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПроизводствоПродукции)
	|				И Документ ССЫЛКА Документ.МаршрутныйЛистПроизводства) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия,
	|	Распоряжение,
	|	КодСтроки,
	|	КодСтрокиЭтапыГрафик");
	//-- НЕ УТКА
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.ВыпускПродукции) КАК Документ,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ ПоступлениеСерииПоВыпуску
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)
	|				И Документ ССЫЛКА Документ.ВыпускПродукции) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоступлениеСерииПоВыпуску.Номенклатура КАК Номенклатура,
	|	ПоступлениеСерииПоВыпуску.Характеристика КАК Характеристика,
	|	ПоступлениеСерииПоВыпуску.Серия КАК Серия,
	|	ТаблицаТовары.Распоряжение КАК Распоряжение,
	|	ПоступлениеСерииПоВыпуску.Количество КАК Количество
	|ПОМЕСТИТЬ ПоступлениеСерииВыпускПоРаспоряжению
	|ИЗ
	|	ПоступлениеСерииПоВыпуску КАК ПоступлениеСерииПоВыпуску
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК ТаблицаДокумента
	|		ПО (ТаблицаДокумента.Ссылка = ПоступлениеСерииПоВыпуску.Документ)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаТовары
	|		ПО (ТаблицаТовары.Ссылка = ПоступлениеСерииПоВыпуску.Документ)
	|			И (ТаблицаТовары.Номенклатура = ПоступлениеСерииПоВыпуску.Номенклатура)
	|ГДЕ
	|	ТаблицаДокумента.ВыпускПоРаспоряжениям
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение");
	
	#КонецОбласти
	//-- НЕ УТ
	
	//++ НЕ УТ
	#Область ДокументыПоступленияСерийПриПроизводстве21
	
	//++ НЕ УТКА
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоступлениеСерии.Номенклатура КАК Номенклатура,
	|	ПоступлениеСерии.Характеристика КАК Характеристика,
	|	ПоступлениеСерии.Серия КАК Серия,
	|	МаршрутныйЛистПроизводства.Ссылка КАК Ссылка,
	|	МаршрутныйЛистПроизводства.Распоряжение КАК Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки КАК КодСтроки,
	|	МаршрутныйЛистПроизводства.Этап КАК Этап,
	|	МаршрутныйЛистПроизводства.НомерПартии КАК НомерПартии
	|ПОМЕСТИТЬ МаршрутныеЛисты
	|ИЗ
	|	ПоступлениеСерииПоГрафикуПроизводства КАК ПоступлениеСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Распоряжение = ПоступлениеСерии.Распоряжение)
	|			И (МаршрутныйЛистПроизводства.КодСтроки = ПоступлениеСерии.КодСтроки)
	|			И (МаршрутныйЛистПроизводства.НомерПартии = ПоступлениеСерии.НомерПартии)
	|			И (МаршрутныйЛистПроизводства.НоменклатураПолуфабриката = ПоступлениеСерии.НоменклатураПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.ХарактеристикаПолуфабриката = ПоступлениеСерии.ХарактеристикаПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.Проведен)
	|			И (МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПоступлениеСерии.Номенклатура,
	|	ПоступлениеСерии.Характеристика,
	|	ПоступлениеСерии.Серия,
	|	МаршрутныйЛистПроизводства.Ссылка,
	|	МаршрутныйЛистПроизводства.Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки,
	|	МаршрутныйЛистПроизводства.Этап,
	|	МаршрутныйЛистПроизводства.НомерПартии
	|ИЗ
	|	ПоступлениеСерииВыпускПоРаспоряжению КАК ПоступлениеСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистВыпуска
	|		ПО (МаршрутныйЛистВыпуска.Ссылка = ПоступлениеСерии.Распоряжение)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Распоряжение = МаршрутныйЛистВыпуска.Распоряжение)
	|			И (МаршрутныйЛистПроизводства.КодСтроки = МаршрутныйЛистВыпуска.КодСтроки)
	|			И (МаршрутныйЛистПроизводства.НомерПартии = МаршрутныйЛистВыпуска.НомерПартии)
	|			И (МаршрутныйЛистПроизводства.НоменклатураПолуфабриката = МаршрутныйЛистВыпуска.НоменклатураПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.ХарактеристикаПолуфабриката = МаршрутныйЛистВыпуска.ХарактеристикаПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.Проведен)
	|			И (МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МаршрутныеЛисты.Номенклатура КАК Номенклатура,
	|	МаршрутныеЛисты.Характеристика КАК Характеристика,
	|	МаршрутныеЛисты.Серия КАК Серия,
	|	МаршрутныеЛисты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ МаршрутныеЛистыСписка
	|ИЗ
	|	МаршрутныеЛисты КАК МаршрутныеЛисты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	ВложенныйЗапрос.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РаспределениеЗатратНаЭтапыГрафика
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПоступлениеСерии.Номенклатура КАК Номенклатура,
	|		ПоступлениеСерии.Характеристика КАК Характеристика,
	|		ПоступлениеСерии.Серия КАК Серия,
	|		РаспределениеЗатрат.Ссылка КАК Ссылка
	|	ИЗ
	|		ПоступлениеСерииПоГрафикуПроизводства КАК ПоступлениеСерии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК РаспределениеЗатрат
	|			ПО (РаспределениеЗатрат.ЗаказНаПроизводство = ПоступлениеСерии.Распоряжение)
	|				И (РаспределениеЗатрат.КодСтрокиПродукция = ПоступлениеСерии.КодСтроки)
	|				И (РаспределениеЗатрат.Этап = ПоступлениеСерии.Этап)
	|				И (РаспределениеЗатрат.Ссылка.Проведен)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МаршрутныеЛисты.Номенклатура,
	|		МаршрутныеЛисты.Характеристика,
	|		МаршрутныеЛисты.Серия,
	|		РаспределениеЗатрат.Ссылка
	|	ИЗ
	|		МаршрутныеЛисты КАК МаршрутныеЛисты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК РаспределениеЗатрат
	|			ПО (РаспределениеЗатрат.ЗаказНаПроизводство = МаршрутныеЛисты.Распоряжение)
	|				И (РаспределениеЗатрат.КодСтрокиПродукция = МаршрутныеЛисты.КодСтроки)
	|				И (РаспределениеЗатрат.Этап = МаршрутныеЛисты.Этап)
	|				И (РаспределениеЗатрат.Ссылка.Проведен)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка");
	//-- НЕ УТКА
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	ВложенныйЗапрос.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ РаспределениеЗатратНаВыпускБезРаспоряжений
	|ИЗ
	|	(ВЫБРАТЬ
	|		РаспределениеЗатрат.Номенклатура КАК Номенклатура,
	|		РаспределениеЗатрат.Характеристика КАК Характеристика,
	|		РаспределениеЗатрат.Серия КАК Серия,
	|		РаспределениеЗатрат.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.РаспределениеПроизводственныхЗатрат.ВыпускиБезРаспоряжения КАК РаспределениеЗатрат
	|	ГДЕ
	|		&ОтборПоНоменклатуреРаспределениеЗатрат
	|		И РаспределениеЗатрат.Ссылка.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПоступлениеСерииПоВыпуску.Номенклатура,
	|		ПоступлениеСерииПоВыпуску.Характеристика,
	|		ПоступлениеСерииПоВыпуску.Серия,
	|		ТаблицаВыходныеИзделия.Ссылка
	|	ИЗ
	|		ПоступлениеСерииПоВыпуску КАК ПоступлениеСерииПоВыпуску
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ТаблицаВыходныеИзделия
	|		ПО ТаблицаВыходныеИзделия.Распоряжение = ПоступлениеСерииПоВыпуску.Документ
	|			И ТаблицаВыходныеИзделия.Номенклатура = ПоступлениеСерииПоВыпуску.Номенклатура
	|			И ТаблицаВыходныеИзделия.Характеристика = ПоступлениеСерииПоВыпуску.Характеристика
	|			И ТаблицаВыходныеИзделия.Ссылка.Проведен) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка");
	
	#КонецОбласти
	//-- НЕ УТ
	
	//++ НЕ УТ
	#Область ПоступлениеСерийПриПроизводстве22
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ДвижениеСерии.Документ КАК Документ
	|ПОМЕСТИТЬ ПоступлениеСерийПриПроизводствеБезЗаказа
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПроизводствоПродукции)
	|				И Документ ССЫЛКА Документ.ПроизводствоБезЗаказа) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ");
	
	//++ НЕ УТКА
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.ЭтапПроизводства2_2).ПартияПроизводства КАК ПартияПроизводства
	|ПОМЕСТИТЬ ПоступлениеСерийВЭтапахПроизводства
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПроизводствоПродукции)
	|				И Документ ССЫЛКА Документ.ЭтапПроизводства2_2) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства");
	//-- НЕ УТКА
	
	#КонецОбласти
	//-- НЕ УТ
	
	//++ НЕ УТКА
	#Область ДокументыПоступленияСерийПриПроизводстве22
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ЭтапыПроизводства
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|ГДЕ
	|	ЭтапПроизводства2_2.ПартияПроизводства В
	|			(ВЫБРАТЬ
	|				ПоступлениеСерий.ПартияПроизводства
	|			ИЗ
	|				ПоступлениеСерийВЭтапахПроизводства КАК ПоступлениеСерий)
	|	И ЭтапПроизводства2_2.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ПартияПроизводства КАК ПартияПроизводства,
	|	Таблица.Номенклатура         КАК Номенклатура,
	|	Таблица.Характеристика       КАК Характеристика
	|ПОМЕСТИТЬ ВыпускНаЭтапе
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВыходныеИзделия.Ссылка.ПартияПроизводства КАК ПартияПроизводства,
	|		ВыходныеИзделия.Номенклатура              КАК Номенклатура,
	|		ВыходныеИзделия.Характеристика            КАК Характеристика
	|	ИЗ
	|		Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ВыходныеИзделия
	|	ГДЕ
	|		ВыходныеИзделия.Ссылка В
	|				(ВЫБРАТЬ
	|					ЭтапыПроизводства.Ссылка
	|				ИЗ
	|					ЭтапыПроизводства)
	|		И ВыходныеИзделия.Ссылка.ПартияПроизводства = ВыходныеИзделия.ЭтапПотребитель.ПартияПроизводства
	|		И НЕ ВыходныеИзделия.Отменено
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПобочныеИзделия.Ссылка.ПартияПроизводства КАК ПартияПроизводства,
	|		ПобочныеИзделия.Номенклатура              КАК Номенклатура,
	|		ПобочныеИзделия.Характеристика            КАК Характеристика
	|	ИЗ
	|		Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ПобочныеИзделия
	|	ГДЕ
	|		ПобочныеИзделия.Ссылка В
	|				(ВЫБРАТЬ
	|					ЭтапыПроизводства.Ссылка
	|				ИЗ
	|					ЭтапыПроизводства)
	|		И ПобочныеИзделия.Ссылка.ПартияПроизводства = ПобочныеИзделия.ЭтапПотребитель.ПартияПроизводства
	|		И НЕ ПобочныеИзделия.Отменено
	|	
	|) КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства,
	|	Номенклатура,
	|	Характеристика
	|");
	
	#КонецОбласти
	//-- НЕ УТКА
	
	#Область ПоступлениеСерийПриСборке
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.СборкаТоваров) КАК Документ,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ ПоступлениеСерииПоСборке
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаСобранныхКомплектов)) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Документ");
	
	#КонецОбласти
	
	#Область РасходСерий
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.НоменклатураСписка КАК НоменклатураСписка,
	|	ВложенныйЗапрос.ХарактеристикаСписка КАК ХарактеристикаСписка, 
	|	ВложенныйЗапрос.СерияСписка КАК СерияСписка,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	ВложенныйЗапрос.Серия.Представление КАК СерияПредставление,
	|	СУММА(ВложенныйЗапрос.Израсходовано) КАК Израсходовано
	|ИЗ
	|(
	|	ВЫБРАТЬ
	|		ПоступлениеСерииПоСборке.Номенклатура КАК НоменклатураСписка,
	|		ПоступлениеСерииПоСборке.Характеристика КАК ХарактеристикаСписка,
	|		ПоступлениеСерииПоСборке.Серия КАК СерияСписка,
	|		ИспользованныеСерии.Номенклатура КАК Номенклатура,
	|		ИспользованныеСерии.Характеристика КАК Характеристика,
	|		ИспользованныеСерии.Серия КАК Серия,
	|		ИспользованныеСерии.КоличествоОборот КАК Израсходовано
	|	ИЗ
	|			РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|						(ВЫБРАТЬ
	|							ПоступлениеСерииПоСборке.Документ
	|						ИЗ
	|							ПоступлениеСерииПоСборке)
	|					И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектующихДляСборки)) КАК ИспользованныеСерии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСерииПоСборке КАК ПоступлениеСерииПоСборке
	|			ПО (ПоступлениеСерииПоСборке.Документ = ИспользованныеСерии.Документ)
	//++ НЕ УТ
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		МаршрутныеЛистыСписка.Номенклатура КАК НоменклатураСписка,
	|		МаршрутныеЛистыСписка.Характеристика КАК ХарактеристикаСписка,
	|		МаршрутныеЛистыСписка.Серия КАК СерияСписка,
	|		ИспользованныеСерии.Номенклатура КАК Номенклатура,
	|		ИспользованныеСерии.Характеристика КАК Характеристика,
	|		ИспользованныеСерии.Серия КАК Серия,
	|		ИспользованныеСерии.КоличествоОборот КАК Израсходовано
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|						(ВЫБРАТЬ
	|							МаршрутныеЛистыСписка.Ссылка
	|						ИЗ
	|							МаршрутныеЛистыСписка)
	|					И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПотреблениеМатериаловПриПроизводстве)) КАК ИспользованныеСерии
	|			ЛЕВОЕ СОЕДИНЕНИЕ МаршрутныеЛистыСписка КАК МаршрутныеЛистыСписка
	|			ПО (МаршрутныеЛистыСписка.Ссылка = ИспользованныеСерии.Документ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РаспределениеЗатратНаЭтапыГрафика.Номенклатура,
	|		РаспределениеЗатратНаЭтапыГрафика.Характеристика,
	|		РаспределениеЗатратНаЭтапыГрафика.Серия,
	|		ИспользованныеСерии.Номенклатура,
	|		ИспользованныеСерии.Характеристика,
	|		ИспользованныеСерии.Серия,
	|		ИспользованныеСерии.КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|						(ВЫБРАТЬ
	|							РаспределениеЗатратНаЭтапыГрафика.Ссылка
	|						ИЗ
	|							РаспределениеЗатратНаЭтапыГрафика)
	|					И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты)) КАК ИспользованныеСерии
	|			ЛЕВОЕ СОЕДИНЕНИЕ РаспределениеЗатратНаЭтапыГрафика КАК РаспределениеЗатратНаЭтапыГрафика
	|			ПО (РаспределениеЗатратНаЭтапыГрафика.Ссылка = ИспользованныеСерии.Документ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ПоступлениеСерий.Номенклатура КАК НоменклатураСписка,
	|		ПоступлениеСерий.Характеристика КАК ХарактеристикаСписка,
	|		ПоступлениеСерий.Серия КАК СерияСписка,
	|		ИспользованныеСерии.Номенклатура КАК Номенклатура,
	|		ИспользованныеСерии.Характеристика КАК Характеристика,
	|		ИспользованныеСерии.Серия КАК Серия,
	|		ИспользованныеСерии.КоличествоОборот КАК Израсходовано
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|						(ВЫБРАТЬ
	|							ЭтапыПроизводства.Ссылка
	|						ИЗ
	|							ЭтапыПроизводства КАК ЭтапыПроизводства)
	|					И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПотреблениеМатериаловПриПроизводстве)) КАК ИспользованныеСерии
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|			ПО ИспользованныеСерии.Документ = ЭтапПроизводства2_2.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСерийВЭтапахПроизводства КАК ПоступлениеСерий
	|			ПО ЭтапПроизводства2_2.ПартияПроизводства = ПоступлениеСерий.ПартияПроизводства
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВыпускНаЭтапе КАК ВыпускНаЭтапе
	|			ПО ИспользованныеСерии.Номенклатура = ВыпускНаЭтапе.Номенклатура
	|				И ИспользованныеСерии.Характеристика = ВыпускНаЭтапе.Характеристика
	|				И ЭтапПроизводства2_2.ПартияПроизводства = ВыпускНаЭтапе.ПартияПроизводства
	|
	|	ГДЕ
	|		ВыпускНаЭтапе.Номенклатура ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПоступлениеСерий.Номенклатура КАК НоменклатураСписка,
	|		ПоступлениеСерий.Характеристика КАК ХарактеристикаСписка,
	|		ПоступлениеСерий.Серия КАК СерияСписка,
	|		ИспользованныеСерии.Номенклатура КАК Номенклатура,
	|		ИспользованныеСерии.Характеристика КАК Характеристика,
	|		ИспользованныеСерии.Серия КАК Серия,
	|		ИспользованныеСерии.КоличествоОборот КАК Израсходовано
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|					(ВЫБРАТЬ
	|						РаспределениеЗатрат.Ссылка КАК Ссылка
	|					ИЗ
	|						ПоступлениеСерийВЭтапахПроизводства КАК ПоступлениеСерий
	|							ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК РаспределениеЗатрат
	|							ПО
	|								ПоступлениеСерий.ПартияПроизводства = РаспределениеЗатрат.ПартияПроизводства)) КАК ИспользованныеСерии
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК РаспределениеЗатрат
	|			ПО ИспользованныеСерии.Документ = РаспределениеЗатрат.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСерийВЭтапахПроизводства КАК ПоступлениеСерий
	|			ПО (РаспределениеЗатрат.ПартияПроизводства = ПоступлениеСерий.ПартияПроизводства)
	//-- НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РаспределениеЗатратНаВыпускБезРаспоряжений.Номенклатура,
	|		РаспределениеЗатратНаВыпускБезРаспоряжений.Характеристика,
	|		РаспределениеЗатратНаВыпускБезРаспоряжений.Серия,
	|		ИспользованныеСерии.Номенклатура,
	|		ИспользованныеСерии.Характеристика,
	|		ИспользованныеСерии.Серия,
	|		ИспользованныеСерии.КоличествоОборот
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|						(ВЫБРАТЬ
	|							РаспределениеЗатратНаВыпускБезРаспоряжений.Ссылка
	|						ИЗ
	|							РаспределениеЗатратНаВыпускБезРаспоряжений)
	|					И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты)) КАК ИспользованныеСерии
	|			ЛЕВОЕ СОЕДИНЕНИЕ РаспределениеЗатратНаВыпускБезРаспоряжений КАК РаспределениеЗатратНаВыпускБезРаспоряжений
	|			ПО (РаспределениеЗатратНаВыпускБезРаспоряжений.Ссылка = ИспользованныеСерии.Документ)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ 
	|		ПоступлениеСерий.Номенклатура КАК НоменклатураСписка,
	|		ПоступлениеСерий.Характеристика КАК ХарактеристикаСписка,
	|		ПоступлениеСерий.Серия КАК СерияСписка,
	|		ИспользованныеСерии.Номенклатура КАК Номенклатура,
	|		ИспользованныеСерии.Характеристика КАК Характеристика,
	|		ИспользованныеСерии.Серия КАК Серия,
	|		ИспользованныеСерии.КоличествоОборот КАК Израсходовано
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|						(ВЫБРАТЬ
	|							ПоступлениеСерий.Документ
	|						ИЗ
	|							ПоступлениеСерийПриПроизводствеБезЗаказа КАК ПоступлениеСерий)
	|					И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПотреблениеМатериаловПриПроизводстве)) КАК ИспользованныеСерии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСерийПриПроизводствеБезЗаказа КАК ПоступлениеСерий
	|			ПО (ПоступлениеСерий.Документ = ИспользованныеСерии.Документ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ПоступлениеСерий.Номенклатура КАК НоменклатураСписка,
	|		ПоступлениеСерий.Характеристика КАК ХарактеристикаСписка,
	|		ПоступлениеСерий.Серия КАК СерияСписка,
	|		ИспользованныеСерии.Номенклатура КАК Номенклатура,
	|		ИспользованныеСерии.Характеристика КАК Характеристика,
	|		ИспользованныеСерии.Серия КАК Серия,
	|		ИспользованныеСерии.КоличествоОборот КАК Израсходовано
	|	ИЗ
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|				,
	|				,
	|				,
	|				Документ В
	|					(ВЫБРАТЬ
	|						РаспределениеЗатрат.Ссылка КАК Ссылка
	|					ИЗ
	|						ПоступлениеСерийПриПроизводствеБезЗаказа КАК ПоступлениеСерий
	|							ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК РаспределениеЗатрат
	|							ПО
	|								ПоступлениеСерий.Документ = РаспределениеЗатрат.ПартияПроизводства.Документ)) КАК ИспользованныеСерии
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК РаспределениеЗатрат
	|			ПО ИспользованныеСерии.Документ = РаспределениеЗатрат.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПоступлениеСерийПриПроизводствеБезЗаказа КАК ПоступлениеСерий
	|			ПО (РаспределениеЗатрат.ПартияПроизводства.Документ = ПоступлениеСерий.Документ)
	//-- НЕ УТ
	|) КАК ВложенныйЗапрос
	|ГДЕ
	|	НЕ(ВложенныйЗапрос.НоменклатураСписка = ВложенныйЗапрос.Номенклатура
	|		И ВложенныйЗапрос.ХарактеристикаСписка = ВложенныйЗапрос.Характеристика
	|		И ВложенныйЗапрос.СерияСписка = ВложенныйЗапрос.Серия)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.НоменклатураСписка,
	|	ВложенныйЗапрос.ХарактеристикаСписка,
	|	ВложенныйЗапрос.СерияСписка,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураПредставление,
	|	ХарактеристикаПредставление,
	|	СерияПредставление");
	
	#КонецОбласти
	
	Разделитель = 
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, Разделитель);
	
	Если ОтборПоХарактеристике Тогда
		
		ОтборПоСпискуНенклатуры = 
		"(Номенклатура, Характеристика, Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Характеристика,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры)";
		
		ОтборПоНоменклатуреРаспределениеЗатрат = 
		"(РаспределениеЗатрат.Номенклатура, РаспределениеЗатрат.Характеристика, РаспределениеЗатрат.Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Характеристика,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры КАК СписокНоменклатуры)";
		
	Иначе
		
		ОтборПоСпискуНенклатуры = 
		"(Номенклатура, Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры)";
		
		ОтборПоНоменклатуреРаспределениеЗатрат = 
		"(РаспределениеЗатрат.Номенклатура, РаспределениеЗатрат.Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры КАК СписокНоменклатуры)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ОтборПоСпискуНоменклатуры",
		ОтборПоСпискуНенклатуры);
		
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ОтборПоНоменклатуреРаспределениеЗатрат",
		ОтборПоНоменклатуреРаспределениеЗатрат);
		
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Результат.Индексы.Добавить("НоменклатураСписка,СерияСписка");
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСтрокиРекурсивно(СтруктураСерииДерево, ОбластьСтрока, Уровень, ДокументРезультат)

	Для каждого СтрокаДерева Из СтруктураСерииДерево.Строки Цикл
		
		ПараметрыОбласти = Новый Структура("Номенклатура,Серия,НоменклатураПредставление,
											|СерияПредставление,ЕдиницаИзмерения,Израсходовано");
		ЗаполнитьЗначенияСвойств(ПараметрыОбласти, СтрокаДерева);
		
		СтруктураРасшифровки = Новый Структура("Номенклатура,Характеристика,Серия");
		ЗаполнитьЗначенияСвойств(СтруктураРасшифровки, СтрокаДерева);
		ПараметрыОбласти.Вставить("СтруктураРасшифровки", СтруктураРасшифровки);
		
		ОбластьСтрока.Параметры.Заполнить(ПараметрыОбласти);
		ДокументРезультат.Вывести(ОбластьСтрока, Уровень,, Истина);
		
		ЗаполнитьСтрокиРекурсивно(СтрокаДерева, ОбластьСтрока, Уровень + 1, ДокументРезультат);
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли