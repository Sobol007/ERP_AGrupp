Процедура СформироватьОтчет(ТабДок) Экспорт
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	Если ВариантОтчета="По иерархии" Тогда 	
		Макет = ПолучитьМакет("ПоИерархии");
	Иначе
		Макет = ПолучитьМакет("ПоХронологии");
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	Если НачалоПериода = Дата("00010101") и НачалоПериода = ОкончаниеПериода тогда
		Область.Параметры.ПредставлениеПериода = "без ограничения";
	Иначе
		Область.Параметры.ПредставлениеПериода = ПредставлениеПериода(НачалоПериода,КонецДня( ОкончаниеПериода));
	КонецЕсли;
	Область.Параметры.Организация = ?(ЗначениеЗаполнено(Организация), Организация, "Все");
	Область.Параметры.Контрагент = ?(ЗначениеЗаполнено(Контрагент), Контрагент, "Все");
	Область.Параметры.КонтрактВЭД = ?(ЗначениеЗаполнено(КонтрактВЭД), КонтрактВЭД, "Все");
	Область.Параметры.ВидСправкиВалютногоКонтроля = ВидСправкиВалютногоКонтроля;
	ТабДок.Вывести(Область);
	
	ВысотаЗаголовка = ТабДок.ВысотаТаблицы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));	
	Запрос.УстановитьПараметр("КонецПериода", ОкончаниеПериода);	
	Запрос.УстановитьПараметр("Организация", Организация);	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);	
	Запрос.УстановитьПараметр("КонтрактВЭД", КонтрактВЭД);	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СВО.ПаспортСделки,
	|	СВО.Ссылка,
	|	СВО.ВидВалютнойОперации.Код КАК КодВОПД,
	|	СВО.ОжидаемыйСрок,
	|	СВО.РасчетныйДокумент,
	|	СВО.ВалютаДокумента,
	|	СВО.СуммаВВалютеДокумента,
	|	СВО.СуммаВВалютеКонтракта,
	|	СВО.Ссылка.БанковскийСчет.НомерСчета КАК НомерГТД,
	|	СВО.ВалютаКонтракта,
	|	СВО.РасчетныйДокумент.БанковскийСчет.НомерСчета КАК НомерСчетаРус,
	|	СВО.Ссылка.ВидОперации,
	|	СВО.Ссылка.БанковскийСчет,
	|	СВО.НомерСтроки,
	|	СВО.КонтрактВЭД
	|ПОМЕСТИТЬ Справки
	|ИЗ
	|	Документ.РЭЙ_СправкаОВалютныхОперациях.ВалютныеОперации КАК СВО
	|ГДЕ
	|	(&КонецПериода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И СВО.Ссылка.Дата > &НачалоПериода
	|			ИЛИ СВО.Ссылка.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ))
	|	И СВО.Ссылка.Проведен = ИСТИНА
	|	И СВО.Ссылка.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СВО_Ручные.ПаспортСделки,
	|	СВО_Ручные.Ссылка,
	|	СВО_Ручные.ВидВалютнойОперации.Код,
	|	СВО_Ручные.ОжидаемыйСрок,
	|	СВО_Ручные.РасчетныйДокумент,
	|	СВО_Ручные.ВалютаДокумента,
	|	СВО_Ручные.СуммаВВалютеДокумента,
	|	СВО_Ручные.СуммаВВалютеКонтракта,
	|	СВО_Ручные.Ссылка.БанковскийСчет.НомерСчета,
	|	СВО_Ручные.ВалютаКонтракта,
	|	СВО_Ручные.РасчетныйДокумент.БанковскийСчет.НомерСчета,
	|	СВО_Ручные.Ссылка.ВидОперации,
	|	СВО_Ручные.Ссылка.БанковскийСчет,
	|	СВО_Ручные.НомерСтроки,
	|	СВО_Ручные.КонтрактВЭД
	|ИЗ
	|	Документ.РЭЙ_СправкаОВалютныхОперациях.ВалютныеОперацииРучные КАК СВО_Ручные
	|ГДЕ
	|	(&КонецПериода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И СВО_Ручные.Ссылка.Дата > &НачалоПериода
	|			ИЛИ СВО_Ручные.Ссылка.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ))
	|	И СВО_Ручные.Ссылка.Проведен = ИСТИНА
	|	И СВО_Ручные.Ссылка.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СПД.Ссылка.ПаспортСделки,
	|	СПД.Ссылка.Ссылка,
	|	СПД.ВидДокумента.Код,
	|	СПД.ОжидаемыйСрок,
	|	СПД.Документ,
	|	СПД.ВалютаДокумента,
	|	СПД.СуммаВВалютеДокумента,
	|	СПД.СуммаВВалютеКонтракта,
	|	СПД.НомерГТД,
	|	СПД.ВалютаКонтракта,
	|	NULL,
	|	NULL,
	|	NULL,
	|	СПД.НомерСтроки,
	|	СПД.Ссылка.КонтрактВЭД
	|ИЗ
	|	Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПодтверждающиеДокументы КАК СПД
	|ГДЕ
	|	(&КонецПериода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И СПД.Ссылка.Дата > &НачалоПериода
	|			ИЛИ СПД.Ссылка.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ))
	|	И СПД.Ссылка.Проведен = ИСТИНА
	|	И СПД.Ссылка.Организация = &Организация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СПД_Ручные.Ссылка.ПаспортСделки,
	|	СПД_Ручные.Ссылка,
	|	СПД_Ручные.ВидДокумента.Код,
	|	СПД_Ручные.ОжидаемыйСрок,
	|	NULL,
	|	СПД_Ручные.ВалютаДокумента,
	|	СПД_Ручные.СуммаВВалютеДокумента,
	|	СПД_Ручные.СуммаВВалютеКонтракта,
	|	СПД_Ручные.НомерГТД,
	|	СПД_Ручные.ВалютаКонтракта,
	|	NULL,
	|	NULL,
	|	NULL,
	|	СПД_Ручные.НомерСтроки,
	|	СПД_Ручные.Ссылка.КонтрактВЭД
	|ИЗ
	|	Документ.РЭЙ_СправкаОПодтверждающихДокументах.ПодтверждающиеДокументыРучные КАК СПД_Ручные
	|ГДЕ
	|	(&КонецПериода = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И СПД_Ручные.Ссылка.Дата > &НачалоПериода
	|			ИЛИ СПД_Ручные.Ссылка.Дата МЕЖДУ &НачалоПериода И КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ))
	|	И СПД_Ручные.Ссылка.Проведен = ИСТИНА
	|	И СПД_Ручные.Ссылка.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Справки.КонтрактВЭД.Контрагент КАК Контрагент,
	|	Справки.КонтрактВЭД КАК КонтрактВЭД,
	|	Справки.Ссылка КАК Регистратор,
	|	Справки.ВалютаДокумента КАК ВалютаДокумента,
	|	Справки.КодВОПД КАК КодВОПД,
	|	Справки.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|	Справки.СуммаВВалютеКонтракта КАК СуммаВВалютеКонтракта,
	|	Справки.НомерГТД КАК НомераСчет_ГТД,
	|	Справки.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаКонтракта,
	|	Справки.ВалютаКонтракта КАК ВалютаКонтракта1,
	|	ЕСТЬNULL(Справки.РасчетныйДокумент, """") КАК РасчетныйДокумент,
	|	Справки.КонтрактВЭД.ВидКонтрактаВЭД КАК ВидКонтрактаВЭД,
	|	Справки.ВидОперации,
	|	Справки.БанковскийСчет,
	|	Справки.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Справки КАК Справки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Контрагент = НЕОПРЕДЕЛЕНО
	|					ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Справки.КонтрактВЭД.Контрагент = &Контрагент
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &КонтрактВЭД = НЕОПРЕДЕЛЕНО
	|					ИЛИ &КонтрактВЭД = ЗНАЧЕНИЕ(Справочник.РЭЙ_КонтрактыВЭД.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Справки.КонтрактВЭД = &КонтрактВЭД
	|		КОНЕЦ";
	
	Если ВидСправкиВалютногоКонтроля = "Только СКВО" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	И (Справки.Ссылка ССЫЛКА Документ.РЭЙ_СправкаОВалютныхОперациях)";
	ИначеЕсли ВидСправкиВалютногоКонтроля="Только СПД" Тогда 
		ТекстЗапроса = ТекстЗапроса + "
		|	И (Справки.Ссылка ССЫЛКА Документ.РЭЙ_СправкаОПодтверждающихДокументах)";
	КонецЕсли;
	
	Если ВариантОтчета = "По иерархии" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО
		|	КонтрактВЭД.Контрагент.Наименование,
		|	КонтрактВЭД.КонтрактДатаПодписания,
		|	КонтрактВЭД.КонтрактНомер,
		|	КонтрактВЭД.Наименование,
		|	Справки.Ссылка.Дата,
		|	Справки.НомерСтроки
		|ИТОГИ
		|	СУММА(СуммаВВалютеДокумента),
		|	СУММА(СуммаВВалютеКонтракта)
		|ПО
		|	Контрагент,
		|	КонтрактВЭД,
		|	Регистратор";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО
		|	Справки.Ссылка.Дата";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	
	Область = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(Область);
	ТабДок.ФиксацияСверху = ТабДок.ВысотаТаблицы;
	Если ВариантОтчета = "По иерархии" Тогда 
		ВыборкаКонтрагент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагент.Следующий() Цикл
			Область = Макет.ПолучитьОбласть("Контрагент");
			Область.Параметры.Заполнить(ВыборкаКонтрагент);
			ТабДок.Вывести(Область);
			
			ВыборкаКонтрактВЭД = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаКонтрактВЭД.Следующий() Цикл
				ВывестиВалюту = Истина;
				ВывестиПаспортСделки = Истина;
				
				Область = Макет.ПолучитьОбласть("КонтрактВЭД");
				Область.Параметры.Заполнить(ВыборкаКонтрактВЭД);
				Если ЗначениеЗаполнено(ВыборкаКонтрактВЭД.КонтрактВЭД) Тогда 
					Область.Параметры.КонтрактВЭДПредставление = ПредставлениеКонтрактаВЭД(ВыборкаКонтрактВЭД.КонтрактВЭД);
				КонецЕсли;
				ТабДок.Вывести(Область);
				текПаспортСделки = РЭЙ_СлужебныйСервер.ПолучитьПаспортСделкиПоКонтрактуВЭД(ВыборкаКонтрактВЭД.КонтрактВЭД);
				
				ВыборкаДокумент1 = ВыборкаКонтрактВЭД.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаДокумент1.Следующий() Цикл
					Область = Макет.ПолучитьОбласть("Документ1");
					Область.Параметры.Заполнить(ВыборкаДокумент1);
					Область.Параметры.Документ1Представление = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(ВыборкаДокумент1.Регистратор);
					Область.Параметры.Документ1 = ВыборкаДокумент1.Регистратор;
					Если ВывестиВалюту Тогда
						Область.Параметры.ВалютаПредставление = ВыборкаКонтрактВЭД.КонтрактВЭД.ДоговорКонтрагента.ВалютаВзаиморасчетов;
						ВывестиВалюту = Ложь;
					КонецЕсли;
					ТабДок.Вывести(Область);
					
					ВыборкаДокумент2 = ВыборкаДокумент1.Выбрать();
					Пока ВыборкаДокумент2.Следующий() Цикл
						Область = Макет.ПолучитьОбласть("Документ2");
						Область.Параметры.Заполнить(ВыборкаДокумент2);
						Область.Параметры.НомераСчет_ГТД = СокрЛП(ВыборкаДокумент2.НомераСчет_ГТД);
						Если Не (ВыборкаДокумент2.ВидКонтрактаВЭД = Перечисления.РЭЙ_ВидыКонтрактовВЭД.ВвозТоваровНаТаможеннуюТерриториюРФ или ВыборкаДокумент2.ВидКонтрактаВЭД=Перечисления.РЭЙ_ВидыКонтрактовВЭД.ВывозТоваровСТаможеннойТерриторииРФ) Тогда 
							Если ЗначениеЗаполнено(ВыборкаДокумент2.РасчетныйДокумент) И ТипЗнч(ВыборкаДокумент2.Регистратор) = Тип("ДокументСсылка.РЭЙ_СправкаОПодтверждающихДокументах") Тогда
								Область.Параметры.НомераСчет_ГТД = ПолучитьНомерНаПечать(ВыборкаДокумент2.РасчетныйДокумент);
							КонецЕсли;
						КонецЕсли;
						Если ВыборкаДокумент2.ВидОперации = Перечисления.РЭЙ_ВидыОперацийСправкаОВалютныхОперациях.СправкаОРасчетахЧерезСчетаЗаРубежом Тогда 
							ТекСчет = РЭЙ_СлужебныйСервер.ПолучитьБанковскийСчетВалютныйПоТиповому(ВыборкаДокумент2.БанковскийСчет);
							Если ЗначениеЗаполнено(ТекСчет) Тогда 
								Область.Параметры.НомераСчет_ГТД = СокрЛП(ТекСчет.НомерСчета);
							КонецЕсли;
						КонецЕсли;
						
						Если ВывестиПаспортСделки Тогда
							Область.Параметры.НомерПаспортаСделкиПредставление = текПаспортСделки.НомерПаспортаСделки;
							Область.Параметры.ПаспортСделкиРасшифровка = текПаспортСделки;
							ВывестиПаспортСделки = Ложь;
						КонецЕсли;
						
						Если ЗначениеЗаполнено(ВыборкаДокумент2.РасчетныйДокумент) Тогда 
							Область.Параметры.РасчетныйДокументПредставление = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(ВыборкаДокумент2.РасчетныйДокумент);
						Иначе
							Область.Параметры.РасчетныйДокументПредставление = "Расч. док-т не указан";
						КонецЕсли;
						
						Область.Параметры.РасчетныйДокументРасшифровка = ВыборкаДокумент2.РасчетныйДокумент;
						ТабДок.Вывести(Область);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	Иначе
		ВыборкаКонтрагент = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
		ТекНомер = 1;
		Пока ВыборкаКонтрагент.Следующий() цикл
			Область=Макет.ПолучитьОбласть("Документ");
			Область.Параметры.Заполнить(ВыборкаКонтрагент);
			текПаспортСделки = РЭЙ_СлужебныйСервер.ПолучитьПаспортСделкиПоКонтрактуВЭД(ВыборкаКонтрагент.КонтрактВЭД);
			Область.Параметры.НомерПаспортаСделкиПредставление = текПаспортСделки.НомерПаспортаСделки;
			Область.Параметры.ПаспортСделкиРасшифровка = текПаспортСделки;
			Область.Параметры.Документ1Представление = РЭЙ_СлужебныйСервер.КраткоеПредставлениеДокумента(ВыборкаКонтрагент.Регистратор);
			Область.Параметры.НомераСчет_ГТД = СокрЛП(ВыборкаКонтрагент.НомераСчет_ГТД);
			Если Не (ВыборкаКонтрагент.ВидКонтрактаВЭД = Перечисления.РЭЙ_ВидыКонтрактовВЭД.ВвозТоваровНаТаможеннуюТерриториюРФ Или ВыборкаКонтрагент.ВидКонтрактаВЭД = Перечисления.РЭЙ_ВидыКонтрактовВЭД.ВывозТоваровСТаможеннойТерриторииРФ) Тогда 
				Если ЗначениеЗаполнено(ВыборкаКонтрагент.РасчетныйДокумент) и ТипЗнч(ВыборкаКонтрагент.Регистратор) = Тип("ДокументСсылка.РЭЙ_СправкаОПодтверждающихДокументах") Тогда
					Область.Параметры.НомераСчет_ГТД = ПолучитьНомерНаПечать(ВыборкаКонтрагент.РасчетныйДокумент);
				КонецЕсли;
			КонецЕсли;
			Если ВыборкаКонтрагент.ВидОперации = Перечисления.РЭЙ_ВидыОперацийСправкаОВалютныхОперациях.СправкаОРасчетахЧерезСчетаЗаРубежом Тогда 
				ТекСчет = РЭЙ_СлужебныйСервер.ПолучитьБанковскийСчетВалютныйПоТиповому(ВыборкаКонтрагент.БанковскийСчет);
				Если ЗначениеЗаполнено(ТекСчет) Тогда 
					Область.Параметры.НомераСчет_ГТД = СокрЛП(ТекСчет.НомерСчета);
				КонецЕсли;
			КонецЕсли;
			Область.Параметры.НомерСтроки = ТекНомер;
			Если ЗначениеЗаполнено(ВыборкаКонтрагент.КонтрактВЭД) Тогда 
				Область.Параметры.КонтрактВЭДПредставление = ПредставлениеКонтрактаВЭД(ВыборкаКонтрагент.КонтрактВЭД);
			КонецЕсли;
			ТекНомер = ТекНомер + 1;
			ТабДок.Вывести(Область);
		КонецЦикла;
	КонецЕсли;;
	Область = Макет.ПолучитьОбласть("Дно");
	ТабДок.Вывести(Область);
	ТабДок.АвтоМасштаб = Истина;
КонецПроцедуры

Функция ПолучитьНомерНаПечать(Документ)
	Если ЗначениеЗаполнено(Документ) Тогда 
		Если Документ.Метаданные().Реквизиты.Найти("НомерВходящегоДокумента") <> Неопределено Тогда
			НомерДок = Документ.НомерВходящегоДокумента;
		Иначе
			НомерДок = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Документ.Номер, Истина, Истина);
		КонецЕсли;
	КонецЕсли;
	Возврат НомерДок;
КонецФункции

Функция ПредставлениеКонтрактаВЭД(Элемент)
	Представление = "";
	Если ЗначениеЗаполнено(Элемент) Тогда 
		Если не ЗначениеЗаполнено(Элемент.КонтрактНомер)тогда
			Представление = "Конт. №б/н"+" от "+ Формат( Элемент.КонтрактДатаПодписания,"ДФ=dd.MM.yyyy");
		иначе
			Представление = "Конт. №"+Элемент.КонтрактНомер+" от "+ Формат( Элемент.КонтрактДатаПодписания,"ДФ=dd.MM.yyyy");
		КонецЕсли;
	КонецЕсли;
	Возврат Представление;
КонецФункции	           