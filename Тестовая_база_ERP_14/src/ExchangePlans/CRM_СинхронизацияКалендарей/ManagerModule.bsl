#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает ссылку на узел плана обмена, соответствующий календарю.
// В случае отсутствия создаёт новый узел и возвращает ссылку на него.
//
// Параметры:
//  Календарь - СправочникСсылка.CRM_КалендариСотрудников - Календарь.
// 
// Возвращаемое значение:
//  ПланОбменаСсылка.CRM_СинхронизацияКалендарей - ссылка на узел плана обмена с сервисом календарей.
// 
Функция УзелДляКалендаря(Календарь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СинхронизацияКалендарей.Ссылка
	|ИЗ
	|	ПланОбмена.CRM_СинхронизацияКалендарей КАК СинхронизацияКалендарей
	|ГДЕ
	|	НЕ СинхронизацияКалендарей.ЭтотУзел
	|	И СинхронизацияКалендарей.Календарь = &Календарь");
	Запрос.УстановитьПараметр("Календарь", Календарь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ПланыОбменаОбъект = ПланыОбмена.CRM_СинхронизацияКалендарей.СоздатьУзел();
		ПланыОбменаОбъект.УстановитьНовыйКод();
		ПланыОбменаОбъект.Календарь = Календарь;
		ПланыОбменаОбъект.Наименование = Календарь;
		ПланыОбменаОбъект.Записать();
		Возврат ПланыОбменаОбъект.Ссылка;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецЕсли