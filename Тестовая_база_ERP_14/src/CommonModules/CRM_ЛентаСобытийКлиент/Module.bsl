
#Область СлужебныйПрограммныйИнтерфейс

Функция ИменаКомандОбъектовЛенты() Экспорт
	
	ИменаКоманд = Новый Массив;
	
	ИменаКоманд.Добавить("ЛентаОткрытьВзаимодействиеПланов_");
	ИменаКоманд.Добавить("ЛентаОткрытьВзаимодействиеИстории_");
	ИменаКоманд.Добавить("ЛентаОткрытьЛичнуюЗадачуПланов_");
	ИменаКоманд.Добавить("ЛентаОткрытьЛичнуюЗадачуИстории_");
	ИменаКоманд.Добавить("ЛентаОткрытьПримечание_");
	ИменаКоманд.Добавить("ЛентаОтменитьВзаимодействие_");
	ИменаКоманд.Добавить("ЛентаУдалитьЗадачу_");
	ИменаКоманд.Добавить("ЛентаУдалитьПримечание_");
	
	Возврат ИменаКоманд;
	
КонецФункции

Процедура ВыполнитьКомандуЛенты(ИмяКоманды, ПараметрКоманды, Форма) Экспорт
	
	Если ИмяКоманды = "ЛентаОткрытьВзаимодействиеПланов_" Тогда
		
		ОбъектСсылка = CRM_ЛентаСобытий.СсылкаПоИдентификатору(ПараметрКоманды, "CRM_Взаимодействие");
		ОткрытьФорму("Документ.CRM_Взаимодействие.Форма.ФормаДокументаКраткая",
			Новый Структура("Ключ", ОбъектСсылка),
			Форма,
			Форма.УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	ИначеЕсли ИмяКоманды = "ЛентаОткрытьВзаимодействиеИстории_" Тогда
		
		ОбъектСсылка = CRM_ЛентаСобытий.СсылкаПоИдентификатору(ПараметрКоманды, "CRM_Взаимодействие");
		ОткрытьФорму("Документ.CRM_Взаимодействие.Форма.ФормаДокумента",
			Новый Структура("Ключ, ОткрыватьФорму", ОбъектСсылка, Истина),
			Форма,
			Форма.УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ИмяКоманды = "ЛентаОткрытьЛичнуюЗадачуПланов_" Тогда
		
		ОбъектСсылка = CRM_ЛентаСобытий.СсылкаПоИдентификатору(ПараметрКоманды, "ЗадачаИсполнителя");
		ОткрытьФорму("Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачиКраткая",
			Новый Структура("Ключ", ОбъектСсылка),
			Форма,
			Форма.УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ИмяКоманды = "ЛентаОткрытьЛичнуюЗадачуИстории_" Тогда
		
		ОбъектСсылка = CRM_ЛентаСобытий.СсылкаПоИдентификатору(ПараметрКоманды, "ЗадачаИсполнителя");
		ОткрытьФорму("Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачи",
			Новый Структура("Ключ", ОбъектСсылка),
			Форма,
			Форма.УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ИмяКоманды = "ЛентаОткрытьПримечание_" Тогда
		
		ОбъектСсылка = CRM_ЛентаСобытий.СсылкаПоИдентификатору(ПараметрКоманды, "Заметки");
		ОткрытьФорму("Справочник.Заметки.Форма.CRM_ФормаЭлемента",
			Новый Структура("Ключ", ОбъектСсылка),
			Форма,
			Форма.УникальныйИдентификатор,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ИмяКоманды = "ЛентаОтменитьВзаимодействие_" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Команда",			"Отменить");
		ДополнительныеПараметры.Вставить("Тип",				"CRM_Взаимодействие");
		ДополнительныеПараметры.Вставить("Идентификатор",	ПараметрКоманды);
		
		ТекстВопроса = НСтр("ru = 'Взаимодействие будет отменено. Продолжить?'");
		ОповещениеЗавершения = Новый ОписаниеОповещения("ВыполнитьКомандуЛентыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОповещениеЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли ИмяКоманды = "ЛентаУдалитьЗадачу_" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Команда",			"Удалить");
		ДополнительныеПараметры.Вставить("Тип",				"ЗадачаИсполнителя");
		ДополнительныеПараметры.Вставить("Идентификатор",	ПараметрКоманды);
		
		ТекстВопроса = НСтр("ru = 'Задача будет удалена. Продолжить?'");
		ОповещениеЗавершения = Новый ОписаниеОповещения("ВыполнитьКомандуЛентыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОповещениеЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли ИмяКоманды = "ЛентаУдалитьПримечание_" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Команда",			"Удалить");
		ДополнительныеПараметры.Вставить("Тип",				"Заметки");
		ДополнительныеПараметры.Вставить("Идентификатор",	ПараметрКоманды);
		
		ТекстВопроса = НСтр("ru = 'Примечание будет удалено. Продолжить?'");
		ОповещениеЗавершения = Новый ОписаниеОповещения("ВыполнитьКомандуЛентыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОповещениеЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКомандуЛентыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	CRM_ЛентаСобытий.ВыполнитьКомандуЛентыНаСервере(ДополнительныеПараметры);
	Оповестить("ЛентаСобытий_Обновить");
	
КонецПроцедуры

Функция ПроверитьЗаполнениеБыстрогоВвода(Знач ТипОбъекта, Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ПроверяемыеРеквизиты = Новый Соответствие;
	
	Если ТипОбъекта = "Примечание" Тогда
		
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_Тема",				Элементы.БыстрыйВвод_ПримечаниеТема.Имя);
		
	ИначеЕсли ТипОбъекта = "Взаимодействие" Тогда
		
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_Тема",				Элементы.БыстрыйВвод_ВзаимодействиеТема.Имя);
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_ДатаНачала",			Элементы.БыстрыйВвод_ВзаимодействиеДатаНачала_Дата.Имя);
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_ДатаОкончания",		Элементы.БыстрыйВвод_ВзаимодействиеДатаОкончания_Дата.Имя);
		
	ИначеЕсли ТипОбъекта = "Задача" Тогда
		
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_ЗадачаПолучатель",	Элементы.БыстрыйВвод_ЗадачаПолучатель.Имя);
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_ДатаОкончания",		Элементы.БыстрыйВвод_ЗадачаДатаОкончания_Дата.Имя);
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_Тема",				Элементы.БыстрыйВвод_ЗадачаТема.Имя);
		
	ИначеЕсли ТипОбъекта = "Письмо" Тогда
		
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_ПисьмоСписокПолучателейПисьма",	Элементы.БыстрыйВвод_ПисьмоСписокПолучателейПисьма.Имя);
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_Тема",							Элементы.БыстрыйВвод_ПисьмоТема.Имя);
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_Содержание",						Элементы.БыстрыйВвод_ПисьмоСодержание.Имя);
		
	ИначеЕсли ТипОбъекта = "Чат" Тогда
		
		ПроверяемыеРеквизиты.Вставить("БыстрыйВвод_Содержание",			Элементы.БыстрыйВвод_ЧатСодержание.Имя);
		
	КонецЕсли;
	
	Успех = Истина;
	Для Каждого ОписаниеРеквизита Из ПроверяемыеРеквизиты Цикл
		
		ИмяРеквизита = ОписаниеРеквизита.Ключ;
		ИмяЭлемента = ОписаниеРеквизита.Значение;
		
		Если Не ЗначениеЗаполнено(Форма[ИмяРеквизита]) Тогда
			Успех = Ложь;
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено '"), Элементы[ИмяЭлемента].Заголовок);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, ИмяРеквизита);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Успех;
	
КонецФункции

Процедура ПоказатьПолнуюФормуОбъекта(Знач Режим, Знач ТипОбъекта, Форма) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	
	ДанныеЗаполнения = ДанныеЗаполненияОбъектаБыстрогоВвода(Режим, ТипОбъекта, Форма);
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ДанныеЗаполнения);
	
	Если ТипОбъекта = "Взаимодействие" Тогда
		ПараметрыОткрытия.Вставить("ОткрыватьФорму", Истина);
		ИмяФормы = "Документ.CRM_Взаимодействие.Форма.ФормаДокумента";
	ИначеЕсли ТипОбъекта = "Задача" Тогда
		ПараметрыОткрытия.Вставить("ИзСпискаАктивностей", Ложь);
		ИмяФормы = "Задача.ЗадачаИсполнителя.Форма.CRM_ФормаЛичнойЗадачи";
	ИначеЕсли ТипОбъекта = "Письмо" Тогда
		ИмяФормы = "Документ.ЭлектронноеПисьмоИсходящее.Форма.CRM_ФормаДокумента";
	ИначеЕсли ТипОбъекта = "Заметки" Тогда
		ИмяФормы = "Справочник.Заметки.Форма.CRM_ФормаЭлемента";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормы, ПараметрыОткрытия,
		Форма,
		Форма.УникальныйИдентификатор,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура НастроитьОбластьВводаПослеФормированияЛенты(Знач ПолучателиЧата, Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	ВидимостьКомандыЧат = Форма.Лента_ЗначенияНастроек.ИспользоватьЧаты И (ПолучателиЧата.Количество() > 0);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "БыстрыйВвод_Чат", "Видимость", ВидимостьКомандыЧат);
	
	Если ВидимостьКомандыЧат Тогда
		
		Форма.БыстрыйВвод_ЧатПолучатели.Очистить();
		ЧатПолучателиСписокВыбора = Элементы.БыстрыйВвод_ЧатПредставлениеПолучателя.СписокВыбора;
		ЧатПолучателиСписокВыбора.Очистить();
		
		Для Каждого ДанныеПолучателя Из ПолучателиЧата Цикл
			НоваяСтрока = Форма.БыстрыйВвод_ЧатПолучатели.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПолучателя);
			ЧатПолучателиСписокВыбора.Добавить(ДанныеПолучателя.Идентификатор, ДанныеПолучателя.Представление);
		КонецЦикла;
		
		ПервыйПолучатель = Форма.БыстрыйВвод_ЧатПолучатели[0];
		Форма.БыстрыйВвод_ЧатИдентификаторПолучателя	= ПервыйПолучатель.Идентификатор;
		Форма.БыстрыйВвод_ЧатАдрес						= ПервыйПолучатель.Адрес;
		Форма.БыстрыйВвод_ЧатПолучатель					= ПервыйПолучатель.Контакт;
		Форма.БыстрыйВвод_ЧатПредставлениеПолучателя	= ПервыйПолучатель.Представление;
		Форма.БыстрыйВвод_ЧатУчетнаяЗапись				= ПервыйПолучатель.УчетнаяЗапись;
		Форма.БыстрыйВвод_ЧатГруппа						= ПервыйПолучатель.Группа;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеЗаполненияОбъектаБыстрогоВвода(Знач Режим, Знач ТипОбъекта, Форма) Экспорт
	
	ДанныеЗаполнения = Новый Структура;
	
	Если ТипОбъекта = "Примечание" Тогда
		
		ДанныеЗаполнения.Вставить("ДатаИзменения",			ТекущаяДата());
		ДанныеЗаполнения.Вставить("Предмет",				Форма.Объект.Ссылка);
		ДанныеЗаполнения.Вставить("Автор",					Форма.ТекущийПользователь);
		ДанныеЗаполнения.Вставить("ТекстСодержания",		Форма.БыстрыйВвод_Тема);
		ДанныеЗаполнения.Вставить("CRM_ЗаметкаЛенты",		Истина);
		
	ИначеЕсли ТипОбъекта = "Взаимодействие" Тогда
		
		ДанныеЗаполнения.Вставить("Дата",					ТекущаяДата());
		ДанныеЗаполнения.Вставить("ВидВзаимодействия",		Форма.БыстрыйВвод_ВзаимодействиеВид);
		Если Режим = "ЛентаИнтереса" Тогда
			ДанныеЗаполнения.Вставить("ДокументОснование",	Форма.Объект.Ссылка);
			ДанныеЗаполнения.Вставить("Партнер",			Форма.Объект.Партнер);
			ДанныеЗаполнения.Вставить("СостояниеИнтереса",	Форма.Объект.СостояниеИнтереса);
		ИначеЕсли Режим = "ЛентаКлиента" Тогда
			ДанныеЗаполнения.Вставить("Партнер",			Форма.Объект.Ссылка);
		ИначеЕсли Режим = "ЛентаКонтакта" Тогда
			ДанныеЗаполнения.Вставить("Партнер",			Форма.Объект.Владелец);
		КонецЕсли;
		ДанныеЗаполнения.Вставить("КонтактноеЛицо",			Форма.БыстрыйВвод_ВзаимодействиеПолучатель);
		ДанныеЗаполнения.Вставить("ПлановаяДата",			Форма.БыстрыйВвод_ДатаНачала);
		ДанныеЗаполнения.Вставить("ПлановаяДатаЗавершение",	Форма.БыстрыйВвод_ДатаОкончания);
		ДанныеЗаполнения.Вставить("НаВесьДень",				Форма.БыстрыйВвод_ВзаимодействиеНаВесьДень);
		ДанныеЗаполнения.Вставить("Тема",					Форма.БыстрыйВвод_Тема);
		ДанныеЗаполнения.Вставить("Ответственный",			Форма.ТекущийПользователь);
		
	ИначеЕсли ТипОбъекта = "Задача" Тогда
		
		ДанныеЗаполнения.Вставить("Дата",						ТекущаяДата());
		ДанныеЗаполнения.Вставить("Автор",						Форма.ТекущийПользователь);
		ДанныеЗаполнения.Вставить("Исполнитель",				Форма.БыстрыйВвод_ЗадачаПолучатель);
		ДанныеЗаполнения.Вставить("Предмет",					Форма.Объект.Ссылка);
		ДанныеЗаполнения.Вставить("ПредметСтрокой",				CRM_БизнесПроцессыИЗадачиСервер.ПредметСтрокой(Форма.Объект.Ссылка));
		ДанныеЗаполнения.Вставить("ДатаНачала",					Форма.БыстрыйВвод_ДатаНачала);
		ДанныеЗаполнения.Вставить("СрокИсполнения",				Форма.БыстрыйВвод_ДатаОкончания);
		ДанныеЗаполнения.Вставить("Наименование",				Форма.БыстрыйВвод_Тема);
		ДанныеЗаполнения.Вставить("Важность",					ПредопределенноеЗначение("Перечисление.ВариантыВажностиЗадачи.Обычная"));
		Если Режим = "ЛентаИнтереса" Тогда
			ДанныеЗаполнения.Вставить("CRM_Партнер",			Форма.Объект.Партнер);
			ДанныеЗаполнения.Вставить("CRM_КонтактноеЛицо",		Форма.Объект.КонтактноеЛицо);
			ДанныеЗаполнения.Вставить("CRM_Проект",				Форма.Объект.Проект);
		ИначеЕсли Режим = "ЛентаКлиента" Тогда
			ДанныеЗаполнения.Вставить("CRM_Партнер",			Форма.Объект.Ссылка);
		Иначе
			ДанныеЗаполнения.Вставить("CRM_Партнер",			Форма.Объект.Владелец);
			ДанныеЗаполнения.Вставить("CRM_КонтактноеЛицо",		Форма.Объект.Ссылка);
		КонецЕсли;
		ДанныеЗаполнения.Вставить("СостояниеБизнесПроцесса",	ПредопределенноеЗначение("Перечисление.СостоянияБизнесПроцессов.Активен"));
		ДанныеЗаполнения.Вставить("ПринятаКИсполнению",			Истина);
		ДанныеЗаполнения.Вставить("CRM_Личная",					Истина);
		
	ИначеЕсли ТипОбъекта = "Письмо" Тогда
		
		ДанныеЗаполнения.Вставить("ДанныеБыстрогоВвода", 			Истина);
		Если Режим = "ЛентаИнтереса" Тогда
			ДанныеЗаполнения.Вставить("ДокументОснование",			Форма.Объект.Ссылка);
			ДанныеЗаполнения.Вставить("CRM_Проект",					Форма.Объект.Проект);
			ДанныеЗаполнения.Вставить("СостояниеИнтереса",			Форма.Объект.СостояниеИнтереса);
		Иначе
			ДанныеЗаполнения.Вставить("ДокументОснование",			Неопределено);
		КонецЕсли;
		ДанныеЗаполнения.Вставить("Дата",							ТекущаяДата());
		ДанныеЗаполнения.Вставить("Автор",							Форма.ТекущийПользователь);
		ДанныеЗаполнения.Вставить("Ответственный",					Форма.ТекущийПользователь);
		ДанныеЗаполнения.Вставить("СписокПолучателейПисьма",		Форма.БыстрыйВвод_ПисьмоСписокПолучателейПисьма);
		ДанныеЗаполнения.Вставить("СписокПолучателейКопий",			Форма.БыстрыйВвод_ПисьмоСписокПолучателейКопий);
		ДанныеЗаполнения.Вставить("СписокПолучателейСкрытыхКопий",	Форма.БыстрыйВвод_ПисьмоСписокПолучателейСкрытыхКопий);
		ДанныеЗаполнения.Вставить("Тема",							Форма.БыстрыйВвод_Тема);
		ДанныеЗаполнения.Вставить("ОтправлятьСразу",				Форма.БыстрыйВвод_ПисьмоОтправлятьСразу);
		ДанныеЗаполнения.Вставить("УникальныйИдентификатор",		Форма.УникальныйИдентификатор);
		
		ТекстыПисьма = CRM_ЛентаСобытий.ТекстыПисьма(Форма.БыстрыйВвод_Содержание,
			Форма.БыстрыйВвод_ПисьмоУчетнаяЗапись, Форма.ТекущийПользователь);
		
		ДанныеЗаполнения.Вставить("Текст",							ТекстыПисьма.ТекстПисьма);
		ДанныеЗаполнения.Вставить("ТекстHTML",						ТекстыПисьма.ТекстHTML);
		
		ДанныеЗаполнения.Вставить("ТипТекста",						ПредопределенноеЗначение("Перечисление.ТипыТекстовЭлектронныхПисем.HTML"));
		ДанныеЗаполнения.Вставить("УчетнаяЗапись",					Форма.БыстрыйВвод_ПисьмоУчетнаяЗапись);
		ДанныеЗаполнения.Вставить("ОтправительПредставление",		Форма.БыстрыйВвод_ПисьмоОтправительПредставление);
		
		ПолучателиМассив = Новый Массив;
		Для Каждого СтрокаПолучателя Из Форма.БыстрыйВвод_ПисьмоПолучатели Цикл
			НовыйЭлемент = Новый Структура("Адрес,Представление,Контакт");
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтрокаПолучателя);
			ПолучателиМассив.Добавить(НовыйЭлемент);
		КонецЦикла;
		ДанныеЗаполнения.Вставить("ПолучателиПисьма",				ПолучателиМассив);
		
		ПолучателиМассив = Новый Массив;
		Для Каждого СтрокаПолучателя Из Форма.БыстрыйВвод_ПисьмоПолучателиКопий Цикл
			НовыйЭлемент = Новый Структура("Адрес,Представление,Контакт");
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтрокаПолучателя);
			ПолучателиМассив.Добавить(НовыйЭлемент);
		КонецЦикла;
		ДанныеЗаполнения.Вставить("ПолучателиКопий",				ПолучателиМассив);
		
		ПолучателиМассив = Новый Массив;
		Для Каждого СтрокаПолучателя Из Форма.БыстрыйВвод_ПисьмоПолучателиСкрытыхКопий Цикл
			НовыйЭлемент = Новый Структура("Адрес,Представление,Контакт");
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтрокаПолучателя);
			ПолучателиМассив.Добавить(НовыйЭлемент);
		КонецЦикла;
		ДанныеЗаполнения.Вставить("ПолучателиСкрытыхКопий",			ПолучателиМассив);
		
	ИначеЕсли ТипОбъекта = "Чат" Тогда
		
		ДанныеЗаполнения.Вставить("Содержание",						Форма.БыстрыйВвод_Содержание);
		ДанныеЗаполнения.Вставить("УчетнаяЗапись",					Форма.БыстрыйВвод_ЧатУчетнаяЗапись);
		ДанныеЗаполнения.Вставить("Группа",							Форма.БыстрыйВвод_ЧатГруппа);
		ДанныеЗаполнения.Вставить("Адрес",							Форма.БыстрыйВвод_ЧатАдрес);
		ДанныеЗаполнения.Вставить("Получатель",						Форма.БыстрыйВвод_ЧатПолучатель);
		ДанныеЗаполнения.Вставить("ПредставлениеПолучателя",		Форма.БыстрыйВвод_ЧатПредставлениеПолучателя);
		Если Режим = "ЛентаИнтереса" Тогда
			ДанныеЗаполнения.Вставить("ДокументОснование",			Форма.Объект.Ссылка);
			ДанныеЗаполнения.Вставить("СостояниеИнтереса",			Форма.Объект.СостояниеИнтереса);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеЗаполнения;
	
КонецФункции

Процедура СоздатьОбъектОбычныйВвод(Знач Режим, Знач ТипОбъекта, Форма) Экспорт
	
	Если ТипОбъекта = "Интерес" Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Основание", Форма.Объект.Ссылка);
		ОткрытьФорму("Документ.CRM_Интерес.ФормаОбъекта", ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
	ИначеЕсли ТипОбъекта = "Счет" Тогда
		
		ИмяФормы = "Документ.CRM_СчетНаОплатуПокупателю.ФормаОбъекта";
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Основание", Форма.Объект.Ссылка);
		ОткрытьФорму(ИмяФормы, ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
	ИначеЕсли ТипОбъекта = "ЗаказКлиента" Тогда
		
		ИмяФормы = "Документ.ЗаказКлиента.ФормаОбъекта";
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Основание", Форма.Объект.Ссылка);
		ОткрытьФорму(ИмяФормы, ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
	ИначеЕсли ТипОбъекта = "КоммерческоеПредложение" Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Основание", Форма.Объект.Ссылка);
		ОткрытьФорму("Документ.КоммерческоеПредложениеКлиенту.ФормаОбъекта", ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
	ИначеЕсли ТипОбъекта = "Поручение" Тогда
		
		ПараметрыОснования = Новый Структура;
		ПараметрыОснования.Вставить("Основание", Форма.Объект.Ссылка);
		ПараметрыОснования.Вставить("КартаМаршрута", ПредопределенноеЗначение("Справочник.CRM_КартыМаршрутов.Поручение"));
		
		ПараметрыОткрытия = Новый Структура("Основание", ПараметрыОснования);
		
		ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаПоручения", ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
	ИначеЕсли ТипОбъекта = "БизнесПроцесс" Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ПредметОснование", Форма.Объект.Ссылка);
		ОткрытьФорму("БизнесПроцесс.CRM_БизнесПроцесс.Форма.ФормаВыборИзДерева", ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
	ИначеЕсли ТипОбъекта = "СообщениеSMS" Тогда
		
		Если Режим = "ЛентаИнтереса" Тогда
			ПотенциальныйКлиент	= Форма.Объект.ПотенциальныйКлиент;
			КонтактноеЛицо		= Форма.Объект.КонтактноеЛицо;
			Партнер				= Форма.Объект.Партнер;
		ИначеЕсли Режим = "ЛентаКлиента" Тогда
			ПотенциальныйКлиент	= Неопределено;
			КонтактноеЛицо		= Форма.Объект.CRM_ОсновноеКонтактноеЛицо;
			Партнер				= Форма.Объект.Ссылка;
		Иначе
			ПотенциальныйКлиент	= Неопределено;
			КонтактноеЛицо		= Форма.Объект.Ссылка;
			Партнер				= Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПотенциальныйКлиент) Тогда
			Контакт = ПотенциальныйКлиент;
		Иначе
			Если ЗначениеЗаполнено(КонтактноеЛицо) Тогда
				Контакт = КонтактноеЛицо;
			Иначе
				Контакт = Партнер;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Контакт) Тогда
			Возврат;
		КонецЕсли;
		
		ТелефонКонтакта = "";
		СписокТелефонов = SMS4B_Взаимодействия.SMS4B_ТелефоныКонтакта(Контакт);
		Если СписокТелефонов.Количество() > 0 Тогда
			ТелефонКонтакта = СписокТелефонов[0].Значение;
		КонецЕсли;
		
		ПараметрыОснования = Новый Структура;
		ПараметрыОснования.Вставить("Ссылка",	Контакт);
		ПараметрыОснования.Вставить("Телефон",	ТелефонКонтакта);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Основание", ПараметрыОснования);
		ПараметрыОткрытия.Вставить("СокращеннаяФорма", Истина);
		ПараметрыОткрытия.Вставить("ДанныеЗаполнения", Форма.Объект.Ссылка);
		
		ОткрытьФорму("Документ.СообщениеSMS.ФормаОбъекта", ПараметрыОткрытия, Форма, Форма.УникальныйИдентификатор);
		
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти