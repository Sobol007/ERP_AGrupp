
#Область СлужебныйПрограммныйИнтерфейс

#Область ФункциональностьФорм

Процедура SMS4B_ИмяОтправителяОбработкаВыбора(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	Если ВыбранноеЗначение = "SMS4B_Подключиться" Тогда
		СтандартнаяОбработка = Ложь;
		ОповещениеЗавершения = Новый ОписаниеОповещения("SMS4B_ОбновитьНастройкиСервиса",
			SMS4B_ВзаимодействияКлиент, Новый Структура("ФормаНастроек", Форма));
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура SMS4B_ОбновитьНастройкиСервиса(Результат, ДополнительныеПараметры) Экспорт
	
	ФормаНастроек = ДополнительныеПараметры.ФормаНастроек;
	ФормаНастроек.Записать();
	
	РезультатПодключения = SMS4B_ОтправкаSMS.SMS4B_ОбновитьНастройкиСервиса(Истина);
	Если РезультатПодключения.Ошибка Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатПодключения.ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	СписокВыбора = ФормаНастроек.Элементы.ИмяОтправителя.СписокВыбора;
	СписокВыбора.Очистить();
	
	ВыбранноеИмяОтправителя = ФормаНастроек.ИмяОтправителя;
	
	НастройкиСервиса = РезультатПодключения.НастройкиСервиса;
	
	Для Каждого ИмяОтправителя Из НастройкиСервиса.ИменаОтправителя Цикл
		СписокВыбора.Добавить(ИмяОтправителя);
	КонецЦикла;
	Если СписокВыбора.НайтиПоЗначению(ВыбранноеИмяОтправителя) = Неопределено Тогда
		ФормаНастроек.ИмяОтправителя = НастройкиСервиса.ИменаОтправителя[0];
	КонецЕсли;
	
	СписокВыбора.Добавить("SMS4B_Подключиться", НСтр("ru='Подключиться и обновить';en='Connect and update'"),, БиблиотекаКартинок.Обновить);
	
	ТекстОповещения = НСтр("ru='Обновление параметров SMS4B';en='Update SMS4B settings'");
	ТекстПояснения = НСтр("ru='Параметры сервиса SMS4B успешно обновлены.';en='SMS4B service parameters have been successfully updated.'");
	ПоказатьОповещениеПользователя(ТекстОповещения,, ТекстПояснения, БиблиотекаКартинок.Информация32, СтатусОповещенияПользователя.Информация, "SMS4B");
	
КонецПроцедуры

Процедура SMS4B_ОткрытьРасширенныеНастройки(Форма) Экспорт
	
	ОповещениеЗавершения = Новый ОписаниеОповещения("SMS4B_ОткрытьРасширенныеНастройкиЗавершение",
		SMS4B_ВзаимодействияКлиент, Новый Структура);
	
	ОткрытьФорму("ОбщаяФорма.SMS4B_РасширенныеНастройки"
		,
		,
		Форма,
		Форма.УникальныйИдентификатор,
		,
		,
		ОповещениеЗавершения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура SMS4B_ОткрытьРасширенныеНастройкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура SMS4B_ОткрытьОписаниеСервиса(Форма) Экспорт
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("https://www.sms4b.ru");
	
КонецПроцедуры

Процедура SMS4B_ОткрытьЛичныйКабинетСервиса(Форма) Экспорт
	
	ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку("https://www.sms4b.ru/office/");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
