
////////////////////////////////////////////////////////////////////////////////
// Интеграция с UniSender клиент, сервер 
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Данные доступа к unisender
// 
// Возвращаемое значение:
//  Структура - Данные доступа.
//
Функция ДанныеДоступаКUniSender() Экспорт
	ДанныеДоступа = Константы.CRM_ДанныеДоступаКUniSender.Получить().Получить();
	Если ДанныеДоступа = Неопределено Тогда
		ДанныеДоступа = Новый Структура("КлючДоступа, СписокДляРассылок, СписокДляОповещений", "", "", "");
	КонецЕсли;
	Возврат ДанныеДоступа;
КонецФункции

// Записать данные доступа к uni sender
//
// Параметры:
//  КлючДоступа			 - Строка	 - Ключ доступа.
//  СписокДляРассылок	 - Строка	 - Список для рассылок.
//  СписокДляОповещений	 - Строка	 - Список для оповещений.
//
Процедура ЗаписатьДанныеДоступаКUniSender(КлючДоступа, СписокДляРассылок, СписокДляОповещений) Экспорт
	ДанныеДоступа = Новый Структура("КлючДоступа, СписокДляРассылок, СписокДляОповещений", КлючДоступа, СписокДляРассылок, СписокДляОповещений);
	Константы.CRM_ДанныеДоступаКUniSender.Установить(Новый ХранилищеЗначения(ДанныеДоступа));
КонецПроцедуры

// Получить статус письма
//
// Параметры:
//  Письмо	 - ДокументСсылка - Ссылка на письмо.
// 
// Возвращаемое значение:
//  Структура - Структура статусов письма. 
//
Функция ПолучитьСтатусПисьма(Письмо) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	CRM_СтатусыПисемEmailРассылки.Статус,
	                      |	CRM_СтатусыПисемEmailРассылки.СтатусСтрокой,
	                      |	CRM_РасшифровкиСтатусовПисемEmailРассылки.Расшифровка
	                      |ИЗ
	                      |	РегистрСведений.CRM_СтатусыПисемEmailРассылки КАК CRM_СтатусыПисемEmailРассылки
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_РасшифровкиСтатусовПисемEmailРассылки КАК CRM_РасшифровкиСтатусовПисемEmailРассылки
	                      |		ПО CRM_СтатусыПисемEmailРассылки.СтатусСтрокой = CRM_РасшифровкиСтатусовПисемEmailРассылки.КодСтатуса
	                      |ГДЕ
	                      |	CRM_СтатусыПисемEmailРассылки.Письмо = &Письмо");
	Запрос.УстановитьПараметр("Письмо", Письмо);
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураСтатуса = Новый Структура;
	Если Выборка.Следующий() Тогда
		СтруктураСтатуса.Вставить("Статус", Выборка.Статус);
		СтруктураСтатуса.Вставить("СтатусСтрокой", Выборка.СтатусСтрокой);
		СтруктураСтатуса.Вставить("РасшифровкаСтатуса", Выборка.Расшифровка);
	Иначе
		СтруктураСтатуса.Вставить("Статус", Перечисления.CRM_СтатусыПисемEmailРассылки.ПустаяСсылка());
		СтруктураСтатуса.Вставить("СтатусСтрокой", "");
		СтруктураСтатуса.Вставить("РасшифровкаСтатуса", "");
	КонецЕсли;
	Возврат СтруктураСтатуса;
	
КонецФункции

#КонецОбласти


