////////////////////////////////////////////////////////////////////////////////
// Подсистема "Торговые предложения".
// ОбщийМодуль.ТорговыеПредложенияПереопределяемый.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает признак того, что функциональная опция использования видов номенклатуры включена.
// 
// Параметры:
//   ИмяФормы - Строка - имя формы, для которой получаются данные.
//   Результат - Булево - признак того, что функциональная опция включена.
//
Процедура ФункциональнаяОпцияИспользуется(Знач ИмяФормы, Результат) Экспорт
	
	//++ НЕ ГОСИС
	МассивОпций = ЗависимостьФункционалаОтФункциональныхОпций().Получить(ИмяФормы);
	Если ЗначениеЗаполнено(МассивОпций) Тогда
		Для Каждого Опция из МассивОпций Цикл
			Если Не ПолучитьФункциональнуюОпцию(Опция) Тогда
				Результат = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат = Истина;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Выдает сообщение пользователю о том, что функциональная опция использования видов номенклатуры отключена.
//
// Параметры:
//  ИмяФормы - Строка - имя формы из которой вызывается сообщение.
//  Используется - Булево - признак того, что функциональная опция включена или нет.
//  Отказ - Булево - признак того, что форму сопоставления номенклатуры открывать не нужно.
//
Процедура СообщитьОНеобходимостиИспользованияФункциональнойОпции(Знач ИмяФормы, Знач Используется, Отказ) Экспорт
	
	//++ НЕ ГОСИС
	Если Используется Тогда
		Возврат;
	КонецЕсли;
	
	МассивОпций = ЗависимостьФункционалаОтФункциональныхОпций().Получить(ИмяФормы);
	
	Для Каждого Опция из МассивОпций Цикл
		Если Опция = "ИспользоватьНесколькоВидовНоменклатуры" Тогда
			ТекстСообщения = НСтр("ru = 'Опция использования нескольких видов номенклатуры отключена.';
									|en = 'The option of several product kinds usage is disabled.'");
		ИначеЕсли Опция = "ИспользоватьСоглашенияСКлиентами" Тогда
			ТекстСообщения = НСтр("ru = 'Опция использования соглашений с клиентами отключена.';
									|en = 'The option of customer agreements usage is disabled.'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,,,,Отказ);
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение валюты регламентированного учета.
// 
// Параметры:
//  Валюта - СправочникСсылка - валюта регламентированного учета.
//
Процедура ПолучитьВалютуРегламентированногоУчета(Валюта) Экспорт
	
	//++ НЕ ГОСИС
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Создает документ заказ поставщику на основании данных торгового предложения.
//
// Параметры:
//  ДанныеЗаполнения - Структура - данные торгового предложения:
//   * Организация       - СправочникСсылка - организация торгового предложения.
//   * Контрагент        - СправочникСсылка - поставщик торгового предложения.
//   * Валюта            - СправочникСсылка.Валюты - валюта торгового предложения.
//   * СтрокиЗаказа      - ТаблицаЗначений - содержит данные табличной части.
//       * Номенклатура - СправочникСсылка.
//       * Характеристика - СправочникСсылка.
//       * ЕдиницаИзмерения - СправочникСсылка.
//       * Количество - Число.
//       * Цена - Число.
//       * ЦенаВключаетНДС - Булево.
//       * СтавкаНДС - ПеречислениеСсылка.
//       * СуммаНДС - Число.
//       * СуммаСНДС - Число.
//       * Сумма - Число.
//       * ИдентификаторНоменклатуры - Строка - идентификатор номенклатуры поставщика.
//   * КонтекстИсточника - Структура - набор данных из основания содержащий данные для заполнения шапки документа.
//   * СпособДоставки    - Ссылка - способ доставки прикладного решения.
//   * АдресДоставки     - Строка - представление адреса доставки.
//   * АдресДоставкиЗначенияПолей - Строка - значение адреса доставки в формате XML.
//  ДокументОбъект - ДокументОбъект - возвращается созданный, но не записанный документ.
//  Отказ - Булево - признак ошибки при работе метода.
//
Процедура СоздатьДокументЗаказПоставщикуНаОснованииТорговогоПредложения(Знач ДанныеЗаполнения, ДокументОбъект, Отказ) Экспорт
	
	//++ НЕ ГОСИС
	КонтекстИсточника = ДанныеЗаполнения.КонтекстИсточника;
	СтрокиЗаказа = ДанныеЗаполнения.СтрокиЗаказа;
	Организация = ДанныеЗаполнения.Организация;
	Контрагент = ДанныеЗаполнения.Контрагент;
	Валюта = ДанныеЗаполнения.Валюта;
	
	ДокументОбъект = Документы.ЗаказПоставщику.СоздатьДокумент();
	ДокументОбъект.Дата = ТекущаяДатаСеанса();
	ДокументОбъект.Автор = Пользователи.АвторизованныйПользователь();

	// Реквизиты шапки.
	ДокументОбъект.Организация = Организация;
	ДокументОбъект.Контрагент =  Контрагент;
	ДокументОбъект.Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер");
	ДокументОбъект.Валюта = Валюта;
	ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	ДокументОбъект.Статус = Перечисления.СтатусыЗаказовПоставщикам.НеСогласован;
	Если КонтекстИсточника <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДокументОбъект, КонтекстИсточника);
	КонецЕсли;
	
	// Заполнение данных по доставке.
	Если ДанныеЗаполнения.Свойство("СпособДоставки") Тогда
		ДокументОбъект.СпособДоставки = ДанныеЗаполнения.СпособДоставки;
		Если ДокументОбъект.СпособДоставки = Перечисления.СпособыДоставки.НашимиСиламиСАдресаОтправителя Тогда
			ДокументОбъект.АдресДоставки = ДанныеЗаполнения.АдресДоставки;
			ДокументОбъект.АдресДоставкиЗначенияПолей = ДанныеЗаполнения.АдресДоставкиЗначенияПолей;
		ИначеЕсли ДокументОбъект.СпособДоставки = Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада Тогда
			ДокументОбъект.АдресДоставкиДляПоставщика = ДанныеЗаполнения.АдресДоставки;
			ДокументОбъект.АдресДоставкиЗначенияПолей = ДанныеЗаполнения.АдресДоставкиЗначенияПолей;
		КонецЕсли;
	КонецЕсли;

	// Заполнение соглашения по статистике.
	ЗаполняемыеРеквизиты = Новый Структура("Соглашение");
	РеквизитыДляОтбора = Новый Структура;
	РеквизитыДляОтбора.Вставить("Партнер", ДокументОбъект.Партнер);
	РеквизитыДляОтбора.Вставить("Валюта", ДокументОбъект.Валюта);
	РеквизитыДляОтбора.Вставить("ЦенаВключаетНДС", ДокументОбъект.ЦенаВключаетНДС);
	ЗаполнениеСвойствПоСтатистикеСервер.ПолучитьЗначенияСвойств(
		ДокументОбъект.Ссылка, ЗаполняемыеРеквизиты, РеквизитыДляОтбора);
	Если ЗначениеЗаполнено(ЗаполняемыеРеквизиты.Соглашение) Тогда
		ДокументОбъект.Соглашение = ЗаполняемыеРеквизиты.Соглашение;
	КонецЕсли;
	
	Сегодня = НачалоДня(ТекущаяДата());
	Сутки = 60 * 60 * 24;
	
	// Товары.
	Для каждого СтрокаЗаказа Из СтрокиЗаказа Цикл
		СтрокаТовары = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовары, СтрокаЗаказа);
		СтрокаТовары.Характеристика = СтрокаЗаказа.Характеристика;
		СтрокаТовары.КоличествоУпаковок = СтрокаЗаказа.Количество;
		СтрокаТовары.ДатаПоступления = Сегодня + (СтрокаЗаказа.СрокПоставки * Сутки);
		Если ЗначениеЗаполнено(СтрокаЗаказа.КонтекстСтроки) Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТовары, СтрокаЗаказа.КонтекстСтроки);
		КонецЕсли;
		СтрокаТовары.НоменклатураПоставщика = Справочники.НоменклатураПоставщиков.НайтиПоРеквизиту("Идентификатор", СтрокаЗаказа.ИдентификаторНоменклатуры);
	КонецЦикла;
	
	ДокументОбъект.Заполнить(Неопределено); // Инициализация документа.
	ДокументОбъект.ЗаполнитьЭтапыГрафикаОплаты();
	ДокументОбъект.УстановитьНовыйНомер();
	
	ТаблицаДатПоступления = ДокументОбъект.Товары.Выгрузить(, "ДатаПоступления");
	ТаблицаДатПоступления.Свернуть("ДатаПоступления");
	ДокументОбъект.ПоступлениеОднойДатой = ТаблицаДатПоступления.Количество() = 1;
	Если ДокументОбъект.ПоступлениеОднойДатой Тогда
		ДокументОбъект.ДатаПоступления = ТаблицаДатПоступления[0].ДатаПоступления;
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Удаляет созданные заказы поставщику при переходе на шаг назад.
//
// Параметры:
//  ТаблицаДокументы - ТаблицаЗначений - таблица документов для удаления, состав:
//    * Ссылка - ДокументСсылка - ссылка на удаляемый документ.
//  Отказ - Булево - признак результата удаления документов.
//
Процедура УдалитьДокументыЗаказПоставщику(ТаблицаДокументы, Отказ) Экспорт
	
	//++ НЕ ГОСИС
	Сообщение = Новый СообщениеПользователю();
	ТекстИмеютсяСсылки = НСтр("ru = 'Имеются ссылки на %1 - %2.';
								|en = 'There are references to %1 - %2.'");
	ТекстПомечен = НСтр("ru = '%1 - помечен на удаление.';
						|en = '%1 - marked for deletion.'");
	ТекстЗаблокирован  = НСтр("ru = '%1 - заблокирован. Пометить на удаление не удалось.';
								|en = '%1 is locked. Cannot mark for deletion.'");
	ТекстУдален = НСтр("ru = '%1 - удален.';
						|en = '%1 - deleted.'");
	
	ИменаОбъектовМетаданных = "Документ.ЗаказПоставщику";
	Регистры = ОбщегоНазначенияУТПовтИсп.РегистрыСведенийПоМетаданнымИзмерений(ИменаОбъектовМетаданных, Истина);
	
	СсылкиНаУдаление = Новый Массив();
	Для Каждого Строка Из ТаблицаДокументы Цикл
		СсылкиНаУдаление.Добавить(Строка.Ссылка);
	КонецЦикла;
	
	НазначенияЗаказов = НазначенияЗаказов(СсылкиНаУдаление);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СсылкиНаУдаление, НазначенияЗаказов);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаСсылок = НайтиПоСсылкам(СсылкиНаУдаление);
	
	ВсегоСтрок = ТаблицаСсылок.Количество();
	Для Счетчик = 1 По ВсегоСтрок Цикл
		Ссылка = ТаблицаСсылок[ВсегоСтрок - Счетчик];
		Если Ссылка[0] = Ссылка[1] Или ЭтоЗаписьВедомогоРегистраСведений(Ссылка[1], Регистры) Тогда
			ТаблицаСсылок.Удалить(Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ПомечатьНаУдаление = Новый Массив();
	
	ВсегоСтрок = ТаблицаСсылок.Количество();
	Для Счетчик = 1 По ВсегоСтрок Цикл
		Ссылка = ТаблицаСсылок[ВсегоСтрок - Счетчик];
		Индекс = СсылкиНаУдаление.Найти(Ссылка[1]);
		Если Индекс = Неопределено Тогда
			Если ПомечатьНаУдаление.Найти(Ссылка[0]) = Неопределено Тогда
				ПомечатьНаУдаление.Добавить(Ссылка[0]);
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстИмеютсяСсылки, СокрЛП(Ссылка[0]), СокрЛП(Ссылка[1]));
				Сообщение.Сообщить();
			КонецЕсли;
			ТаблицаСсылок.Удалить(Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ОбработкаЗавершена = Ложь;
	Пока Не ОбработкаЗавершена Цикл
		ОбработкаЗавершена = Истина;
		ВсегоСтрок = ТаблицаСсылок.Количество();
		Для Счетчик = 1 По ВсегоСтрок Цикл
			Ссылка = ТаблицаСсылок[ВсегоСтрок - Счетчик];
			Если ПомечатьНаУдаление.Найти(Ссылка[0]) <> Неопределено Тогда
				Если ПомечатьНаУдаление.Найти(Ссылка[1]) = Неопределено Тогда
					ПомечатьНаУдаление.Добавить(Ссылка[1]);
				КонецЕсли;
				ТаблицаСсылок.Удалить(Ссылка);
				ОбработкаЗавершена = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаДокументы.Очистить();
	
	Для Каждого Ссылка из ПомечатьНаУдаление Цикл
		Индекс = СсылкиНаУдаление.Найти(Ссылка);
		Если Индекс <> Неопределено Тогда
			СсылкиНаУдаление.Удалить(Индекс);
		КонецЕсли;
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		Если Не ДокументОбъект.Заблокирован() Тогда
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПомечен, СокрЛП(Ссылка));
		Иначе
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаблокирован, СокрЛП(Ссылка));
		КонецЕсли;
		Сообщение.Сообщить();
	КонецЦикла;
	
	Для Каждого Ссылка Из СсылкиНаУдаление Цикл
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстУдален, СокрЛП(Ссылка));
		Сообщение.Сообщить();
	КонецЦикла;
	
	УдалитьОбъекты(СсылкиНаУдаление, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получает значения контекста на основании которого будет производиться поиск предложений
// и которые будут переданы в шапку создаваемых заказов.
//
// Параметры:
//  ПараметрКоманды - Массив - ссылки на документы для заполнения таблицы поиска товаров в 1С:Бизнес-сеть.
//  КонтекстИсточника - Структура - свойства источника, используемые для формирования заказов.
//  ТаблицаТовары - ТаблицаЗначений - товары для поиска с реквизитами, состав:
//    * Номенклатура - СправочникСсылка - номенклатура.
//    * Характеристика - СправочникСсылка - характеристика номенклатуры.
//    * Упаковка - СправочникСсылка - упаковка номенклатуры.
//    * Количество - Число - количество.
//    * ЕдиницаИзмерения - СправочникСсылка - единица измерения номенклатуры.
//    * Числитель - Число - числитель упаковки.
//    * Знаменатель - число - знаменатель номенклатуры.
//    * Склад - СправочникСсылка - склад для заказа.
//    * КонтекстСтроки - Произвольный - дополнительные свойства по источнику.
//
Процедура СвойстваКонтекстаДокументовДляПоискаПредложений(Знач ПараметрКоманды, КонтекстИсточника, ТаблицаТовары) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	
	Если ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		ДополнительныеРеквизиты = Новый Структура();
		ДополнительныеРеквизиты.Вставить("Организация");
		ДополнительныеРеквизиты.Вставить("Валюта");
		ДополнительныеРеквизиты.Вставить("Приоритет");
		ДополнительныеРеквизиты.Вставить("Проведен");
		ДополнительныеРеквизиты.Вставить("Сделка");
		ДополнительныеРеквизиты.Вставить("Статус");
		ДополнительныеРеквизиты.Вставить("Склад");
		ДополнительныеРеквизиты.Вставить("НаправлениеДеятельности");
		ДополнительныеРеквизиты.Вставить("ЗакупкаПодДеятельность");
		
		СтруктураКонтекстаСтроки = Новый Структура("КонтекстСтрокиСклад");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Номенклатура                      КАК Номенклатура,
		|	ЗаказПоставщикуТовары.Характеристика                    КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ЗаказПоставщикуТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ ЗаказПоставщикуТовары.Упаковка
		|	КОНЕЦ                                                   КАК Упаковка,
		|	ЗаказПоставщикуТовары.КоличествоУпаковок                КАК Количество,
		|	ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмерения     КАК ЕдиницаИзмерения,
		|	ЗаказПоставщикуТовары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ЗаказПоставщикуТовары.Упаковка.Числитель                КАК Числитель,
		|	ЗаказПоставщикуТовары.Упаковка.Знаменатель              КАК Знаменатель,
		|	ЗаказПоставщикуТовары.Склад                             КАК КонтекстСтрокиСклад
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка В(&СписокСсылок)"
		
	ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ПланЗакупок") Тогда
		
		ДополнительныеРеквизиты = "Валюта, Склад";
		СтруктураКонтекстаСтроки = Новый Структура("КонтекстСтрокиСклад");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПланЗакупокТовары.Номенклатура                      КАК Номенклатура,
		|	ПланЗакупокТовары.Характеристика                    КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ПланЗакупокТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ПланЗакупокТовары.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ ПланЗакупокТовары.Упаковка
		|	КОНЕЦ                                               КАК Упаковка,
		|	ПланЗакупокТовары.КоличествоУпаковок                КАК Количество,
		|	ПланЗакупокТовары.Номенклатура.ЕдиницаИзмерения     КАК ЕдиницаИзмерения,
		|	ПланЗакупокТовары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ПланЗакупокТовары.Упаковка.Числитель                КАК Числитель,
		|	ПланЗакупокТовары.Упаковка.Знаменатель              КАК Знаменатель,
		|	ПланЗакупокТовары.Склад                             КАК КонтекстСтрокиСклад
		|ИЗ
		|	Документ.ПланЗакупок.Товары КАК ПланЗакупокТовары
		|ГДЕ
		|	ПланЗакупокТовары.Ссылка В(&СписокСсылок)"
		
	ИначеЕсли ТипЗнч(ПараметрКоманды[0]) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		ДополнительныеРеквизиты = Новый Структура();
		ДополнительныеРеквизиты.Вставить("Организация");
		ДополнительныеРеквизиты.Вставить("Валюта");
		ДополнительныеРеквизиты.Вставить("Приоритет");
		ДополнительныеРеквизиты.Вставить("Проведен");
		ДополнительныеРеквизиты.Вставить("Сделка");
		ДополнительныеРеквизиты.Вставить("Склад");
		ДополнительныеРеквизиты.Вставить("Статус");
		ДополнительныеРеквизиты.Вставить("НаправлениеДеятельности");
		ДополнительныеРеквизиты.Вставить("ДокументОснование",      "Ссылка");
		ДополнительныеРеквизиты.Вставить("ЗакупкаПодДеятельность", "НалогообложениеНДС");
		
		СтруктураКонтекстаСтроки = Новый Структура("КонтекстСтрокиСклад");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура                      КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика                    КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ ЗаказКлиентаТовары.Упаковка
		|	КОНЕЦ                                                КАК Упаковка,
		|	ЗаказКлиентаТовары.КоличествоУпаковок                КАК Количество,
		|	ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения     КАК ЕдиницаИзмерения,
		|	ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод,
		|	ЗаказКлиентаТовары.Упаковка.Числитель                КАК Числитель,
		|	ЗаказКлиентаТовары.Упаковка.Знаменатель              КАК Знаменатель,
		|	ЗаказКлиентаТовары.Склад                             КАК КонтекстСтрокиСклад
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка В(&СписокСсылок)"
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СписокСсылок", ПараметрКоманды);
	Товары = Запрос.Выполнить().Выгрузить();
	Товары.Колонки.Добавить("КонтекстСтроки");
	Для Каждого СтрокаТоваров Из Товары Цикл
		КонтекстСтроки = Новый Структура();
		Для Каждого Свойство из СтруктураКонтекстаСтроки Цикл
			ИмяРеквизита = СтрЗаменить(Свойство.Ключ, "КонтекстСтроки", "");
			КонтекстСтроки.Вставить(ИмяРеквизита, СтрокаТоваров[Свойство.Ключ])
		КонецЦикла;
		СтрокаТоваров.КонтекстСтроки = КонтекстСтроки;
		ЗаполнитьЗначенияСвойств(ТаблицаТовары.Добавить(), СтрокаТоваров);
	КонецЦикла;
	КонтекстИсточника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрКоманды[0], ДополнительныеРеквизиты);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Производит пересчет суммы для заказа товаров.
//
// Параметры:
//  СтрокаТоваров - Структура - реквизиты строки товаров для пересчета суммы при формировании заказов:
//    * ПроцентНДС      - Строка - значение НДС, полученное из сервиса.
//    * СтавкаНДС       - Ссылка - значение ставки НДС.
//    * Цена            - Число - цена товара для заказа.
//    * ЦенаВключаетНДС - Булево - признак включения НДС в цену.
//    * Количество      - Число - количество товара для заказа.
//    * Сумма           - Число - сумма заказа по строке.
//    * СуммаНДС        - Число - сумма НДС строки.
//    * СуммаСНДС       - Число - значение суммы с НДС.
//
Процедура ПересчитатьСуммуПоСтроке(СтрокаТоваров) Экспорт
	
	//++ НЕ ГОСИС
	СтрокаТоваров.Сумма = СтрокаТоваров.Количество * СтрокаТоваров.Цена;
	СтрокаТоваров.СуммаНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(СтрокаТоваров.Сумма,
																			СтрокаТоваров.СтавкаНДС,
																			СтрокаТоваров.ЦенаВключаетНДС);
	Если СтрокаТоваров.ЦенаВключаетНДС Тогда
		СтрокаТоваров.СуммаСНДС = СтрокаТоваров.Сумма;
	Иначе
		СтрокаТоваров.СуммаСНДС = СтрокаТоваров.Сумма + СтрокаТоваров.СуммаНДС;
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Устанавливаем условное оформление для единиц измерения номенклатуры.
//
// Параметры:
// 	 Форма - Форма - управляемая форму.
// 	 ИмяПоляВводаЕдиницИзмерения - Строка - наименование элемента формы, содержащего ед. измерения номенклатуры.
// 	 ПутьКПолюОтбора - Строка - полный путь к реквизиту.
//
Процедура УстановитьУсловноеОформлениеЕдиницИзмерения(Форма, Знач ИмяПоляВводаЕдиницИзмерения, Знач ПутьКПолюОтбора) Экспорт
	
	//++ НЕ ГОСИС
	// См. процедуру НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения.
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(Форма, ИмяПоляВводаЕдиницИзмерения, ПутьКПолюОтбора);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Устанавливает значение свойства элементов формы, если находит элемент на форме.
//
// Параметры:
//  ЭлементыФормы  - ВсеЭлементыФормы - элементы формы, среди которых содержится искомый элемент.
//  ИменаЭлементов - Массив, Строка, Структура - имена искомых элементов.
//  ИмяСвойства    - Строка - имя свойства, для которого будет устанавливаться значение.
//  Значение       - Произвольный - значение, которое будет установлено.
//  УстанавливатьДляПодчиненных - Булево - установить аналогичное свойство для подчиненных элементов искомых элементов.
//
Процедура УстановитьСвойствоЭлементовФормы(ЭлементыФормы, ИменаЭлементов, ИмяСвойства, Значение, УстанавливатьДляПодчиненных = Ложь) Экспорт
	
	//++ НЕ ГОСИС
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(ЭлементыФормы, ИменаЭлементов, ИмяСвойства, Значение, УстанавливатьДляПодчиненных);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение доступных типов значений свойств для сопоставления.
// 
// Параметры:
//  ТипыЗначенийСвойств - СписокЗначений - доступные типы значений, например Тип("СправочникСсылка.Производители").
//
Процедура ПолучитьДоступныеТипыЗначенийСвойствДляСопоставления(ТипыЗначенийСвойств) Экспорт
	
	//++ НЕ ГОСИС
	ТипыЗначенийСвойств.Добавить(Тип("СправочникСсылка.ЗначенияСвойствОбъектов"));
	ТипыЗначенийСвойств.Добавить(Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия"));
	ТипыЗначенийСвойств.Добавить(Тип("СправочникСсылка.Производители"));
	ТипыЗначенийСвойств.Добавить(Тип("СправочникСсылка.Марки"));
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Возвращает пустую ссылку реквизита объекта для сопоставления.
// 
// Параметры:
//  Ссылка - ЛюбаяСсылка - пустая ссылка для заполнения сопоставления,
//                         например ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.
//
Процедура ПустаяСсылкаРеквизитаОбъектаДляСопоставления(Ссылка) Экспорт
	
	//++ НЕ ГОСИС
	Ссылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполнение контактной информации по контактному лицу.
//
// Параметры:
//  КонтактноеЛицо    - СправочникСсылка - контактное лицо в информационной базе, например СправочникСсылка.ФизическиеЛица.
//  ЭлектроннаяПочта  - Строка - адрес электронной почты.
//  Телефон           - Строка - номер телефона.
//  УведомлятьПоПочте - Булево - признак необходимости уведомления по электронной почте.
//  Перезаполнять     - Булево - признак необходимости перезаполнения информации.
//
Процедура ЗаполнитьКонтактнуюИнформациюПоКонтактномуЛицу(Знач КонтактноеЛицо, ЭлектроннаяПочта, Телефон, УведомлятьПоПочте, Знач Перезаполнять = Ложь) Экспорт
	
	//++ НЕ ГОСИС
	Если ТипЗнч(КонтактноеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") и Не КонтактноеЛицо.Пустая() Тогда
		Если ПустаяСтрока(ЭлектроннаяПочта) или Перезаполнять Тогда
			ЭлектроннаяПочта = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(КонтактноеЛицо,
				Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица, ТекущаяДатаСеанса());
		КонецЕсли;
		Если ПустаяСтрока(Телефон) или Перезаполнять Тогда
			Телефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(КонтактноеЛицо,
				Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица, ТекущаяДатаСеанса());
		КонецЕсли;
	КонецЕсли;
	
	УведомлятьПоПочте = Не ПустаяСтрока(ЭлектроннаяПочта);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполнение реквизитов номенклатуры в процедуре формирования заказа.
//
// Параметры:
//  Номенклатура - СправочникСсылка - ссылка на справочник номенклатура.
//  Результат	 - Структура - возвращаемые реквизиты ссылки справочника:
//    * ИспользованиеХарактеристик - Булево - признак использования характеристик номенклатуры.
//    * ЕдиницаИзмерения - СправочникСсылка - единица измерения номенклатуры.
//    * Упаковка - СправочникСсылка - упаковка номенклатуры.
//
Процедура ЗаполнитьРеквизитыНоменклатурыДляФормированияЗаказа(Знач Номенклатура, Результат) Экспорт
	
	//++ НЕ ГОСИС
	Результат.ИспользованиеХарактеристик = Ложь;
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		Результат.ИспользованиеХарактеристик = Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ИспользованиеХарактеристик") = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать;
	КонецЕсли;
	
	Результат.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	Результат.Вставить("Упаковка", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Инициализация динамического списка сопоставления номенклатуры в форме сопоставления номенклатуры.
// Заполняется переопределяемый текст запроса и свойства списка.
// См. форму обработки ТорговыеПредложения.СопоставлениеНоменклатуры.
//
// Параметры:
//  Список - ЭлементФормы - элемент динамического списка в форме.
//
Процедура ИнициализацияСпискаСопоставленияНоменклатуры(Список) Экспорт
	
	//++ НЕ ГОСИС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СправочникВидыНоменклатуры.Ссылка КАК Ссылка,
	|	СправочникВидыНоменклатуры.Наименование КАК Наименование,
	|	СоответствиеКатегорий.ПредставлениеКатегории КАК Категория,
	|	СоответствиеКатегорий.ИдентификаторКатегории КАК ИдентификаторКатегории
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеКатегорий
	|		ПО СправочникВидыНоменклатуры.Ссылка = СоответствиеКатегорий.ОбъектСопоставления";
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "Справочник.ВидыНоменклатуры";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Список, СвойстваСписка);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Добавление служебных колонок в таблицу товары.
//
// Параметры:
//  Товары - ТаблицаЗначений - исходная таблица с товарами, колонки:
//    Обязательные поля:
//    * Номенклатура   - СправочникСсылка - номенклатурная позиция.
//    * Характеристика - СправочникСсылка - характеристика номенклатуры.
//    * Упаковка       - СправочникСсылка - упаковка номенклатуры.
//    * Количество     - Число - количество.
//    Заполняемые поля:
//    * УпаковкаКод         - Строка - код упаковки.
//    * ЕдиницаИзмеренияКод - Строка - код единицы измерения.
//    * УпаковкаЧислитель   - Число - числитель упаковки номенклатуры.
//    * УпаковкаЗнаменатель - Число - знаменатель упаковки номенклатуры.
//    * Артикул             - Строка - артикул номенклатуры.
//    * Наименование        - Строка - наименование номенклатуры.
//    * Штрихкоды            - Массив - штрихкоды номенклатуры.
//  ПолучатьШтрихКоды - Булево - признак необходимости заполнения штрихкодов номенклатуры.
//                               Для значения Ложь, штрихкоды не заполняются.
//
Процедура ДобавитьСлужебныеКолонкиТовары(Товары, Знач ПолучатьШтрихКоды = Ложь) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(Товары.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка,
	|	ВЫРАЗИТЬ(Товары.Количество КАК ЧИСЛО(15, 3)) КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура.Артикул КАК Артикул,
	|	Товары.Номенклатура.Наименование КАК Наименование,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	СоответствиеНоменклатурыБизнесСеть.ИдентификаторКатегории КАК ИдентификаторКатегории,
	|	Товары.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Товары.Упаковка.Знаменатель = 0
	|			ТОГДА 1
	|		ИНАЧЕ Товары.Упаковка.Знаменатель
	|	КОНЕЦ КАК УпаковкаЗнаменатель,
	|	ВЫБОР
	|		КОГДА Товары.Упаковка.Числитель = 0
	|			ТОГДА 1
	|		ИНАЧЕ Товары.Упаковка.Числитель
	|	КОНЕЦ КАК УпаковкаЧислитель,
	|	Товары.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА Товары.Упаковка.Знаменатель = 0
	|			ТОГДА ПРЕДСТАВЛЕНИЕ(Товары.Упаковка)
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(Товары.Упаковка.ЕдиницаИзмерения)
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА Товары.Упаковка.Знаменатель = 0
	|			ТОГДА Товары.Упаковка.Код
	|		ИНАЧЕ Товары.Упаковка.ЕдиницаИзмерения.Код
	|	КОНЕЦ КАК УпаковкаКод,
	|	Товары.Номенклатура.ЕдиницаИзмерения.Код КАК ЕдиницаИзмеренияКод
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеНоменклатурыБизнесСеть
	|		ПО Товары.Номенклатура.ВидНоменклатуры = СоответствиеНоменклатурыБизнесСеть.ОбъектСопоставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Упаковка КАК Упаковка,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО Товары.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И Товары.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И Товары.Упаковка = ШтрихкодыНоменклатуры.Упаковка";
	
	Запрос.УстановитьПараметр("Товары", Товары);
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	Выборка = РезультатПакета[1].Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТовары = Товары.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, Упаковка",
			Выборка.Номенклатура, Выборка.Характеристика, Выборка.Упаковка));
		Если СтрокаТовары.Количество() Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТовары[0], Выборка,, "Номенклатура, Характеристика, Упаковка");
		КонецЕсли;
	КонецЦикла;
	
	Если ПолучатьШтрихКоды Тогда
		ВыборкаШтрихКоды = РезультатПакета[2].Выбрать();
		Пока ВыборкаШтрихКоды.Следующий() Цикл
			СтрокаТовары = Товары.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, Упаковка",
				ВыборкаШтрихКоды.Номенклатура, ВыборкаШтрихКоды.Характеристика, ВыборкаШтрихКоды.Упаковка));
				
			Если СтрокаТовары <> Неопределено Тогда
				Если СтрокаТовары[0].ШтрихКоды = Неопределено Тогда
					МассивШтрихКодов = Новый Массив;
					МассивШтрихКодов.Добавить(ВыборкаШтрихКоды.ШтрихКод);
					СтрокаТовары[0].ШтрихКоды = МассивШтрихКодов;
				Иначе
					СтрокаТовары[0].ШтрихКоды.Добавить(ВыборкаШтрихКоды.ШтрихКод);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение сопоставленных значений реквизита вида номенклатуры.
//
// Параметры:
//  ОбъектСопоставления - СправочникСсылка - ссылка на справочник виды номенклатуры.
//  РеквизитОбъекта	    - Строка, СправочникСсылка - реквизит объекта.
//  ТипЗначения	        - ОписаниеТипов - тип значений реквизита.
//  ЗначенияРеквизита   - ТаблицаЗначений - список сопоставленных реквизитов, колонки:
//   * Значение                                - СправочникСсылка, Строка, Число, Булево - сопоставленное значение.
//   * ИдентификаторЗначенияРеквизитаКатегории - Строка - идентификатор сопоставленного значения.
//   * ПредставлениеЗначенияРеквизитаКатегории - Строка - представление сопоставленного значения.
//
Процедура ПолучитьСопоставленныеЗначенияРеквизитаВидаНоменклатуры(Знач ОбъектСопоставления, Знач РеквизитОбъекта, Знач ТипЗначения, ЗначенияРеквизита) Экспорт
	
	//++ НЕ ГОСИС
	// Вывод в список всех реквизитов в ИБ.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка КАК Ссылка,
	|	ЗначенияСвойствОбъектов.Владелец КАК Владелец
	|ПОМЕСТИТЬ ЗначенияСвойствОбъектов
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|ГДЕ
	|	ЗначенияСвойствОбъектов.Владелец В
	|			(ВЫБРАТЬ
	|				ВЫБОР
	|					КОГДА ДополнительныеРеквизитыИСведения.ВладелецДополнительныхЗначений = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка)
	|						ТОГДА &Владелец
	|					ИНАЧЕ ДополнительныеРеквизитыИСведения.ВладелецДополнительныхЗначений
	|				КОНЕЦ КАК Поле1
	|			ИЗ
	|				ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|			ГДЕ
	|				ДополнительныеРеквизитыИСведения.Ссылка = &Владелец)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗначенияСвойствОбъектовИерархия.Ссылка,
	|	ЗначенияСвойствОбъектовИерархия.Владелец
	|ИЗ
	|	Справочник.ЗначенияСвойствОбъектовИерархия КАК ЗначенияСвойствОбъектовИерархия
	|ГДЕ
	|	ЗначенияСвойствОбъектовИерархия.Владелец = &Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗначенияСвойствОбъектов.Ссылка КАК Значение,
	|	СоответствиеЗначенийРеквизитовБизнесСеть.ИдентификаторЗначенияРеквизитаКатегории КАК ИдентификаторЗначенияРеквизитаКатегории,
	|	СоответствиеЗначенийРеквизитовБизнесСеть.ПредставлениеЗначенияРеквизитаКатегории КАК ПредставлениеЗначенияРеквизитаКатегории
	|ИЗ
	|	ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|		ПО ЗначенияСвойствОбъектов.Ссылка = СоответствиеЗначенийРеквизитовБизнесСеть.Значение
	|			И (СоответствиеЗначенийРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Значение
	|АВТОУПОРЯДОЧИВАНИЕ";	
	
	Если ТипЗнч(РеквизитОбъекта) = Тип("ПеречислениеСсылка.РеквизитыСопоставленияНоменклатурыБизнесСеть") Тогда
		
		// Модификация запроса для получения значений справочника.
		Тип = ТипЗначения.Типы()[0];
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
		Если ОбъектМетаданных = Неопределено Тогда
			ЗначенияРеквизита = Неопределено;
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ".ЗначенияСвойствОбъектовИерархия", "." + ОбъектМетаданных.Имя);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗначенияСвойствОбъектовИерархия.Владелец = &Владелец", "ИСТИНА");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗначенияСвойствОбъектовИерархия.Владелец", "NULL");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Владелец", РеквизитОбъекта);
	Запрос.УстановитьПараметр("ОбъектСопоставления", ОбъектСопоставления);
	
	ЗначенияРеквизита = Запрос.Выполнить().Выгрузить();
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполнение реквизитов заказов.
//
// Параметры:
//  ТаблицаЗаказов	 - ТаблицаЗначений - таблица с ссылками и реквизитами для заполнения.
//                     см. табличную часть "Заказы" обработки ТорговыеПредложения.ФормированиеЗаказов.
//
Процедура ЗаполнитьРеквизитыЗаказов(ТаблицаЗаказов) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказПоставщику.Ссылка,
	|	ЗаказПоставщику.Контрагент,
	|	ЗаказПоставщику.Организация,
	|	ЗаказПоставщику.СуммаДокумента,
	|	ЗаказПоставщику.Валюта,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщику.Проведен
	|			ТОГДА 1
	|		КОГДА ЗаказПоставщику.ПометкаУдаления
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЗначениеКартинки
	|ИЗ
	|	Документ.ЗаказПоставщику КАК ЗаказПоставщику
	|ГДЕ
	|	ЗаказПоставщику.Ссылка В(&СписокДокументов)
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("СписокДокументов", ТаблицаЗаказов.ВыгрузитьКолонку("Ссылка"));
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаЗаказов.Найти(Выборка.Ссылка, "Ссылка");
		Если СтрокаТаблицы = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение публикуемых реквизитов номенклатуры.
// 
// Параметры:
//  ПубликуемыеРеквизиты - Соответствие - реквизиты справочника Номенклатура, публикуемые для торговых предложений.
//    * Ключ     - Произвольный - значение реквизита сопоставления, см. определяемый тип ДополнительныеРеквизитыБизнесСеть.
//    * Значение - Структура - свойства реквизита:
//      ** Представление    - Строка - наименование для отображения.
//      ** ПутьКДанным      - Строка - текст запроса для получения данных.
//      ** ТипЗначения      - ОписаниеТипов - описание типа данных.
//      ** ПустоеЗначение   - Произвольный - Значение реквизита, при котором, он считается не заполненным.
//
Процедура ПолучитьРеквизитыНоменклатурыДоступныеДляПубликации(ПубликуемыеРеквизиты) Экспорт
	
	//++ НЕ ГОСИС
	// Номенклатура.Вес.
	СвойстваРеквизита = Новый Структура;
	СвойстваРеквизита.Вставить("Представление", НСтр("ru = 'Вес';
													|en = 'Weight'"));
	СвойстваРеквизита.Вставить("ПутьКДанным",
		"ВЫБОР КОГДА Номенклатура.ВесИспользовать И Номенклатура.ВесЗнаменатель <> 0 ТОГДА
		|		Номенклатура.ВесЧислитель/Номенклатура.ВесЗнаменатель ИНАЧЕ 0 КОНЕЦ");
	СвойстваРеквизита.Вставить("ТипЗначения", Новый ОписаниеТипов("Число"));
	СвойстваРеквизита.Вставить("ПустоеЗначение", 0);
	ПубликуемыеРеквизиты.Вставить(Перечисления.РеквизитыСопоставленияНоменклатурыБизнесСеть.Вес, СвойстваРеквизита);
		
	// Номенклатура.Длина.
	СвойстваРеквизита = Новый Структура;
	СвойстваРеквизита.Вставить("Представление", НСтр("ru = 'Длина';
													|en = 'Length'"));
	СвойстваРеквизита.Вставить("ПутьКДанным",
		"ВЫБОР КОГДА Номенклатура.ДлинаИспользовать И Номенклатура.ДлинаЗнаменатель <> 0 ТОГДА
		|		Номенклатура.ДлинаЧислитель/Номенклатура.ДлинаЗнаменатель ИНАЧЕ 0 КОНЕЦ");
	СвойстваРеквизита.Вставить("ТипЗначения", Новый ОписаниеТипов("Число"));
	СвойстваРеквизита.Вставить("ПустоеЗначение", 0);
	ПубликуемыеРеквизиты.Вставить(Перечисления.РеквизитыСопоставленияНоменклатурыБизнесСеть.Длина, СвойстваРеквизита);
	
	// Номенклатура.Объем.
	СвойстваРеквизита = Новый Структура;
	СвойстваРеквизита.Вставить("Представление", НСтр("ru = 'Объем';
													|en = 'Volume'"));
	СвойстваРеквизита.Вставить("ПутьКДанным",
		"ВЫБОР КОГДА Номенклатура.ОбъемИспользовать И Номенклатура.ОбъемЗнаменатель <> 0 ТОГДА
		|		Номенклатура.ОбъемЧислитель/Номенклатура.ОбъемЗнаменатель ИНАЧЕ 0 КОНЕЦ");
	СвойстваРеквизита.Вставить("ТипЗначения", Новый ОписаниеТипов("Число"));
	СвойстваРеквизита.Вставить("ПустоеЗначение", 0);
	ПубликуемыеРеквизиты.Вставить(Перечисления.РеквизитыСопоставленияНоменклатурыБизнесСеть.Объем, СвойстваРеквизита);
	
	// Номенклатура.Производитель.
	СвойстваРеквизита = Новый Структура;
	СвойстваРеквизита.Вставить("Представление", НСтр("ru = 'Производитель';
													|en = 'Manufacturer'"));
	СвойстваРеквизита.Вставить("ПутьКДанным", "Номенклатура.Производитель");
	СвойстваРеквизита.Вставить("ТипЗначения", Новый ОписаниеТипов("СправочникСсылка.Производители"));
	СвойстваРеквизита.Вставить("ПустоеЗначение", Справочники.Производители.ПустаяСсылка());
	ПубликуемыеРеквизиты.Вставить(Перечисления.РеквизитыСопоставленияНоменклатурыБизнесСеть.Производитель, СвойстваРеквизита);
	
	// Номенклатура.Марка.
	СвойстваРеквизита = Новый Структура;
	СвойстваРеквизита.Вставить("Представление", НСтр("ru = 'Марка';
													|en = 'Brand'"));
	СвойстваРеквизита.Вставить("ПутьКДанным", "Номенклатура.Марка");
	СвойстваРеквизита.Вставить("ТипЗначения", Новый ОписаниеТипов("СправочникСсылка.Марки"));
	СвойстваРеквизита.Вставить("ПустоеЗначение", Справочники.Марки.ПустаяСсылка());
	ПубликуемыеРеквизиты.Вставить(Перечисления.РеквизитыСопоставленияНоменклатурыБизнесСеть.Марка, СвойстваРеквизита);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Процедура, вызываемая из обработчика события формы ПриСозданииНаСервере.
//
// Параметры:
//  Форма - УправляемаяФорма - из обработчика события которой происходит вызов процедуры.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	//++ НЕ ГОСИС
	Если Найти(Форма.ИмяФормы, "ПоискПоТоварам") Тогда
		
		ГруппЭлементов = Форма.Элементы.Найти("ГруппаКоманднойПанелиПереопределяемая");
		Если ГруппЭлементов = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ИмяКоманды = ГруппЭлементов.Имя + "ВставкаИзБуфера";
		
		Если Форма.Команды.Найти(ИмяКоманды) <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// Формирование команды вставки из буфера.
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Действие = "Подключаемый_ПереопределяемаяКоманда";
		КомандаФормы.Заголовок = "Вставка из буфера";
		КомандаФормы.ИзменяетСохраняемыеДанные = Истина;
		КомандаФормы.Отображение = ОтображениеКнопки.Картинка;
		КомандаФормы.Картинка = БиблиотекаКартинок.ВставитьИзБуфераОбмена;
		КомандаФормы.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.V, , Истина, Истина);
		КомандаФормы.Подсказка = "Вставка из буфера";
		КнопкаФормы = Форма.Элементы.Добавить("ТоварыВставитьСтроки", Тип("КнопкаФормы"), ГруппЭлементов);
		КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаФормы.ИмяКоманды = ИмяКоманды;
		
		// Формирование команды вставки из буфера.
		ИмяКоманды = ГруппЭлементов.Имя + "ПодобратьТовары";
		КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
		КомандаФормы.Действие = "Подключаемый_ПереопределяемаяКоманда";
		КомандаФормы.Заголовок = "Подобрать товары";
		КомандаФормы.ИзменяетСохраняемыеДанные = Истина;
		КомандаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КомандаФормы.Картинка = БиблиотекаКартинок.ПодобратьТовары;
		КомандаФормы.Подсказка = "Открыть подбор товаров";
		КнопкаФормы = Форма.Элементы.Добавить("ПодобратьТовары", Тип("КнопкаФормы"), ГруппЭлементов);
		КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		КнопкаФормы.ИмяКоманды = ИмяКоманды;
		
		УстановитьДоступностьКомандБуфераОбмена(Форма);
		
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Обработка выбора формы (переопределяемое событие).
// См. описание метода ОбработкаВыбора.
//
// Параметры:
//  Форма				 - УправляемаяФорма - форма источник события.
//  ВыбранноеЗначение	 - Произвольный - выбранное событие.
//  ИсточникВыбора		 - Произвольный - форма, где осуществлен выбор.
//
Процедура ОбработатьВыбор(Форма, Знач ВыбранноеЗначение, Знач ИсточникВыбора) Экспорт
	
	//++ НЕ ГОСИС
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборТоваровПоОтбору.Форма.Форма"
		И Форма.ИмяФормы = "Обработка.ТорговыеПредложения.Форма.ПоискПоТоварам" Тогда
		
		ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);
		
		ЕдиницыИзмеренияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
			ТаблицаТоваров.ВыгрузитьКолонку("Номенклатура"),
			"ЕдиницаИзмерения");
		
		Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
			
			ТекущаяСтрока = Форма.Список.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара);
			
			ТекущаяСтрока.Упаковка = ЕдиницыИзмеренияНоменклатуры.Получить(СтрокаТовара.Номенклатура);
			ТекущаяСтрока.Количество = 1;
			
		КонецЦикла;
		
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Обработка оповещения формы (переопределяемое событие).
// См. описание метода ОбработкаОповещения.
//
// Параметры:
//  ИмяСобытия - Строка - имя оповещения.
//  Параметр   - Произвольный - параметр сообщения.
//  Источник   - УправляемаяФорма - форма источник события.
//
Процедура ОбработатьОповещение(Знач ИмяСобытия, Параметр, Источник) Экспорт
	
	//++ НЕ ГОСИС
	Если ИмяСобытия = "КопированиеСтрокВБуферОбмена"
		И Источник.ИмяФормы = "Обработка.ТорговыеПредложения.Форма.ПоискПоТоварам" Тогда
		
		УстановитьДоступностьКомандБуфераОбмена(Источник);
		
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение данных для сопоставления реквизитов и значений реквизитов информационной базы и сервиса.
//
// Параметры:
//  ОбъектСопоставления      - СправочникСсылка - ссылка на справочник виды номенклатуры.
//  СопоставленныеРеквизиты	 - ТаблицаЗначений - сопоставленные реквизиты в ИБ.
//    * РеквизитОбъекта                 - Произвольный - реквизит.
//    * ИдентификаторРеквизитаКатегории - Строка - идентификатор реквизита категории в сервисе.
//    * Представление                   - Строка - представление реквизита.
//  ДополнительныеРеквизиты  - ТаблицаЗначений - дополнительные реквизиты номенклатуры.
//    * РеквизитОбъекта - Произвольный - реквизит.
//    * ТипЗначения     - ОписаниеТипов - тип реквизита.
//  РасчетСоответствий       - ТаблицаЗначений - таблица количества соответствий значений реквизитов.
//    * РеквизитОбъекта                    - Произвольный - реквизит.
//    * КоличествоЗначенийРеквизита        - Число - количество значений реквизита в ИБ.
//    * КоличествоСопоставленныхРеквизитов - Число - количество сопоставленных значений реквизитов.
//
Процедура ПолучитьДанныеСопоставленияРеквизитов(ОбъектСопоставления, СопоставленныеРеквизиты, ДополнительныеРеквизиты, РасчетСоответствий) Экспорт
	
	//++ НЕ ГОСИС
	// Заполнение списка набора реквизитов.
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДополнительныеРеквизиты.РеквизитОбъекта КАК РеквизитОбъекта,
	|	ДополнительныеРеквизиты.Представление КАК Представление
	|ПОМЕСТИТЬ ДополнительныеРеквизиты
	|ИЗ
	|	&ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеквизитыНоменклатуры.Свойство КАК РеквизитОбъекта,
	|	РеквизитыНоменклатуры.Свойство.ТипЗначения КАК ТипЗначения,
	|	РеквизитыНоменклатуры.Свойство.Наименование КАК Представление
	|ПОМЕСТИТЬ ТаблицаРеквизитов
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК РеквизитыНоменклатуры
	|ГДЕ
	|	РеквизитыНоменклатуры.Ссылка В
	|			(ВЫБРАТЬ
	|				ВидыНоменклатуры.НаборСвойств
	|			ИЗ
	|				Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|			ГДЕ
	|				ВидыНоменклатуры.Ссылка = &ОбъектСопоставления
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				ВидыНоменклатуры.НаборСвойствХарактеристик
	|			ИЗ
	|				Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|			ГДЕ
	|				ВидыНоменклатуры.Ссылка = &ОбъектСопоставления
	|		
	|			ОБЪЕДИНИТЬ ВСЕ
	|		
	|			ВЫБРАТЬ
	|				&ОбщийРеквизитНоменклатура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеквизитов.РеквизитОбъекта КАК РеквизитОбъекта,
	|	СоответствиеРеквизитовБизнесСеть.ИдентификаторРеквизитаКатегории КАК ИдентификаторРеквизитаКатегории,
	|	ТаблицаРеквизитов.Представление КАК Представление
	|ИЗ
	|	ТаблицаРеквизитов КАК ТаблицаРеквизитов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|		ПО ТаблицаРеквизитов.РеквизитОбъекта = СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта
	|			И (СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ДополнительныеРеквизиты.РеквизитОбъекта,
	|	СоответствиеРеквизитовБизнесСеть.ИдентификаторРеквизитаКатегории,
	|	ВЫРАЗИТЬ(ДополнительныеРеквизиты.Представление КАК СТРОКА(50))
	|ИЗ
	|	ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|		ПО (ДополнительныеРеквизиты.РеквизитОбъекта = (ВЫРАЗИТЬ(СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта КАК Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть)))
	|			И (СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДополнительныеРеквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Производитель) КАК РеквизитОбъекта,
	|	Производители.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Справочник_Производители
	|ИЗ
	|	Справочник.Производители КАК Производители
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Марка) КАК РеквизитОбъекта,
	|	Марки.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Справочник_Марки
	|ИЗ
	|	Справочник.Марки КАК Марки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРеквизитов.РеквизитОбъекта КАК РеквизитОбъекта,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияСвойствОбъектов.Ссылка) КАК КоличествоЗначенийРеквизита,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеЗначенийРеквизитовБизнесСеть.Значение) КАК КоличествоСопоставленныхРеквизитов
	|ИЗ
	|	ТаблицаРеквизитов КАК ТаблицаРеквизитов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|			ПО (ЗначенияСвойствОбъектов.Владелец В (СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта, СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта.ВладелецДополнительныхЗначений))
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|			ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = СоответствиеЗначенийРеквизитовБизнесСеть.РеквизитОбъекта
	|		ПО ТаблицаРеквизитов.РеквизитОбъекта = СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРеквизитов.РеквизитОбъекта
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник_Производители.Ссылка),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеЗначенийРеквизитовБизнесСеть.Значение)
	|ИЗ
	|	РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник_Производители КАК Справочник_Производители
	|		ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Справочник_Производители.РеквизитОбъекта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|		ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = СоответствиеЗначенийРеквизитовБизнесСеть.РеквизитОбъекта
	|			И СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = СоответствиеЗначенийРеквизитовБизнесСеть.ОбъектСопоставления
	|ГДЕ
	|	СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления
	|	И СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Производитель)
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник_Марки.Ссылка),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеЗначенийРеквизитовБизнесСеть.Значение)
	|ИЗ
	|	РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник_Марки КАК Справочник_Марки
	|		ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Справочник_Марки.РеквизитОбъекта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|		ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = СоответствиеЗначенийРеквизитовБизнесСеть.РеквизитОбъекта
	|			И СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = СоответствиеЗначенийРеквизитовБизнесСеть.ОбъектСопоставления
	|ГДЕ
	|	СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления
	|	И СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Марка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта";
	
	СоответствиеРеквизитов = Новый Соответствие;
	ПолучитьРеквизитыНоменклатурыДоступныеДляПубликации(СоответствиеРеквизитов);
	Для Каждого Реквизит из СоответствиеРеквизитов Цикл
		НоваяСтрока = ДополнительныеРеквизиты.Добавить();
		НоваяСтрока.РеквизитОбъекта = Реквизит.Ключ;
		НоваяСтрока.ТипЗначения     = Реквизит.Значение.ТипЗначения;
		НоваяСтрока.Представление   = Реквизит.Значение.Представление;
	КонецЦикла;
	
	Если ТипЗнч(ОбъектСопоставления) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		ВидНоменклатуры = ОбъектСопоставления;
	ИначеЕсли ТипЗнч(ОбъектСопоставления) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСопоставления, "ВидНоменклатуры");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидНоменклатуры",           ВидНоменклатуры);
	Запрос.УстановитьПараметр("ОбъектСопоставления",       ОбъектСопоставления);
	Запрос.УстановитьПараметр("ДополнительныеРеквизиты",   ДополнительныеРеквизиты);
	Запрос.УстановитьПараметр("ОбщийРеквизитНоменклатура", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Номенклатура_Общие);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	Выборка = ПакетЗапросов[2].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СопоставленныеРеквизиты.Добавить(), Выборка);
	КонецЦикла;
	
	ВременныйМассив = Новый Массив;
	Выборка = ПакетЗапросов[6].Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(РасчетСоответствий.Добавить(), Выборка);
	КонецЦикла;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение свойств торгового предложения.
//
// Параметры:
//  Источник        - СправочникСсылка - источник торгового соглашения, например СправочникСсылка.Соглашения.
//  ЗначенияСвойств - Структура - возвращаемое значение заполнения.
//
Процедура ПолучитьСвойстваТорговогоПредложения(Знач Источник, ЗначенияСвойств) Экспорт
	
	//++ НЕ ГОСИС
	ЗаполнитьЗначенияСвойств(ЗначенияСвойств,
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник, "Организация, Валюта"));
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполнение адресов абонента по зарегистрированным организациям и складам.
//
// Параметры:
//  АдресаАбонента - ТаблицаЗначения - таблица для заполнения:
//   * Представление - Строка - представление адреса.
//   * ЗначенияПолей - Строка - адрес в формате XML.
//   * Описание - Строка - комментарий адреса.
//   * Ссылка - Ссылка - объект хранения контактной информации.
//   * Вид - СправочникСсылка.ВидыКонтактнойИнформации - вид контактной информации.
//
Процедура ЗаполнитьАдресаАбонента(АдресаАбонента) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОрганизацииКонтактнаяИнформация.Представление КАК Представление,
	|	ОрганизацииКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
	|	ОрганизацииКонтактнаяИнформация.Вид КАК Вид,
	|	ОрганизацииКонтактнаяИнформация.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|		ПО (ОрганизацииКонтактнаяИнформация.Ссылка = ОрганизацииБизнесСеть.Организация)
	|ГДЕ
	|	ОрганизацииКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СкладыКонтактнаяИнформация.Представление,
	|	СкладыКонтактнаяИнформация.ЗначенияПолей,
	|	СкладыКонтактнаяИнформация.Вид,
	|	СкладыКонтактнаяИнформация.Ссылка
	|ИЗ
	|	Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформация
	|ГДЕ
	|	СкладыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ПустаяСтрока(Выборка.Представление) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаАдреса = АдресаАбонента.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаАдреса, Выборка);
		СтрокаАдреса.Описание = СтрШаблон("%1 (%2)", Выборка.Вид, Выборка.Ссылка);
	КонецЦикла;
	
	АдресаАбонента.Сортировать("Представление");	
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение способов доставки для заполнения по умолчанию значений при формировании заказов.
//
// Параметры:
//  СпособыДоставки - СписокЗначений - возможные значения способа доставки.
//                                     Первое значение является значением по умолчанию.
//  ВидДоставки	- Строка - значение вида доставки, варианты "Самовывоз", "Доставка".
//
Процедура ПолучитьСпособыДоставки(СпособыДоставки, ВидДоставки = Неопределено) Экспорт
	
	//++ НЕ ГОСИС
	Если ВРег(ВидДоставки) = "САМОВЫВОЗ" Тогда
		СпособыДоставки.Добавить(Перечисления.СпособыДоставки.НашимиСиламиСАдресаОтправителя);
	ИначеЕсли ВРег(ВидДоставки) = "ДОСТАВКА" Тогда
		СпособыДоставки.Добавить(Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада);
	Иначе
		СпособыДоставки.Добавить(Перечисления.СпособыДоставки.НашимиСиламиСАдресаОтправителя);
		СпособыДоставки.Добавить(Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада);
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение имени ставки НДС.
//
// Параметры:
//  Ссылка		 - СправочникСсылка, ПеречислениеСсылка - ссылка на значение ставки НДС прикладного решения.
//  ИмяСтавкиНДС - Строка - идентификатор ставки НДС, вида: "НДС0", "НДС10", НДС10_110, "НДС18", НДС18_118, "НДС20", НДС20_120, "БезНДС".
//
Процедура ПолучитьИмяСтавкиНДС(Знач Ссылка, ИмяСтавкиНДС) Экспорт
	
	//++ НЕ ГОСИС
	ОбъектМетаданных = Ссылка.Метаданные();
	ИндексЗначения = Перечисления.СтавкиНДС.Индекс(Ссылка);
	Если ИндексЗначения <> -1 Тогда
		ИмяСтавкиНДС = ОбъектМетаданных.ЗначенияПеречисления[ИндексЗначения].Имя;
	Иначе
		ИмяСтавкиНДС = Неопределено;
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение значения ставки НДС по идентификатору.
//
// Параметры:
//  ИмяСтавкиНДС - Строка - идентификатор ставки НДС, например "НДС0", "НДС10", "НДС18", "НДС20", "БезНДС".
//  Ссылка		 - СправочникСсылка, ПеречислениеСсылка - ссылка на значение ставки НДС прикладного решения.
//
Процедура ПолучитьЗначениеСтавкиНДС(Знач ИмяСтавкиНДС, Ссылка) Экспорт
	
	//++ НЕ ГОСИС
	Если Не ПустаяСтрока(ИмяСтавкиНДС)
		И Метаданные.Перечисления.СтавкиНДС.ЗначенияПеречисления.Найти(ИмяСтавкиНДС) <> Неопределено Тогда
		Ссылка = Перечисления.СтавкиНДС[ИмяСтавкиНДС];
	Иначе
		Ссылка = Перечисления.СтавкиНДС.БезНДС;
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Инициализация динамического списка сопоставления номенклатуры в форме сопоставления номенклатуры.
// Заполняется переопределяемый текст запроса и свойства списка.
// См. форму обработки ТорговыеПредложения.СопоставлениеНоменклатуры.
//
// Параметры:
//  Список - ЭлементФормы - элемент динамического списка в форме.
//
Процедура ИнициализацияСпискаСопоставленияПоИерархии(Список) Экспорт
	
	//++ НЕ ГОСИС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.Наименование КАК Наименование,
	|	СоответствиеКатегорий.ПредставлениеКатегории КАК Категория,
	|	СоответствиеКатегорий.ИдентификаторКатегории КАК ИдентификаторКатегории
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеКатегорий
	|		ПО Номенклатура.Ссылка = СоответствиеКатегорий.ОбъектСопоставления
	|ГДЕ
	|	Номенклатура.ЭтоГруппа";
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "Справочник.Номенклатура";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Список, СвойстваСписка);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Инициализация динамического списка сопоставления номенклатуры в форме сопоставления номенклатуры.
// Заполняется переопределяемый текст запроса и свойства списка.
// См. форму обработки ТорговыеПредложения.СопоставлениеНоменклатуры.
//
// Параметры:
//  Список - ЭлементФормы - элемент динамического списка в форме.
//
Процедура ИнициализацияСпискаСопоставленияПоНоменклатуре(Список) Экспорт
	
	//++ НЕ ГОСИС
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	ЕСТЬNULL(СоответствиеПоНоменклатуре.ПредставлениеКатегории, ЕСТЬNULL(СоответствиеПоВидам.ПредставлениеКатегории, СоответствиеПоИерархии.ПредставлениеКатегории)) КАК Категория,
	|	ЕСТЬNULL(СоответствиеПоНоменклатуре.ИдентификаторКатегории, ЕСТЬNULL(СоответствиеПоВидам.ИдентификаторКатегории, СоответствиеПоИерархии.ИдентификаторКатегории)) КАК ИдентификаторКатегории,
	|	СоответствиеПоНоменклатуре.ПредставлениеКатегории КАК КатегорияПоНоменклатуре,
	|	СоответствиеПоНоменклатуре.ИдентификаторКатегории КАК ИдентификаторКатегорииПоНоменклатуре,
	|	СоответствиеПоВидам.ПредставлениеКатегории КАК КатегорияПоВидам,
	|	СоответствиеПоВидам.ИдентификаторКатегории КАК ИдентификаторКатегорииПоВидам,
	|	СоответствиеПоИерархии.ПредставлениеКатегории КАК КатегорияПоИерархии,
	|	СоответствиеПоИерархии.ИдентификаторКатегории КАК ИдентификаторКатегорииПоИерархии,
	|	Номенклатура.Родитель КАК Родитель,
	|	Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеПоНоменклатуре
	|		ПО Номенклатура.Ссылка = СоответствиеПоНоменклатуре.ОбъектСопоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеПоИерархии
	|		ПО Номенклатура.Родитель = СоответствиеПоИерархии.ОбъектСопоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеПоВидам
	|		ПО Номенклатура.ВидНоменклатуры = СоответствиеПоВидам.ОбъектСопоставления";
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица              = "Справочник.Номенклатура";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса                 = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Список, СвойстваСписка);	
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получение доступности функционала запроса цен в прикладном решении.
//
// Параметры:
//  Результат - Булево - признак доступности функционала запроса цен.
//
Процедура ПолучитьДоступностьФункционалаЗапросаЦен(Результат) Экспорт
	
	//++ НЕ ГОСИС
	// Запрос цен не используется.
	Результат = Ложь;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Заполнение штрихкодов для выгрузки.
//
// Параметры:
//  Организации	    - Массив - список организация, для которых производится выгрузка.
//  РежимПоставщика - Булево - выгружать только данные по продажам.
//  РежимПокупателя - Булево - выгружать только данные по покупкам.
//  Штрихкоды       - ТаблицаЗначений - данные по штрихкодам:
//    * Организация - СправочникСсылка - ссылка на организацию.
//    * Штрихкод - Строка - штрихкод товара.
//    * Наименование - Строка - наименование товара.
//    * РежимПоставщика - Булево - признак использования штрихкода в продажах.
//    * РежимПокупателя - Булево - признак использования штрихкода в закупках.
//
Процедура ЗаполнитьШтрихкодыДляВыгрузки(Знач Организации, Знач РежимПоставщика, Знач РежимПокупателя, Штрихкоды) Экспорт
	
	//++ НЕ ГОСИС
	ВзаиморасчетыСервер.ЗаполнитьШтрихкодыДляВыгрузки(Организации, РежимПоставщика, РежимПокупателя, Штрихкоды)
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Добавление служебных колонок в таблицу товары.
//
// Параметры:
//  Ссылка - ДокументСсылка - ссылка на документ с товарами.
//  ПолучатьШтрихКоды - Булево - признак необходимости заполнения штрихкодов номенклатуры.
//  Товары - ТаблицаЗначений - таблица с товарами:
//   * Номенклатура - Ссылка - номенклатура.
//   * Характеристика - Ссылка - характеристика.
//   * Упаковка - Ссылка - упаковка.
//   * Количество - Число - количество товара.
//   * УпаковкаКод - Строка - код упаковки по классификатору ОКЕИ.
//   * ЕдиницаИзмеренияКод - Строка - код единицы измерений по классификатору ОКЕИ.
//   * УпаковкаЧислитель - Число - числитель упаковки по отношению к единице измерения.
//   * УпаковкаЗнаменатель - Знаменатель - знаменатель упаковки по отношению к единице измерения.
//   * Артикул - Строка - артикул товара.
//   * Наименование - Строка - наименование товара.
//   * Штрихкоды - Массив - штрихкоды товара.
//
Процедура ПолучитьТоварыДляПодсказокПоСсылке(Знач Ссылка, Знач ПолучатьШтрихКоды, Товары) Экспорт
	
	//++ НЕ ГОСИС
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуТовары.Характеристика КАК Характеристика,
		|	ЗаказПоставщикуТовары.Упаковка КАК Упаковка,
		|	ЗаказПоставщикуТовары.Количество КАК Количество
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), Выборка);
		КонецЦикла;
		ДобавитьСлужебныеКолонкиТовары(Товары, ПолучатьШтрихКоды);
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.Номенклатура") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика, 
		|	Номенклатура.ЕдиницаИзмерения КАК Упаковка,
		|	1 КАК Количество
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), Выборка);
		КонецЦикла;
		ДобавитьСлужебныеКолонкиТовары(Товары, ПолучатьШтрихКоды);
		
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Выгружать относительные остатки товаров.
// Определяет возможность выгрузки в конфигурации торговых предложений
// с указанием остатков номенклатуры в значениях "мало-много".
//
// Параметры:
//  Результат - Булево - признак наличия функционала.
//
Процедура ВыгружатьОтносительныеОстаткиТоваров(Результат) Экспорт
	
	//++ НЕ ГОСИС
	Результат = Истина;
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Получить параметры поиска номенклатуры сервиса.
//
// Параметры:
//  Номенклатура	 - Ссылка - номенклатура.
//  Характеристика	 - Ссылка - характеристика.
//  Результат		 - Структура - возвращаемые данные:
//   * НаименованиеНоменклатуры - Строка - имя номенклатуры.
//   * НаименованиеХарактеристики - Строка - наименование характеристики.
//   * Штрихкоды - Массив - штрихкоды по ссылке, тип строка.
//
Процедура ПолучитьПараметрыПоискаНоменклатурыСервиса(Знач Номенклатура, Знач Характеристика, Результат) Экспорт
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Наименование КАК НаименованиеНоменклатуры,
	|	Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Наименование КАК НаименованиеХарактеристики
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|ГДЕ
	|	ХарактеристикиНоменклатуры.Ссылка = &Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Штрихкоды.Штрихкод КАК Штрихкод
	|ИЗ
	|	РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО Штрихкоды.Номенклатура = СпрНоменклатура.Ссылка
	|			И Штрихкоды.Упаковка = СпрНоменклатура.ЕдиницаИзмерения
	|ГДЕ
	|	Штрихкоды.Характеристика = &Характеристика
	|	И СпрНоменклатура.Ссылка = &Номенклатура";
	
	Запрос.Параметры.Вставить("Номенклатура", Номенклатура);
	Запрос.Параметры.Вставить("Характеристика", Характеристика);
	
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаНоменклатура = ПакетЗапроса[0].Выбрать();
	Если Не ВыборкаНоменклатура.Следующий() Тогда
		Результат = Неопределено;
		Возврат;
	КонецЕсли;
	Результат.НаименованиеНоменклатуры = ВыборкаНоменклатура.НаименованиеНоменклатуры;
	
	ВыборкаХарактеристика = ПакетЗапроса[1].Выбрать();
	Если ВыборкаХарактеристика.Следующий() Тогда
		Результат.НаименованиеХарактеристики = ВыборкаХарактеристика.НаименованиеХарактеристики;
	КонецЕсли;
	
	ВыборкаШтрихкоды = ПакетЗапроса[2].Выбрать();
	Пока ВыборкаШтрихкоды.Следующий() Цикл
		Результат.Штрихкоды.Добавить(ВыборкаШтрихкоды.Штрихкод);
	КонецЦикла;
	//-- НЕ ГОСИС

КонецПроцедуры

// Получить единицы измерения.
//
// Параметры:
//  Код	   - Строка - код единицы измерения.
//  Ссылка - Ссылка - ссылку на единицу измерения
//  Представление - Строка - представление единицы измерения в базе.
//
Процедура ПолучитьПараметрыЕдиницыИзмерения(Знач Код, Ссылка, Представление) Экспорт
	
	//++ НЕ ГОСИС
	Ссылка = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(Код);
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Представление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование");
	КонецЕсли;
	//-- НЕ ГОСИС

КонецПроцедуры

// Инициализация запроса соответствия значений реквизитов вида номенклатуры.
// 
// В запросе необходимо вернуть таблицу содержащую количество значений
// реквизита в информационной базе и количество сопоставленных реквизитов.
// Параметры запроса: &РеквизитОбъекта, &ОбъектСопоставления.
// Поля запроса:
//  * КоличествоЗначенийРеквизита - Число - количество возможных значений реквизита.
//  * КоличествоСопоставленныхРеквизитов - Число - количество реквизитов, сопоставленных в базе.
//
// Параметры:
//  Запрос - Запрос - запрос инициализации, установки параметров
//
Процедура ИнициализацияЗапросаСоответствияЗначенийРеквизитовВидаНоменклатуры(Запрос) Экспорт
	
	//++ НЕ ГОСИС
	Запрос.Текст = "ВЫБРАТЬ
	|	Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Производитель) КАК РеквизитОбъекта,
	|	Производители.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Справочник_Производители
	|ИЗ
	|	Справочник.Производители КАК Производители
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Марка) КАК РеквизитОбъекта,
	|	Марки.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ Справочник_Марки
	|ИЗ
	|	Справочник.Марки КАК Марки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.РеквизитОбъекта КАК РеквизитОбъекта,
	|	ВложенныйЗапрос.КоличествоЗначенийРеквизита КАК КоличествоЗначенийРеквизита,
	|	ВложенныйЗапрос.КоличествоСопоставленныхРеквизитов КАК КоличествоСопоставленныхРеквизитов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаРеквизитов.Свойство КАК РеквизитОбъекта,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗначенияСвойствОбъектов.Ссылка) КАК КоличествоЗначенийРеквизита,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеЗначенийРеквизитовБизнесСеть.Значение) КАК КоличествоСопоставленныхРеквизитов
	|	ИЗ
	|		Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ТаблицаРеквизитов
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
	|				ПО (ЗначенияСвойствОбъектов.Владелец В (СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта, СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта.ВладелецДополнительныхЗначений))
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|				ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = СоответствиеЗначенийРеквизитовБизнесСеть.РеквизитОбъекта
	|			ПО ТаблицаРеквизитов.Свойство = СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта
	|				И (ТаблицаРеквизитов.Ссылка = &ОбъектСопоставления
	|					ИЛИ ТаблицаРеквизитов.Ссылка.Родитель В (ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_Номенклатура), ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_ХарактеристикиНоменклатуры)))
	|	ГДЕ
	|		(ВЫРАЗИТЬ(ТаблицаРеквизитов.Свойство КАК ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения)) = &РеквизитОбъекта
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаРеквизитов.Свойство
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник_Производители.Ссылка),
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеЗначенийРеквизитовБизнесСеть.Значение)
	|	ИЗ
	|		РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник_Производители КАК Справочник_Производители
	|			ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Справочник_Производители.РеквизитОбъекта
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|			ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = СоответствиеЗначенийРеквизитовБизнесСеть.РеквизитОбъекта
	|				И СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = СоответствиеЗначенийРеквизитовБизнесСеть.ОбъектСопоставления
	|	ГДЕ
	|		СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления
	|		И СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Производитель)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник_Марки.Ссылка),
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СоответствиеЗначенийРеквизитовБизнесСеть.Значение)
	|	ИЗ
	|		РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник_Марки КАК Справочник_Марки
	|			ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Справочник_Марки.РеквизитОбъекта
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеЗначенийРеквизитовБизнесСеть КАК СоответствиеЗначенийРеквизитовБизнесСеть
	|			ПО СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = СоответствиеЗначенийРеквизитовБизнесСеть.РеквизитОбъекта
	|				И СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = СоответствиеЗначенийРеквизитовБизнесСеть.ОбъектСопоставления
	|	ГДЕ
	|		СоответствиеРеквизитовБизнесСеть.ОбъектСопоставления = &ОбъектСопоставления
	|		И СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта = Значение(Перечисление.РеквизитыСопоставленияНоменклатурыБизнесСеть.Марка)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.РеквизитОбъекта = &РеквизитОбъекта";
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Инициализация запроса публикуемых прайс листов.
// В запросе необходимо описать получение временных таблицы с предопределенными полями.
// Возможно добавление дополнительных полей для использования в других временных таблицах.
//
// Параметры запроса:
//  * ПрайсЛист - ссылка на прайс-лист.
//  * Период    - дата получения периодических данных, например дата установки цены.
//
// Временные таблицы и предопределенные поля:
// ПрайсЛисты:
//  * Ссылка                - ОпределяемыйТип.ТорговоеПредложение - ссылка на прайс-лист.
//  * Организация           - Ссылка - ссылка на организацию.
//  * Наименование          - Строка - наименование.
//  * Валюта                - СправочникСсылка.Валюты - валюта.
//  * ДатаНачалаДействия    - Дата - дата начала действия.
//  * ДатаОкончанияДействия - Дата - дата окончания действия.
//
// Контрагенты (для которых доступны торговые предложения):
//  * ПрайсЛист - ОпределяемыйТип.ТорговоеПредложение - ссылка прайс-листа.
//  * Контрагент - ОпределяемыйТип.КонтрагентыБЭД - ссылка на контрагента.
//
// Контакты - временная таблица с полями:
//  * ПрайсЛист             - ОпределяемыйТип.ТорговоеПредложение - ссылка на прайс-лист.
//  * ПредставлениеКонтакта - Строка - представление контакта, например ФИО.
//  * Телефон               - Строка - телефон.
//  * ЭлектроннаяПочта      - Строка - адрес электронной почты.
//  * ВебСайт               - Строка - адрес сайта.
//
// Товары:
//  * ПрайсЛист        - ОпределяемыйТип.ТорговоеПредложение - ссылка на прайс-лист.
//  * Номенклатура     - ОпределяемыйТип.НоменклатураБЭД - номенклатура.
//  * Характеристика   - ОпределяемыйТип.ХарактеристикаБЭД - характеристика.
//  * ЕдиницаИзмерения - СправочникСсылка - базовая единица измерения товара.
//  * НаименованиеНоменклатуры   - Строка - публикуемое представление номенклатуры.
//  * НаименованиеХарактеристики - Строка - публикуемое представление характеристики.
//  * Артикул              - Строка - артикул товара.
//  * Упаковка             - Ссылка - упаковка товара.
//  * НаименованиеУпаковки - Строка - наименование упаковки товара.
//  * ЧислительУпаковки    - Число  - числитель отношения упаковки к единице измерения.
//  * ЗнаменательУпаковки  - Число  - знаменатель отношения упаковки к единице измерения.
//  * СтавкаНДС            - Ссылка - значение ставки НДС.
//  * ЦенаВключаетНДС      - Булево - признак включения НДС в цену
//  * СрокПоставки         - Число  - срок поставки на склад поставщика.
//  * Наличие              - Число  - наличие товара на складах (1 - мало, 2 - достаточно, 3 - много).
//
// Свойства:
//  * Номенклатура   - ОпределяемыйТип.НоменклатураБЭД - номенклатура.
//  * Характеристика - ОпределяемыйТип.ХарактеристикаБЭД - характеристика.
//  * Свойство       - Ссылка - свойство товара.
//  * Значение       - Ссылка - значение свойства товара.
//
// Штрихкоды:
//  * Номенклатура   - ОпределяемыйТип.НоменклатураБЭД - номенклатура.
//  * Характеристика - ОпределяемыйТип.ХарактеристикаБЭД - характеристика.
//  * Упаковка       - СправочникСсылка - единица измерения упаковки товара.
//  * Значение       - Строка - штрихкод.
//
// Параметры:
//  Запрос - Запрос - запрос для получения публикуемых прайс-листов и товаров.
//
Процедура ИнициализацияЗапросаПубликуемыхТоваров(Запрос, ФормироватьСвойства = Ложь) Экспорт
	
	//++ НЕ ГОСИС
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоглашенияСКлиентами.Ссылка КАК Ссылка,
	|	СоглашенияСКлиентами.Организация КАК Организация,
	|	СоглашенияСКлиентами.Наименование КАК Наименование,
	|	СоглашенияСКлиентами.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	СоглашенияСКлиентами.ДатаОкончанияДействия КАК ДатаОкончанияДействия,
	|	СоглашенияСКлиентами.СегментНоменклатуры КАК СегментНоменклатуры,
	|	СоглашенияСКлиентами.СегментПартнеров КАК СегментПартнеров,
	|	СоглашенияСКлиентами.Контрагент КАК Контрагент,
	|	СоглашенияСКлиентами.Партнер КАК Партнер,
	|	СоглашенияСКлиентами.Типовое КАК Типовое,
	|	СоглашенияСКлиентами.ВидЦен КАК ВидЦены,
	|	СоглашенияСКлиентами.Валюта КАК Валюта,
	|	СоглашенияСКлиентами.Валюта.Код КАК ВалютаКод,
	|	СоглашенияСКлиентами.СрокПоставки КАК СрокПоставки,
	|	СоглашенияСКлиентами.Склад КАК Склад,
	|	СостоянияСинхронизации.ПубликоватьЦены КАК ПубликоватьЦены,
	|	СостоянияСинхронизации.ПубликоватьСрокиПоставки КАК ПубликоватьСрокиПоставки,
	|	СостоянияСинхронизации.ПубликоватьОстатки КАК ПубликоватьОстатки,
	|	СоглашенияСКлиентами.Менеджер.ФизическоеЛицо КАК МенеджерФизическоеЛицо,
	|	СоглашенияСКлиентами.Менеджер КАК Менеджер
	|ПОМЕСТИТЬ ПрайсЛисты
	|ИЗ
	|	РегистрСведений.СостоянияСинхронизацииТорговыеПредложения КАК СостоянияСинхронизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|		ПО СостоянияСинхронизации.ТорговоеПредложение = СоглашенияСКлиентами.Ссылка
	|ГДЕ
	|	НЕ СоглашенияСКлиентами.ПометкаУдаления
	|	И (СоглашенияСКлиентами.Ссылка = &ПрайсЛист
	|			ИЛИ &ПрайсЛист = НЕОПРЕДЕЛЕНО
	|			ИЛИ &ПрайсЛист = ЗНАЧЕНИЕ(Справочник.СоглашенияСклиентами.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка КАК ПрайсЛист,
	|	Контрагенты.Ссылка КАК Контрагент
	|ПОМЕСТИТЬ Контрагенты
	|ИЗ
	|	ПрайсЛисты КАК ПрайсЛисты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|			ПО ПартнерыСегмента.Партнер = Контрагенты.Партнер
	|		ПО ПрайсЛисты.СегментПартнеров = ПартнерыСегмента.Сегмент
	|ГДЕ
	|	НЕ Контрагенты.ИНН = """"
	|	И ПрайсЛисты.Типовое = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	Контрагенты.Ссылка,
	|	ПрайсЛисты.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка,
	|	Контрагенты.Ссылка
	|ИЗ
	|	ПрайсЛисты КАК ПрайсЛисты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ПрайсЛисты.Партнер = Контрагенты.Партнер
	|ГДЕ
	|	НЕ Контрагенты.ИНН = """"
	|	И ПрайсЛисты.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И ПрайсЛисты.Типовое = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	Контрагенты.Ссылка,
	|	ПрайсЛисты.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка,
	|	Контрагенты.Ссылка
	|ИЗ
	|	ПрайсЛисты КАК ПрайсЛисты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ПрайсЛисты.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	НЕ Контрагенты.ИНН = """"
	|	И ПрайсЛисты.Типовое = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	Контрагенты.Ссылка,
	|	ПрайсЛисты.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка КАК ПрайсЛист,
	|	ПРЕДСТАВЛЕНИЕ(ПрайсЛисты.МенеджерФизическоеЛицо) КАК ПредставлениеКонтакта,
	|	ЕСТЬNULL(ФизическиеЛицаТелефон.Представление, """") КАК Телефон,
	|	ЕСТЬNULL(ФизическиеЛицаЭлектроннаяПочта.Представление, """") КАК ЭлектроннаяПочта
	|ПОМЕСТИТЬ Контакты
	|ИЗ
	|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК Контакты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО Контакты.Ссылка = ПрайсЛисты.МенеджерФизическоеЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаТелефон
	|		ПО Контакты.Ссылка = ФизическиеЛицаТелефон.Ссылка
	|			И (ФизическиеЛицаТелефон.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаЭлектроннаяПочта
	|		ПО Контакты.Ссылка = ФизическиеЛицаЭлектроннаяПочта.Ссылка
	|			И (ФизическиеЛицаЭлектроннаяПочта.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EMailФизическиеЛица))
	|ГДЕ
	|	НЕ ПрайсЛисты.МенеджерФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ПрайсЛисты.Менеджер),
	|	ЕСТЬNULL(ПользователиТелефон.Представление, """"),
	|	ЕСТЬNULL(ПользователиЭлектроннаяПочта.Представление, """")
	|ИЗ
	|	Справочник.Пользователи.КонтактнаяИнформация КАК Контакты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО Контакты.Ссылка = ПрайсЛисты.Менеджер
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиТелефон
	|		ПО Контакты.Ссылка = ПользователиТелефон.Ссылка
	|			И (ПользователиТелефон.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонПользователя))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи.КонтактнаяИнформация КАК ПользователиЭлектроннаяПочта
	|		ПО Контакты.Ссылка = ПользователиЭлектроннаяПочта.Ссылка
	|			И (ПользователиЭлектроннаяПочта.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailПользователя))
	|ГДЕ
	|	ПрайсЛисты.МенеджерФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад,
	|	Группы.Ссылка КАК Родитель
	|ПОМЕСТИТЬ ВсеСклады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Группы
	|		ПО (НЕ Склады.ЭтоГруппа)
	|			И (Склады.Ссылка = Группы.Ссылка
	|				ИЛИ Склады.Родитель = Группы.Ссылка
	|				ИЛИ Склады.Родитель.Родитель = Группы.Ссылка
	|				ИЛИ Склады.Родитель.Родитель.Родитель = Группы.Ссылка
	|				ИЛИ Склады.Родитель.Родитель.Родитель.Родитель = Группы.Ссылка)
	|ГДЕ
	|	НЕ Группы.Ссылка ЕСТЬ NULL
	|	И Склады.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				ПрайсЛисты.Склад
	|			ИЗ
	|				ПрайсЛисты
	|			ГДЕ
	|				ПрайсЛисты.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Родитель,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка КАК ТорговоеПредложение,
	|	ЕСТЬNULL(ВсеСклады.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
	|	ЕСТЬNULL(ВсеСклады.Родитель, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Родитель
	|ПОМЕСТИТЬ Склады
	|ИЗ
	|	ПрайсЛисты КАК ПрайсЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеСклады КАК ВсеСклады
	|		ПО ПрайсЛисты.Склад = ВсеСклады.Родитель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговоеПредложение,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеСклады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка КАК ТорговоеПредложение,
	|	СвободныеОстаткиОстатки.Номенклатура КАК Номенклатура,
	|	СвободныеОстаткиОстатки.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ОсновнаяВыборка
	|ИЗ
	|	ПрайсЛисты КАК ПрайсЛисты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Склады КАК Склады
	|		ПО ПрайсЛисты.Ссылка = Склады.ТорговоеПредложение
	|			И ПрайсЛисты.Склад = Склады.Родитель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки({(&Период)}, ) КАК СвободныеОстаткиОстатки
	|		ПО (Склады.Склад = СвободныеОстаткиОстатки.Склад
	|				ИЛИ ПрайсЛисты.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|ГДЕ
	|	ПрайсЛисты.СегментНоменклатуры = ЗНАЧЕНИЕ(Справочник.СегментыНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрайсЛисты.Ссылка,
	|	СвободныеОстаткиОстатки.Номенклатура,
	|	СвободныеОстаткиОстатки.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрайсЛисты.Ссылка,
	|	НоменклатураСегмента.Номенклатура,
	|	НоменклатураСегмента.Характеристика
	|ИЗ
	|	РегистрСведений.НоменклатураСегмента КАК НоменклатураСегмента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО НоменклатураСегмента.Сегмент = ПрайсЛисты.СегментНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПрайсЛисты.Ссылка,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяВыборка.ТорговоеПредложение КАК ТорговоеПредложение,
	|	ОсновнаяВыборка.Номенклатура КАК Номенклатура,
	|	ОсновнаяВыборка.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ЦеновыеГруппы.ВидЦен, ПрайсЛисты.ВидЦены) КАК ВидЦены,
	|	ЕСТЬNULL(ЦеновыеГруппы.СрокПоставки, ПрайсЛисты.СрокПоставки) КАК СрокПоставки
	|ПОМЕСТИТЬ ВыборкаСЦеновымиГруппами
	|ИЗ
	|	ОсновнаяВыборка КАК ОсновнаяВыборка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО ОсновнаяВыборка.ТорговоеПредложение = ПрайсЛисты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами.ЦеновыеГруппы КАК ЦеновыеГруппы
	|		ПО ОсновнаяВыборка.ТорговоеПредложение = ЦеновыеГруппы.Ссылка
	|			И ОсновнаяВыборка.Номенклатура.ЦеноваяГруппа = ЦеновыеГруппы.ЦеноваяГруппа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговоеПредложение,
	|	Номенклатура,
	|	Характеристика,
	|	ВидЦены
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОсновнаяВыборка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Уточнение.Ссылка КАК ТорговоеПредложение,
	|	Уточнение.Номенклатура КАК Номенклатура,
	|	Уточнение.Характеристика КАК Характеристика,
	|	Уточнение.Упаковка КАК Упаковка,
	|	Уточнение.ВидЦены КАК ВидЦены,
	|	ПрайсЛисты.ВидЦены КАК ТорговоеПредложениеВидЦены,
	|	Уточнение.СрокПоставки КАК СрокПоставки,
	|	Уточнение.Цена КАК Цена
	|ПОМЕСТИТЬ ВыборкаУточнения
	|ИЗ
	|	Справочник.СоглашенияСКлиентами.Товары КАК Уточнение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО Уточнение.Ссылка = ПрайсЛисты.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговоеПредложение,
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка,
	|	ВидЦены
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ВыборкаСЦеновымиГруппами.ТорговоеПредложение, Уточнение.ТорговоеПредложение) КАК ТорговоеПредложение,
	|	ЕСТЬNULL(ВыборкаСЦеновымиГруппами.Номенклатура, Уточнение.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ВыборкаСЦеновымиГруппами.Характеристика, Уточнение.Характеристика) КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Уточнение.Упаковка ЕСТЬ NULL
	|			ТОГДА ВыборкаСЦеновымиГруппами.Номенклатура.ЕдиницаИзмерения
	|		КОГДА Уточнение.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА Уточнение.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ Уточнение.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Уточнение.ВидЦены ЕСТЬ NULL
	|			ТОГДА ВыборкаСЦеновымиГруппами.ВидЦены
	|		КОГДА Уточнение.ВидЦены = ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|			ТОГДА Уточнение.ТорговоеПредложениеВидЦены
	|		ИНАЧЕ Уточнение.ВидЦены
	|	КОНЕЦ КАК ВидЦены,
	|	ЕСТЬNULL(Уточнение.СрокПоставки, ВыборкаСЦеновымиГруппами.СрокПоставки) КАК СрокПоставки,
	|	ВЫБОР
	|		КОГДА Уточнение.Цена = 0
	|			ТОГДА NULL
	|		ИНАЧЕ Уточнение.Цена
	|	КОНЕЦ КАК Цена
	|ПОМЕСТИТЬ ВыборкаСУточнениями
	|ИЗ
	|	ВыборкаСЦеновымиГруппами КАК ВыборкаСЦеновымиГруппами
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВыборкаУточнения КАК Уточнение
	|		ПО (Уточнение.ТорговоеПредложение = ВыборкаСЦеновымиГруппами.ТорговоеПредложение)
	|			И (Уточнение.Номенклатура = ВыборкаСЦеновымиГруппами.Номенклатура)
	|			И (Уточнение.Характеристика = ВыборкаСЦеновымиГруппами.Характеристика)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговоеПредложение,
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка,
	|	ВидЦены
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыборкаСЦеновымиГруппами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыборкаУточнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыборкаСУточнениями.ТорговоеПредложение КАК ТорговоеПредложение,
	|	ВыборкаСУточнениями.Номенклатура КАК Номенклатура,
	|	ВыборкаСУточнениями.Характеристика КАК Характеристика,
	|	ВыборкаСУточнениями.Упаковка КАК Упаковка,
	|	ВыборкаСУточнениями.ВидЦены КАК ВидЦены,
	|	ВыборкаСУточнениями.СрокПоставки КАК СрокПоставки,
	|	ЕСТЬNULL(ВыборкаСУточнениями.Цена, ЦеныНоменклатуры.Цена) КАК Цена,
	|	ВЫБОР
	|		КОГДА НЕ ЦеныНоменклатуры.Упаковка ЕСТЬ NULL
	|				И ЦеныНоменклатуры.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КоэффициентУпаковки
	|ПОМЕСТИТЬ ВыборкаСЦенами
	|ИЗ
	|	ВыборкаСУточнениями КАК ВыборкаСУточнениями
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних({(&Период)}, ) КАК ЦеныНоменклатуры
	|		ПО ВыборкаСУточнениями.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ВыборкаСУточнениями.Характеристика = ЦеныНоменклатуры.Характеристика
	|			И ВыборкаСУточнениями.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговоеПредложение,
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка,
	|	ВидЦены
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыборкаСУточнениями
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыборкаСЦенами.ТорговоеПредложение КАК ТорговоеПредложение,
	|	ВыборкаСЦенами.Номенклатура КАК Номенклатура,
	|	ВыборкаСЦенами.Характеристика КАК Характеристика,
	|	ВыборкаСЦенами.Упаковка КАК Упаковка,
	|	ВыборкаСЦенами.ВидЦены КАК ВидЦены,
	|	МАКСИМУМ(ВыборкаСЦенами.Цена) КАК Цена,
	|	МАКСИМУМ(ВыборкаСЦенами.СрокПоставки) КАК СрокПоставки,
	|	СУММА(СвободныеОстатки.ВНаличииОстаток) КАК Количество
	|ПОМЕСТИТЬ ВыборкаСОстатками
	|ИЗ
	|	ВыборкаСЦенами КАК ВыборкаСЦенами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО ВыборкаСЦенами.ТорговоеПредложение = ПрайсЛисты.Ссылка
	|			И (ВыборкаСЦенами.Цена <> 0)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Склады КАК Склады
	|		ПО (ПрайсЛисты.Склад = Склады.Родитель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СвободныеОстатки.Остатки(
	|				{(&Период)},
	|				(Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						ВыборкаСЦенами.Номенклатура,
	|						ВыборкаСЦенами.Характеристика
	|					ИЗ
	|						ВыборкаСЦенами КАК ВыборкаСЦенами)) КАК СвободныеОстатки
	|		ПО ВыборкаСЦенами.Номенклатура = СвободныеОстатки.Номенклатура
	|			И ВыборкаСЦенами.Характеристика = СвободныеОстатки.Характеристика
	|			И (СвободныеОстатки.ВНаличииОстаток > 0)
	|			И (СвободныеОстатки.Склад = Склады.Склад
	|				ИЛИ Склады.Склад = ЗНАЧЕНИЕ(Справочник.Склады.пустаяСсылка))
	|ГДЕ
	|	ВыборкаСЦенами.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяССылка)
	|	И НЕ ВыборкаСЦенами.Упаковка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыборкаСЦенами.ТорговоеПредложение,
	|	ВыборкаСЦенами.Номенклатура,
	|	ВыборкаСЦенами.Характеристика,
	|	ВыборкаСЦенами.Упаковка,
	|	ВыборкаСЦенами.ВидЦены,
	|	ПрайсЛисты.ПубликоватьОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТорговоеПредложение,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыборкаСЦенами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.ТорговоеПредложение КАК ТорговоеПредложение,
	|	ТоварныеОграничения.Номенклатура КАК Номенклатура,
	|	ТоварныеОграничения.Характеристика КАК Характеристика,
	|	СУММА(ТоварныеОграничения.МаксимальноеКоличествоЗапаса) КАК МаксимальноеКоличествоЗапаса,
	|	СУММА(ТоварныеОграничения.МинимальноеКоличествоЗапаса) КАК МинимальноеКоличествоЗапаса
	|ПОМЕСТИТЬ ТоварныеОграничения
	|ИЗ
	|	РегистрСведений.ТоварныеОграничения КАК ТоварныеОграничения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыборкаСОстатками КАК ВыборкаСОстатками
	|		ПО (ВыборкаСОстатками.Номенклатура = ТоварныеОграничения.Номенклатура)
	|			И (ВыборкаСОстатками.Характеристика = ТоварныеОграничения.Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Склады КАК Склады
	|		ПО (ТоварныеОграничения.Склад = Склады.Склад
	|				ИЛИ Склады.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварныеОграничения.Номенклатура,
	|	ТоварныеОграничения.Характеристика,
	|	Склады.ТорговоеПредложение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Склады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыборкаСОстатками.ТорговоеПредложение КАК ПрайсЛист,
	|	ПрайсЛисты.Организация КАК Организация,
	|	ВыборкаСОстатками.Номенклатура КАК Номенклатура,
	|	ВыборкаСОстатками.Номенклатура.Наименование КАК НаименованиеНоменклатуры,
	|	ВыборкаСОстатками.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ВыборкаСОстатками.Характеристика.Наименование, """") КАК НаименованиеХарактеристики,
	|	ЕСТЬNULL(СоответствиеНоменклатурыРаботаСНоменклатурой.ПредставлениеНоменклатурыСервиса, """") КАК ПредставлениеНоменклатурыСервиса,
	|	ЕСТЬNULL(СоответствиеНоменклатурыРаботаСНоменклатурой.ИдентификаторНоменклатурыСервиса, """") КАК ИдентификаторНоменклатурыСервиса,
	|	ЕСТЬNULL(СоответствиеНоменклатурыРаботаСНоменклатурой.ПредставлениеХарактеристикиСервиса, """") КАК ПредставлениеХарактеристикиСервиса,
	|	ЕСТЬNULL(СоответствиеНоменклатурыРаботаСНоменклатурой.ИдентификаторХарактеристикиСервиса, """") КАК ИдентификаторХарактеристикиСервиса,
	|	ВЫБОР
	|		КОГДА НЕ ВыборкаСОстатками.Упаковка.Владелец В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения), ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры))
	|			ТОГДА ВыборкаСОстатками.Упаковка.Владелец.ЕдиницаИзмерения
	|		ИНАЧЕ ВыборкаСОстатками.Упаковка
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВыборкаСОстатками.Номенклатура.Артикул КАК Артикул,
	|	ВыборкаСОстатками.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ВыборкаСОстатками.ВидЦены.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ВыборкаСОстатками.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА НЕ ВыборкаСОстатками.Упаковка.Владелец В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения), ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры))
	|			ТОГДА ВыборкаСОстатками.Упаковка.Владелец.ЕдиницаИзмерения.Код
	|		ИНАЧЕ ВыборкаСОстатками.Упаковка.Код
	|	КОНЕЦ КАК УпаковкаКод,
	|	ВыборкаСОстатками.Упаковка.Наименование КАК НаименованиеУпаковки,
	|	ВЫБОР
	|		КОГДА ВыборкаСОстатками.Упаковка.Числитель = 0
	|			ТОГДА 1
	|		ИНАЧЕ ВыборкаСОстатками.Упаковка.Числитель
	|	КОНЕЦ КАК ЧислительУпаковки,
	|	ВЫБОР
	|		КОГДА ВыборкаСОстатками.Упаковка.Знаменатель = 0
	|			ТОГДА 1
	|		ИНАЧЕ ВыборкаСОстатками.Упаковка.Знаменатель
	|	КОНЕЦ КАК ЗнаменательУпаковки,
	|	ВыборкаСОстатками.ВидЦены КАК ВидЦены,
	|	ПрайсЛисты.Валюта КАК Валюта,
	|	ПрайсЛисты.ВалютаКод КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА ПрайсЛисты.ПубликоватьЦены
	|			ТОГДА ЕСТЬNULL(ВыборкаСОстатками.Цена, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ПрайсЛисты.ПубликоватьСрокиПоставки
	|			ТОГДА ЕСТЬNULL(ВыборкаСОстатками.СрокПоставки, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СрокПоставки,
	|	ВЫБОР
	|		КОГДА ПрайсЛисты.ПубликоватьОстатки
	|			ТОГДА ВЫБОР
	|					КОГДА ВыборкаСОстатками.Количество <= 0
	|						ТОГДА НЕОПРЕДЕЛЕНО
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ВыборкаСОстатками.Количество <= ТоварныеОграничения.МинимальноеКоличествоЗапаса
	|								ТОГДА 1
	|							ИНАЧЕ ВЫБОР
	|									КОГДА ВыборкаСОстатками.Количество < ТоварныеОграничения.МаксимальноеКоличествоЗапаса
	|										ТОГДА 2
	|									ИНАЧЕ ВЫБОР
	|											КОГДА ВыборкаСОстатками.Количество >= ТоварныеОграничения.МаксимальноеКоличествоЗапаса
	|												ТОГДА 3
	|											ИНАЧЕ НЕОПРЕДЕЛЕНО
	|										КОНЕЦ
	|								КОНЕЦ
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Наличие,
	|	ВЫБОР
	|		КОГДА ПрайсЛисты.ПубликоватьОстатки
	|			ТОГДА ЕСТЬNULL(ВыборкаСОстатками.Количество, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	ВыборкаСОстатками КАК ВыборкаСОстатками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПрайсЛисты КАК ПрайсЛисты
	|		ПО ВыборкаСОстатками.ТорговоеПредложение = ПрайсЛисты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеПоНоменклатуре
	|		ПО ВыборкаСОстатками.Номенклатура = СоответствиеПоНоменклатуре.ОбъектСопоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеПоВидам
	|		ПО ВыборкаСОстатками.Номенклатура.ВидНоменклатуры = СоответствиеПоВидам.ОбъектСопоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыБизнесСеть КАК СоответствиеПоИерархии
	|		ПО ВыборкаСОстатками.Номенклатура.Родитель = СоответствиеПоИерархии.ОбъектСопоставления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыРаботаСНоменклатурой КАК СоответствиеНоменклатурыРаботаСНоменклатурой
	|		ПО ВыборкаСОстатками.Номенклатура = СоответствиеНоменклатурыРаботаСНоменклатурой.Номенклатура
	|			И ВыборкаСОстатками.Характеристика = СоответствиеНоменклатурыРаботаСНоменклатурой.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварныеОграничения КАК ТоварныеОграничения
	|		ПО ВыборкаСОстатками.Номенклатура = ТоварныеОграничения.Номенклатура
	|			И ВыборкаСОстатками.Характеристика = ТоварныеОграничения.Характеристика
	|			И ВыборкаСОстатками.ТорговоеПредложение = ТоварныеОграничения.ТорговоеПредложение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	НоменклатураДополнительныеРеквизиты.Свойство КАК Свойство,
	|	НоменклатураДополнительныеРеквизиты.Значение КАК Значение
	|ПОМЕСТИТЬ Свойства
	|ИЗ
	|	Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|		ПО (Товары.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|		ПО НоменклатураДополнительныеРеквизиты.Свойство = СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	ХарактеристикиНоменклатурыДополнительныеРеквизиты.Свойство,
	|	ХарактеристикиНоменклатурыДополнительныеРеквизиты.Значение
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры.ДополнительныеРеквизиты КАК ХарактеристикиНоменклатурыДополнительныеРеквизиты
	|		ПО Товары.Характеристика = ХарактеристикиНоменклатурыДополнительныеРеквизиты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеРеквизитовБизнесСеть КАК СоответствиеРеквизитовБизнесСеть
	|		ПО (ХарактеристикиНоменклатурыДополнительныеРеквизиты.Свойство = СоответствиеРеквизитовБизнесСеть.РеквизитОбъекта)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	&Свойство,
	|	&Значение
	|ИЗ
	|	Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.ЕдиницаИзмерения КАК Упаковка,
	|	ЕСТЬNULL(Штрихкоды.Штрихкод, """") КАК Значение
	|ПОМЕСТИТЬ Штрихкоды
	|ИЗ
	|	Товары КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК Штрихкоды
	|		ПО Товары.Номенклатура = Штрихкоды.Номенклатура
	|			И Товары.Характеристика = Штрихкоды.Характеристика
	|			И (Товары.ЕдиницаИзмерения = ВЫБОР
	|				КОГДА НЕ Штрихкоды.Упаковка.Владелец В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения), ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры))
	|					ТОГДА Штрихкоды.Упаковка.Владелец.ЕдиницаИзмерения
	|				КОГДА Штрихкоды.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					ТОГДА ВЫБОР
	|							КОГДА НЕ Штрихкоды.Номенклатура.ЕдиницаИзмерения.Владелец В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения), ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.ИндивидуальныйДляНоменклатуры))
	|								ТОГДА Штрихкоды.Номенклатура.ЕдиницаИзмерения.Владелец.ЕдиницаИзмерения
	|							ИНАЧЕ Товары.ЕдиницаИзмерения
	|						КОНЕЦ
	|				ИНАЧЕ Штрихкоды.Упаковка
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.ЕдиницаИзмерения,
	|	ЕСТЬNULL(Штрихкоды.Штрихкод, """")";
	
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	
	ДобавитьРеквизитыНоменклатурыВЗапрос(Запрос);
	//-- НЕ ГОСИС
	
КонецПроцедуры

#Область УстаревшиеПроцедурыИФункции

// Устарела. Не используется
// Заполнение пакета торговых предложений.
//
// Параметры:
//  ТорговоеПредложение	 - СправочникСсылка - ссылка на элемент справочника определяющего торговые предложения,
//                                            например СправочникСсылка.Соглашения.
//  ПакетПредложений	 - Структура - свойства публикуемого торгового предложения.
//                         см. ТорговыеПредложения.ОписаниеПакетаПредложений.
//
Процедура ЗаполнитьПакетПредложений(Знач ТорговоеПредложение, ПакетПредложений) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПрограммныйИнтерфейсУП

// Добавление команды поиска торговых предложений в формах.
//
// Параметры:
//  КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании.
//
Процедура ДобавитьКомандуПоискаТорговыхПредложений(КомандыСоздатьНаОсновании) Экспорт
	
	//++ НЕ ГОСИС
	Если ПравоДоступа("Просмотр", Метаданные.Обработки.ТорговыеПредложения) Тогда
		КомандаСоздатьНаОсновании =  КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "ТорговыеПредложенияКлиентПереопределяемый.ОткрытьПоискТоваровПоСписку";
		КомандаСоздатьНаОсновании.Идентификатор = "ПоискТоваровПоСписку";
		КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Заказы поставщикам в 1С:Бизнес-сеть';
														|en = 'Purchase orders to 1C:Business Network '");
		КомандаСоздатьНаОсновании.РежимЗаписи = "";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьОбменБизнесСеть";
	КонецЕсли;
	//-- НЕ ГОСИС
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получение признака записи ведомого регистра сведений.
//
// Параметры:
//  Ссылка	 - Произвольный - ссылка на объект информационной базы.
//  Регистры - Массив - список регистров сведений.
// 
// Возвращаемое значение:
//  Булево - признак записи ведомого регистра.
//
Функция ЭтоЗаписьВедомогоРегистраСведений(Ссылка, Регистры)
	
	//++ НЕ ГОСИС
	Для Каждого Элемент Из Регистры Цикл
		Если ТипЗнч(Ссылка) = Тип("РегистрСведенийКлючЗаписи." + Элемент.Имя) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	//-- НЕ ГОСИС
	
КонецФункции

// Получение дополнительных ссылок назначения заказов.
//
// Параметры:
//  УдалямыеСсылки	 - Массив - ссылки документов Заказ поставщику для удаления.
// 
// Возвращаемое значение:
//  СписокЗначений - дополнительные ссылки для удаления, тип СправочникСсылка.Назначения.
//
Функция НазначенияЗаказов(УдалямыеСсылки)
	
	//++ НЕ ГОСИС
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Назначения КАК Таблица
	|ГДЕ
	|	Таблица.Заказ В(&УдалямыеСсылки)";
	Запрос.УстановитьПараметр("УдалямыеСсылки", УдалямыеСсылки);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	//-- НЕ ГОСИС
	
КонецФункции

// Получение соответствия для определения функциональных опция по имени объектов.
// 
// Возвращаемое значение:
//  Соответствие - значение зависимостей, где Ключ - имя формы, Значение - функциональная опция.
//
Функция ЗависимостьФункционалаОтФункциональныхОпций()
	
	//++ НЕ ГОСИС
	Зависимость = Новый Соответствие;
	Зависимость.Вставить("Обработка.ТорговыеПредложения.Форма.СопоставлениеНоменклатуры",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ИспользоватьНесколькоВидовНоменклатуры"));
	Зависимость.Вставить("Обработка.ТорговыеПредложения.Форма.ФормаСписка",
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("ИспользоватьСоглашенияСКлиентами"));
		
	Возврат Зависимость;
	//-- НЕ ГОСИС
	
КонецФункции

// Дополнение в запрос источников по дополнительным реквизитам номенклатуры.
//
// Параметры:
//   Запрос - Запрос - основной запрос сбора данных.
//
Процедура ДобавитьРеквизитыНоменклатурыВЗапрос(Запрос)
	
	//++ НЕ ГОСИС
	БлокРеквизитовШаблон =
	"ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	&Свойство,
	|	&Значение
	|ИЗ
	|	Товары КАК Товары
	|";
	БлокРеквизитовПодстановка = "";
	
	ДоступныеДляПубликацииРеквизиты = Новый Соответствие;
	ПолучитьРеквизитыНоменклатурыДоступныеДляПубликации(ДоступныеДляПубликацииРеквизиты);
	
	МассивПодзапросов = Новый Массив;
	СчетчикРеквизитов = 0;
	Для каждого СвойстваРеквизита Из ДоступныеДляПубликацииРеквизиты Цикл
		
		ИндексСтрокой = XMLСтрока(СчетчикРеквизитов);
		
		СчетчикРеквизитов = СчетчикРеквизитов + 1;
		ТекстПодзапроса = БлокРеквизитовШаблон;
		
		ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&Значение", СвойстваРеквизита.Значение.ПутьКДанным);
		ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&Свойство", "&Свойство" + ИндексСтрокой);
		
		Запрос.УстановитьПараметр("Свойство" + ИндексСтрокой, СвойстваРеквизита.Ключ);
		
		Если СвойстваРеквизита.Значение.Свойство("ПустоеЗначение") Тогда
			ТекстПодзапроса = ТекстПодзапроса + Символы.ПС
				+ "ГДЕ НЕ "+ СвойстваРеквизита.Значение.ПутьКДанным + " = &ПустоеЗначение" + ИндексСтрокой;
			Запрос.УстановитьПараметр("ПустоеЗначение" + ИндексСтрокой,
				СвойстваРеквизита.Значение.ПустоеЗначение);
		КонецЕсли;
		
		МассивПодзапросов.Добавить(ТекстПодзапроса);
		
	КонецЦикла;
	
	БлокРеквизитовПодстановка = СтрСоединить(МассивПодзапросов, Символы.ПС);
	
	Если СтрЧислоВхождений(Запрос.Текст, БлокРеквизитовШаблон) = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка компоновки запроса получения данных номенклатуры.';
								|en = 'An error occurred in query composition to receive product data. '");
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, БлокРеквизитовШаблон, БлокРеквизитовПодстановка);
	//-- НЕ ГОСИС
	
КонецПроцедуры

// Установка доступность команд буфера обмена в форме.
//
// Параметры:
//  Форма	 - УправляемаяФорма - форма установки доступности команд.
//
Процедура УстановитьДоступностьКомандБуфераОбмена(Форма)
	
	//++ НЕ ГОСИС
	// Наименование элементов формы.
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("ТоварыВставитьСтроки");
	МассивЭлементов.Добавить("ТоварыКонтекстноеМенюВставитьСтроки");
	
	ПустойБуферОбмена = ОбщегоНазначения.ПустойБуферОбмена("Строки");
	
	УстановитьСвойствоЭлементовФормы(Форма.Элементы, МассивЭлементов, "Доступность",
		НЕ ПустойБуферОбмена);
	//-- НЕ ГОСИС
	
КонецПроцедуры

#КонецОбласти
