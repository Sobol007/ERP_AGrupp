
////////////////////////////////////////////////////////////////////////////////
// В данный модуль вынесены методы подсистемы CRM, вызываемые из модулей типовых объектов. 
// Выносить можно только те методы, которые не вызывают стандартные методы типового модуля или обработчики форм. 
// Т.е. вызывают только те методы, что тоже вынесены из типового или не содержат таких вызовов.
// Для каждого объекта необходимо задать свою #Область с именем объекта и модуля, как он называется в метаданных.  
////////////////////////////////////////////////////////////////////////////////
#Область СлужебныйПрограммныйИнтерфейс

#Область Документ_CRM_Интерес_МодульОбъекта

#Область ПрограммныйИнтерфейс

// Заполняет условия продаж в коммерческом предложении
//
// Параметры:
//	УсловияПродаж - Структура - Структура для заполнения.
//
Процедура ИнтересЗаполнитьУсловияПродаж(ИнтересОбъект, Знач УсловияПродаж) Экспорт
	
	Если УсловияПродаж = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнтересОбъект.Валюта = УсловияПродаж.Валюта;
	
	Если ЗначениеЗаполнено(УсловияПродаж.Организация) Тогда
		ИнтересОбъект.Организация = УсловияПродаж.Организация;
	КонецЕсли;
	
	ИнтересОбъект.ЦенаВключаетНДС      = УсловияПродаж.ЦенаВключаетНДС;
	
КонецПроцедуры

// Заполняет условия продаж по умолчанию в коммерческом предложении
//
Процедура ИнтересЗаполнитьУсловияПродажПоУмолчанию(ИнтересОбъект) Экспорт
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(ИнтересОбъект.Партнер, ИнтересОбъект.КонтактноеЛицо);
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Если ЗначениеЗаполнено (ИнтересОбъект.Партнер) ИЛИ НЕ ИспользоватьСоглашенияСКлиентами Тогда
		
		УсловияПродажПоУмолчанию = ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(
			ИнтересОбъект.Партнер,
			Новый Структура("УчитыватьГруппыСкладов,ВыбранноеСоглашение,ПустаяСсылкаДокумента", 
			Истина,
			ИнтересОбъект.Соглашение,
			Документы.CRM_Интерес.ПустаяСсылка()));
		
		Если УсловияПродажПоУмолчанию <> Неопределено Тогда
		
			ИнтересОбъект.Соглашение = УсловияПродажПоУмолчанию.Соглашение;
			ИнтересЗаполнитьУсловияПродаж(ИнтересОбъект, УсловияПродажПоУмолчанию);
			
			ПараметрыЗаполнения = CRM_Модуль_МетодыМодулейМенеджеровДокументов.ИнтересПараметрыЗаполненияНалогообложенияНДСПродажи(ИнтересОбъект);
			УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(ИнтересОбъект.НалогообложениеНДС, ПараметрыЗаполнения);
			
			Если ИспользоватьСоглашенияСКлиентами Тогда
				СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ИнтересОбъект);
				
				ПараметрыЗаполнения = Новый Структура;
				ПараметрыЗаполнения.Вставить("Дата", ИнтересОбъект.Дата);
				ПараметрыЗаполнения.Вставить("Валюта", ИнтересОбъект.Валюта);
				ПараметрыЗаполнения.Вставить("Соглашение", ИнтересОбъект.Соглашение);
				ПараметрыЗаполнения.Вставить("НалогообложениеНДС", ИнтересОбъект.НалогообложениеНДС);
				ПараметрыЗаполнения.Вставить("РассчитыватьНаборы", Истина);
				ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена, СтавкаНДС, ВидЦены");
				
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
				СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСуммуРучнойСкидки", "КоличествоУпаковок");
				СтруктураДействий.Вставить("ОчиститьАвтоматическуюСкидку", Неопределено);
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
				
				ПродажиСервер.ЗаполнитьЦены(
					ИнтересОбъект.Товары, // Табличная часть
					, // Массив строк или структура отбора
					ПараметрыЗаполнения,
					СтруктураДействий);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет условия продаж по соглашению в коммерческом предложении клиенту
//
Процедура ИнтересЗаполнитьУсловияПродажПоСоглашению(ИнтересОбъект) Экспорт
	
	УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(ИнтересОбъект.Соглашение, Истина);
	ИнтересЗаполнитьУсловияПродаж(ИнтересОбъект, УсловияПродаж);
	
	ПараметрыЗаполнения = CRM_Модуль_МетодыМодулейМенеджеровДокументов.ИнтересПараметрыЗаполненияНалогообложенияНДСПродажи(ИнтересОбъект);
	УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(ИнтересОбъект.НалогообложениеНДС, ПараметрыЗаполнения);
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ИнтересОбъект);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Дата", ИнтересОбъект.Дата);
	ПараметрыЗаполнения.Вставить("Валюта", ИнтересОбъект.Валюта);
	ПараметрыЗаполнения.Вставить("Соглашение", ИнтересОбъект.Соглашение);
	ПараметрыЗаполнения.Вставить("НалогообложениеНДС", ИнтересОбъект.НалогообложениеНДС);
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена, СтавкаНДС, ВидЦены");
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуРучнойСкидки", "КоличествоУпаковок");
	СтруктураДействий.Вставить("ОчиститьАвтоматическуюСкидку", Неопределено);
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	
	ПродажиСервер.ЗаполнитьЦены(
		ИнтересОбъект.Товары, // Табличная часть
		, // Массив строк или структура отбора
		ПараметрыЗаполнения, // Массив строк или структура отбора
		СтруктураДействий);
			
	
КонецПроцедуры
	
//
//
Процедура ИнтересИнициализироватьДокумент(ИнтересОбъект, ДанныеЗаполнения = Неопределено) Экспорт
	
	ИнтересОбъект.Валюта = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(ИнтересОбъект.Валюта);
	
	ПараметрыЗаполнения = CRM_Модуль_МетодыМодулейМенеджеровДокументов.ИнтересПараметрыЗаполненияНалогообложенияНДСПродажи(ИнтересОбъект);
	УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(ИнтересОбъект.НалогообложениеНДС, ПараметрыЗаполнения);
	
КонецПроцедуры

//
//
Процедура ИнтересИнициализироватьУсловияПродаж(ИнтересОбъект) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") Тогда
		ИнтересЗаполнитьУсловияПродажПоУмолчанию(ИнтересОбъект);
	КонецЕсли;
	
КонецПроцедуры

//
//
Процедура ИнтересЗаполнитьРеквизитыДокумента(ИнтересОбъект) Экспорт
	
	ИнтересИнициализироватьУсловияПродаж(ИнтересОбъект);
	
	ИнтересИнициализироватьДокумент(ИнтересОбъект);
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область ОбновлениеНаНовыйИнтерфейс

Процедура ЗаполнитьРанееСозданныйИнтерес(ИнтересСсылка) Экспорт
	
	ИнтересОбъект = ИнтересСсылка.ПолучитьОбъект();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") Тогда
		Если ЗначениеЗаполнено(ИнтересОбъект.Соглашение) Тогда
			ИнтересЗаполнитьУсловияПродажПоСоглашению(ИнтересОбъект);
		Иначе
			ИнтересЗаполнитьУсловияПродажПоУмолчанию(ИнтересОбъект);
		КонецЕсли;
	Иначе
		 ИнтересЗаполнитьУсловияПродажПоУмолчанию(ИнтересОбъект);
	КонецЕсли;
	
	ИнтересИнициализироватьДокумент(ИнтересОбъект);
	
	ИнтересОбъект.Записать();
			
КонецПроцедуры // ЗаполнитьРанееСозданныйИнтерес(ИнтересСсылка) Экспорт
				
#КонецОбласти// ОбновлениеНаНовыйИнтерфейс				


#КонецОбласти // Документ_CRM_Интерес_МодульОбъекта

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
