
////////////////////////////////////////////////////////////////////////////////
// Классификаторы клиент
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

#Область ПодборПоКлассифкации

// Процедура - Открыть подбор по классификации
//
// Параметры:
//  Форма	 - УправляемаяФорма - Передаваемая форма.
//  Команда	 - КомандаФормы 	- Выполняемая команда.
//
Процедура ОткрытьПодборПоКлассификации(Форма, Команда) Экспорт

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимПодбора", Истина);

	ИмяЭлемента = СтрЗаменить(Команда.Имя, "Подбор", "");
	ТипПредмета = Форма.Элементы[ИмяЭлемента+"Предмет"].ОграничениеТипа.Типы()[0];
	ИмяФормы = ИмяФормыПодбораПоКлассификатору(ТипПредмета);
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормы, Форма.Элементы[ИмяЭлемента], Новый УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область РедактированиеЗначенийКлассификаторовОбъектаВОтдельнойФорме

// Процедура - Выполнить команду
//
// Параметры:
//  Форма - УправляемаяФорма - Передаваемая форма.
//  Команда	 - КомандаФормы  - Выполняемая команда.
//  ДополнительныеПараметры	 - Структура - Дополнительные параметры команды. 
//
Процедура ВыполнитьКоманду(Форма, Команда, ДополнительныеПараметры = Неопределено) Экспорт

	Если Команда.Имя = "CRM_ГиперСсылкаРедактироватьКлассификацию" Тогда
		ПараметрыОповещения = Новый Структура("Форма, Команда, ДополнительныеПараметры", Форма, Команда, ДополнительныеПараметры);
		Если Форма.Модифицированность Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьКомандуПродолжение", ЭтотОбъект, ПараметрыОповещения);
			ПоказатьВопрос(ОписаниеОповещения , НСтр("ru='Объект был изменен, Записать?';en='The object was changed, Record?'"), РежимДиалогаВопрос.ОКОтмена);
		Иначе
			ВыполнитьКомандуПродолжение(Истина, ПараметрыОповещения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяФормыПодбораПоКлассификатору(ТипПредмета) Экспорт
	
	Если ТипПредмета = Тип("СправочникСсылка.Партнеры") Тогда
		ИмяФормы = "Обработка.CRM_УправлениеКлассификацей.Форма.ФормаКлиентскаяБаза";
	ИначеЕсли ТипПредмета = Тип("СправочникСсылка.Номенклатура") Тогда
		ИмяФормы = "Обработка.CRM_УправлениеКлассификацей.Форма.ФормаНоменклатура";
	Иначе
		ИмяФормы = "";
	КонецЕсли;
	
	Возврат ИмяФормы;
	
КонецФункции

Процедура ВыполнитьКомандуПродолжение(Результат, Контекст) Экспорт

	Если Контекст.Команда.Имя = "CRM_ГиперСсылкаРедактироватьКлассификацию" Тогда
		
		Если Результат = Истина ИЛИ Результат = КодВозвратаДиалога.ОК Тогда
			
			Если Результат = КодВозвратаДиалога.ОК Тогда
				Контекст.Форма.Записать();
				Если Контекст.Форма.Модифицированность Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			ПараметрыОповещения = Новый Структура("Форма, Команда", Контекст.Форма, Контекст.Команда);
			ПараметрыФормы = Новый Структура("ВладелецКлассификации", Контекст.Форма.Объект.Ссылка);
			
			ОткрытьФорму("Обработка.CRM_УправлениеКлассификацей.Форма.ЗначенияКлассификаторовОбъекта", ПараметрыФормы, Контекст.Форма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти