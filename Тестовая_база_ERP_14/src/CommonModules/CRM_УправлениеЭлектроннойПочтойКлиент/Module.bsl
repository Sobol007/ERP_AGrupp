
#Область СлужебныеПроцедурыИФункции

// Выполнить получение почты по всем доступным учетным записям.
// Параметры:
//  ЭлементСписок  - ЭлементФормы - Элемент формы, который необходимо обновить, после получения писем.
//
Процедура ЗагрузитьПочтуПользователя(ЭлементСписок = Неопределено, УчетнаяЗаписьОтбор = Неопределено) Экспорт // +CRM УчетнаяЗаписьОтбор = Неопределено -CRM
	
	ПолученоПисем = 0;
	ДоступноУчетныхЗаписей = 0;
	ЕстьОшибки = Ложь;
	
	СписокПолученныхПисем = Новый СписокЗначений;
	
	Состояние(,,НСтр("ru='Идет получение электронной почты ...';en='There are an obtaining of e-mail...'"), БиблиотекаКартинок.CRM_Ожидание);
	CRM_УправлениеЭлектроннойПочтой.ЗагрузитьПочтуПользователя(ПолученоПисем, ДоступноУчетныхЗаписей, ЕстьОшибки, СписокПолученныхПисем, УчетнаяЗаписьОтбор);
	Состояние();
	
	Стр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Получено писем: %1';en='It is receiv letters: %1'"),ПолученоПисем);
	Если ДоступноУчетныхЗаписей > 1 Тогда
		Стр = Стр + " " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='(учетных записей: %1)';en='(accounts: %1)'"),ДоступноУчетныхЗаписей);
	КонецЕсли;
	
	Если ЭлементСписок <> Неопределено Тогда
		ЭлементСписок.Обновить();
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(,,Стр);
	
	Если ЕстьОшибки Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='При получении почты были ошибки. Подробности см. в журнале регистрации';en='When obtaining mail there errors. Particulars of logging see in log'"));
	
	КонецЕсли;
	
КонецПроцедуры

// Функция открывает форму письма из объекта ИнтернетПочтовогоСообщения
// для целей, когда письмо является вложением другого письма.
//
// Параметры:
//  ОбъектФайла - СправочникСсылка.ВложенияЭлектронныхПисем
//  ТекущийПользователь - СправочникСсылка.Пользователи
//
// Возвращаемое значение
//  Булево - успешное завершение операции.
//  
Процедура ОткрытьФайлMSG(ВложениеСсылка, ДанныеВложения) Экспорт

	ФормаПисьма = ПолучитьФорму("Документ.ЭлектронноеПисьмоВходящее.ФормаОбъекта", ДанныеВложения);
	ОткрытьФорму(ФормаПисьма);
	
КонецПроцедуры

// Процедура сохраняет вложение.
//
Процедура СохранитьВложение(Ссылка, УникальныйИдентификаторФормы) Экспорт
	
	Попытка
		ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ПолучитьДанныеФайла(Ссылка, УникальныйИдентификаторФормы);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru='Данный формат файлов не поддерживается!';en='This file format is not supported!'"));
		Возврат;
	КонецПопытки;
	РасширениеВложения = ВРег(СокрЛП(ДанныеФайла.Расширение));
	Если РасширениеВложения = "EML" Тогда
		ПоказатьПредупреждение(,НСтр("ru='Это вложение не может быть сохранено.';en='This enclosure could not be saved.'"));
	Иначе	
		РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла);
	КонецЕсли;
	
КонецПроцедуры

// Функция задает вопрос, если включена соответствующая настройка, и возвращает папку в которую нужно переносить письмо
// в противном случае возвращается пустая ссылка (перенос не выполняется).
//
Процедура ВопросПеренестиПисьмоВПапкуОбработанные(УчетнаяЗапись, ТекущаяПапкаПисьма = Неопределено, ОписаниеОповещенияОЗавершении) Экспорт
	
	ПапкаОтработанные = CRM_УправлениеЭлектроннойПочтой.ПолучитьПапкуЭлектронногоПисьма(УчетнаяЗапись,ПредопределенноеЗначение("Перечисление.CRM_ВидыПапокЭлектроннойПочты.Обработанные"));
	ПустаяСсылка = ПредопределенноеЗначение("Справочник.ПапкиЭлектронныхПисем.ПустаяСсылка");
	бЗадаватьВопрос = (CRM_ОбщегоНазначенияПовтИсп.ПолучитьЗначениеНастройки("НеПредлагатьПереносВОбработанныеДляПисем") = Ложь);
	
	// Если персональная настройка включена, то вопрос не задаём, а сразу возвращаем пустую ссылку (то есть перенос не
	// делаем).
	Если НЕ бЗадаватьВопрос Тогда 
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗавершении, ПустаяСсылка);
		Возврат;
	КонецЕсли;
	
	Если НЕ ПапкаОтработанные = ПустаяСсылка Тогда
		Если (ТекущаяПапкаПисьма = Неопределено) ИЛИ (НЕ ТекущаяПапкаПисьма = ПапкаОтработанные) Тогда
			ДополнительныеПараметры = Новый Структура("ПапкаОтработанные, ПустаяСсылка, ОписаниеОповещенияОЗавершении", ПапкаОтработанные, ПустаяСсылка, ОписаниеОповещенияОЗавершении);
			ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПеренестиПисьмоВПапкуОбработанныеЗавершения", CRM_УправлениеЭлектроннойПочтойКлиент, ДополнительныеПараметры);
			ПоказатьВопрос(ОписаниеОповещения, НСтр("ru='Перенести письмо в папку ""Обработанные""?';en='Transfer the letter to a folder ""Process""?'"),РежимДиалогаВопрос.ДаНет);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗавершении, ПустаяСсылка);
	
КонецПроцедуры

Процедура ВопросПеренестиПисьмоВПапкуОбработанныеЗавершения(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияОЗавершении, ДополнительныеПараметры.ПапкаОтработанные);
	Иначе	
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОписаниеОповещенияОЗавершении, ДополнительныеПараметры.ПустаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
