#Область СлужебныйПрограммныйИнтерфейс

Функция ТекстШаблонаЛенты() Экспорт
	
	Возврат Документы.CRM_Интерес.ПолучитьМакет("Лента_Шаблон").ПолучитьТекст();
	
КонецФункции

Функция ТекстЗаглушкиЛенты() Экспорт
	
	Возврат Документы.CRM_Интерес.ПолучитьМакет("Лента_Заглушка").ПолучитьТекст();
	
КонецФункции

Функция ТекстЗапросаДанныхЛенты(Знач ПараметрыЗапроса) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЖурналДокументов.Объект                    КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ЖурналДокументов.Объект)       КАК ТипОбъекта,
	|	ЖурналДокументов.Дата                      КАК Дата,
	|	ЖурналДокументов.Номер                     КАК Номер,
	|	ЖурналДокументов.Сумма                     КАК Сумма,
	|	ЖурналДокументов.Валюта                    КАК Валюта,
	|	ЖурналДокументов.Автор                     КАК Автор,";
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	""""                                   КАК ТемаИнтереса,
		|	НЕОПРЕДЕЛЕНО                           КАК Интерес,";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Или ПараметрыЗапроса.Режим = "ЛентаКонтакта" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	ЖурналДокументов.CRM_Интерес.Тема      КАК ТемаИнтереса,
		|	ЖурналДокументов.CRM_Интерес           КАК Интерес,";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|	ПРЕДСТАВЛЕНИЕ(ЖурналДокументов.ТипОбъекта) КАК Вид
	|ПОМЕСТИТЬ ВсеОбъектыЖурнала
	|ИЗ
	|	РегистрСведений.CRM_ЖурналДокументов КАК ЖурналДокументов";
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ЖурналДокументов.CRM_Интерес = &Объект
		|	И ЖурналДокументов.ГлавнаяЗапись
		|	И НЕ ЖурналДокументов.ПометкаУдаления";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ЖурналДокументов.Клиент = &Объект
		|	И НЕ ЖурналДокументов.ПометкаУдаления";
	ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКонтакта" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛица
		|		ПО ЖурналДокументов.КонтактноеЛицо = КонтактныеЛица.Ссылка
		|			И ЖурналДокументов.КонтактноеЛицо = &Объект
		|			И ЖурналДокументов.Клиент = КонтактныеЛица.Владелец
		|ГДЕ
		|	НЕ ЖурналДокументов.ПометкаУдаления";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОбъектыЖурнала.Ссылка       КАК Ссылка,
	|	ВсеОбъектыЖурнала.Дата         КАК Дата,
	|	ВсеОбъектыЖурнала.Номер        КАК Номер,
	|	ВсеОбъектыЖурнала.Сумма        КАК Сумма,
	|	ВсеОбъектыЖурнала.Валюта       КАК Валюта,
	|	ВсеОбъектыЖурнала.Автор        КАК Автор,
	|	ВсеОбъектыЖурнала.Вид          КАК Вид,
	|	ВсеОбъектыЖурнала.ТемаИнтереса КАК ТемаИнтереса,
	|	ВсеОбъектыЖурнала.Интерес      КАК Интерес
	|ПОМЕСТИТЬ ПрочиеОбъектыЖурнала
	|ИЗ
	|	ВсеОбъектыЖурнала КАК ВсеОбъектыЖурнала
	|ГДЕ
	|	ВсеОбъектыЖурнала.ТипОбъекта В(&ПрочиеТипыОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОбъектыЖурнала.Ссылка       КАК Ссылка,
	|	ВсеОбъектыЖурнала.Дата         КАК Дата,
	|	ВсеОбъектыЖурнала.Номер        КАК Номер,
	|	ВсеОбъектыЖурнала.Сумма        КАК Сумма,
	|	ВсеОбъектыЖурнала.Валюта       КАК Валюта,
	|	ВсеОбъектыЖурнала.Автор        КАК Автор,
	|	ВсеОбъектыЖурнала.ТемаИнтереса КАК ТемаИнтереса,
	|	ВсеОбъектыЖурнала.Интерес      КАК Интерес
	|ПОМЕСТИТЬ ВыбранныеОбъектыЖурнала
	|ИЗ
	|	ВсеОбъектыЖурнала КАК ВсеОбъектыЖурнала
	|ГДЕ
	|	ВсеОбъектыЖурнала.ТипОбъекта В(&ВключенныеТипыОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА                                                                КАК ЕстьКоманды,
	|	ЛОЖЬ                                                                  КАК Группировка,
	|	ОбъектыЖурнала.Ссылка                                                 КАК Ссылка,
	|	Взаимодействие.ПлановаяДата                                           КАК Дата,
	|	ОбъектыЖурнала.Номер                                                  КАК Номер,
	|	Взаимодействие.Тема                                                   КАК Тема,
	|	""ВзаимодействиеПланов""                                              КАК Тип,
	|	Взаимодействие.Ответственный                                          КАК Отправитель,
	|	СтатусыВзаимодействий.ТипСостояния                                    КАК Состояние,
	|	Взаимодействие.КонтактноеЛицо                                         КАК Получатель,
	|	ЛОЖЬ                                                                  КАК Входящий,
	|	РАЗНОСТЬДАТ(Взаимодействие.ПлановаяДатаЗавершение, &ТекущаяДата, ЧАС) КАК Длительность,
	|	НЕОПРЕДЕЛЕНО                                                          КАК УчетнаяЗапись,
	|	0                                                                     КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                                                          КАК Валюта,
	|	Взаимодействие.Результат                                              КАК Результат,
	|	Взаимодействие.ВидВзаимодействия                                      КАК Вид,
	|	ОбъектыЖурнала.ТемаИнтереса                                           КАК ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес                                                КАК Интерес
	|ПОМЕСТИТЬ ДанныеОбъектовПланов
	|ИЗ
	|	Документ.CRM_Взаимодействие КАК Взаимодействие
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО Взаимодействие.Ссылка = ОбъектыЖурнала.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_СостоянияСобытий КАК СтатусыВзаимодействий
	|		ПО Взаимодействие.СтатусВзаимодействия = СтатусыВзаимодействий.Ссылка
	|			И (СтатусыВзаимодействий.ТипСостояния В (ЗНАЧЕНИЕ(Перечисление.CRM_ТипыСостоянийСобытий.Запланировано), ЗНАЧЕНИЕ(Перечисление.CRM_ТипыСостоянийСобытий.ВРаботе)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_ВидыВзаимодействий КАК ВидыВзаимодействий
	|		ПО Взаимодействие.ВидВзаимодействия = ВидыВзаимодействий.Ссылка
	|			И ВидыВзаимодействий.Направление = ЗНАЧЕНИЕ(Перечисление.CRM_ВходящееИсходящееСобытие.Исходящее)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА                                                                КАК ЕстьКоманды,
	|	ЛОЖЬ                                                                  КАК Группировка,
	|	ОбъектыЖурнала.Ссылка                                                 КАК Ссылка,
	|	ЗадачаИсполнителя.СрокИсполнения                                      КАК Дата,
	|	ОбъектыЖурнала.Номер                                                  КАК Номер,
	|	ЗадачаИсполнителя.Наименование                                        КАК Тема,
	|	""ЗадачаПланов""                                                      КАК Тип,
	|	ЗадачаИсполнителя.Автор                                               КАК Отправитель,
	|	ЗадачаИсполнителя.ПометкаУдаления                                     КАК Состояние,
	|	ЗадачаИсполнителя.Исполнитель                                         КАК Получатель,
	|	НЕОПРЕДЕЛЕНО                                                          КАК Входящий,
	|	РАЗНОСТЬДАТ(ЗадачаИсполнителя.СрокИсполнения, &ТекущаяДата, ЧАС)      КАК Длительность,
	|	НЕОПРЕДЕЛЕНО                                                          КАК УчетнаяЗапись,
	|	0                                                                     КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                                                          КАК Валюта,
	|	ЗадачаИсполнителя.РезультатВыполнения                                 КАК Результат,
	|	ЗадачаИсполнителя.CRM_Личная                                          КАК Вид,
	|	ОбъектыЖурнала.ТемаИнтереса                                           КАК ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес                                                КАК Интерес
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО ЗадачаИсполнителя.Ссылка = ОбъектыЖурнала.Ссылка
	|			И НЕ( ЗадачаИсполнителя.Выполнена ИЛИ ЗадачаИсполнителя.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИСТИНА                                                                КАК ЕстьКоманды,
	|	ЛОЖЬ                                                                  КАК Группировка,
	|	ОбъектыЖурнала.Ссылка                                                 КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СтатусыВзаимодейтсвий.ТипСостояния = ЗНАЧЕНИЕ(Перечисление.CRM_ТипыСостоянийСобытий.Завершено)
	|			ТОГДА Взаимодействие.ДатаЗавершенияВзаимодействия
	|		ИНАЧЕ Взаимодействие.ПлановаяДатаЗавершение
	|	КОНЕЦ                                                                 КАК Дата,
	|	ОбъектыЖурнала.Номер                                                  КАК Номер,
	|	Взаимодействие.Тема                                                   КАК Тема,
	|	""ВзаимодействиеИстории""                                             КАК Тип,
	|	Взаимодействие.Ответственный                                          КАК Отправитель,
	|	СтатусыВзаимодейтсвий.ТипСостояния                                    КАК Состояние,
	|	Взаимодействие.КонтактноеЛицо                                         КАК Получатель,
	|	ВЫБОР
	|		КОГДА ВидыВзаимодействий.Направление = ЗНАЧЕНИЕ(Перечисление.CRM_ВходящееИсходящееСобытие.Входящее)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                 КАК Входящий,
	|	ВЫБОР
	|		КОГДА СтатусыВзаимодейтсвий.ТипСостояния = ЗНАЧЕНИЕ(Перечисление.CRM_ТипыСостоянийСобытий.Завершено)
	|			ТОГДА РАЗНОСТЬДАТ(Взаимодействие.ДатаЗавершенияВзаимодействия, Взаимодействие.ПлановаяДатаЗавершение, ЧАС)
	|		ИНАЧЕ РАЗНОСТЬДАТ(Взаимодействие.ПлановаяДатаЗавершение, &ТекущаяДата, ЧАС)
	|	КОНЕЦ                                                                 КАК Длительность,
	|	НЕОПРЕДЕЛЕНО                                                          КАК УчетнаяЗапись,
	|	0                                                                     КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                                                          КАК Валюта,
	|	Взаимодействие.Результат                                              КАК Результат,
	|	Взаимодействие.ВидВзаимодействия                                      КАК Вид,
	|	ОбъектыЖурнала.ТемаИнтереса                                           КАК ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес                                                КАК Интерес
	|ПОМЕСТИТЬ ДанныеОбъектовИстории
	|ИЗ
	|	Документ.CRM_Взаимодействие КАК Взаимодействие
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО Взаимодействие.Ссылка = ОбъектыЖурнала.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_СостоянияСобытий КАК СтатусыВзаимодейтсвий
	|		ПО Взаимодействие.СтатусВзаимодействия = СтатусыВзаимодейтсвий.Ссылка
	|			И (СтатусыВзаимодейтсвий.ТипСостояния В (ЗНАЧЕНИЕ(Перечисление.CRM_ТипыСостоянийСобытий.Завершено), ЗНАЧЕНИЕ(Перечисление.CRM_ТипыСостоянийСобытий.Отменено)))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.CRM_ВидыВзаимодействий КАК ВидыВзаимодействий
	|		ПО Взаимодействие.ВидВзаимодействия = ВидыВзаимодействий.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА                                                                КАК ЕстьКоманды,
	|	ЛОЖЬ                                                                  КАК Группировка,
	|	ОбъектыЖурнала.Ссылка                                                 КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ЗадачаИсполнителя.ПометкаУдаления
	|			ТОГДА ЗадачаИсполнителя.СрокИсполнения
	|		ИНАЧЕ ЗадачаИсполнителя.ДатаИсполнения
	|	КОНЕЦ                                                                 КАК Дата,
	|	ОбъектыЖурнала.Номер                                                  КАК Номер,
	|	ЗадачаИсполнителя.Наименование                                        КАК Тема,
	|	""ЗадачаИстории""                                                     КАК Тип,
	|	ЗадачаИсполнителя.Автор                                               КАК Отправитель,
	|	ЗадачаИсполнителя.ПометкаУдаления                                     КАК Состояние,
	|	ЗадачаИсполнителя.Исполнитель                                         КАК Получатель,
	|	НЕОПРЕДЕЛЕНО                                                          КАК Входящий,
	|	0                                                                     КАК Длительность,
	|	НЕОПРЕДЕЛЕНО                                                          КАК УчетнаяЗапись,
	|	0                                                                     КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                                                          КАК Валюта,
	|	ЗадачаИсполнителя.РезультатВыполнения                                 КАК Результат,
	|	ЗадачаИсполнителя.CRM_Личная                                          КАК Вид,
	|	ОбъектыЖурнала.ТемаИнтереса                                           КАК ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес                                                КАК Интерес
	|ИЗ
	|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО ЗадачаИсполнителя.Ссылка = ОбъектыЖурнала.Ссылка
	|			И (ЗадачаИсполнителя.Выполнена ИЛИ ЗадачаИсполнителя.ПометкаУдаления)";
	
	Если ПараметрыЗапроса.ВключеныПримечания Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	Заметка.Ссылка,
		|	Заметка.ДатаИзменения,
		|	"""",
		|	Заметка.ТекстСодержания,
		|	""Примечание"",
		|	Заметка.Автор,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	Справочник.Заметки КАК Заметка
		|ГДЕ
		|	Заметка.Предмет = &Объект
		|	И НЕ Заметка.ПометкаУдаления
		|	И Заметка.CRM_ЗаметкаЛенты";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ИСТИНА,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	ТелефонныйЗвонок.Комментарий,
	|	""ТелефонныйЗвонок"",
	|	ВЫБОР
	|		КОГДА ТелефонныйЗвонок.Входящий
	|			ТОГДА ТелефонныйЗвонок.АбонентПредставление
	|		ИНАЧЕ ТелефонныйЗвонок.Автор
	|	КОНЕЦ,
	|	ТелефонныйЗвонок.сфпСостояниеЗвонка,
	|	ВЫБОР
	|		КОГДА ТелефонныйЗвонок.Входящий
	|			ТОГДА ТелефонныйЗвонок.Ответственный
	|		ИНАЧЕ ТелефонныйЗвонок.АбонентПредставление
	|	КОНЕЦ,
	|	ТелефонныйЗвонок.Входящий,
	|	ТелефонныйЗвонок.сфпДлительностьЗвонка,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.ТелефонныйЗвонок КАК ТелефонныйЗвонок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО ТелефонныйЗвонок.Ссылка = ОбъектыЖурнала.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ИСТИНА,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	ЭлектронноеПисьмоВходящее.Тема,
	|	""ЭлектронноеПисьмо"",
	|	ЭлектронноеПисьмоВходящее.ОтправительПредставление,
	|	НЕОПРЕДЕЛЕНО,
	|	ЭлектронноеПисьмоВходящее.СписокПолучателейПисьма,
	|	ИСТИНА,
	|	0,
	|	ЭлектронноеПисьмоВходящее.УчетнаяЗапись,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.ЭлектронноеПисьмоВходящее КАК ЭлектронноеПисьмоВходящее
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО ЭлектронноеПисьмоВходящее.Ссылка = ОбъектыЖурнала.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ИСТИНА,
	|	ОбъектыЖурнала.Ссылка,
	|	ВЫБОР
	|		КОГДА ЭлектронноеПисьмоИсходящее.СтатусПисьма = ЗНАЧЕНИЕ(Перечисление.СтатусыИсходящегоЭлектронногоПисьма.Отправлено)
	|			ТОГДА ЭлектронноеПисьмоИсходящее.ДатаОтправления
	|		ИНАЧЕ ЭлектронноеПисьмоИсходящее.Дата
	|	КОНЕЦ,
	|	ОбъектыЖурнала.Номер,
	|	ЭлектронноеПисьмоИсходящее.Тема,
	|	""ЭлектронноеПисьмо"",
	|	ЭлектронноеПисьмоИсходящее.Автор,
	|	ЭлектронноеПисьмоИсходящее.СтатусПисьма,
	|	ЭлектронноеПисьмоИсходящее.СписокПолучателейПисьма,
	|	ЛОЖЬ,
	|	0,
	|	ЭлектронноеПисьмоИсходящее.УчетнаяЗапись,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.ЭлектронноеПисьмоИсходящее КАК ЭлектронноеПисьмоИсходящее
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО ЭлектронноеПисьмоИсходящее.Ссылка = ОбъектыЖурнала.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ИСТИНА,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	СообщениеМессенджера.ТекстСообщения,
	|	""СообщениеЧата"",
	|	ВЫБОР
	|		КОГДА СообщениеМессенджера.ВидСообщения = ЗНАЧЕНИЕ(Перечисление.CRM_ВидыСообщенияМессенджера.Входящее)
	|			ТОГДА СообщениеМессенджера.КонтактПредставление
	|		ИНАЧЕ СообщениеМессенджера.Ответственный
	|	КОНЕЦ,
	|	СообщениеМессенджера.Прочитано,
	|	ВЫБОР
	|		КОГДА СообщениеМессенджера.ВидСообщения = ЗНАЧЕНИЕ(Перечисление.CRM_ВидыСообщенияМессенджера.Входящее)
	|			ТОГДА ВЫБОР
	|					КОГДА СообщениеМессенджера.Ответственный = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|						ТОГДА СообщениеМессенджера.CRM_РольОтветственного
	|					ИНАЧЕ СообщениеМессенджера.Ответственный
	|				КОНЕЦ
	|		ИНАЧЕ СообщениеМессенджера.КонтактПредставление
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СообщениеМессенджера.ВидСообщения = ЗНАЧЕНИЕ(Перечисление.CRM_ВидыСообщенияМессенджера.Входящее)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	0,
	|	СообщениеМессенджера.УчетнаяЗапись,
	|	0,
	|	СообщениеМессенджера.Группа,
	|	СообщениеМессенджера.ID_Пользователя,
	|	СообщениеМессенджера.КонтактПредставление,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.CRM_СообщениеМессенджера КАК СообщениеМессенджера
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО СообщениеМессенджера.Ссылка = ОбъектыЖурнала.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ИСТИНА,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	СообщениеSMS.Тема,
	|	""СообщениеSMS"",
	|	СообщениеSMS.Автор,
	|	ВЫБОР
	|		КОГДА СообщениеSMS.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияДокументаСообщениеSMS.Доставлено)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	СообщениеSMS.СписокУчастников,
	|	ВЫБОР
	|		КОГДА СообщениеSMS.SMS4B_ТипСообщения = ЗНАЧЕНИЕ(Перечисление.SMS4B_ТипыСообщений.Входящее)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.СообщениеSMS КАК СообщениеSMS
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО СообщениеSMS.Ссылка = ОбъектыЖурнала.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	Заявка.Тема,
	|	""Заявка"",
	|	Заявка.Наименование,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ИСТИНА,
	|	0,
	|	Заявка.ИсточникПолучения,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	Заявка.Организация,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.CRM_Заявка КАК Заявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО Заявка.Ссылка = ОбъектыЖурнала.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	ДокументИнтерес.Тема,
	|	""Интерес"",
	|	ДокументИнтерес.Ответственный,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.Сумма,
	|	ОбъектыЖурнала.Валюта,
	|	"""",
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	Документ.CRM_Интерес КАК ДокументИнтерес
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыбранныеОбъектыЖурнала КАК ОбъектыЖурнала
	|		ПО ДокументИнтерес.Ссылка = ОбъектыЖурнала.Ссылка";
	
	Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|			И ДокументИнтерес.ДокументОснование = &Объект";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА,
	|	ЛОЖЬ,
	|	ОбъектыЖурнала.Ссылка,
	|	ОбъектыЖурнала.Дата,
	|	ОбъектыЖурнала.Номер,
	|	"""",
	|	""ПрочийДокумент"",
	|	ОбъектыЖурнала.Автор,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	ОбъектыЖурнала.Сумма,
	|	ОбъектыЖурнала.Валюта,
	|	"""",
	|	ОбъектыЖурнала.Вид,
	|	ОбъектыЖурнала.ТемаИнтереса,
	|	ОбъектыЖурнала.Интерес
	|ИЗ
	|	ПрочиеОбъектыЖурнала КАК ОбъектыЖурнала";
	
	Если ПараметрыЗапроса.ВключенаИстория Тогда
		
		// Записи о создании и завершении интереса.
		Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЛОЖЬ,
			|	ЛОЖЬ,
			|	ДокументИнтерес.Ссылка,
			|	ДокументИнтерес.Дата,
			|	ДокументИнтерес.Номер,
			|	ДокументИнтерес.Тема,
			|	""СозданиеИнтереса"",
			|	ДокументИнтерес.Автор,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	"""",
			|	НЕОПРЕДЕЛЕНО,
			|	"""",
			|	НЕОПРЕДЕЛЕНО
			|ИЗ
			|	Документ.CRM_Интерес КАК ДокументИнтерес
			|ГДЕ
			|	ДокументИнтерес.Ссылка = &Объект
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЛОЖЬ,
			|	ЛОЖЬ,
			|	ДокументИнтерес.Ссылка,
			|	ДокументИнтерес.ДатаЗакрытия,
			|	ДокументИнтерес.Номер,
			|	ДокументИнтерес.Тема,
			|	""ЗавершениеИнтереса"",
			|	ДокументИнтерес.Ответственный,
			|	ВЫБОР
			|		КОГДА ДокументИнтерес.ПричинаОтказа = ЗНАЧЕНИЕ(Справочник.CRM_ПричиныОтказаПоИнтересам.ПустаяСсылка)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ,
			|	НЕОПРЕДЕЛЕНО,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	0,
			|	НЕОПРЕДЕЛЕНО,
			|	"""",
			|	НЕОПРЕДЕЛЕНО,
			|	"""",
			|	НЕОПРЕДЕЛЕНО
			|ИЗ
			|	Документ.CRM_Интерес КАК ДокументИнтерес
			|ГДЕ
			|	ДокументИнтерес.Ссылка = &Объект
			|	И ДокументИнтерес.Завершен";
		КонецЕсли;
		
		// История изменения реквизитов.
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЗаписиИстории.Партнер,
		|	ЗаписиИстории.Период,
		|	"""",
		|	"""",
		|	""ЗаписьИстории"",
		|	ЗаписиИстории.СтароеЗначение,
		|	ЗаписиИстории.ЭтоНовый,
		|	ЗаписиИстории.Значение,
		|	НЕОПРЕДЕЛЕНО,
		|	0,
		|	ЗаписиИстории.Автор,
		|	0,
		|	НЕОПРЕДЕЛЕНО,
		|	"""",
		|	ЕСТЬNULL(ОписаниеРеквизитов.Представление, ЗаписиИстории.ПутьКДанным),
		|	"""",
		|	НЕОПРЕДЕЛЕНО
		|ИЗ
		|	РегистрСведений.CRM_ИсторияРеквизитовПартнеров КАК ЗаписиИстории
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.CRM_ОписаниеРеквизитовМетаданных КАК ОписаниеРеквизитов
		|		ПО ЗаписиИстории.ПутьКДанным = ОписаниеРеквизитов.Реквизит";
		
		Если ПараметрыЗапроса.Режим = "ЛентаИнтереса" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|			И ОписаниеРеквизитов.Объект = ""Документ.CRM_Интерес""";
		ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКлиента" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|			И ОписаниеРеквизитов.Объект = ""Справочник.Партнеры""";
		ИначеЕсли ПараметрыЗапроса.Режим = "ЛентаКонтакта" Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|			И ОписаниеРеквизитов.Объект = ""Справочник.КонтактныеЛицаПартнеров""";
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ЗаписиИстории.Партнер = &Объект
		|		И НЕ ЗаписиИстории.ЭтоНовый";
		
	КонецЕсли;
	
	// Финальная выборка с количеством объектов.
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ДанныеОбъектов.Ссылка) КАК Количество
	|ИЗ
	|	ДанныеОбъектовИстории КАК ДанныеОбъектов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0                                        КАК Раздел,
	|	ДанныеОбъектов.ЕстьКоманды               КАК ЕстьКоманды,
	|	ДанныеОбъектов.Группировка               КАК Группировка,
	|	ДанныеОбъектов.Тип                       КАК Тип,
	|	ДанныеОбъектов.Дата                      КАК Дата,
	|	ДанныеОбъектов.Номер                     КАК Номер,
	|	НАЧАЛОПЕРИОДА(ДанныеОбъектов.Дата, ДЕНЬ) КАК ДатаБезВремени,
	|	ГОД(ДанныеОбъектов.Дата)                 КАК Год,
	|	ДанныеОбъектов.Входящий                  КАК Входящий,
	|	ДанныеОбъектов.Отправитель               КАК Отправитель,
	|	ДанныеОбъектов.Получатель                КАК Получатель,
	|	ДанныеОбъектов.Состояние                 КАК Состояние,
	|	ДанныеОбъектов.Длительность              КАК Длительность,
	|	ДанныеОбъектов.Тема                      КАК Тема,
	|	ДанныеОбъектов.Ссылка                    КАК Ссылка,
	|	ДанныеОбъектов.УчетнаяЗапись             КАК УчетнаяЗапись,
	|	ДанныеОбъектов.Сумма                     КАК Сумма,
	|	ДанныеОбъектов.Валюта                    КАК Валюта,
	|	ДанныеОбъектов.Результат                 КАК Результат,
	|	ДанныеОбъектов.Вид                       КАК Вид,
	|	ДанныеОбъектов.ТемаИнтереса              КАК ТемаИнтереса,
	|	ДанныеОбъектов.Интерес                   КАК Интерес
	|ИЗ
	|	ДанныеОбъектовПланов КАК ДанныеОбъектов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ" + ?(ПараметрыЗапроса.ОбъектовИстории > 0, " ПЕРВЫЕ " + СтрЗаменить(ПараметрыЗапроса.ОбъектовИстории, Символы.НПП, ""), "") + "
	|	1                                        КАК Раздел,
	|	ДанныеОбъектов.ЕстьКоманды               КАК ЕстьКоманды,
	|	ДанныеОбъектов.Группировка               КАК Группировка,
	|	ДанныеОбъектов.Тип                       КАК Тип,
	|	ДанныеОбъектов.Дата                      КАК Дата,
	|	ДанныеОбъектов.Номер                     КАК Номер,
	|	НАЧАЛОПЕРИОДА(ДанныеОбъектов.Дата, ДЕНЬ) КАК ДатаБезВремени,
	|	ГОД(ДанныеОбъектов.Дата)                 КАК Год,
	|	ДанныеОбъектов.Входящий                  КАК Входящий,
	|	ДанныеОбъектов.Отправитель               КАК Отправитель,
	|	ДанныеОбъектов.Получатель                КАК Получатель,
	|	ДанныеОбъектов.Состояние                 КАК Состояние,
	|	ДанныеОбъектов.Длительность              КАК Длительность,
	|	ДанныеОбъектов.Тема                      КАК Тема,
	|	ДанныеОбъектов.Ссылка                    КАК Ссылка,
	|	ДанныеОбъектов.УчетнаяЗапись             КАК УчетнаяЗапись,
	|	ДанныеОбъектов.Сумма                     КАК Сумма,
	|	ДанныеОбъектов.Валюта                    КАК Валюта,
	|	ДанныеОбъектов.Результат                 КАК Результат,
	|	ДанныеОбъектов.Вид                       КАК Вид,
	|	ДанныеОбъектов.ТемаИнтереса              КАК ТемаИнтереса,
	|	ДанныеОбъектов.Интерес                   КАК Интерес
	|ИЗ
	|	ДанныеОбъектовИстории КАК ДанныеОбъектов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Раздел,
	|	Дата УБЫВ,
	|	Тип";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ОтборыОбъектовЛенты(Знач ЗначенияНастроек) Экспорт
	
	ОтборЛенты = Новый СписокЗначений;
	
	ОтборЛенты.Добавить("ВсеСобытия",		НСтр("ru = 'Все события'"),		Истина);
	ОтборЛенты.Добавить("Взаимодействия",	НСтр("ru = 'Взаимодействия'"),	Истина);
	
	ОтборЛенты.Добавить("Задачи",		НСтр("ru = 'Задачи'"),			Истина);
	
	Если ЗначенияНастроек.ИспользоватьСофтФон Тогда
		ОтборЛенты.Добавить("Звонки",		НСтр("ru = 'Звонки'"),			Истина);
	КонецЕсли;
	
	ОтборЛенты.Добавить("Письма",			НСтр("ru = 'Письма'"),			Истина);
	
	Если ЗначенияНастроек.ИспользоватьЧаты Тогда
		ОтборЛенты.Добавить("Чаты",			НСтр("ru = 'Чаты'"),			Истина);
	КонецЕсли;
	
	ОтборЛенты.Добавить("SMS",				НСтр("ru = 'SMS'"),				Истина);
	
	ОтборЛенты.Добавить("История",			НСтр("ru = 'История'"),			Истина);
	ОтборЛенты.Добавить("Примечания",		НСтр("ru = 'Примечания'"),		Истина);
	ОтборЛенты.Добавить("Документы",		НСтр("ru = 'Документы'"),		Истина);
	
	Возврат ОтборЛенты;
	
КонецФункции

Функция ОсновныеТипыОбъектовЛенты() Экспорт
	
	ТипыОбъектов = Новый Массив;
	
	ТипыОбъектов.Добавить(Тип("СправочникСсылка.Заметки"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Взаимодействие"));
	ТипыОбъектов.Добавить(Тип("ЗадачаСсылка.ЗадачаИсполнителя"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.ТелефонныйЗвонок"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоВходящее"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.ЭлектронноеПисьмоИсходящее"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_СообщениеМессенджера"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.СообщениеSMS"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Заявка"));
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.CRM_Интерес"));
	
	Возврат ТипыОбъектов;
	
КонецФункции

Функция ПрочиеТипыОбъектовЛенты() Экспорт
	
	ПрочиеОбъекты = Новый Массив;
	ОсновныеОбъекты = ОсновныеТипыОбъектовЛенты();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОбъектыЖурналаДокументов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.CRM_ОбъектыЖурналаДокументов КАК ОбъектыЖурналаДокументов");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ПрочиеОбъекты;
	КонецЕсли;
	
	ТипБизнесПроцесс = Тип("БизнесПроцессСсылка.CRM_БизнесПроцесс");
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТипыОбъектов = Выборка.ТипЗначения.Типы();
		Для Каждого ТипОбъекта Из ТипыОбъектов Цикл
			Если ТипОбъекта = ТипБизнесПроцесс Тогда
				Продолжить; // Исключить из ленты бизнес-процессы.
			КонецЕсли;
			Если ОсновныеОбъекты.Найти(ТипОбъекта) = Неопределено Тогда
				ИмяОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта).ПолноеИмя();
				Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ИмяОбъекта) Тогда
					ПрочиеОбъекты.Добавить(ТипОбъекта);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ПрочиеОбъекты;
	
КонецФункции

#КонецОбласти