
Процедура АГ_ФормированиеФинансовыхДокументов() Экспорт
	ОбработкаФормированияДокументов = Обработки.АГ_ФормированиеФинансовыхДокументов.СоздатьДокументы();
КонецПроцедуры

Процедура ДействияПриПроведенииДокумента(Источник, Отказ, РежимПроведения) Экспорт
	
	//<--Давиденко А.В.: УЗК0012 03.06.2019 13:24:17
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.ВыкупПринятыхНаХранениеТоваров") 
		И Источник.АГ_СтатусСогласованияСПоставщиком = Перечисления.АГ_СтатусЗаявкиНаВыкупСХранения.Согласован Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыкупПринятыхНаХранениеТоваровТовары.АГ_ЗаявкаНаВыкуп КАК АГ_ЗаявкаНаВыкуп,
		|	ВыкупПринятыхНаХранениеТоваровТовары.АГ_ЗаказКлиента КАК АГ_ЗаказКлиента,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Склад КАК Склад,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ втТЧДок
		|ИЗ
		|	Документ.ВыкупПринятыхНаХранениеТоваров.Товары КАК ВыкупПринятыхНаХранениеТоваровТовары
		|ГДЕ
		|	ВыкупПринятыхНаХранениеТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыкупПринятыхНаХранениеТоваровТовары.АГ_ЗаявкаНаВыкуп,
		|	ВыкупПринятыхНаХранениеТоваровТовары.АГ_ЗаказКлиента,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Склад,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Характеристика,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АГ_ТоварыКВыкупуОстатки.ЗаявкаНаВыкупТоваровСХранения КАК ЗаявкаНаВыкупТоваровСХранения,
		|	АГ_ТоварыКВыкупуОстатки.ЗаказКлиента КАК ЗаказКлиента,
		|	АГ_ТоварыКВыкупуОстатки.Склад КАК Склад,
		|	АГ_ТоварыКВыкупуОстатки.Номенклатура КАК Номенклатура,
		|	АГ_ТоварыКВыкупуОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	АГ_ТоварыКВыкупуОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.АГ_ТоварыКВыкупу.Остатки(
		|			,
		|			Организация = &Организация
		|				И (ЗаявкаНаВыкупТоваровСХранения, ЗаказКлиента, Склад, Номенклатура, ХарактеристикаНоменклатуры) В
		|					(ВЫБРАТЬ
		|						втТЧДок.АГ_ЗаявкаНаВыкуп КАК АГ_ЗаявкаНаВыкуп,
		|						втТЧДок.АГ_ЗаказКлиента КАК АГ_ЗаказКлиента,
		|						втТЧДок.Склад КАК Склад,
		|						втТЧДок.Номенклатура КАК Номенклатура,
		|						втТЧДок.Характеристика КАК Характеристика
		|					ИЗ
		|						втТЧДок КАК втТЧДок)) КАК АГ_ТоварыКВыкупуОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	втОстатки.ЗаявкаНаВыкупТоваровСХранения КАК ЗаявкаНаВыкупТоваровСХранения,
		|	втОстатки.ЗаказКлиента КАК ЗаказКлиента,
		|	втОстатки.Склад КАК Склад,
		|	втОстатки.Номенклатура КАК Номенклатура,
		|	втОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	втОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	втОстатки КАК втОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТЧДок КАК втТЧДок
		|		ПО втОстатки.ЗаявкаНаВыкупТоваровСХранения = втТЧДок.АГ_ЗаявкаНаВыкуп
		|			И втОстатки.ЗаказКлиента = втТЧДок.АГ_ЗаказКлиента
		|			И втОстатки.Склад = втТЧДок.Склад
		|			И втОстатки.Номенклатура = втТЧДок.Номенклатура
		|			И втОстатки.ХарактеристикаНоменклатуры = втТЧДок.Характеристика";
		
		Запрос.УстановитьПараметр("Организация", Источник.Организация);
		Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
		Запрос.УстановитьПараметр("Период", Источник.Дата);
		
		Источник.Движения.АГ_ТоварыКВыкупу.Записывать = Истина;
		Таблица = Запрос.Выполнить().Выгрузить();
		Источник.Движения.АГ_ТоварыКВыкупу.Загрузить(Таблица);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыкупПринятыхНаХранениеТоваровТовары.Серия КАК Серия,
		|	ВыкупПринятыхНаХранениеТоваровТовары.АГ_ЗаказКлиента КАК Заказ,
		|	ВыкупПринятыхНаХранениеТоваровТовары.АГ_Резерв КАК Резерв,
		|	ВыкупПринятыхНаХранениеТоваровТовары.КоличествоУпаковок КАК Количество,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Ссылка.Дата КАК Период,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Номенклатура КАК Номенклатура,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Характеристика КАК Характеристика,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Ссылка.Организация КАК Организация,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Ссылка.Контрагент КАК Поставщик,
		|	ВыкупПринятыхНаХранениеТоваровТовары.Склад КАК Склад
		|ИЗ
		|	Документ.ВыкупПринятыхНаХранениеТоваров.Товары КАК ВыкупПринятыхНаХранениеТоваровТовары
		|ГДЕ
		|	ВыкупПринятыхНаХранениеТоваровТовары.Ссылка = &Ссылка
		|	И НЕ ВыкупПринятыхНаХранениеТоваровТовары.АГ_ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
		
		Источник.Движения.АГ_РезервПоЗаказамИСериям.Записывать = Истина;
		Таблица = Запрос.Выполнить().Выгрузить();
		Источник.Движения.АГ_РезервПоЗаказамИСериям.Загрузить(Таблица);
		
		Источник.Движения.Записать();
		
	КонецЕсли;
	//-->Давиденко А.В.: УЗК0012
	
	
	
КонецПроцедуры

//<--Давиденко А.В.: УЗК0012 03.06.2019 13:24:17
Процедура АГ_ПроверитьЗакрытиеНеделимыхСерий(ДокументВыкупаОбъект, Отказ) Экспорт
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тзТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	тзТовары.Серия КАК Серия,
		|	тзТовары.КвоПоСерии КАК КвоПоСерии
		|ПОМЕСТИТЬ втСерииДок0
		|ИЗ
		|	&тзТовары КАК тзТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСерииДок0.Серия КАК Серия,
		|	СУММА(втСерииДок0.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	МАКСИМУМ(втСерииДок0.КвоПоСерии) КАК КвоПоСерии
		|ПОМЕСТИТЬ втГруппировка
		|ИЗ
		|	втСерииДок0 КАК втСерииДок0
		|
		|СГРУППИРОВАТЬ ПО
		|	втСерииДок0.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втГруппировка.Серия КАК Серия,
		|	втГруппировка.КоличествоУпаковок КАК КоличествоУпаковок,
		|	втГруппировка.КвоПоСерии КАК КвоПоСерии
		|ИЗ
		|	втГруппировка КАК втГруппировка
		|ГДЕ
		|	НЕ втГруппировка.КоличествоУпаковок = втГруппировка.КвоПоСерии";
	
	//тзТовары =  Новый ТаблицаЗначений();
	//тзТовары = ДокументВыкупаОбъект.Товары.СкопироватьКолонки();
	тзТовары = ДокументВыкупаОбъект.Товары.Выгрузить();
	тзТовары.Колонки.Добавить("КвоПоСерии", Новый ОписаниеТипов("Число"));
	тзТовары.Очистить();
	Для Каждого СтрТЗ Из ДокументВыкупаОбъект.Товары Цикл
		Если СтрТЗ.Серия.АГ_НеделимаяСерия Тогда
			НовСтр = тзТовары.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, СтрТЗ);
			НовСтр.КвоПоСерии = СтрТЗ.Серия.АГ_Тонны;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("тзТовары", тзТовары); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Отказ = Истина;
		
		Сообщить("Не выполнено закрытие неделимой серии " + Выборка.Серия + ", в документе " 
		+ Выборка.КоличествоУпаковок + ", количество по серии " + Выборка.КвоПоСерии );
		
	КонецЦикла;
	
	
КонецПроцедуры	
//-->Давиденко А.В.: УЗК0012
