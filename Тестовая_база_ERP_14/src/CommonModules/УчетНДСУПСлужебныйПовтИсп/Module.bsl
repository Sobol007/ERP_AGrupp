#Область СлужебныеПроцедурыИФункции

#Область ПолучениеНастроек

// См. УчетНДСУП.ПараметрыУчетаОрганизации()
//
Функция ПолучитьПараметрыУчетаОрганизации(ПараметрыУчета, Организация, Дата, Склад) Экспорт
	
	ПараметрыУчета.ОсновноеНалогообложениеНДСПродажи      = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	ПараметрыУчета.ОсновнойВидДеятельностиНДСЗакупки      = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	ПараметрыУчета.НалогообложениеНДСРозничнойПродажи     = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	
	ДатаСреза = ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса());
	РозничнаяТорговляОблагаетсяЕНВД = Справочники.Организации.РозничнаяТорговляОблагаетсяЕНВД(Организация, Склад, ДатаСреза);
	ПараметрыУчетнойПолитики = РегистрыСведений.УчетнаяПолитикаОрганизаций.ПараметрыУчетнойПолитики(Организация, ДатаСреза);
	Если ПараметрыУчетнойПолитики.УчетнаяПолитикаСуществует Тогда
		
		Если ПараметрыУчетнойПолитики.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
			ПараметрыУчета.ОсновноеНалогообложениеНДСПродажи = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
		ИначеЕсли ПараметрыУчетнойПолитики.ПрименяетсяОсвобождениеОтУплатыНДС Тогда
			ПараметрыУчета.ОсновноеНалогообложениеНДСПродажи = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
		Иначе
			ПараметрыУчета.ОсновноеНалогообложениеНДСПродажи = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		КонецЕсли;
		
		Если ПараметрыУчетнойПолитики.ПрименяетсяУчетНДСПоФактическомуИспользованию Тогда
			ПараметрыУчета.ОсновнойВидДеятельностиНДСЗакупки = Перечисления.ТипыНалогообложенияНДС.ПоФактическомуИспользованию;
		Иначе
			ПараметрыУчета.ОсновнойВидДеятельностиНДСЗакупки = ПараметрыУчета.ОсновноеНалогообложениеНДСПродажи;
		КонецЕсли;
		
		Если РозничнаяТорговляОблагаетсяЕНВД Тогда
			ПараметрыУчета.НалогообложениеНДСРозничнойПродажи = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД;
		Иначе
			ПараметрыУчета.НалогообложениеНДСРозничнойПродажи = ПараметрыУчета.ОсновноеНалогообложениеНДСПродажи;
		КонецЕсли;
		
		ПараметрыУчета.РаздельныйУчетНДС = ПараметрыУчетнойПолитики.РаздельныйУчетТоваровПоНалогообложениюНДС;
		ПараметрыУчета.ПрименяетсяУСН = ПараметрыУчетнойПолитики.ПрименяетсяУСН;
		ПараметрыУчета.ПрименяетсяЕНВД = ПараметрыУчетнойПолитики.ПрименяетсяЕНВД;
		ПараметрыУчета.ВариантУчетаНДСПриИзмененииВидаДеятельностиНаНеоблагаемую = ПараметрыУчетнойПолитики.ВариантУчетаНДСПриИзмененииВидаДеятельности;
		ПараметрыУчета.СтатьяРасходов_НеОблагаетсяНДС = ПараметрыУчетнойПолитики.СтатьяРасходовНеНДС;
		ПараметрыУчета.АналитикаРасходов_НеОблагаетсяНДС = ПараметрыУчетнойПолитики.АналитикаРасходовНеНДС;
		ПараметрыУчета.СтатьяРасходов_ОблагаетсяЕНВД = ПараметрыУчетнойПолитики.СтатьяРасходовЕНВД;
		ПараметрыУчета.АналитикаРасходов_ОблагаетсяЕНВД = ПараметрыУчетнойПолитики.АналитикаРасходовЕНВД;
		//++ НЕ УТ
		ПараметрыУчета.ОсобыйПорядокУчетаНДСПриПроизводствеДЦ = ПараметрыУчетнойПолитики.УчетНДСДлительногоЦиклаПроизводства;
		//-- НЕ УТ
	КонецЕсли;
	
	Возврат ПараметрыУчета;
	
КонецФункции

#КонецОбласти

#КонецОбласти
