////////////////////////////////////////////////////////////////////////////////
// Подсистема "Расчет имущественных налогов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область НалогНаИмущество

// Возвращает необходимость уплаты авансов по налогу на имущество.
//
// Параметры:
// 	Организации - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоНалогуНаИмущество(Организации, Период) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.УплачиваютсяАвансыПоНалогуНаИмущество(Организации, Период);
	
КонецФункции

// Возвращает результат расчета налога (или аванса по налогу) на имущество.
//
// Параметры:
//  Организация						 - СправочникСсылка.Организации	 - Организация, для которой необходимо выполнить проверку.
//  ПериодРасчета					 - Дата							 - Дата закрытия месяца.
//  ДополнительныеПараметрыРасчета	 - Произвольный					 - Дополнительные параметры расчета.
// 
// Возвращаемое значение:
//  ТаблицаРасчетНалога - ТаблицаЗначений - Результат расчета налога.
//
Функция ПолучитьРасчетПоНалогуНаИмущество(Организация, ПериодРасчета, ДополнительныеПараметрыРасчета = Неопределено) Экспорт
	
	ТаблицаРасчетНалога = РасчетИмущественныхНалоговПереопределяемый.ПустаяСправкаРасчет("РасчетНалогаНаИмущество");

	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат ТаблицаРасчетНалога;
	КонецЕсли;
	
	ПараметрыРасчета = ПараметрыРасчетаНалогаНаИмущество(Организация, ПериодРасчета, ДополнительныеПараметрыРасчета);
	
	// По среднегодовой стоимости.
	РасчетНалога = ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоСреднейСтоимости(ПараметрыРасчета);
	ВыполнитьРасчетНалогаНаИмуществоПоСреднейСтоимости(РасчетНалога, ПараметрыРасчета, ТаблицаРасчетНалога);
	
	// По кадастровой стоимости.
	ДанныеДляРасчета = ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоКадастровойСтоимости(ПараметрыРасчета);
	ВыполнитьРасчетНалогаНаИмуществоПоКадастровойСтоимости(ДанныеДляРасчета, ПараметрыРасчета, ТаблицаРасчетНалога);
	
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ПараметрыРасчета.мДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(Организация, "Организация");
	
	Возврат ТаблицаРасчетНалога;
	
КонецФункции

#КонецОбласти

#Область ТранспортныйНалог

// Возвращает необходимость уплаты авансов по транспортному налогу.
//
// Параметры:
// 	Организации - СправочникСсылка.Организация - Организация, для которой необходимо выполнить расчет.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоТранспортномуНалогу(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация КАК Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, -1), КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияТранспортныхСредств
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияТранспортныхСредств
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|	ГДЕ
	|		РегистрацияТранспортныхСредств.Организация В (&Организации)
	|		И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И РегистрацияТранспортныхСредств.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|		И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

// Возвращает результат расчета транспортного налога (или аванса по налогу).
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
// 	ПериодРасчета - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	ТаблицаРасчетТранспортногоНалога - ТаблицаЗначений - Результат расчета транспортного налога.
//
Функция ПолучитьРасчетПоТранспортномуНалогу(Организация, ПериодРасчета) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	ГодовойРасчет = КонецКвартала(ПериодРасчета) = КонецГода(ПериодРасчета);
	
	ДатаНачалаПериодаОтчета = ?(ГодовойРасчет, НачалоГода(ПериодРасчета), НачалоКвартала(ПериодРасчета));
	ДатаКонцаПериодаОтчета = КонецКвартала(ПериодРасчета);
	
	ТаблицаРасчетТранспортногоНалога = РасчетИмущественныхНалоговПереопределяемый.ПустаяСправкаРасчет("РасчетТранспортногоНалога");
	ТаблицаРасходыНаПлатон = РасчетИмущественныхНалоговПереопределяемый.ПустаяТаблицаРасходыНаПлатон();
	
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат Новый Структура("Налог, Платон", ТаблицаРасчетТранспортногоНалога, ТаблицаРасходыНаПлатон);
	КонецЕсли;

	НетТСЗарегистрированныхВРеестреСистемыПлатон = Истина;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
	
	ТекстОбъединяющегоЗапроса = "";
	
	Период = ДатаНачалаПериодаОтчета;
	
	Пока Период <= ДатаКонцаПериодаОтчета Цикл
		
		// С 2016 года, если регистрация ТС произошла до 15-го числа соответствующего месяца включительно
		// или снятие транспортного средства с регистрации (снятие с учета, исключение из государственного
		// судового реестра и так далее) произошло после 15-го числа соответствующего месяца, за полный 
		// месяц принимается месяц регистрации (снятия с регистрации) транспортного средства.
		
		Если Год(Период) < 2016 Тогда
			КонецПериода = КонецМесяца(Период);
			КонецПрошлогоПериода = НачалоМесяца(Период) - 1;
		Иначе
			КонецПериода = Дата(Год(Период), Месяц(Период), 15, 23, 59, 59);
			КонецПрошлогоПериода = КонецПериода;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("КонецПрошлогоПериода", КонецПрошлогоПериода);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.Период КАК Период,
		|	РегистрацияТранспортныхСредствСрезПоследних.Организация КАК Организация,
		|	РегистрацияТранспортныхСредствСрезПоследних.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&КонецПрошлогоПериода, Организация = &Организация) КАК РегистрацияТранспортныхСредствСрезПоследних
		|ГДЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредств.Период КАК Период,
		|	РегистрацияТранспортныхСредств.Организация КАК Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период = &НачалоМесяца
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ВТ_РегистрацииВПервыйДеньМесяца.Период, ВТ_РегистрацииНаКонецПрошлогоПериода.Период) КАК Период,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.Организация,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_СписокОсновныхСредств
		|ИЗ
		|	ВТ_РегистрацииНаКонецПрошлогоПериода КАК ВТ_РегистрацииНаКонецПрошлогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацииВПервыйДеньМесяца КАК ВТ_РегистрацииВПервыйДеньМесяца
		|		ПО ВТ_РегистрацииНаКонецПрошлогоПериода.Организация = ВТ_РегистрацииВПервыйДеньМесяца.Организация
		|			И ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство = ВТ_РегистрацииВПервыйДеньМесяца.ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрацияТранспортныхСредств.Период,
		|	РегистрацияТранспортныхСредств.Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоМесяца И &КонецПериода
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(СписокОсновныхСредств.Период) КАК Период,
		|	СписокОсновныхСредств.Организация КАК Организация,
		|	СписокОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_ПериодРегистрацииТранспортныхСредств
		|ИЗ
		|	ВТ_СписокОсновныхСредств КАК СписокОсновныхСредств
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокОсновныхСредств.ОсновноеСредство,
		|	СписокОсновныхСредств.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ПериодРегистрацииТранспортныхСредств.Период КАК ДатаРегистрационныхДанных,
		|	ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
		|	РегистрацияТранспортныхСредств.Марка КАК Марка,
		|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияТранспортныхСредств.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(РегистрацияТранспортныхСредств.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
		|		ИНАЧЕ РегистрацияТранспортныхСредств.КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
		|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
		|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
		|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
		|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
		|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
		|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
		|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
		|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
		|	ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
		|	1 КАК КоличествоМесяцев
		|ПОМЕСТИТЬ РегистрацияТранспортныхСредств
		|ИЗ
		|	ВТ_ПериодРегистрацииТранспортныхСредств КАК ВТ_ПериодРегистрацииТранспортныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|		ПО ВТ_ПериодРегистрацииТранспортныхСредств.Период = РегистрацияТранспортныхСредств.Период
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.Организация = РегистрацияТранспортныхСредств.Организация
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство = РегистрацияТранспортныхСредств.ОсновноеСредство";
		
		Подстрока = "ПОМЕСТИТЬ РегистрацияТранспортныхСредств";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, Подстрока, Подстрока + Формат(Период, "ДФ=MM"));
		
		Запрос.Выполнить();
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ РегистрацияТранспортныхСредств
									  |",
									  "")
								  + "ИЗ РегистрацияТранспортныхСредств" + Формат(Период, "ДФ=MM") + "
									|";
									
		Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТ_СписокОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ПериодРегистрацииТранспортныхСредств";
		Запрос.Выполнить();
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		Запрос.МенеджерВременныхТаблиц, Организация, ПериодРасчета, "ТранспортныйНалог");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.Регистрация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_Снятия
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодПредыдущегоСнятия
	|ПОМЕСТИТЬ ВТ_ПредыдущиеСнятия
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период > ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период КАК Период,
	|	МИНИМУМ(ВТ_Регистрации.Период) КАК ПериодПервоначальнойРегистрации
	|ПОМЕСТИТЬ ВТ_ПервоначальныеРегистрации
	|ИЗ
	|	ВТ_ПредыдущиеСнятия КАК ВТ_ПредыдущиеСнятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_ПредыдущиеСнятия.ОсновноеСредство = ВТ_Регистрации.ОсновноеСредство
	|			И ВТ_ПредыдущиеСнятия.ПериодПредыдущегоСнятия < ВТ_Регистрации.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодСнятияСУчета
	|ПОМЕСТИТЬ ВТ_СнятияСУчета
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период < ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ВТ_ПервоначальныеРегистрации.ПериодПервоначальнойРегистрации) КАК ПериодПервоначальнойРегистрации,
	|	МАКСИМУМ(ВТ_СнятияСУчета.ПериодСнятияСУчета) КАК ПериодСнятияСУчета
	|ПОМЕСТИТЬ ВТ_ДатыПостановкиИСнятия
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеРегистрации КАК ВТ_ПервоначальныеРегистрации
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_ПервоначальныеРегистрации.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_ПервоначальныеРегистрации.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СнятияСУчета КАК ВТ_СнятияСУчета
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_СнятияСУчета.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_СнятияСУчета.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|ПОМЕСТИТЬ ВТ_РазличныеЗаписи
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_РазличныеЗаписи.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	ВТ_РазличныеЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка КАК Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс КАК ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_РазличныеЗаписи.НалоговаяЛьгота <> ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоЗаписейЛьгот
	|ПОМЕСТИТЬ ВТ_ЗаписиБезУчетаЛьгот
	|ИЗ
	|	ВТ_РазличныеЗаписи КАК ВТ_РазличныеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РазличныеЗаписи.ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавка,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных) КАК ДатаРегистрационныхДанных,
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцевЛьготы,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев, 0) = 0
	|			ТОГДА СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев)
	|		ИНАЧЕ ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев
	|	КОНЕЦ КАК КоличествоМесяцевВладения,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК ИФНС,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмерения,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0) КАК КоличествоЗаписейЛьгот,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы,
	|	ОсновныеСредства.ДатаВыпуска КАК ДатаВыпуска,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации КАК ДатаРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета КАК ДатаПрекращенияРегистрации
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗаписиБезУчетаЛьгот КАК ВТ_ЗаписиБезУчетаЛьгот
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ЗаписиБезУчетаЛьгот.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.РегистрационныйЗнак = ВТ_ЗаписиБезУчетаЛьгот.РегистрационныйЗнак
	|			И РегистрацияТранспортныхСредств.Марка = ВТ_ЗаписиБезУчетаЛьгот.Марка
	|			И РегистрацияТранспортныхСредств.ИдентификационныйНомер = ВТ_ЗаписиБезУчетаЛьгот.ИдентификационныйНомер
	|			И РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_ЗаписиБезУчетаЛьгот.РегистрацияВНалоговомОргане
	|			И РегистрацияТранспортныхСредств.КодПоОКТМО = ВТ_ЗаписиБезУчетаЛьгот.КодПоОКТМО
	|			И РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства = ВТ_ЗаписиБезУчетаЛьгот.КодВидаТранспортногоСредства
	|			И РегистрацияТранспортныхСредств.НалоговаяБаза = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяБаза
	|			И РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы = ВТ_ЗаписиБезУчетаЛьгот.ЕдиницаИзмеренияНалоговойБазы
	|			И РегистрацияТранспортныхСредств.НалоговаяСтавка = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяСтавка
	|			И РегистрацияТранспортныхСредств.ЭкологическийКласс = ВТ_ЗаписиБезУчетаЛьгот.ЭкологическийКласс
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЧислитель
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЗнаменатель
	|			И РегистрацияТранспортныхСредств.ПовышающийКоэффициент = ВТ_ЗаписиБезУчетаЛьгот.ПовышающийКоэффициент
	|			И РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|			И (ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот = 1)
	|			И РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон = ВТ_ЗаписиБезУчетаЛьгот.ЗарегистрированоВРеестреСистемыПлатон
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ОсновныеСредства.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыПостановкиИСнятия КАК ВТ_ДатыПостановкиИСнятия
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ДатыПостановкиИСнятия.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных = ВТ_ДатыПостановкиИСнятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ОсновныеСредства.ДатаВыпуска,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0),
	|	РегистрацияТранспортныхСредств.ЗарегистрированоВРеестреСистемыПлатон,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИФНС,
	|	КодПоОКТМО,
	|	ОсновноеСредство";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		РасчетТранспортногоНалога = Результат.Выгрузить();
		
		Если ГодовойРасчет Тогда
			
			РасчетНалога1Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			РасчетНалога2Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			РасчетНалога3Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			
			Отбор = Новый Структура("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			
			НайденнаяСтрока = РасчетТранспортногоНалога.Найти(Истина, "УплачиваютсяАвансы");
			Если НайденнаяСтрока <> Неопределено Тогда
				РасчетНалога1Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 03, 31));
				РасчетНалога1Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");

				РасчетНалога2Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 06, 30));
				РасчетНалога2Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");

				РасчетНалога3Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 09, 30));
				РасчетНалога3Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаРасчета Из РасчетТранспортногоНалога Цикл
			
			Если НЕ ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
				Продолжить;
			КонецЕсли;

			Если НетТСЗарегистрированныхВРеестреСистемыПлатон И СтрокаРасчета.ЗарегистрированоВРеестреСистемыПлатон Тогда
				НетТСЗарегистрированныхВРеестреСистемыПлатон = Ложь;
			КонецЕсли;
			
			// Для расчета используется та строка, в которой содержится информация о льготе.
			Если СтрокаРасчета.КоличествоЗаписейЛьгот = 1
			   И СтрокаРасчета.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется Тогда
				Продолжить;
			КонецЕсли;

			Если Год(ДатаКонцаПериодаОтчета) > 2015 
			   И НачалоМесяца(СтрокаРасчета.ДатаРегистрации) = НачалоМесяца(СтрокаРасчета.ДатаПрекращенияРегистрации)
			   И ((День(СтрокаРасчета.ДатаРегистрации) <= 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) <= 15)
			       ИЛИ (День(СтрокаРасчета.ДатаРегистрации) > 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) > 15)) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетТранспортногоНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета, ,
				"КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,
				|ПроцентУменьшения,ЛьготнаяСтавка");
				
			Если СтрокаРасчета.НалоговаяСтавкаЗависитОтГодаВыпускаТС И ЗначениеЗаполнено(СтрокаРасчета.ДатаВыпуска) Тогда
					НоваяСтрока.КоличествоЛетПрошедшихСГодаВыпускаТС = 
						Год(ДатаКонцаПериодаОтчета) - Год(СтрокаРасчета.ДатаВыпуска);
			КонецЕсли;
			
			КоличествоМесяцевВПериоде = ?(ГодовойРасчет, 12, 3);
			Коэффициент = Окр(НоваяСтрока.КоличествоМесяцевВладения / КоличествоМесяцевВПериоде, 4);
			
			Если Год(ДатаКонцаПериодаОтчета) > 2014 ИЛИ (ГодовойРасчет И Год(ДатаКонцаПериодаОтчета) = 2014) Тогда
				
				Если НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0 
					ИЛИ НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0 Тогда
					
					ДоляВПравеОбщейСобственности = 1;
					
				Иначе
					
					ДоляВПравеОбщейСобственности = НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель 
						/ НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель;
					
				КонецЕсли;
					
			Иначе
				
				НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0;
				НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0;
				ДоляВПравеОбщейСобственности = 1;
				
			КонецЕсли;
			
			Если СтрокаРасчета.ПовышающийКоэффициент <> 0
				И (Год(ДатаКонцаПериодаОтчета) >= 2015 ИЛИ (ГодовойРасчет И Год(ДатаКонцаПериодаОтчета) = 2014)) Тогда
				
				ПовышающийКоэффициент = СтрокаРасчета.ПовышающийКоэффициент;
				
			Иначе
				
				ПовышающийКоэффициент = 1;
				НоваяСтрока.ПовышающийКоэффициент = 0;
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалога = Окр(Окр(НоваяСтрока.НалоговаяБаза 
				* НоваяСтрока.НалоговаяСтавка 
				* ДоляВПравеОбщейСобственности 
				* Коэффициент
				* ПовышающийКоэффициент
				* ?(ГодовойРасчет, 1, 1 / 4), 2));
			
			НалоговаяЛьгота = СтрокаРасчета.НалоговаяЛьгота;
			
			Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется
				И НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда 
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета,
					"КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,ПроцентУменьшения,ЛьготнаяСтавка");
				
				КоэффициентЛьготы = Окр(НоваяСтрока.КоличествоМесяцевЛьготы / 12, 4);
					
				Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы 
							* (НоваяСтрока.ПроцентУменьшения / 100)
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
						* (НоваяСтрока.НалоговаяСтавка - НоваяСтрока.ЛьготнаяСтавка)
						* КоэффициентЛьготы
						* ДоляВПравеОбщейСобственности
						* ПовышающийКоэффициент, 2));

				КонецЕсли;
				
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаНалоговойЛьготы;
			
			Если ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);

				СуммаНалоговогоВычета1Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета1Кв = СуммаНалоговогоВычета1Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				СуммаНалоговогоВычета2Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета2Кв = СуммаНалоговогоВычета2Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;
				
				СуммаНалоговогоВычета3Кв = 0;
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
					СуммаНалоговогоВычета3Кв = СуммаНалоговогоВычета3Кв + СтрокаРасчетаАвансовыхПлатежей.СуммаНалоговогоВычета;
				КонецЦикла;

				НоваяСтрока.СуммаНалоговогоВычета = СуммаНалоговогоВычета1Кв + СуммаНалоговогоВычета2Кв + СуммаНалоговогоВычета3Кв;
				
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв;
				
			КонецЕсли;

			Если НЕ ГодовойРасчет Тогда
				НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетТранспортногоНалога.ЗаполнитьЗначения(ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетТранспортногоНалога.ЗаполнитьЗначения(Организация, "Организация");

	// Учет платы в систему "Платон".
	Если НетТСЗарегистрированныхВРеестреСистемыПлатон Тогда
		Возврат Новый Структура("Налог, Платон", ТаблицаРасчетТранспортногоНалога, ТаблицаРасходыНаПлатон);
	КонецЕсли;
	
	ТаблицаРасходыНаПлатон = РасчетИмущественныхНалоговПереопределяемый.ПолучитьРасходыНаПлатон(
		Организация, ПериодРасчета, ТаблицаРасчетТранспортногоНалога);
	
	Возврат Новый Структура("Налог, Платон", ТаблицаРасчетТранспортногоНалога, ТаблицаРасходыНаПлатон);

КонецФункции

#КонецОбласти

#Область ЗемельныйНалог

// Возвращает необходимость уплаты авансов по земельному налогу.
//
// Параметры:
// 	Организации - СправочникСсылка.Организация - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоЗемельномуНалогу(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация КАК Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, -1), КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияЗемельныхУчастков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияЗемельныхУчастков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|	ГДЕ
	|		РегистрацияЗемельныхУчастков.Организация В (&Организации)
	|		И РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И РегистрацияЗемельныхУчастков.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|		И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

// Возвращает результат расчета земельного налога (или аванса по налогу).
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
// 	ПериодРасчета - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	ТаблицаРасчетНалога - ТаблицаЗначений - Результат расчета земельного налога.
//
Функция ПолучитьРасчетПоЗемельномуНалогу(Организация, ПериодРасчета) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	ГодовойРасчет = КонецКвартала(ПериодРасчета) = КонецГода(ПериодРасчета);
	
	ДатаНачалаПериодаОтчета = ?(ГодовойРасчет,	НачалоГода(ПериодРасчета),	НачалоКвартала(ПериодРасчета));
	ДатаКонцаПериодаОтчета = КонецКвартала(ПериодРасчета);
	
	ТаблицаРасчетНалога = ПустаяСправкаРасчет("РасчетЗемельногоНалога");
	
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ (ЭтоНеЮрЛицо И ПериодРасчета >= '20150101') Тогда
		Возврат ТаблицаРасчетНалога;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаКонцаПериодаОтчета", ДатаКонцаПериодаОтчета);
	
	Период = ДатаНачалаПериодаОтчета;
	
	ТекстОбъединяющегоЗапроса = "";
	
	Пока Период <= ДатаКонцаПериодаОтчета Цикл
		
		// Выборка земельных участков и числа полных месяцев владения ими.
		// При этом если возникновение (прекращение) у налогоплательщика в течение налогового периода 
		// права собственности (постоянного (бессрочного) пользования, пожизненного наследуемого владения) 
		// на земельный участок (его долю) произошло до 15-го числа соответствующего месяца включительно, 
		// за полный месяц принимается месяц возникновения указанных прав. Если возникновение (прекращение)
		// указанных прав произошло после 15-го числа соответствующего месяца, за полный месяц принимается 
		// месяц прекращения указанных прав.
		
		Запрос.УстановитьПараметр("КонецПредыдущегоМесяца", НачалоМесяца(Период) - 1);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("СерединаМесяца", Дата(Год(Период), Месяц(Период), 15));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Организация КАК Организация,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Период КАК Период,
		|	NULL КАК НалоговаяЛьготаПоНалоговойБазе,
		|	1 КАК КоличествоМесяцев,
		|	0 КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаНачалаПроектирования КАК ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаРегистрацииПравНаОбъектНедвижимости КАК ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастковСрезПоследних.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(&СерединаМесяца, Организация = &Организация) КАК РегистрацияЗемельныхУчастковСрезПоследних
		|ГДЕ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	NULL,
		|	1,
		|	0,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.Организация = &Организация
		|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоМесяца И &СерединаМесяца
		|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Организация,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Период,
		|	РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяЛьготаПоНалоговойБазе,
		|	0,
		|	1,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастковСрезПоследних.СниженнаяНалоговаяСтавка
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(&КонецПредыдущегоМесяца, Организация = &Организация) КАК РегистрацияЗемельныхУчастковСрезПоследних
		|ГДЕ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе,
		|	0,
		|	1,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастков.КадастроваяСтоимость,
		|	РегистрацияЗемельныхУчастков.КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастков.НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастков.СниженнаяНалоговаяСтавка
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.Организация = &Организация
		|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.Организация КАК Организация,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрацияЗемельныхУчастков.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаПоследнегоСнятияСРегистрационногоУчета
		|ПОМЕСТИТЬ ДатыПоследнегоСнятияСРегистрационногоУчета
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ВладениеЗемельнымиУчастками.Организация = РегистрацияЗемельныхУчастков.Организация
		|			И (РегистрацияЗемельныхУчастков.Период < ВладениеЗемельнымиУчастками.Период)
		|			И (НЕ РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство КАК ОсновноеСредство,
		|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаРегистрацииЗемли
		|ПОМЕСТИТЬ ДатыРегистрацииЗемли
		|ИЗ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета КАК ДатыПоследнегоСнятияСРегистрационногоУчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|		ПО ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ДатыПоследнегоСнятияСРегистрационногоУчета.Организация = РегистрацияЗемельныхУчастков.Организация
		|			И (РегистрацияЗемельныхУчастков.Период > ДатыПоследнегоСнятияСРегистрационногоУчета.ДатаПоследнегоСнятияСРегистрационногоУчета)
		|			И (РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МЕСЯЦ(&НачалоМесяца) КАК НомерМесяца,
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
		|	ВладениеЗемельнымиУчастками.КоличествоМесяцев КАК КоличествоМесяцев,
		|	ВладениеЗемельнымиУчастками.КоличествоМесяцевПримененияЛьготы КАК КоличествоМесяцевПримененияЛьготы,
		|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	ВладениеЗемельнымиУчастками.НалоговаяСтавка КАК НалоговаяСтавка,
		|	ВладениеЗемельнымиУчастками.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	ВЫБОР
		|		КОГДА ВладениеЗемельнымиУчастками.ЖилищноеСтроительство
		|			ТОГДА ВЫБОР
		|					КОГДА ДатыРегистрацииЗемли.ДатаРегистрацииЗемли >= ДАТАВРЕМЯ(2008, 1, 1)
		|						ТОГДА ВЫБОР
		|								КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|										ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|									ТОГДА ВЫБОР
		|											КОГДА ДОБАВИТЬКДАТЕ(ДатыРегистрацииЗемли.ДатаРегистрацииЗемли, ГОД, 3) <= &СерединаМесяца
		|												ТОГДА ""2""
		|											ИНАЧЕ ""1""
		|										КОНЕЦ
		|								ИНАЧЕ """"
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ВладениеЗемельнымиУчастками.ДатаНачалаПроектирования <> ДАТАВРЕМЯ(1, 1, 1)
		|								ТОГДА ВЫБОР
		|										КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|												ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|											ТОГДА ВЫБОР
		|													КОГДА ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.ДатаНачалаПроектирования, ГОД, 3) <= &СерединаМесяца
		|														ТОГДА ""2""
		|													ИНАЧЕ ""1""
		|												КОНЕЦ
		|										ИНАЧЕ """"
		|									КОНЕЦ
		|							ИНАЧЕ """"
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ПериодСтроительства
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками" + Формат(Период, "ДФ=MM") + "
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыРегистрацииЗемли КАК ДатыРегистрацииЗемли
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = ДатыРегистрацииЗемли.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВладениеЗемельнымиУчастками
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыПоследнегоСнятияСРегистрационногоУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыРегистрацииЗемли";
		Запрос.Выполнить();
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
									  |",
									  "")
								  + "ИЗ ВладениеЗемельнымиУчастками" + Формат(Период, "ДФ=MM") + "
									|";
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		Запрос.МенеджерВременныхТаблиц, Организация, ПериодРасчета, "ЗемельныйНалог");
	
	Запрос.УстановитьПараметр("Период", КонецГода(ПериодРасчета));
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(ПериодРасчета));
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_НомераПоследнихМесяцевЛьготы
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НомерМесяца КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе
	|ПОМЕСТИТЬ ВТ_ВидыЛьгот
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевПримененияЛьготы) КАК КоличествоМесяцевПримененияЛьготы
	|ПОМЕСТИТЬ ВТ_ПрименениеЛьготы
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПрименениеЛьготы.ОсновноеСредство КАК ОсновноеСредство,
	|	ПрименениеЛьготы.КоличествоМесяцевПримененияЛьготы КАК КоличествоМесяцевПримененияЛьготы
	|ПОМЕСТИТЬ ВТ_Льготы
	|ИЗ
	|	ВТ_НомераПоследнихМесяцевЛьготы КАК НомераПоследнихМесяцев
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыЛьгот КАК ВидыЛьгот
	|		ПО НомераПоследнихМесяцев.ОсновноеСредство = ВидыЛьгот.ОсновноеСредство
	|			И НомераПоследнихМесяцев.НомерМесяца = ВидыЛьгот.НомерМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрименениеЛьготы КАК ПрименениеЛьготы
	|		ПО (ВидыЛьгот.ОсновноеСредство = ПрименениеЛьготы.ОсновноеСредство)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПрименениеЛьготы.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость
	|ПОМЕСТИТЬ ВТ_НомераПоследнихМесяцевКадастроваяСтоимость
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НомерМесяца КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ВладениеЗемельнымиУчастками.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	ВладениеЗемельнымиУчастками.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВладениеЗемельнымиУчастками.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
	|ПОМЕСТИТЬ ВТ_КодКатегорииЗемельСтавкиЗаписи
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	ВладениеЗемельнымиУчастками.КоличествоМесяцев > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КодКатегорииЗемельСтавкиЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|	КодКатегорииЗемельСтавкиЗаписи.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	КодКатегорииЗемельСтавкиЗаписи.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	КодКатегорииЗемельСтавкиЗаписи.НалоговаяСтавка КАК НалоговаяСтавка,
	|	КодКатегорииЗемельСтавкиЗаписи.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка
	|ПОМЕСТИТЬ ВТ_КодКатегорииЗемельНалоговыеСтавки
	|ИЗ
	|	ВТ_НомераПоследнихМесяцевКадастроваяСтоимость КАК НомераПоследнихМесяцев
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КодКатегорииЗемельСтавкиЗаписи КАК КодКатегорииЗемельСтавкиЗаписи
	|		ПО НомераПоследнихМесяцев.ОсновноеСредство = КодКатегорииЗемельСтавкиЗаписи.ОсновноеСредство
	|			И НомераПоследнихМесяцев.КадастроваяСтоимость = КодКатегорииЗемельСтавкиЗаписи.КадастроваяСтоимость
	|			И НомераПоследнихМесяцев.НомерМесяца = КодКатегорииЗемельСтавкиЗаписи.НомерМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодКатегорииЗемельСтавкиЗаписи.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства КАК ПериодСтроительства,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) КАК КоличествоМесяцевВладения
	|ПОМЕСТИТЬ ВТ_ВладениеУчастками
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	ПериодСтроительства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства КАК ПериодСтроительства,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) КАК КоличествоМесяцевИспользования
	|ПОМЕСТИТЬ ВТ_ИспользованиеУчастков
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	КадастроваяСтоимость
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МИНИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_НомераПервыхМесяцев
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.КадастроваяСтоимость КАК КадастроваяСтоимость
	|ПОМЕСТИТЬ ВТ_ПервоначальнаяКадастроваяСтоимость
	|ИЗ
	|	ВТ_НомераПервыхМесяцев КАК НомераПервыхМесяцев
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|		ПО НомераПервыхМесяцев.ОсновноеСредство = ВладениеЗемельнымиУчастками.ОсновноеСредство
	|			И НомераПервыхМесяцев.НомерМесяца = ВладениеЗемельнымиУчастками.НомерМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Если ПериодРасчета >= '20180101' Тогда
	
		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИспользованиеУчастков.ОсновноеСредство КАК ОсновноеСредство,
		|	ЕСТЬNULL(ИспользованиеУчастков.КадастроваяСтоимость, 0) КАК КадастроваяСтоимость,
		|	ЕСТЬNULL(ИспользованиеУчастков.КоличествоМесяцевИспользования, 0) КАК КоличествоМесяцевИспользования,
		|	ЕСТЬNULL(ВладениеУчастками.КоличествоМесяцевВладения, 0) КАК КоличествоМесяцевВладения,
		|	КодКатегорииЗемельНалоговыеСтавки.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	КодКатегорииЗемельНалоговыеСтавки.НалоговаяСтавка КАК НалоговаяСтавка,
		|	КодКатегорииЗемельНалоговыеСтавки.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КадастровыйНомер КАК КадастровыйНомер,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КБК КАК КБК,
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияЗемельныхУчастковСрезПоследних.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|				ИЛИ РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|			ТОГДА РегистрацияЗемельныхУчастковСрезПоследних.КодПоОКТМО
		|		ИНАЧЕ ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
		|	КОНЕЦ КАК КодПоОКТМО,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ОбщаяСобственность, ЛОЖЬ) КАК ОбщаяСобственность,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ДоляВПравеОбщейСобственностиЧислитель, 0) КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ДоляВПравеОбщейСобственностиЗнаменатель, 0) КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство, ЛОЖЬ) КАК ЖилищноеСтроительство,
		|	РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
		|	ЕСТЬNULL(Льготы.КоличествоМесяцевПримененияЛьготы, 0) КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ДоляНеОблагаемойНалогомПлощадиЧислитель, 0) КАК ДоляНеоблагаемойПлощадиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 0) КАК ДоляНеоблагаемойПлощадиЗнаменатель,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.УменьшениеНалоговойБазыПоСтатье391, ЛОЖЬ) КАК УменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.УменьшениеНалоговойБазыНаСумму, ЛОЖЬ) КАК УменьшениеНалоговойБазыНаСумму,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.НеОблагаемаяНалогомСумма, 0) КАК НеОблагаемаяНалогомСумма,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ПроцентУменьшенияСуммыНалога, 0) КАК ПроцентУменьшенияСуммыНалога,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.СуммаУменьшенияСуммыНалога, 0) КАК СуммаУменьшенияСуммыНалога,
		|	ЕСТЬNULL(ИспользованиеУчастков.ПериодСтроительства, """") КАК ПериодСтроительства
		|ПОМЕСТИТЬ ВТ_РасчетНалога
		|ИЗ
		|	ВТ_ИспользованиеУчастков КАК ИспользованиеУчастков
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВладениеУчастками КАК ВладениеУчастками
		|		ПО ИспользованиеУчастков.ОсновноеСредство = ВладениеУчастками.ОсновноеСредство
		|			И ИспользованиеУчастков.ПериодСтроительства = ВладениеУчастками.ПериодСтроительства
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КодКатегорииЗемельНалоговыеСтавки КАК КодКатегорииЗемельНалоговыеСтавки
		|		ПО ИспользованиеУчастков.ОсновноеСредство = КодКатегорииЗемельНалоговыеСтавки.ОсновноеСредство
		|			И ИспользованиеУчастков.КадастроваяСтоимость = КодКатегорииЗемельНалоговыеСтавки.КадастроваяСтоимость
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Льготы КАК Льготы
		|		ПО ИспользованиеУчастков.ОсновноеСредство = Льготы.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
		|				&Период,
		|				Организация = &Организация
		|					И ВключатьВНалоговуюБазу = ИСТИНА) КАК РегистрацияЗемельныхУчастковСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
		|					&Период,
		|					Организация = &Организация
		|						И ВключатьВНалоговуюБазу = ИСТИНА
		|						И НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)) КАК РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам
		|			ПО РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство = РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ОсновноеСредство
		|		ПО ИспользованиеУчастков.ОсновноеСредство = РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИФНС";
		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	Иначе
		
		Запрос.Текст = Запрос.Текст +
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВладениеУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	0 КАК КоличествоМесяцевИспользования,
		|	ЕСТЬNULL(ВладениеУчастками.КоличествоМесяцевВладения, 0) КАК КоличествоМесяцевВладения,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КодКатегорииЗемель КАК КодКатегорииЗемель,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КадастровыйНомер КАК КадастровыйНомер,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КБК КАК КБК,
		|	ЕСТЬNULL(ПервоначальнаяКадастроваяСтоимость.КадастроваяСтоимость, 0) КАК КадастроваяСтоимость,
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияЗемельныхУчастковСрезПоследних.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|	ВЫБОР
		|		КОГДА РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|				ИЛИ РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|			ТОГДА РегистрацияЗемельныхУчастковСрезПоследних.КодПоОКТМО
		|		ИНАЧЕ ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
		|	КОНЕЦ КАК КодПоОКТМО,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ОбщаяСобственность, ЛОЖЬ) КАК ОбщаяСобственность,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ДоляВПравеОбщейСобственностиЧислитель, 0) КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ДоляВПравеОбщейСобственностиЗнаменатель, 0) КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство, ЛОЖЬ) КАК ЖилищноеСтроительство,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
		|	РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
		|	ЕСТЬNULL(Льготы.КоличествоМесяцевПримененияЛьготы, 0) КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ДоляНеОблагаемойНалогомПлощадиЧислитель, 0) КАК ДоляНеоблагаемойПлощадиЧислитель,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 0) КАК ДоляНеоблагаемойПлощадиЗнаменатель,
		|	РегистрацияЗемельныхУчастковСрезПоследних.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.УменьшениеНалоговойБазыПоСтатье391, ЛОЖЬ) КАК УменьшениеНалоговойБазыПоСтатье391,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.УменьшениеНалоговойБазыНаСумму, ЛОЖЬ) КАК УменьшениеНалоговойБазыНаСумму,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.НеОблагаемаяНалогомСумма, 0) КАК НеОблагаемаяНалогомСумма,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.СниженнаяНалоговаяСтавка, 0) КАК СниженнаяНалоговаяСтавка,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ПроцентУменьшенияСуммыНалога, 0) КАК ПроцентУменьшенияСуммыНалога,
		|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.СуммаУменьшенияСуммыНалога, 0) КАК СуммаУменьшенияСуммыНалога,
		|	ЕСТЬNULL(ВладениеУчастками.ПериодСтроительства, """") КАК ПериодСтроительства
		|ПОМЕСТИТЬ ВТ_РасчетНалога
		|ИЗ
		|	ВТ_ВладениеУчастками КАК ВладениеУчастками
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Льготы КАК Льготы
		|		ПО ВладениеУчастками.ОсновноеСредство = Льготы.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяКадастроваяСтоимость КАК ПервоначальнаяКадастроваяСтоимость
		|		ПО ВладениеУчастками.ОсновноеСредство = ПервоначальнаяКадастроваяСтоимость.ОсновноеСредство
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
		|				&Период,
		|				Организация = &Организация
		|					И ВключатьВНалоговуюБазу = ИСТИНА) КАК РегистрацияЗемельныхУчастковСрезПоследних
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
		|					&Период,
		|					Организация = &Организация
		|						И ВключатьВНалоговуюБазу = ИСТИНА
		|						И НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)) КАК РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам
		|			ПО РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство = РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ОсновноеСредство
		|		ПО ВладениеУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИФНС";
		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	ВТ_РасчетНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_РасчетНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	ВТ_РасчетНалога.КоличествоМесяцевИспользования КАК КоличествоМесяцевИспользования,
	|	ВТ_РасчетНалога.КодКатегорииЗемель КАК КодКатегорииЗемель,
	|	ВТ_РасчетНалога.КадастровыйНомер КАК КадастровыйНомер,
	|	ВТ_РасчетНалога.КБК КАК КБК,
	|	ВТ_РасчетНалога.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ВТ_РасчетНалога.ИФНС КАК ИФНС,
	|	ВТ_РасчетНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_РасчетНалога.ОбщаяСобственность КАК ОбщаяСобственность,
	|	ВТ_РасчетНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РасчетНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РасчетНалога.ЖилищноеСтроительство КАК ЖилищноеСтроительство,
	|	ВТ_РасчетНалога.ПериодСтроительства КАК ПериодСтроительства,
	|	ВТ_РасчетНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_РасчетНалога.НалоговаяЛьготаПоНалоговойБазе КАК НалоговаяЛьготаПоНалоговойБазе,
	|	ВТ_РасчетНалога.КоличествоМесяцевПримененияЛьготы КАК КоличествоМесяцевПримененияЛьготы,
	|	ВТ_РасчетНалога.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 КАК КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
	|	ВТ_РасчетНалога.ДоляНеоблагаемойПлощадиЧислитель КАК ДоляНеоблагаемойПлощадиЧислитель,
	|	ВТ_РасчетНалога.ДоляНеоблагаемойПлощадиЗнаменатель КАК ДоляНеоблагаемойПлощадиЗнаменатель,
	|	ВТ_РасчетНалога.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391 КАК КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
	|	ВТ_РасчетНалога.УменьшениеНалоговойБазыПоСтатье391 КАК УменьшениеНалоговойБазыПоСтатье391,
	|	ВТ_РасчетНалога.УменьшениеНалоговойБазыНаСумму КАК УменьшениеНалоговойБазыНаСумму,
	|	ВТ_РасчетНалога.НеОблагаемаяНалогомСумма КАК НеОблагаемаяНалогомСумма,
	|	ВТ_РасчетНалога.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
	|	ВТ_РасчетНалога.ПроцентУменьшенияСуммыНалога КАК ПроцентУменьшенияСуммыНалога,
	|	ВТ_РасчетНалога.СуммаУменьшенияСуммыНалога КАК СуммаУменьшенияСуммыНалога,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_РасчетНалога КАК ВТ_РасчетНалога
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО ВТ_РасчетНалога.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		РасчетЗемельногоНалога = РезультатЗапроса.Выгрузить();
		
		Если ГодовойРасчет Тогда
			
			РасчетНалога1Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			РасчетНалога2Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			РасчетНалога3Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			
			Отбор = Новый Структура("ИФНС, ОсновноеСредство, ПериодСтроительства, КадастроваяСтоимость");
			
			НайденнаяСтрока = РасчетЗемельногоНалога.Найти(Истина, "УплачиваютсяАвансы");
			Если НайденнаяСтрока <> Неопределено Тогда
				РасчетНалога1Кв = ПолучитьРасчетПоЗемельномуНалогу(Организация, Дата(Год(ПериодРасчета), 03, 31));
				РасчетНалога2Кв = ПолучитьРасчетПоЗемельномуНалогу(Организация, Дата(Год(ПериодРасчета), 06, 30));
				РасчетНалога3Кв = ПолучитьРасчетПоЗемельномуНалогу(Организация, Дата(Год(ПериодРасчета), 09, 30));
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаРасчета Из РасчетЗемельногоНалога Цикл
			
			Если НЕ ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета);
			
			Если ПериодРасчета >= '20170101' Тогда
				НоваяСтрока.УменьшениеНалоговойБазыПоСтатье391 = Ложь;
			КонецЕсли;
			
			ЧислоМесяцевВОтчетномПериоде = 12;
			
			// Коэффициент Кл определяется как отношение числа полных месяцев, 
			// в течение которых отсутствует налоговая льгота, к числу календарных 
			// месяцев в налоговом периоде.
			
			Если НоваяСтрока.КоличествоМесяцевПримененияЛьготы = 0 Тогда
				КоэффициентКл = 1;
			Иначе
				ЧислоМесяцевИспользованияЛьготы = НоваяСтрока.КоличествоМесяцевВладения - НоваяСтрока.КоличествоМесяцевПримененияЛьготы;
				КоэффициентКл = Макс(ЧислоМесяцевИспользованияЛьготы / ЧислоМесяцевВОтчетномПериоде, 0);
				КоэффициентКл = Окр(КоэффициентКл, 4);
			КонецЕсли;
			
			Если НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель <> 0 И НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
				ДоляВПравеОбщейСобственности = 
					Мин(1, НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель / НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель);
			Иначе
				ДоляВПравеОбщейСобственности = 1;
			КонецЕсли;
			
			Если НоваяСтрока.ДоляНеоблагаемойПлощадиЧислитель <> 0 И НоваяСтрока.ДоляНеоблагаемойПлощадиЗнаменатель <> 0 Тогда
				ДоляНеоблагаемойПлощади = 
					Мин(1, НоваяСтрока.ДоляНеоблагаемойПлощадиЧислитель / НоваяСтрока.ДоляНеоблагаемойПлощадиЗнаменатель);
			Иначе
				ДоляНеоблагаемойПлощади = 0;
			КонецЕсли;
			
			// Расчет налоговой базы
			
			ОсноваДляРасчетаНалоговойБазы = НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности;
			Если НоваяСтрока.УменьшениеНалоговойБазыПоСтатье391 Тогда 
				СуммаУменьшенияНалоговойБазыПоСтатье391 = 10000;
			Иначе
				СуммаУменьшенияНалоговойБазыПоСтатье391 = 0;
			КонецЕсли;
			
			Если НоваяСтрока.НеОблагаемаяНалогомСумма <> 0 ИЛИ СуммаУменьшенияНалоговойБазыПоСтатье391 <> 0 Тогда
				НалоговаяБаза = 
					Макс(ОсноваДляРасчетаНалоговойБазы - (НоваяСтрока.НеОблагаемаяНалогомСумма + СуммаУменьшенияНалоговойБазыПоСтатье391), 0);
			ИначеЕсли ДоляНеоблагаемойПлощади > 0 Тогда
				НалоговаяБаза =
					Макс((ОсноваДляРасчетаНалоговойБазы - (ОсноваДляРасчетаНалоговойБазы * ДоляНеоблагаемойПлощади * (1 - КоэффициентКл))), 0);
			Иначе
				НалоговаяБаза = Макс(ОсноваДляРасчетаНалоговойБазы, 0);
			КонецЕсли;
			
			НоваяСтрока.НалоговаяБаза = Окр(НалоговаяБаза);
			
			// Коэффициент Кв определяется как отношение числа полных месяцев, 
			// в течение которых данный земельный участок находился в собственности
			// (постоянном (бессрочном) пользовании, пожизненном наследуемом владении) 
			// налогоплательщика, указанных по строке с кодом 150, к числу календарных 
			// месяцев в налоговом периоде.
			
			КоэффициентКв = Окр(НоваяСтрока.КоличествоМесяцевВладения / ЧислоМесяцевВОтчетномПериоде, 4);
			
			Если ПериодРасчета >= '20180101' 
			   И НоваяСтрока.КоличествоМесяцевИспользования <> НоваяСтрока.КоличествоМесяцевВладения
			   И НоваяСтрока.КоличествоМесяцевВладения <> 0 Тогда
				КоэффициентКи = Окр(НоваяСтрока.КоличествоМесяцевИспользования / НоваяСтрока.КоличествоМесяцевВладения, 4);
			Иначе
				КоэффициентКи = 1;
			КонецЕсли;
			
			// Определение периода строительства
			КоэффициентПериодаСтроительства = 1;
			Если СтрокаРасчета.ЖилищноеСтроительство Тогда
				
				Если НоваяСтрока.ПериодСтроительства = "2" Тогда
					КоэффициентПериодаСтроительства = 4;
				ИначеЕсли НоваяСтрока.ПериодСтроительства = "1" Тогда
					КоэффициентПериодаСтроительства = 2;
				КонецЕсли;
				
			КонецЕсли;
			
			Если ПериодРасчета < '20170101' Или СтрокаРасчета.СниженнаяНалоговаяСтавка = 0 Тогда
				НалоговаяСтавка = НоваяСтрока.НалоговаяСтавка;
			Иначе
				НалоговаяСтавка = СтрокаРасчета.СниженнаяНалоговаяСтавка;
			КонецЕсли;
			
			НоваяСтрока.СуммаНалога = 
				Окр(НоваяСтрока.НалоговаяБаза * НалоговаяСтавка * КоэффициентПериодаСтроительства * КоэффициентКв * КоэффициентКи / 100);
			
			// Расчет сумм налоговых льгот
			СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное = 0;
			СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = 0;
			СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = 0;
			СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 0;
			
			Если НоваяСтрока.НалоговаяЛьготаПоНалоговойБазе = 
				Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияМестное Тогда
				
				СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное = Окр(НоваяСтрока.СуммаНалога * (1 - КоэффициентКл));
				
			ИначеЕсли НоваяСтрока.НалоговаяЛьготаПоНалоговойБазе =
				Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395 Тогда
				
				СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = Окр(НоваяСтрока.СуммаНалога * (1 - КоэффициентКл));
				
			КонецЕсли;
			
			Если СтрокаРасчета.ПроцентУменьшенияСуммыНалога > 0 Тогда
				СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога =
					Окр(НоваяСтрока.СуммаНалога * СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
			КонецЕсли;	
				
			Если СтрокаРасчета.СуммаУменьшенияСуммыНалога > 0 Тогда
				СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога
					+ Окр(СтрокаРасчета.СуммаУменьшенияСуммыНалога);
			КонецЕсли;
			
			Если ПериодРасчета < '20170101' И СтрокаРасчета.СниженнаяНалоговаяСтавка > 0 Тогда
				СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 
					Окр(НоваяСтрока.НалоговаяБаза * (НоваяСтрока.НалоговаяСтавка - СтрокаРасчета.СниженнаяНалоговаяСтавка) / 100);
			Иначе
				СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 0;
			КонецЕсли;
			
			НоваяСтрока.СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога;
			НоваяСтрока.СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки;
			
			ПараметрыЛьготы = Новый Структура(
				"ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 
				|ДоляНеОблагаемойНалогомПлощадиЧислитель, 
				|КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395, 
				|КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391, 
				|НалоговаяЛьготаПоНалоговойБазе, 
				|НеОблагаемаяНалогомСумма, 
				|ПроцентУменьшенияСуммыНалога, 
				|СниженнаяНалоговаяСтавка, 
				|СуммаУменьшенияСуммыНалога, 
				|УменьшениеНалоговойБазыНаСумму, 
				|УменьшениеНалоговойБазыПоСтатье391");
				
			ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, СтрокаРасчета);
			
			ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель = СтрокаРасчета.ДоляНеоблагаемойПлощадиЗнаменатель;
			ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЧислитель = СтрокаРасчета.ДоляНеоблагаемойПлощадиЧислитель;
			
			НоваяСтрока.ПредставлениеНалоговойЛьготы =
				УправлениеВнеоборотнымиАктивамиКлиентСервер.ПредставлениеНалоговойЛьготыПоЗемельномуНалогу(ПараметрыЛьготы);
			
			// Расчет суммы налога к уплате
			Если СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога;
			ИначеЕсли СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки;
			ИначеЕсли СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное;
			ИначеЕсли СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395;
			Иначе
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
			КонецЕсли;
			
			НоваяСтрока.СуммаНалогаКУплате = Макс(0, НоваяСтрока.СуммаНалогаКУплате);
			
			Если ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
					+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв;
				
			КонецЕсли;
			
			Если НЕ ГодовойРасчет Тогда
				НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(Организация, "Организация");

	Возврат ТаблицаРасчетНалога;
	
КонецФункции

#КонецОбласти

// Процедура формирует временную таблицу в менеджере временных таблиц,
// содержащую сведения о регистрации в налоговых органах, в которые 
// уплачивается авансовые платежи организации по указанному виду налога.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором создать таблицы.
//	Организация - СправочникСсылка.Организации - организация, для которой определяются авансовые платежи.
//	Период - Дата - Дата, по состоянию на которую определяются сведения.
//	Налог - Строка - Может содержать одно из значений:
//			- "ЗемельныйНалог";
//			- "ТранспортныйНалог";
//			- "НалогНаИмущество".
// 
Процедура СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(МенеджерВременныхТаблиц, Организация, Период, Налог) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество;
	
	Если Налог = "ЗемельныйНалог" Тогда
		ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог;
	ИначеЕсли Налог = "ТранспортныйНалог" Тогда
		ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог;
	КонецЕсли;
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ГоловнаяОрганизация");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Налог",       ВидНалога);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрацииВНалоговомОргане.Ссылка КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ВсеРегистрацииВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &ГоловнаяОрганизация
	|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ПорядокУплатыНалоговНаМестах
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И Налог = &Налог
	|				И УплачиваютсяАвансы) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА ВТ_ВсеРегистрацииВНалоговомОргане.НалоговыйОрган
	|		ИНАЧЕ ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган
	|	КОНЕЦ КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|ИЗ
	|	ВТ_ПорядокУплатыНалоговНаМестах КАК ВТ_ПорядокУплатыНалоговНаМестах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВсеРегистрацииВНалоговомОргане КАК ВТ_ВсеРегистрацииВНалоговомОргане
	|		ПО (ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговыйОрган";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует таблицы значений, по структуре соответствующие регистрам сведений 	
// РасчетТранспортногоНалога и РасчетЗемельногоНалога БП.
//
// Параметры:
//	ИмяРегистраСведений - Строка - Имя регистра.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица с колонками, совпадающими с полями регистра.
//
Функция ПустаяСправкаРасчет(ИмяРегистраСведений) Экспорт

	СправкаРасчет = РегистрыСведений[ИмяРегистраСведений].СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	СправкаРасчет.Колонки.Удалить("Регистратор");
	СправкаРасчет.Колонки.Удалить("Активность");
	СправкаРасчет.Колонки.Удалить("НомерСтроки");
	
	Возврат СправкаРасчет;
	
КонецФункции

// Возвращает таблицы выполнения регламентных операций расчета имущественных налогов.
//
// Параметры:
// 	ТаблицаРеквизиты - ТаблицаЗначений - Таблица реквизитов документа.
// 	Отказ - Булево - Флаг отказа проведения.
// 
// Возвращаемое значение:
// 	 СтруктураТаблиц - Структура - Таблицы расчета налога:
// 							Ключи:
// 								* ТаблицаСправкиРасчета - ТаблицаЗначений - Справка-расчет налога.
// 								* ТаблицаРасходыПоНалогу - ТаблицаЗначений - Отражение расходов по налогу.
//
Функция ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ);
	
КонецФункции

// Формирует движения регистров по данным расчета налога.
//
// Параметры:
//	ТаблицыНачислениеНалога - Структура - Содержит таблицы с результатами расчета налога.
// 	ТаблицаРеквизитов - ТаблицаЗначений - Таблица реквизитов документа.
//	Движения - КоллекцияДвижений - Движения документа.
//	Отказ - Булево - Признак отказа от формирования движений.
//
Процедура СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ) Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ);
	
КонецПроцедуры

// Добавляем в состав параметров таблицу значений со структурой колонок для справки - расчета.
// 
// Параметры:
//	Параметры - Структура - Содержит параметры формирования движений для справки-расчета.
//	Реквизиты - СтрокаТаблицыЗначений - Содержит поля:
//		* ВидНалога - ПеречислениеСсылка.ВидыИмущественныхНалогов - Вид налога
//	ТаблицаСправкиРасчета - ТаблицаЗначений - По структуре соответствует регистру сведений для соответствующего налога
//			(РасчетТранспортногоНалога и др.).
//
Процедура ДополнитьПараметрыДвиженийСправкаРасчет(Параметры, Реквизиты, ТаблицаСправкиРасчета) Экспорт
	
	СписокОбязательныхКолонок = "";
	
	// Подготовка таблицы Параметры.СправкаРасчет
	Если Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ "ПериодРасчета,"										// <Дата> - период расчета (конец квартала)
		+ "ДатаСведений,"										// <Дата>
		+ "Организация,"										// <СправочникСсылка.Организации> - организация
		+ "ВидНалоговойБазы,"									// <ПеречислениеСсылка.НалоговаяБазаПоНалогуНаИмущество>
		+ "ИФНС,"												// <СправочникСсылка.РегистрацииВНалоговомОргане>
		+ "КодПоОКТМО,"											// <Строка, 11> 
		+ "КодВидаИмущества,"									// <Строка, 2>
		+ "ОсновноеСредство,"									// <СправочникСсылка.ОсновныеСредства>
		+ "ДоляВПравеОбщейСобственностиЧислитель,"				// <Число, 10, 0>
		+ "ДоляВПравеОбщейСобственностиЗнаменатель,"			// <Число, 10, 0>
		+ "ДоляПлощадиЧислитель,"								// <Число, 10, 0>
		+ "ДоляПлощадиЗнаменатель,"								// <Число, 10, 0>
		+ "ДоляСтоимостиЧислитель,"								// <Число, 10, 0>
		+ "ДоляСтоимостиЗнаменатель,"							// <Число, 10, 0>
		+ "КБК,"												// <Строка, 20>
		+ "КадастровыйНомер,"									// <Строка, 100>
		+ "КадастровыйНомерПомещения,"							// <Строка, 100>
		+ "УсловныйНомер,"										// <Строка, 100>
		+ "КадастроваяСтоимость,"								// <Число, 15, 0>
		+ "НеоблагаемаяКадастроваяСтоимость,"					// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0101,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0102,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0103,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0104,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0105,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0106,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0107,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0108,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0109,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0110,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0111,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0112,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа3112,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНедвижимости,"					// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьОсновныхСредств,"					// <Число, 15, 0>
		+ "СредняяСтоимость,"									// <Число, 15, 0>
		+ "НеоблагаемаяСредняяСтоимость,"						// <Число, 15, 0>
		+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"	// <Строка, 7>
		+ "НалоговаяБаза,"										// <Число, 15, 0>
		+ "ПонижениеНалоговойСтавки,"							// <Булево>
		+ "НалоговаяСтавка,"									// <Число, 3, 2>
		+ "СуммаНалога,"										// <Число, 15, 0>
		+ "СуммаУменьшенияСуммыНалога,"							// <Число, 15, 0>
		+ "СуммаНалогаКУплате,"									// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей,"								// <Число, 15, 0>
		+ "КоличествоМесяцевВладения,"							// <Число, 2, 0>
		+ "КоличествоМесяцевИспользования";						// <Число, 2, 0>
		
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ "ПериодРасчета,"							// <Дата> - период расчета (конец квартала)
		+ "ДатаРегистрационныхДанных,"				// <Дата>
		+ "Организация,"							// <СправочникСсылка.Организации> - организация
		+ "ИФНС,"									// <СправочникСсылка.РегистрацииВНалоговомОргане>
		+ "КодПоОКТМО,"								// <Строка, 11> 
		+ "ОсновноеСредство,"						// <СправочникСсылка.ОсновныеСредства>
		+ "КодВидаТранспортногоСредства,"			// <Строка, 100>
		+ "ИдентификационныйНомер,"					// <Строка, 50>	
		+ "Марка,"									// <Строка, 25>
		+ "РегистрационныйЗнак,"					// <Строка, 25>
		+ "НалоговаяБаза,"							// <Число, 15, 2>
		+ "ЕдиницаИзмерения,"						// <Строка, 5>
		+ "КоличествоМесяцевВладения,"				// <Число, 2, 0>
		+ "НалоговаяСтавка,"						// <Число, 3, 2>
		+ "СуммаНалога,"							// <Число, 15, 0>
		+ "КоличествоМесяцевЛьготы,"				// <Число, 2, 0>
		+ "КодНалоговойЛьготы,"						// <Строка, 6>
		+ "РегиональныйКодЛьготы,"					// <Строка, 12>
		+ "ПроцентУменьшения,"						// <Число, 4, 2>
		+ "ЛьготнаяСтавка,"							// <Число, 8, 2>
		+ "СуммаНалоговойЛьготы,"					// <Число, 15, 0>
		+ "СуммаНалогаКУплате,"						// <Число, 15, 0>
		+ "ЭкологическийКласс,"						// <Строка, 1>
		+ "ПовышающийКоэффициент,"					// <Число, 3, 1>
		+ "КоличествоЛетПрошедшихСГодаВыпускаТС,"	// <Число, 3, 0>
		+ "ДоляВПравеОбщейСобственностиЧислитель,"	// <Число, 10, 0>
		+ "ДоляВПравеОбщейСобственностиЗнаменатель,"// <Число, 10, 0>
		+ "СуммаАвансовыхПлатежей1Кв,"				// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей2Кв,"				// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей3Кв,"				// <Число, 15, 0>
		+ "ЗарегистрированоВРеестреСистемыПлатон,"	// <Булево>
		+ "СуммаНалоговогоВычета,"					// <Число, 15, 0>
		+ "ДатаРегистрации,"						// <Дата>
		+ "ДатаПрекращенияРегистрации,"				// <Дата>
		+ "СуммаПлатыОператоруСистемыПлатон";		// <Число, 15, 0>
			
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ "ПериодРасчета,"												// <Дата> - период расчета (конец квартала)
		+ "Организация,"												// <СправочникСсылка.Организации> - организация
		+ "ИФНС,"														// <СправочникСсылка.РегистрацииВНалоговомОргане>
		+ "ОсновноеСредство,"											// <СправочникСсылка.ОсновныеСредства>
		+ "КадастровыйНомер,"											// <Строка, 100>
		+ "КБК,"														// <Строка, 20>	
		+ "КодПоОКТМО,"													// <Строка, 11> 
		+ "КодКатегорииЗемель,"											// <Строка, 12>
		+ "ПериодСтроительства,"										// <Строка, 1>	
		+ "КадастроваяСтоимость,"										// <Число, 15, 0>	
		+ "ДоляВПравеОбщейСобственностиЧислитель,"						// <Число, 10, 0>	
		+ "ДоляВПравеОбщейСобственностиЗнаменатель,"					// <Число, 10, 0>
		+ "УменьшениеНалоговойБазыНаСумму,"								// <Булево>
		+ "НеОблагаемаяНалогомСумма,"									// <Число, 15, 0>
		+ "УменьшениеНалоговойБазыПоСтатье391,"							// <Булево>
		+ "КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391," 		// <Строка, 7>
		+ "НалоговаяЛьготаПоНалоговойБазе," 							// <ПеречислениеСсылка.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу>
		+ "ДоляНеоблагаемойПлощадиЧислитель,"							// <Число, 10, 0>
		+ "ДоляНеоблагаемойПлощадиЗнаменатель,"							// <Число, 10, 0>
		+ "НалоговаяБаза,"												// <Число, 15, 0>
		+ "КоличествоМесяцевВладения,"									// <Число, 2, 0>
		+ "КоличествоМесяцевИспользования,"								// <Число, 2, 0>
		+ "КоличествоМесяцевПримененияЛьготы,"							// <Число, 2, 0>	
		+ "НалоговаяСтавка,"											// <Число, 3, 2>
		+ "СуммаНалога,"												// <Число, 15, 0>
		+ "КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395," // <Строка, 7>	
		+ "СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога,"				// <Число, 15, 0>
		+ "СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки,"			// <Число, 15, 0>
		+ "ПредставлениеНалоговойЛьготы," 								// <Строка, 250>
		+ "СниженнаяНалоговаяСтавка," 									// <Число, 5, 4>
		+ "ПроцентУменьшенияСуммыНалога," 								// <Число, 7, 4>
		+ "СуммаНалогаКУплате,"											// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей1Кв,"									// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей2Кв,"									// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей3Кв";									// <Число, 15, 0>
	
	КонецЕсли;	
		
	Параметры.Вставить("СправкаРасчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСправкиРасчета, СписокОбязательныхКолонок));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, ПериодРасчета)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетТранспортногоНалога.ПериодРасчета КАК ПериодРасчета,
	|	РасчетТранспортногоНалога.Организация КАК Организация,
	|	РасчетТранспортногоНалога.ИФНС КАК ИФНС,
	|	РасчетТранспортногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетТранспортногоНалога.ДатаРегистрационныхДанных КАК ДатаРегистрационныхДанных,
	|	РасчетТранспортногоНалога.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетТранспортногоНалога.Марка КАК Марка,
	|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетТранспортногоНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетТранспортногоНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасчетТранспортногоНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетТранспортногоНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетТранспортногоНалога.КоличествоМесяцевЛьготы КАК КоличествоМесяцевЛьготы,
	|	РасчетТранспортногоНалога.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РасчетТранспортногоНалога.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РасчетТранспортногоНалога.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РасчетТранспортногоНалога.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РасчетТранспортногоНалога.СуммаНалоговойЛьготы КАК СуммаНалоговойЛьготы,
	|	РасчетТранспортногоНалога.СуммаНалогаКУплате КАК СуммаНалогаКУплате,
	|	РасчетТранспортногоНалога.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей1Кв КАК СуммаАвансовыхПлатежей1Кв,
	|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей2Кв КАК СуммаАвансовыхПлатежей2Кв,
	|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей3Кв КАК СуммаАвансовыхПлатежей3Кв,
	|	РасчетТранспортногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетТранспортногоНалога.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС,
	|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетТранспортногоНалога.ЗарегистрированоВРеестреСистемыПлатон КАК ЗарегистрированоВРеестреСистемыПлатон,
	|	РасчетТранспортногоНалога.СуммаНалоговогоВычета КАК СуммаНалоговогоВычета,
	|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаРегистрации,
	|	РасчетТранспортногоНалога.ДатаПрекращенияРегистрации КАК ДатаПрекращенияРегистрации
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.Активность = ИСТИНА
	|	И РасчетТранспортногоНалога.Организация = &Организация
	|	И РасчетТранспортногоНалога.ПериодРасчета = &ПериодРасчета";
	
	РасчетНалога = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаРасчета Из РасчетНалога Цикл
		СтрокаРасчета.КодПоОКТМО = СокрЛП(СтрокаРасчета.КодПоОКТМО);
	КонецЦикла;
	
	Возврат РасчетНалога;
	
КонецФункции

#Область РасчетНалогаНаИмущество

Функция ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоСреднейСтоимости(ПараметрыРасчета)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("Период", ПараметрыРасчета.мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("ТолькоНедвижимость", ПараметрыРасчета.ПериодРасчета >= '20190101');
	
	// Запомним какие были таблицы до начала расчета, чтобы выполнения удалить все кроме этих таблиц.
	ВременныеТаблицыДоРасчета = ОбщегоНазначенияБП.СписокВременныхТаблиц(Запрос.МенеджерВременныхТаблиц);
	
	// Признак применения ЕНВД.
	Если ПараметрыРасчета.ПлательщикЕНВД_НаКонецКвартала Тогда 
		РасчетИмущественныхНалоговПереопределяемый.СоздатьВТКоэффициентыЕНВД(ПараметрыРасчета); 
	КонецЕсли;

	Если ПараметрыРасчета.ПериодРасчета >= '20190101' Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	NULL КАК ОС
		|ПОМЕСТИТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013";
		Запрос.Выполнить();
		
	Иначе
		
		РасчетИмущественныхНалоговПереопределяемый.СоздатьВТДвижимоеИмуществоПринятоеКУчетуПосле2013(ПараметрыРасчета);
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период КАК Период,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.УсловныйНомер КАК УсловныйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_ВсеЗаписи
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(&Период, Организация = &Организация) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
	|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период) КАК Период
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_Период
	|ИЗ
	|	ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_ВсеЗаписи КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.УсловныйНомер КАК УсловныйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
	|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
	|ПОМЕСТИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета
	|ИЗ
	|	ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_Период КАК ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_ВсеЗаписи КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|		ПО (СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство = ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_Период.ОсновноеСредство)
	|			И (СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период = ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_Период.Период)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_ВсеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета_Период";
	Запрос.Выполнить();
	
	ТекстОбъединяющегоЗапроса = "";
	
	Период = ПараметрыРасчета.мДатаНачалаПериодаОтчета;
	Счетчик = 0;
	
	ИспользуемыеВременныеТаблицы = ОбщегоНазначенияБП.СписокВременныхТаблиц(Запрос.МенеджерВременныхТаблиц);
	
	Пока Период <= НачалоМесяца(ПараметрыРасчета.мДатаКонцаПериодаОтчета) Цикл
		
		Счетчик = Счетчик + 1;
		Период = ДобавитьМесяц(ПараметрыРасчета.мДатаНачалаПериодаОтчета, Счетчик - 1);
			
		ПрименениеУСН = УчетнаяПолитика.ПрименяетсяУСН(
							ПараметрыРасчета.Организация,
							?(Период < ПараметрыРасчета.мДатаКонцаПериодаОтчета, Период, ПараметрыРасчета.мДатаКонцаПериодаОтчета));
			
		Если ПрименениеУСН Тогда
			Продолжить;
		КонецЕсли;
			
		ИмяВременнойТаблицы = "Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН=");
		ИспользуемыеВременныеТаблицы.Добавить(ИмяВременнойТаблицы);
		
		СписокЗапросов = Новый Массив;
		
		// Срез последних записей по основному средству в регистре "Ставки налога на имущество по отдельным основным средствам".
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период) КАК ПериодСреза
		|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период <= &КонецПериода
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза КАК НалоговаяБаза,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения КАК ПорядокНалогообложения
		|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ИЗ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов КАК СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
		|			И СтавкиНалогаНаИмуществоПоОтдельнымОСПериодов.ПериодСреза = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период";
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаНеСнятыеСУчетаОС(ПараметрыРасчета, СписокЗапросов);
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ СписокРазрешенныхОС
		|ИЗ
		|	ВТ_НеСнятыеСУчетаОС КАК ОС
		|ГДЕ
		|	НЕ ОС.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
		|	И НЕ ОС.ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|				ИЗ
		|					СтавкиНалогаНаИмуществоПоОтдельнымОС
		|				ГДЕ
		|					СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
		|	И НЕ ОС.ОсновноеСредство В
		|				(ВЫБРАТЬ
		|					СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|				ИЗ
		|					СтавкиНалогаНаИмуществоПоОтдельнымОС
		|				ГДЕ
		|					СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению))
		|	И (НЕ &ТолькоНедвижимость
		|			ИЛИ ОС.НедвижимоеИмущество)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_НеСнятыеСУчетаОС";
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСписокОС(СписокЗапросов);
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОС.Период) КАК Период,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_СрезПоследних
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И ОсновноеСредство В
		|					(ВЫБРАТЬ
		|						СписокОС.ОсновноеСредство
		|					ИЗ
		|						СписокОС КАК СписокОС)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган КАК НалоговыйОрган,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения КАК ПорядокНалогообложения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодНалоговойЛьготыОсвобождениеОтНалогообложения КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.УсловныйНомер КАК УсловныйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомер КАК КадастровыйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС
		|ИЗ
		|	ВТ_СрезПоследних КАК СрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
		|				&КонецПериода,
		|				Организация = &Организация
		|					И ОсновноеСредство В
		|						(ВЫБРАТЬ
		|							СписокОС.ОсновноеСредство
		|						ИЗ
		|							СписокОС)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
		|		ПО СрезПоследних.Период = СтавкиНалогаНаИмуществоПоОтдельнымОС.Период
		|			И СрезПоследних.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтавкиНалогаНаИмуществоСрезПоследних.Период КАК Период,
		|	СтавкиНалогаНаИмуществоСрезПоследних.Организация КАК Организация,
		|	СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
		|	СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки КАК СнижениеНалоговойСтавки,
		|	СтавкиНалогаНаИмуществоСрезПоследних.УдалитьСниженнаяНалоговаяСтавка КАК УдалитьСниженнаяНалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоСрезПоследних.УменьшениеСуммыНалогаВПроцентах КАК УменьшениеСуммыНалогаВПроцентах,
		|	СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения КАК ПроцентУменьшения,
		|	СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавкаДвижимоеИмущество КАК НалоговаяСтавкаДвижимоеИмущество,
		|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
		|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
		|	СтавкиНалогаНаИмуществоСрезПоследних.ПриНаличии1ЛьготыЗаполнять1ЛистРаздела2 КАК ПриНаличии1ЛьготыЗаполнять1ЛистРаздела2
		|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СтавкиНалогаНаИмуществоСрезПоследних";
		
		СписокЗапросов.Добавить(ТекстЗапроса);
		
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаИсторияРегистрацийВНалоговомОргане(СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСчетаБухгалтерскогоУчета(СписокЗапросов);
		РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаСтоимостьИАмортизация(Период, ПараметрыРасчета, СписокЗапросов);
		
		Если ПараметрыРасчета.ПлательщикЕНВД_НаКонецКвартала Тогда
			
			// Получение коэффициента доли выручки от деятельности, облагаемой ЕНВД.
			ПериодПолученияКоэффициентаЕНВД = ?(Период = ПараметрыРасчета.мДатаНачалаПериодаОтчета, Период, ДобавитьМесяц(Период, -1));
			
			КоэффициентРаспределенияЕНВД = УправлениеВнеоборотнымиАктивами.КоэффициентРаспределенияВыручкиПоВидамДеятельности(
												ПараметрыРасчета.Организация, 
												НачалоКвартала(ПериодПолученияКоэффициентаЕНВД), 
												КонецКвартала(ПериодПолученияКоэффициентаЕНВД));
			
			Запрос.УстановитьПараметр("КоэффициентРаспределенияЕНВД", КоэффициентРаспределенияЕНВД);
			
			РасчетИмущественныхНалоговПереопределяемый.ДобавитьТекстЗапросаКоэффициентыЕНВД_ПоОС(СписокЗапросов);
			
			ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СписокОС.ОсновноеСредство,
			|
			// ЭтоПерваяВтораяАмортизационныеГруппы
			|	ВЫБОР
			|		КОГДА СписокОС.ОсновноеСредство.АмортизационнаяГруппа В (
			|				ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПерваяГруппа), 
			|				ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВтораяГруппа))
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоПерваяВтораяАмортизационныеГруппы,
			|
			// РегистрацияВНалоговомОргане
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговыйОрган
			|		ИНАЧЕ 
			|			ЕСТЬNULL(ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган)
			|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
			|
			// КодПоОКТМО
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
			|		  ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|		ТОГДА 
			|			СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
			|		ИНАЧЕ 
			|			ЕСТЬNULL(ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
			|	КОНЕЦ КАК КодПоОКТМО,
			|
			// НалоговаяСтавка
			|	ЕСТЬNULL(ВЫБОР
			|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
			|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.СниженнаяНалоговаяСтавка
			|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяСтавка
			|			ИНАЧЕ
			|				ВЫБОР
			|					КОГДА СписокОС.ОсновноеСредство В (ВЫБРАТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ИЗ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013)
			|						И НЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|						И &Год >= 2018 ТОГДА
			|							СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавкаДвижимоеИмущество
			|					ИНАЧЕ
			|							СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
			|				КОНЕЦ
			|			КОНЕЦ, СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка) КАК НалоговаяСтавка,
			|
			// СнижениеНалоговойСтавки
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки, ЛОЖЬ)
			|	КОНЕЦ КАК СнижениеНалоговойСтавки,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
			|
			// ОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения, ЛОЖЬ)
			|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
			|	СписокОС.ОсновноеСредство.НедвижимоеИмущество КАК ЭтоНедвижимость,
			|
			// ДвижимоеИмуществоПринятоеКУчетуПосле2013
			|	ВЫБОР 
			|		КОГДА СписокОС.ОсновноеСредство В (
			|				ВЫБРАТЬ 
			|					ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС 
			|				ИЗ 
			|					ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013) 
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК ДвижимоеИмуществоПринятоеКУчетуПосле2013,
			|
			// КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
			|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
			|
			|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОС.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.УсловныйНомер КАК УсловныйНомер,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомер КАК КадастровыйНомер,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КБК КАК КБК,
			|	" + Счетчик + " КАК Период,
			|
			// ОстаточнаяСтоимость
			|	ВЫРАЗИТЬ((ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) 
			|				- ЕСТЬNULL(ВЫБОР 
			|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
			|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
			|									ТОГДА АмортизацияОС.СуммаОстатокДт 
			|								ИНАЧЕ АмортизацияОС.СуммаОстатокКт 
			|							КОНЕЦ, 0)) * КоэффициентыЕНВД.НеЕНВД
			|				+ (ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) 
			|					- ЕСТЬNULL(ВЫБОР 
			|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
			|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
			|									ТОГДА АмортизацияОС.СуммаОстатокДт 
			|								ИНАЧЕ АмортизацияОС.СуммаОстатокКт 
			|							КОНЕЦ, 0)) * КоэффициентыЕНВД.Распределение * &КоэффициентРаспределенияЕНВД КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
			|
			|ПОМЕСТИТЬ " + ИмяВременнойТаблицы + "
			|ИЗ
			|	СписокОС
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчета КАК СчетаБухгалтерскогоУчета
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяСтоимостьОС КАК ПервоначальнаяСтоимостьОС
			|			ПО СчетаБухгалтерскогоУчета.ОсновноеСредство = ПервоначальнаяСтоимостьОС.ОсновноеСредство
			|			 И (СчетаБухгалтерскогоУчета.СчетУчета = ПервоначальнаяСтоимостьОС.Счет 
			|					ИЛИ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС) = ПервоначальнаяСтоимостьОС.Счет)
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК АмортизацияОС
			|			ПО СчетаБухгалтерскогоУчета.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
			|			 И СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации = АмортизацияОС.Счет
			|		ПО СписокОС.ОсновноеСредство = СчетаБухгалтерскогоУчета.ОсновноеСредство
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета КАК СтавкиНалогаПоОтдельнымОС_НаДатуРасчета
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.ОсновноеСредство
			|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыЕНВД_ПоОС КАК КоэффициентыЕНВД
			|		ПО СписокОС.ОсновноеСредство = КоэффициентыЕНВД.ОсновноеСредство,
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
			|		ПО (ИСТИНА),
			|	ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
			
			СписокЗапросов.Добавить(ТекстЗапроса);
			
		Иначе
			
			ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СписокОС.ОсновноеСредство,
			|
			// ЭтоПерваяВтораяАмортизационныеГруппы
			|	ВЫБОР
			|		КОГДА СписокОС.ОсновноеСредство.АмортизационнаяГруппа В (
			|					ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПерваяГруппа), 
			|					ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВтораяГруппа))
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоПерваяВтораяАмортизационныеГруппы,
			|
			// РегистрацияВНалоговомОргане
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.НалоговыйОрган
			|		ИНАЧЕ 
			|			ЕСТЬNULL(ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане, &ОсновнойНалоговыйОрган)
			|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
			|
			// КодПоОКТМО
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
			|		  ИЛИ СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|		ТОГДА 
			|			СтавкиНалогаПоОтдельнымОС.КодПоОКТМО
			|		ИНАЧЕ 
			|			ЕСТЬNULL(ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
			|	КОНЕЦ КАК КодПоОКТМО,
			|
			// НалоговаяСтавка
			|	ЕСТЬNULL(ВЫБОР
			|			КОГДА СтавкиНалогаПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
			|				ТОГДА СтавкиНалогаПоОтдельнымОС.СниженнаяНалоговаяСтавка
			|			КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|				ТОГДА СтавкиНалогаПоОтдельнымОС.НалоговаяСтавка
			|			ИНАЧЕ
			|				ВЫБОР
			|					КОГДА СписокОС.ОсновноеСредство В (ВЫБРАТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС ИЗ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013)
			|						И НЕ ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ)
			|						И &Год >= 2018 ТОГДА
			|							СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавкаДвижимоеИмущество
			|					ИНАЧЕ
			|							СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
			|				КОНЕЦ
			|			КОНЕЦ, СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка) КАК НалоговаяСтавка,
			|
			// СнижениеНалоговойСтавки
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки, ЛОЖЬ)
			|	КОНЕЦ КАК СнижениеНалоговойСтавки,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавкиДвижимоеИмущество КАК СнижениеНалоговойСтавкиДвижимоеИмущество,
			|
			// ОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
			|			ТОГДА ИСТИНА
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения, ЛОЖЬ)
			|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
			|
			|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложенияДвижимогоИмущества КАК ОсвобождениеОтНалогообложенияДвижимогоИмущества,
			|	СписокОС.ОсновноеСредство.НедвижимоеИмущество КАК ЭтоНедвижимость,
			|
			// ДвижимоеИмуществоПринятоеКУчетуПосле2013
			|	ВЫБОР 
			|		КОГДА СписокОС.ОсновноеСредство В (
			|					ВЫБРАТЬ 
			|						ВТДвижимоеИмуществоПринятоеКУчетуПосле2013.ОС 
			|					ИЗ 
			|						ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013) 
			|			ТОГДА ИСТИНА 
			|		ИНАЧЕ ЛОЖЬ 
			|	КОНЕЦ КАК ДвижимоеИмуществоПринятоеКУчетуПосле2013,
			|
			// КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|	ВЫБОР
			|		КОГДА СтавкиНалогаПоОтдельнымОС.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
			|			ТОГДА СтавкиНалогаПоОтдельнымОС.КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
			|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
			|
			|	ЕСТЬNULL(СтавкиНалогаПоОтдельнымОС.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
			|	СтавкиНалогаПоОтдельнымОС.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
			|	СтавкиНалогаПоОтдельнымОС.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
			|	СтавкиНалогаПоОтдельнымОС.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.УсловныйНомер КАК УсловныйНомер,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомер КАК КадастровыйНомер,
			|	СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
			|	СтавкиНалогаПоОтдельнымОС.КБК КАК КБК,
			|	" + Счетчик + " КАК Период,
			|
			|	ВЫРАЗИТЬ(ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) 
			|				- ЕСТЬNULL(ВЫБОР 
			|								КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса) 
			|										ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ) 
			|									ТОГДА АмортизацияОС.СуммаОстатокДт 
			|								ИНАЧЕ АмортизацияОС.СуммаОстатокКт 
			|							КОНЕЦ, 0) КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
			|ПОМЕСТИТЬ " + ИмяВременнойТаблицы + "
			|ИЗ
			|	СписокОС
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчета КАК СчетаБухгалтерскогоУчета
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяСтоимостьОС КАК ПервоначальнаяСтоимостьОС
			|			ПО СчетаБухгалтерскогоУчета.ОсновноеСредство = ПервоначальнаяСтоимостьОС.ОсновноеСредство
			|			 И (СчетаБухгалтерскогоУчета.СчетУчета = ПервоначальнаяСтоимостьОС.Счет 
			|					ИЛИ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС) = ПервоначальнаяСтоимостьОС.Счет)
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК АмортизацияОС
			|			ПО СчетаБухгалтерскогоУчета.ОсновноеСредство = АмортизацияОС.ОсновноеСредство
			|			 И СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации = АмортизацияОС.Счет
			|		ПО СписокОС.ОсновноеСредство = СчетаБухгалтерскогоУчета.ОсновноеСредство
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОС КАК СтавкиНалогаПоОтдельнымОС
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС.ОсновноеСредство,
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам_НаДатуРасчета КАК СтавкиНалогаПоОтдельнымОС_НаДатуРасчета
			|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаПоОтдельнымОС_НаДатуРасчета.ОсновноеСредство
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
			|		ПО (ИСТИНА),
			|	ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
			
			СписокЗапросов.Добавить(ТекстЗапроса);
			
		КонецЕсли;
		
		Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
		
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.УстановитьПараметр("КонецПериода", ?(Год(Период) = Год(ПараметрыРасчета.ПериодРасчета), Период, ПараметрыРасчета.мДатаКонцаПериодаОтчета));
		Запрос.УстановитьПараметр("Год", Год(ПараметрыРасчета.ПериодРасчета));
		Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРасчета.РегистрацияВНалоговомОргане);
		РасчетИмущественныхНалоговПереопределяемый.УстановитьПараметрыЗапросаПриРасчетеНалогаЗаПериод(Период, Запрос);
		
		Запрос.Выполнить();
		
		ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ НалоговаяБазаПоНалогуНаИмущество
									  |",
									  "")
								  + "ИЗ " + ИмяВременнойТаблицы + "
		                            |ГДЕ
		                            |	ОстаточнаяСтоимость > 0 ИЛИ ЭтоНедвижимость
									|";
		
	КонецЦикла;
	
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,, ИспользуемыеВременныеТаблицы);
	
	Если ТекстОбъединяющегоЗапроса = "" Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос.Текст = ТекстОбъединяющегоЗапроса;	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("КонечныйПериод", ПараметрыРасчета.Делитель);
	Если ПараметрыРасчета.ПериодРасчета >= '20180101' Тогда
		Запрос.УстановитьПараметр("КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013", "2012000");
	Иначе
		Запрос.УстановитьПараметр("КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013", "2010257");
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НалоговаяБазаПоНалогуНаИмущество.Период КАК Период,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЧислитель, 0) КАК ДоляСтоимостиЧислитель,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЗнаменатель, 0) КАК ДоляСтоимостиЗнаменатель,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.УсловныйНомер, """") КАК УсловныйНомер,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КадастровыйНомер, """") КАК КадастровыйНомер,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КадастровыйНомерПомещения, """") КАК КадастровыйНомерПомещения,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КБК, """") КАК КБК,
	|	НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане КАК ИФНС,
	|	НалоговаяБазаПоНалогуНаИмущество.КодПоОКТМО КАК КодПоОКТМО,
	|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоНалогуНаИмущество.СнижениеНалоговойСтавки
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &Год >= 2018
	|						И (НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|							И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ))
	|					ТОГДА НалоговаяБазаПоНалогуНаИмущество.СнижениеНалоговойСтавкиДвижимоеИмущество
	|			КОНЕЦ
	|	КОНЕЦ КАК ПонижениеНалоговойСтавки,
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложения
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &Год >= 2015
	|						И (НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|							И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ))
	|						И ВЫБОР
	|							КОГДА &Год >= 2018
	|								ТОГДА НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества
	|							ИНАЧЕ ИСТИНА
	|						КОНЕЦ
	|						И НЕ НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложения
	|			ТОГДА НалоговаяБазаПоНалогуНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения
	|		ИНАЧЕ ВЫБОР
	|				КОГДА &Год >= 2015
	|						И (НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|							И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ))
	|						И ВЫБОР
	|							КОГДА &Год >= 2018
	|								ТОГДА НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложенияДвижимогоИмущества
	|							ИНАЧЕ ИСТИНА
	|						КОНЕЦ
	|						И НЕ НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
	|					ТОГДА &КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	НалоговаяБазаПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы,
	|	ВЫБОР
	|		КОГДА &Год < 2015
	|					И НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
	|				ИЛИ &Год >= 2015
	|					И &Год < 2019
	|					И НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
	|			ТОГДА 0
	|		ИНАЧЕ НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость
	|	КОНЕЦ КАК ОстаточнаяСтоимость,
	|	ВЫБОР
	|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ЭтоНедвижимость
	|			ТОГДА НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОстаточнаяСтоимостьНедвижимости,
	|	НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость КАК ОстаточнаяСтоимостьОсновныхСредств
	|ПОМЕСТИТЬ ВТ_НалоговаяБаза
	|ИЗ
	|	НалоговаяБазаПоНалогуНаИмущество КАК НалоговаяБазаПоНалогуНаИмущество
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НалоговаяБаза.КодВидаИмущества КАК КодВидаИмущества,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	ВТ_НалоговаяБаза.ИФНС КАК ИФНС,
	|	ВТ_НалоговаяБаза.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_НалоговаяБаза.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
	|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения КАК ОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.КодНалоговойЛьготыОсвобождениеОтНалогообложения КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_НалоговаяБаза.УплачиваютсяАвансы КАК УплачиваютсяАвансы,
	|	ВТ_НалоговаяБаза.УсловныйНомер КАК УсловныйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомер КАК КадастровыйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	ВТ_НалоговаяБаза.КБК КАК КБК,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 1
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0101,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 2
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0102,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 3
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0103,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 4
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0104,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 5
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0105,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 6
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0106,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 7
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0107,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 8
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0108,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 9
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0109,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 10
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0110,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 11
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0111,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 12
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0112,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = 13
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа3112,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = &КонечныйПериод
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимостьНедвижимости
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьНедвижимости,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_НалоговаяБаза.Период = &КонечныйПериод
	|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимостьОсновныхСредств
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстаточнаяСтоимостьОсновныхСредств
	|ИЗ
	|	ВТ_НалоговаяБаза КАК ВТ_НалоговаяБаза
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_НалоговаяБаза.КодВидаИмущества,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель,
	|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель,
	|	ВТ_НалоговаяБаза.ИФНС,
	|	ВТ_НалоговаяБаза.КодПоОКТМО,
	|	ВТ_НалоговаяБаза.НалоговаяСтавка,
	|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки,
	|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	ВТ_НалоговаяБаза.ОсновноеСредство,
	|	ВТ_НалоговаяБаза.УплачиваютсяАвансы,
	|	ВТ_НалоговаяБаза.УсловныйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомер,
	|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения,
	|	ВТ_НалоговаяБаза.КБК";
	
	Результат = Запрос.Выполнить();
	
	// Удалим все таблицы, которые были созданы во время выполнения процедуры.
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(ПараметрыРасчета.МенеджерВременныхТаблиц,, ВременныеТаблицыДоРасчета);
	
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат.Выгрузить();
	
КонецФункции
 
Процедура ВыполнитьРасчетНалогаНаИмуществоПоСреднейСтоимости(РасчетНалога, ПараметрыРасчета, ТаблицаРасчетНалога)

	Если РасчетНалога = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КолонкиГруппировок = ""
	+ "УплачиваютсяАвансы,"
	+ "ИФНС,"
	+ "КодПоОКТМО,"
	+ "КодВидаИмущества,"
	+ "КБК,"
	+ "ДоляСтоимостиЧислитель,"
	+ "ДоляСтоимостиЗнаменатель,"
	+ "НалоговаяСтавка,"
	+ "ПонижениеНалоговойСтавки,"
	+ "ОсвобождениеОтНалогообложения,"
	+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения";
	
	КолонкиСуммирования = ""
	+ "ОстаточнаяСтоимостьНа0101,"
	+ "ОстаточнаяСтоимостьНа0102,"
	+ "ОстаточнаяСтоимостьНа0103,"
	+ "ОстаточнаяСтоимостьНа0104,"
	+ "ОстаточнаяСтоимостьНа0105,"
	+ "ОстаточнаяСтоимостьНа0106,"
	+ "ОстаточнаяСтоимостьНа0107,"
	+ "ОстаточнаяСтоимостьНа0108,"
	+ "ОстаточнаяСтоимостьНа0109,"
	+ "ОстаточнаяСтоимостьНа0110,"
	+ "ОстаточнаяСтоимостьНа0111,"
	+ "ОстаточнаяСтоимостьНа0112,"
	+ "ОстаточнаяСтоимостьНа3112";
	
	КолонкиРасчета = "" 
	+ "СредняяСтоимость,"
	+ "НеоблагаемаяСредняяСтоимость,"
	+ "НалоговаяБаза,"
	+ "СуммаНалога,"
	+ "СуммаУменьшенияСуммыНалога,"
	+ "СуммаНалогаКУплате,"
	+ "СуммаАвансовыхПлатежей1Кв,"
	+ "СуммаАвансовыхПлатежей2Кв,"
	+ "СуммаАвансовыхПлатежей3Кв";
	
	РасчетНалогаПоЛистам = РасчетНалога.Скопировать();
	РасчетНалогаПоЛистам.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
	
	ДопКолонки = Новый Структура(КолонкиРасчета);
	Для каждого Колонка Из ДопКолонки Цикл
		РасчетНалогаПоЛистам.Колонки.Добавить(Колонка.Ключ,
			ОбщегоНазначения.ОписаниеТипаЧисло(15, 0, ДопустимыйЗнак.Неотрицательный));
	КонецЦикла;
	
	// С целью оптимизации производительности список имён полей-периодов формируется заранее, а не динамически в циклах.
	КолонкиПериоды = Новый ТаблицаЗначений;
	КолонкиПериоды.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)));
	КолонкиПериоды.Колонки.Добавить("ОстаточнаяСтоимостьИмя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(25, ДопустимаяДлина.Фиксированная)));
	Для Счетчик = 1 По ПараметрыРасчета.Делитель Цикл
		
		КолонкаПериод = КолонкиПериоды.Добавить();
		КолонкаПериод.Счетчик = Счетчик;
		КолонкаПериод.ОстаточнаяСтоимостьИмя = "ОстаточнаяСтоимостьНа" + ?(Счетчик = 13, "3112", "01" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
	
	КонецЦикла;
		
	Отбор = Новый Структура(КолонкиГруппировок);
	РасчетНалогаПоЛистам.Индексы.Добавить(КолонкиГруппировок);
		
	Для Каждого СтрокаТаблицы Из РасчетНалогаПоЛистам Цикл
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет И НЕ СтрокаТаблицы.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет И (СтрокаТаблицы.КодВидаИмущества = "04" ИЛИ СтрокаТаблицы.КодВидаИмущества = "07") Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
		
		ДоляСтоимости = 1;
		
		КодВидаИмущества = ?(ПустаяСтрока(СтрокаТаблицы.КодВидаИмущества), "03", СтрокаТаблицы.КодВидаИмущества);
		Если КодВидаИмущества = "04" Тогда
			ДоляСтоимости = 0;
		ИначеЕсли КодВидаИмущества = "02" Тогда
			ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;
		КонецЕсли;
		
		ДелимоеСредняяСтоимость = 0;
		ДелимоеНеоблагаемаяСредняяСтоимость = 0;
		Для каждого КолонкаПериод Из КолонкиПериоды Цикл
			Счетчик = КолонкаПериод.Счетчик;
			
			СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя]);
			ДелимоеСредняяСтоимость = ДелимоеСредняяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
			
			Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
				ДелимоеНеоблагаемаяСредняяСтоимость = ДелимоеНеоблагаемаяСредняяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
			КонецЕсли;
			
			Если УчитыватьАвансы И Счетчик - 1 <> 0 И (Счетчик - 1) % 3 = 0 Тогда
				
				ПрименениеУСН = УчетнаяПолитика.ПрименяетсяУСН(ПараметрыРасчета.Организация, Дата(Год(ПараметрыРасчета.ПериодРасчета), Счетчик - 3, 1));
				СредняяСтоимость = ?(ПрименениеУСН, 0, ДелимоеСредняяСтоимость / Счетчик);
				НеоблагаемаяСредняяСтоимость = ?(ПрименениеУСН, 0, ДелимоеНеоблагаемаяСредняяСтоимость / Счетчик);
				
				Если Счетчик = 4 Тогда
					СредняяСтоимость1Кв = СредняяСтоимость;
					НеоблагаемаяСредняяСтоимость1Кв = НеоблагаемаяСредняяСтоимость;
				ИначеЕсли Счетчик = 7 Тогда
					СредняяСтоимость2Кв = СредняяСтоимость;
					НеоблагаемаяСредняяСтоимость2Кв = НеоблагаемаяСредняяСтоимость;
				ИначеЕсли Счетчик = 10 Тогда
					СредняяСтоимость3Кв = СредняяСтоимость;
					НеоблагаемаяСредняяСтоимость3Кв = НеоблагаемаяСредняяСтоимость;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаТаблицы.СредняяСтоимость = Окр(ДелимоеСредняяСтоимость / ПараметрыРасчета.Делитель);
		СтрокаТаблицы.НеоблагаемаяСредняяСтоимость = Окр(ДелимоеНеоблагаемаяСредняяСтоимость / ПараметрыРасчета.Делитель);
		
		СтоимостьОблагаемогоНалогомИмущества = Макс(СтрокаТаблицы.СредняяСтоимость - СтрокаТаблицы.НеоблагаемаяСредняяСтоимость, 0);
		
		Если УчитыватьАвансы Тогда
			СтоимостьОблагаемогоНалогомИмущества1Кв = Макс(СредняяСтоимость1Кв - НеоблагаемаяСредняяСтоимость1Кв, 0);
			СтоимостьОблагаемогоНалогомИмущества2Кв = Макс(СредняяСтоимость2Кв - НеоблагаемаяСредняяСтоимость2Кв, 0);
			СтоимостьОблагаемогоНалогомИмущества3Кв = Макс(СредняяСтоимость3Кв - НеоблагаемаяСредняяСтоимость3Кв, 0);
		КонецЕсли;
		
		СтрокаТаблицы.НалоговаяБаза = Окр(СтоимостьОблагаемогоНалогомИмущества * ДоляСтоимости);	
		
		Если ПараметрыРасчета.ГодовойРасчет Тогда
			СтрокаТаблицы.СуммаНалога = Окр(СтрокаТаблицы.НалоговаяБаза * СтрокаТаблицы.НалоговаяСтавка / 100);
		Иначе	
			СтрокаТаблицы.СуммаНалога = Окр(СтоимостьОблагаемогоНалогомИмущества
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
		КонецЕсли;
		
		Если УчитыватьАвансы Тогда
			
			СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = Окр(СтоимостьОблагаемогоНалогомИмущества1Кв 
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
			СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = Окр(СтоимостьОблагаемогоНалогомИмущества2Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
			СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = Окр(СтоимостьОблагаемогоНалогомИмущества3Кв
				* ДоляСтоимости * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
			
		КонецЕсли;
		
		
		Если СтрокаТаблицы.ИФНС = ПараметрыРасчета.РегистрацияВНалоговомОргане Тогда
		
			СтрокаТаблицы.СуммаУменьшенияСуммыНалога = Окр(СтрокаТаблицы.СуммаНалога * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100);
			СтрокаТаблицы.СуммаНалогаКУплате = Макс(СтрокаТаблицы.СуммаНалога - СтрокаТаблицы.СуммаУменьшенияСуммыНалога, 0);
			
			Если УчитыватьАвансы Тогда
				
				СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100);
				СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100);
				СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв
					- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100);
					
			КонецЕсли;
			
		Иначе
			
			СтрокаТаблицы.СуммаНалогаКУплате = СтрокаТаблицы.СуммаНалога;
			
		КонецЕсли;
		
		// Распределим полученные суммы по объектам ОС
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
		РасчетНалогаПоОбъектамОС = РасчетНалога.Скопировать(Отбор);
			
		КоэффициентыРаспределения = Новый Массив;
		КоэффициентыРаспределения1Кв = Новый Массив;
		КоэффициентыРаспределения2Кв = Новый Массив;
		КоэффициентыРаспределения3Кв = Новый Массив;
		
		Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
			
			ДелимоеСредняяСтоимость = 0;
			Для каждого КолонкаПериод Из КолонкиПериоды Цикл
				Счетчик = КолонкаПериод.Счетчик;
				
				СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя], 2);
				ДелимоеСредняяСтоимость = ДелимоеСредняяСтоимость + СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя];
				
				Если УчитыватьАвансы Тогда
					Если Счетчик = 4 Тогда
						СредняяСтоимость1Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
					ИначеЕсли Счетчик = 7 Тогда
						СредняяСтоимость2Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
					ИначеЕсли Счетчик = 10 Тогда
						СредняяСтоимость3Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
					КонецЕсли;			
				КонецЕсли;
				
			КонецЦикла;
			СредняяСтоимость = Окр(ДелимоеСредняяСтоимость / ПараметрыРасчета.Делитель, 2);
			
			КоэффициентыРаспределения.Добавить(СредняяСтоимость);
			
			Если УчитыватьАвансы Тогда
				КоэффициентыРаспределения1Кв.Добавить(СредняяСтоимость1Кв);
				КоэффициентыРаспределения2Кв.Добавить(СредняяСтоимость2Кв);
				КоэффициентыРаспределения3Кв.Добавить(СредняяСтоимость3Кв);
			КонецЕсли;
			
		КонецЦикла;	
		
		Для каждого Колонка Из ДопКолонки Цикл
			
			РасчетНалогаПоОбъектамОС.Колонки.Добавить(Колонка.Ключ,
				ОбщегоНазначения.ОписаниеТипаЧисло(15, 2, ДопустимыйЗнак.Неотрицательный));
				
			Если Колонка.Ключ = "СуммаАвансовыхПлатежей1Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения1Кв;
			ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей2Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения2Кв;
			ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей3Кв" Тогда
				Коэффициенты = КоэффициентыРаспределения3Кв;
			Иначе
				Коэффициенты = КоэффициентыРаспределения;
			КонецЕсли;
				
			Если СтрокаТаблицы[Колонка.Ключ] > 0 Тогда
				РаспределенныеСуммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
										СтрокаТаблицы[Колонка.Ключ], Коэффициенты);
				Если РаспределенныеСуммы <> Неопределено Тогда
					РасчетНалогаПоОбъектамОС.ЗагрузитьКолонку(РаспределенныеСуммы, Колонка.Ключ);
				КонецЕсли;
			КонецЕсли;	
			
		КонецЦикла;
		
		Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
			
			НоваяСтрока = ТаблицаРасчетНалога.Добавить();
			НоваяСтрока.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.СреднегодоваяСтоимость;
			НоваяСтрока.КодВидаИмущества = КодВидаИмущества;
			
			СвойстваПоСтрокеТаблицы = ""
			+ "ИФНС,"
			+ "КодПоОКТМО,"
			+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"
			+ "НалоговаяСтавка,"
			+ "КБК,"
			+ "ПонижениеНалоговойСтавки";
			
			Если КодВидаИмущества = "02" Тогда
				СвойстваПоСтрокеТаблицы = СвойстваПоСтрокеТаблицы + ","
				+ "ДоляСтоимостиЧислитель,"
				+ "ДоляСтоимостиЗнаменатель";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, СвойстваПоСтрокеТаблицы);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПоОбъектуОС, ,СвойстваПоСтрокеТаблицы);
			
			Если УчитыватьАвансы Тогда
				
				НоваяСтрока.СуммаАвансовыхПлатежей = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв
													+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв
													+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв;
				
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
				
			КонецЕсли;
			
			Если НЕ ПараметрыРасчета.ГодовойРасчет Тогда
				
				НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
				
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЦикла;
				
КонецПроцедуры

Функция ПолучитьДанныеДляРасчетаНалогаНаИмуществоПоКадастровойСтоимости(ПараметрыРасчета)

	ДанныеДляРасчета = Неопределено;
	
	Если ПараметрыРасчета.ПериодРасчета >= '20150101' 
		ИЛИ ПараметрыРасчета.ПрименениеОСНО 
		ИЛИ (ПараметрыРасчета.ПериодРасчета >= '20140701' 
				И ПараметрыРасчета.ПериодРасчета < '20150101' 
				И ПараметрыРасчета.ПлательщикЕНВД) Тогда
	
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ПараметрыРасчета.МенеджерВременныхТаблиц;
		
		НачалоПериода = ?(ПараметрыРасчета.ГодовойРасчет ИЛИ ПараметрыРасчета.ПериодРасчета < '20190101', 
							НачалоГода(ПараметрыРасчета.ПериодРасчета), 
							НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
		
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(ПараметрыРасчета.ПериодРасчета));
		Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
		Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", ПараметрыРасчета.РегистрацияВНалоговомОргане);
		Запрос.УстановитьПараметр("ПрименениеУСН", ПараметрыРасчета.ПрименениеУСН);
		
		Если ПараметрыРасчета.ПериодРасчета < '20190101' Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК ДатаСведений,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО
			|ПОМЕСТИТЬ ВТ_ЗаписиЗаПериод
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
			|ГДЕ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения <> ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению)
			|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаРегистрацииПраваСобственности > &КонецПериода
			|	И ВЫБОР
			|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности < &НачалоПериода
			|		КОНЕЦ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
			|ГДЕ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ &НачалоПериода И &КонецПериода
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = &Организация
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения <> ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению)
			|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности > &КонецПериода
			|	И ВЫБОР
			|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности < &НачалоПериода
			|		КОНЕЦ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(ВТ_ЗаписиЗаПериод.ДатаСведений) КАК ДатаСведений,
			|	МИНИМУМ(ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеДействия,
			|	ВТ_ЗаписиЗаПериод.Организация КАК Организация,
			|	ВТ_ЗаписиЗаПериод.ОсновноеСредство КАК ОсновноеСредство,
			|	ВТ_ЗаписиЗаПериод.КодПоОКТМО КАК КодПоОКТМО
			|ПОМЕСТИТЬ ВТ_ДатыДействияЗаписей
			|ИЗ
			|	ВТ_ЗаписиЗаПериод КАК ВТ_ЗаписиЗаПериод
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТ_ЗаписиЗаПериод.Организация,
			|	ВТ_ЗаписиЗаПериод.КодПоОКТМО,
			|	ВТ_ЗаписиЗаПериод.ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТ_ЗаписиЗаПериод";
			
		Иначе
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период КАК ДатаСведений,
			|	МИНИМУМ(ЕСТЬNULL(СледующиеСтавки.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ОкончаниеДействия,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация КАК Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство КАК ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО КАК КодПоОКТМО
			|ПОМЕСТИТЬ ВТ_ДатыДействияЗаписей
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СтавкиНалогаНаИмуществоПоОтдельнымОС
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СледующиеСтавки
			|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация = СледующиеСтавки.Организация
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство = СледующиеСтавки.ОсновноеСредство
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО = СледующиеСтавки.КодПоОКТМО
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОС.Период < СледующиеСтавки.Период
			|ГДЕ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОС.ПорядокНалогообложения <> ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению)
			|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаРегистрацииПраваСобственности > &КонецПериода
			|	И ВЫБОР
			|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ НЕ СтавкиНалогаНаИмуществоПоОтдельнымОС.ДатаПрекращенияПраваСобственности < НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД)
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Период,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОС.КодПоОКТМО
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период,
			|	МИНИМУМ(ЕСТЬNULL(СледующиеСтавки.Период, ДАТАВРЕМЯ(1, 1, 1))),
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СледующиеСтавки
			|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = СледующиеСтавки.Организация
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство = СледующиеСтавки.ОсновноеСредство
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО = СледующиеСтавки.КодПоОКТМО
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период < СледующиеСтавки.Период
			|ГДЕ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ &НачалоПериода И &КонецПериода
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = &Организация
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения <> ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению)
			|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности > &КонецПериода
			|	И ВЫБОР
			|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности < НАЧАЛОПЕРИОДА(&НачалоПериода, ГОД)
			|		КОНЕЦ
			|
			|СГРУППИРОВАТЬ ПО
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО";
			
		КонецЕсли;
		
		Запрос.Выполнить();
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.Период КАК ДатаНачала,
		|	МИНИМУМ(ЕСТЬNULL(ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТ_ИсторияРегистрацийВНалоговомОргане
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&НачалоПериода, СтруктурнаяЕдиница = &Организация) КАК ИсторияРегистрацийВНалоговомОрганеСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи
		|		ПО ИсторияРегистрацийВНалоговомОрганеСрезПоследних.СтруктурнаяЕдиница = ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи.СтруктурнаяЕдиница
		|			И ИсторияРегистрацийВНалоговомОрганеСрезПоследних.Период < ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.Период,
		|	ИсторияРегистрацийВНалоговомОрганеСрезПоследних.РегистрацияВНалоговомОргане
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане,
		|	ИсторияРегистрацийВНалоговомОргане.Период,
		|	МИНИМУМ(ЕСТЬNULL(ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи.Период, ДАТАВРЕМЯ(1, 1, 1)))
		|ИЗ
		|	РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОргане
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане КАК ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи
		|		ПО ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница = ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи.СтруктурнаяЕдиница
		|			И ИсторияРегистрацийВНалоговомОргане.Период < ИсторияРегистрацийВНалоговомОрганеСледующиеЗаписи.Период
		|ГДЕ
		|	ИсторияРегистрацийВНалоговомОргане.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ИсторияРегистрацийВНалоговомОргане.СтруктурнаяЕдиница = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияРегистрацийВНалоговомОргане.Период,
		|	ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДатыДействияЗаписей.ДатаСведений КАК ДатаСведений,
		|	ВТ_ДатыДействияЗаписей.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
		|		ИНАЧЕ ЕСТЬNULL(ВТ_ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане, ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация.РегистрацияВНалоговомОргане, ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)))
		|	КОНЕЦ КАК ИФНС,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
		|		ИНАЧЕ ЕСТЬNULL(ВТ_ИсторияРегистрацийВНалоговомОргане.РегистрацияВНалоговомОргане.КодПоОКТМО, ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """"))
		|	КОНЕЦ КАК КодПоОКТМО,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство КАК ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомер КАК КадастровыйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодНалоговойЛьготыОсвобождениеОтНалогообложения КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПонижениеНалоговойСтавки,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СниженнаяНалоговаяСтавка
		|		ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяСтавка
		|	КОНЕЦ КАК НалоговаяСтавка,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаНачала > СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности
		|					ТОГДА ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаНачала
		|				ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаРегистрацииПраваСобственности
		|			КОНЕЦ
		|	КОНЕЦ КАК ДатаРегистрацииПраваСобственности,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
		|						И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности <> ДАТАВРЕМЯ(1, 1, 1)
		|						И ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаОкончания < СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности
		|					ТОГДА ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаОкончания
		|				КОГДА ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
		|						И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|					ТОГДА ВТ_ИсторияРегистрацийВНалоговомОргане.ДатаОкончания
		|				ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДатаПрекращенияПраваСобственности
		|			КОНЕЦ
		|	КОНЕЦ КАК ДатаПрекращенияПраваСобственности,
		|	&ПрименениеУСН
		|		ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД КАК ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодВидаИмущества КАК КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель
		|ПОМЕСТИТЬ ВТ_ЗаписиПоКадастровойСтоимости
		|ИЗ
		|	ВТ_ДатыДействияЗаписей КАК ВТ_ДатыДействияЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
		|		ПО ВТ_ДатыДействияЗаписей.ДатаСведений = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период
		|			И ВТ_ДатыДействияЗаписей.Организация = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация
		|			И ВТ_ДатыДействияЗаписей.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
		|			И ВТ_ДатыДействияЗаписей.КодПоОКТМО = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.КодПоОКТМО
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсторияРегистрацийВНалоговомОргане КАК ВТ_ИсторияРегистрацийВНалоговомОргане
		|		ПО (ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ДатыДействияЗаписей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ИсторияРегистрацийВНалоговомОргане
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДатаСведений КАК ДатаСведений,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ИФНС КАК ИФНС,
		|	ВТ_ЗаписиПоКадастровойСтоимости.КодПоОКТМО КАК КодПоОКТМО,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ОсновноеСредство КАК ОсновноеСредство,
		|	ВТ_ЗаписиПоКадастровойСтоимости.КадастровыйНомер КАК КадастровыйНомер,
		|	ВТ_ЗаписиПоКадастровойСтоимости.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
		|	ВТ_ЗаписиПоКадастровойСтоимости.КадастроваяСтоимость КАК КадастроваяСтоимость,
		|	ВТ_ЗаписиПоКадастровойСтоимости.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
		|	ВЫБОР
		|		КОГДА ВТ_ЗаписиПоКадастровойСтоимости.ОсвобождениеОтНалогообложения
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения, ЛОЖЬ)
		|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
		|	ВЫБОР
		|		КОГДА ВТ_ЗаписиПоКадастровойСтоимости.ОсвобождениеОтНалогообложения
		|			ТОГДА ВТ_ЗаписиПоКадастровойСтоимости.КодНалоговойЛьготыОсвобождениеОтНалогообложения
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения, ЛОЖЬ)
		|					ТОГДА ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения, """")
		|				ИНАЧЕ """"
		|			КОНЕЦ
		|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ПонижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
		|	ВТ_ЗаписиПоКадастровойСтоимости.НалоговаяСтавка КАК НалоговаяСтавка,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДатаРегистрацииПраваСобственности КАК ДатаРегистрацииПраваСобственности,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД КАК ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
		|	ВТ_ЗаписиПоКадастровойСтоимости.КодВидаИмущества КАК КодВидаИмущества,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
		|	ВТ_ЗаписиПоКадастровойСтоимости.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель,
		|	ВЫБОР
		|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК УплачиваютсяАвансы
		|ИЗ
		|	ВТ_ЗаписиПоКадастровойСтоимости КАК ВТ_ЗаписиПоКадастровойСтоимости
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
		|		ПО ВТ_ЗаписиПоКадастровойСтоимости.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&НачалоПериода, Организация = &Организация) КАК СтавкиНалогаНаИмуществоСрезПоследних
		|		ПО (ИСТИНА)";
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			ДанныеДляРасчета = Результат.Выгрузить();
		КонецЕсли; 
		
	КонецЕсли;
	
	Возврат ДанныеДляРасчета;
	
КонецФункции

Процедура ВыполнитьРасчетНалогаНаИмуществоПоКадастровойСтоимости(ДанныеДляРасчета, ПараметрыРасчета, ТаблицаРасчетНалога)

	Если ДанныеДляРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого СтрокаТаблицы Из ДанныеДляРасчета Цикл
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет И НЕ СтрокаТаблицы.УплачиваютсяАвансы Тогда
			Продолжить;
		КонецЕсли;
		
		УчитыватьАвансы = ПараметрыРасчета.ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
		
		Если ПараметрыРасчета.ПериодРасчета < '20140701' И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
			Продолжить;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаРегистрацииПраваСобственности) Тогда
			Продолжить;
		КонецЕсли;
		
		ГодРасчета = Год(ПараметрыРасчета.ПериодРасчета);
		
		Если ГодРасчета < 2016 ИЛИ ПараметрыРасчета.ГодовойРасчет Тогда
			НачалоПериода = 1;
		Иначе
			НачалоПериода = Месяц(НачалоКвартала(ПараметрыРасчета.ПериодРасчета));
		КонецЕсли;
		
		КоличествоМесяцевВладения = 0;
		КоличествоМесяцевВладенияЗаКвартал = 0;
		КоличествоМесяцевИспользования = 0;
		КоличествоМесяцевИспользованияЗаКвартал = 0;
		МесяцыВладения = Новый Соответствие;
		МесяцыИспользования = Новый Соответствие;
		Для Сч = НачалоПериода По Месяц(ПараметрыРасчета.ПериодРасчета) Цикл
			
			Если ГодРасчета < 2016 Тогда
				ПредельнаяДатаВозникновенияПрава = КонецМесяца(Дата(ГодРасчета, Сч, 1));
				ПредельнаяДатаПрекращенияПрава = Дата(ГодРасчета, Сч, 1);
			Иначе
				ПредельнаяДатаВозникновенияПрава = Дата(ГодРасчета, Сч, 15, 23, 59, 59);
				ПредельнаяДатаПрекращенияПрава = Дата(ГодРасчета, Сч, 16);
			КонецЕсли;
			
			Если СтрокаТаблицы.ДатаРегистрацииПраваСобственности <= ПредельнаяДатаВозникновенияПрава
				И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаПрекращенияПраваСобственности)
					ИЛИ СтрокаТаблицы.ДатаПрекращенияПраваСобственности >= ПредельнаяДатаПрекращенияПрава) Тогда
					
				КоличествоМесяцевВладения = КоличествоМесяцевВладения + 1;
				КоличествоМесяцевВладенияЗаКвартал = КоличествоМесяцевВладенияЗаКвартал + 1;
			КонецЕсли;
			
			Если ГодРасчета >= 2019
				И СтрокаТаблицы.ДатаСведений <= ПредельнаяДатаВозникновенияПрава
				И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ОкончаниеДействия)
					ИЛИ СтрокаТаблицы.ОкончаниеДействия >= ПредельнаяДатаПрекращенияПрава)
				И СтрокаТаблицы.ДатаРегистрацииПраваСобственности <= ПредельнаяДатаВозникновенияПрава
				И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаПрекращенияПраваСобственности)
					ИЛИ СтрокаТаблицы.ДатаПрекращенияПраваСобственности >= ПредельнаяДатаПрекращенияПрава) Тогда
					
				КоличествоМесяцевИспользования = КоличествоМесяцевИспользования + 1;
				КоличествоМесяцевИспользованияЗаКвартал = КоличествоМесяцевИспользованияЗаКвартал + 1;
			КонецЕсли;
			
			Если ПараметрыРасчета.ГодовойРасчет И Сч%3 = 0 Тогда
				
				МесяцыВладения.Вставить(Сч, КоличествоМесяцевВладенияЗаКвартал);
				
				Если ГодРасчета >= 2016 Тогда
					КоличествоМесяцевВладенияЗаКвартал = 0;
				КонецЕсли;
				
				Если ГодРасчета >= 2019 Тогда
					МесяцыИспользования.Вставить(Сч, КоличествоМесяцевИспользованияЗаКвартал);
					КоличествоМесяцевИспользованияЗаКвартал = 0;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если КоличествоМесяцевВладения = 0 ИЛИ (ГодРасчета >= 2019 И КоличествоМесяцевИспользования = 0) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаРасчетНалога.Добавить(); 
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
		НоваяСтрока.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость;
		
		Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость;
		КонецЕсли;
		
		Если ПараметрыРасчета.ГодовойРасчет
		   И ГодРасчета = 2014
		   И ПараметрыРасчета.ПлательщикЕНВД
		   И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
		   
			НоваяСтрока.КадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость / 2;
			НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.НеоблагаемаяКадастроваяСтоимость / 2;
			
		КонецЕсли;
		
		Если СтрокаТаблицы.ДоляСтоимостиЧислитель > 0 И СтрокаТаблицы.ДоляСтоимостиЗнаменатель > 0 Тогда
			ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;	
		Иначе
			ДоляСтоимости = 1;
		КонецЕсли;
		
		Если СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель > 0 И СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель > 0 Тогда
			ДоляВПравеОбщейСобственности = 
				СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель / СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель;
		Иначе
			ДоляВПравеОбщейСобственности = 1;
		КонецЕсли;
		
		НоваяСтрока.НалоговаяБаза = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
			* ДоляСтоимости * ДоляВПравеОбщейСобственности);
		
		НоваяСтрока.КоличествоМесяцевВладения = КоличествоМесяцевВладения;
		КоэффициентК = НоваяСтрока.КоличествоМесяцевВладения / ?(ГодРасчета < 2016 ИЛИ ПараметрыРасчета.ГодовойРасчет, Месяц(ПараметрыРасчета.ПериодРасчета), 3);
		
		Если ГодРасчета < 2019 Тогда
			КоэффициентИ = 1;
		Иначе
			НоваяСтрока.КоличествоМесяцевИспользования = КоличествоМесяцевИспользования;
			КоэффициентИ = Окр(НоваяСтрока.КоличествоМесяцевИспользования / НоваяСтрока.КоличествоМесяцевВладения, 4);
			КоэффициентК = Окр(КоэффициентК, 4);
		КонецЕсли;
		
		Если ПараметрыРасчета.ГодовойРасчет Тогда
			НоваяСтрока.СуммаНалога = Окр(НоваяСтрока.НалоговаяБаза * КоэффициентК * КоэффициентИ
				* НоваяСтрока.НалоговаяСтавка / 100);
		Иначе
			НоваяСтрока.СуммаНалога = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
				* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК * КоэффициентИ
				* НоваяСтрока.НалоговаяСтавка / 100 / 4);
		КонецЕсли;
		
		Если УчитыватьАвансы Тогда
			
			КоэффициентК3 = МесяцыВладения[3] / 3;
			Если ГодРасчета < 2019 Тогда
				КоэффициентИ3 = 1;
			Иначе
				КоэффициентК3 = Окр(КоэффициентК3, 4);
				КоэффициентИ3 = ?(МесяцыВладения[3] = 0, 1, Окр(МесяцыИспользования[3] / МесяцыВладения[3], 4));
			КонецЕсли;
			СуммаАвансовыхПлатежей3 = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
				* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК3 * КоэффициентИ3
				* НоваяСтрока.НалоговаяСтавка / 100 / 4);
			
			КоэффициентК6 = МесяцыВладения[6] / ?(ГодРасчета < 2016, 6, 3);
			Если ГодРасчета < 2019 Тогда
				КоэффициентИ6 = 1;
			Иначе
				КоэффициентК6 = Окр(КоэффициентК6, 4);
				КоэффициентИ6 = ?(МесяцыВладения[6] = 0, 1, Окр(МесяцыИспользования[6] / МесяцыВладения[6], 4));
			КонецЕсли;
			СуммаАвансовыхПлатежей6 = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
				* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК6 * КоэффициентИ6
				* НоваяСтрока.НалоговаяСтавка / 100 / 4);
			
			КоэффициентК9 = МесяцыВладения[9] / ?(ГодРасчета < 2016, 9, 3);
			Если ГодРасчета < 2019 Тогда
				КоэффициентИ9 = 1;
			Иначе
				КоэффициентК9 = Окр(КоэффициентК9, 4);
				КоэффициентИ9 = ?(МесяцыВладения[9] = 0, 1, Окр(МесяцыИспользования[9] / МесяцыВладения[9], 4));
			КонецЕсли;
			СуммаАвансовыхПлатежей9 = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
				* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК9 * КоэффициентИ9
				* НоваяСтрока.НалоговаяСтавка / 100 / 4);
			
		КонецЕсли;
		
		Если НоваяСтрока.ИФНС = ПараметрыРасчета.РегистрацияВНалоговомОргане Тогда
		
			НоваяСтрока.СуммаУменьшенияСуммыНалога = Окр(НоваяСтрока.СуммаНалога * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100);
			НоваяСтрока.СуммаНалогаКУплате = Макс(НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаУменьшенияСуммыНалога, 0);
			
			Если УчитыватьАвансы Тогда
				СуммаАвансовыхПлатежей3 = Макс(СуммаАвансовыхПлатежей3
					- Окр(СуммаАвансовыхПлатежей3 * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100), 0);
				СуммаАвансовыхПлатежей6 = Макс(СуммаАвансовыхПлатежей6
					- Окр(СуммаАвансовыхПлатежей6 * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100), 0);
				СуммаАвансовыхПлатежей9 = Макс(СуммаАвансовыхПлатежей9
					- Окр(СуммаАвансовыхПлатежей9 * ПараметрыРасчета.ПроцентУменьшенияСуммыНалога / 100), 0);
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
			
		КонецЕсли;
		
		Если УчитыватьАвансы Тогда
			
			Если ГодРасчета = 2014
			   И ПараметрыРасчета.ПлательщикЕНВД
			   И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
				НоваяСтрока.СуммаАвансовыхПлатежей = 2 * СуммаАвансовыхПлатежей9;
			Иначе
				НоваяСтрока.СуммаАвансовыхПлатежей = СуммаАвансовыхПлатежей3 + СуммаАвансовыхПлатежей6 + СуммаАвансовыхПлатежей9;
			КонецЕсли;
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
			
		КонецЕсли;
		
		Если НЕ ПараметрыРасчета.ГодовойРасчет Тогда
			НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыРасчетаНалогаНаИмущество(Организация, ПериодРасчета, ДополнительныеПараметрыРасчета)

	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("Организация", Организация);
	ПараметрыРасчета.Вставить("ПериодРасчета", ПериодРасчета);
	ПараметрыРасчета.Вставить("ГодовойРасчет", Месяц(ПериодРасчета) = 12);
	ПараметрыРасчета.Вставить("мДатаНачалаПериодаОтчета", НачалоГода(ПериодРасчета));
	ПараметрыРасчета.Вставить("мДатаКонцаПериодаОтчета", КонецКвартала(ПериодРасчета));
	ПараметрыРасчета.Вставить("Делитель", Месяц(ПериодРасчета) + 1);
	
	СистемаНалогообложения = УчетнаяПолитика.СистемаНалогообложения(Организация, ПериодРасчета);
	ПараметрыРасчета.Вставить("ПрименениеОСНО", СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая);
	ПараметрыРасчета.Вставить("ПрименениеУСН", СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная);
	ПараметрыРасчета.Вставить("ПлательщикЕНВД", УчетнаяПолитика.ПлательщикЕНВД(Организация, ПериодРасчета));
	ПараметрыРасчета.Вставить("ПлательщикЕНВД_НаКонецКвартала", УчетнаяПолитика.ПлательщикЕНВД(Организация, КонецКвартала(ПериодРасчета)));
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	ПараметрыРасчета.Вставить("РегистрацияВНалоговомОргане", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
	
	// Сведения о налоговых льготах в виде уменьшения суммы налога.
	#Область ПроцентУменьшенияСуммыНалога
	ПроцентУменьшенияСуммыНалога = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", ПараметрыРасчета.мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения, 0) КАК ПроцентУменьшения
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&Период) КАК СтавкиНалогаНаИмуществоСрезПоследних
	|ГДЕ
	|	СтавкиНалогаНаИмуществоСрезПоследних.Организация = &Организация";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ПроцентУменьшенияСуммыНалога = РезультатЗапроса.Выгрузить()[0].ПроцентУменьшения;
	Иначе
		ПроцентУменьшенияСуммыНалога = 0;
	КонецЕсли;
	ПараметрыРасчета.Вставить("ПроцентУменьшенияСуммыНалога", ПроцентУменьшенияСуммыНалога);
	#КонецОбласти
	
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	
	РасчетИмущественныхНалоговПереопределяемый.ДополнитьПараметрыРасчетаНалогаНаИмущество(
		Организация, ПериодРасчета, ДополнительныеПараметрыРасчета, ПараметрыРасчета);
		
	РасчетИмущественныхНалоговПереопределяемый.СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		ПараметрыРасчета.МенеджерВременныхТаблиц, 
		Организация, 
		ПериодРасчета, 
		"НалогНаИмущество");
		
	Возврат ПараметрыРасчета;

КонецФункции

#КонецОбласти

#КонецОбласти
