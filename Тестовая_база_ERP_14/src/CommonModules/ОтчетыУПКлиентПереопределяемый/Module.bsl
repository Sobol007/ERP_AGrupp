////////////////////////////////////////////////////////////////////////////////
// Варианты отчетов - Форма отчета УП (клиент, переопределяемый)
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик расшифровки табличного документа формы отчета.
//
// Параметры:
//   см. ОтчетыКлиентПереопределяемый.ОбработкаРасшифровки().
//
Процедура ОбработкаРасшифровки(ЭтаФорма, Элемент, Расшифровка, СтандартнаяОбработка) Экспорт
	ПолноеИмяОтчета = ЭтаФорма.НастройкиОтчета.ПолноеИмя;
	КлючТекущегоВарианта = ЭтаФорма.КлючТекущегоВарианта;
	
	Если ПолноеИмяОтчета = "Отчет.ДвижениеМатериаловПолуфабрикатовРаботВПроизводстве" Тогда
		
		МенюОтчетов  = Новый Массив;
		МенюДействий = Новый Массив;
		
		Если КлючТекущегоВарианта = "ДвижениеМатериаловПолуфабрикатовРаботВПроизводстве"
			ИЛИ КлючТекущегоВарианта = "ДвижениеМатериаловПолуфабрикатовРаботВПроизводствеКонтекст" Тогда
			
			// Расшифровать отчетом -> Поступление материалов в производство (расшифровка)
			#Область РасшифровкаПоступлениеМатериаловВПроизводство
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "РасшифровкаПоступлениеМатериаловВПроизводство");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Поступление материалов в производство';
														|en = 'Material receipt to production'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.РасшифровкаПоступлениеМатериаловВПроизводство");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Организация");
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПоляРасшифровки.Добавить("Серия");
			ПоляРасшифровки.Добавить("Назначение");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			НеобходимыеПараметры = Новый Структура;
			НеобходимыеПараметры.Вставить("Подразделение");
			НеобходимыеПараметры.Вставить("Номенклатура");
			ПараметрыОтчета.Вставить("НеобходимыеПараметры", НеобходимыеПараметры);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			СписокПараметров.Добавить("Организация");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
			// Расшифровать отчетом -> Расходы на производство
			#Область РасшифровкаРасходовНаПроизводство
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "РасшифровкаРасходовНаПроизводство2КА");
			//++ НЕ УТКА
			ПараметрыОтчета.Вставить("Имя", "РасшифровкаРасходовНаПроизводство2УП");
			//-- НЕ УТКА
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Расходы на производство';
														|en = 'Manufacturing costs'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.РасшифровкаРасходовНаПроизводство");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Организация");
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПоляРасшифровки.Добавить("Назначение");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			НеобходимыеПараметры = Новый Структура;
			НеобходимыеПараметры.Вставить("Подразделение");
			НеобходимыеПараметры.Вставить("Номенклатура");
			ПараметрыОтчета.Вставить("НеобходимыеПараметры", НеобходимыеПараметры);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			СписокПараметров.Добавить("Организация");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
		КонецЕсли;
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	//++ НЕ УТКА
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ЗагрузкаВидовРабочихЦентров" Тогда
		
		МенюОтчетов  = Новый Массив;
		МенюДействий = Новый Массив;
		
		Если КлючТекущегоВарианта = "ЗагрузкаВидовРабочихЦентровТаблица" 
			ИЛИ КлючТекущегоВарианта = "ЗагрузкаВидовРабочихЦентровТаблицаКонтекст" Тогда
			
			// Перейти -> Занятость вида рабочего центра
			#Область ИзменениеЗанятостиВидаРабочегоЦентра
			Если НЕ ОтчетыУПВызовСервераПереопределяемый.ИспользуетсяПроизводство22() Тогда
				ПараметрыДействия = Новый Структура;
				ПараметрыДействия.Вставить("Имя", "ИзменениеЗанятостиВидаРабочегоЦентра");
				ПараметрыДействия.Вставить("Заголовок", НСтр("ru = 'Занятость вида рабочего центра';
															|en = 'Occupancy of the work center kind'"));
				ПараметрыДействия.Вставить("Действие", "ОткрытьФорму");
				ПараметрыДействия.Вставить("ИмяФормы", "Обработка.ДиспетчированиеГрафикаПроизводства.Форма.ИзменениеЗанятостиВидаРабочегоЦентра");
				
				ПоляРасшифровки = Новый Массив;
				ПоляРасшифровки.Добавить("ВидРабочегоЦентра");
				ПоляРасшифровки.Добавить("ПериодГрафика");
				ПараметрыДействия.Вставить("ПоляРасшифровки", ПоляРасшифровки);
				
				НеобходимыеПараметры = Новый Структура;
				НеобходимыеПараметры.Вставить("ВидРабочегоЦентра");
				НеобходимыеПараметры.Вставить("ПериодГрафика");
				ПараметрыДействия.Вставить("НеобходимыеПараметры", НеобходимыеПараметры);
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("ВидРабочегоЦентра");
				ПараметрыФормы.Вставить("ПериодГрафика");
				ПараметрыДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
				
				ЗаменаПараметров = Новый Структура;
				ЗаменаПараметров.Вставить("ПериодГрафика", "НачалоПериода");
				ПараметрыДействия.Вставить("ЗаменаПараметров", ЗаменаПараметров);
				
				МенюДействий.Добавить(ПараметрыДействия);
			КонецЕсли;
			#КонецОбласти
			
			// Перейти -> Изменить доступность вида рабочих центров
			#Область ИзменитьДоступностьВидаРЦ
			ПараметрыДействия = Новый Структура;
			ПараметрыДействия.Вставить("Имя", "ИзменитьДоступностьВидаРЦ");
			ПараметрыДействия.Вставить("Заголовок", НСтр("ru = 'Изменить доступность вида рабочих центров';
														|en = 'Change availability of work center kind'"));
			ПараметрыДействия.Вставить("ИмяОбщегоМодуля", "ОтчетыУПКлиентПереопределяемый");
			
			ПоляРасшифровки = Новый Массив;
			ПоляРасшифровки.Добавить("ВидРабочегоЦентра");
			ПоляРасшифровки.Добавить("ПериодГрафика");
			ПараметрыДействия.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			НеобходимыеПараметры = Новый Структура;
			НеобходимыеПараметры.Вставить("ВидРабочегоЦентра");
			НеобходимыеПараметры.Вставить("ПериодГрафика");
			ПараметрыДействия.Вставить("НеобходимыеПараметры", НеобходимыеПараметры);
			
			МенюДействий.Добавить(ПараметрыДействия);
			#КонецОбласти
			
		КонецЕсли;
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ЗагрузкаРабочихЦентров" Тогда
		
		МенюОтчетов  = Новый Массив;
		МенюДействий = Новый Массив;
		
		Если КлючТекущегоВарианта = "ЗагрузкаРабочихЦентров" Тогда
			
			// Расшифровать отчетом -> Анализ суточной загрузки
			#Область АнализСуточнойЗагрузкиКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "АнализСуточнойЗагрузкиКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Анализ суточной загрузки';
														|en = 'Daily load analysis'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.АнализСуточнойЗагрузки");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("РабочийЦентр");
			ПоляРасшифровки.Добавить("ВидРабочегоЦентра");
			ПоляРасшифровки.Добавить("ПериодГрафика");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			НеобходимыеПараметры = Новый Структура;
			НеобходимыеПараметры.Вставить("ПериодГрафика");
			ПараметрыОтчета.Вставить("НеобходимыеПараметры", НеобходимыеПараметры);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
		КонецЕсли;
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.АнализВыполненияМаршрутныхЛистов" Тогда
		
		МенюОтчетов  = Новый Массив;
		МенюДействий = Новый Массив;
		
		Если КлючТекущегоВарианта = "ВыполнениеМаршрутныхЛистовСЗадержками" Тогда
			
			// Расшифровать отчетом -> Сведения о выполнении маршрутных листов
			#Область СведенияОВыполненииМаршрутныхЛистовКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "СведенияОВыполненииМаршрутныхЛистовКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Сведения о выполнении маршрутных листов';
														|en = 'Route sheet completion information'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.АнализВыполненияМаршрутныхЛистов");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("МаршрутнаяКарта");
			ПоляРасшифровки.Добавить("Этап");
			ПоляРасшифровки.Добавить("Спецификация");
			ПоляРасшифровки.Добавить("Буфер");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПоляРасшифровки.Добавить("ПричинаЗадержки");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
			// Расшифровать отчетом -> Причины задержек выполнения маршрутных листов
			#Область ПричиныЗадержекКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "ПричиныЗадержекКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Причины задержек выполнения маршрутных листов';
														|en = 'Reasons for route sheet completion delays'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.АнализПричинЗадержекВыполненияМаршрутныхЛистов");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("МаршрутнаяКарта");
			ПоляРасшифровки.Добавить("Этап");
			ПоляРасшифровки.Добавить("Спецификация");
			ПоляРасшифровки.Добавить("Буфер");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПоляРасшифровки.Добавить("ПричинаЗадержки");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
		ИначеЕсли КлючТекущегоВарианта = "ВыполнениеМаршрутныхЛистовБезЗадержек" Тогда
			
			// Расшифровать отчетом -> Сведения о выполнении маршрутных листов
			#Область СведенияОВыполненииМаршрутныхЛистовКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "СведенияОВыполненииМаршрутныхЛистовКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Сведения о выполнении маршрутных листов';
														|en = 'Route sheet completion information'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.АнализВыполненияМаршрутныхЛистов");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("МаршрутнаяКарта");
			ПоляРасшифровки.Добавить("Этап");
			ПоляРасшифровки.Добавить("Спецификация");
			ПоляРасшифровки.Добавить("Буфер");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
		КонецЕсли;
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.АнализПричинЗадержекВыполненияМаршрутныхЛистов" Тогда
			
		МенюОтчетов  = Новый Массив;
		МенюДействий = Новый Массив;
		
		Если КлючТекущегоВарианта = "ПричиныЗадержек" Тогда
			
			// Расшифровать отчетом -> Сведения о выполнении маршрутных листов
			#Область СведенияОВыполненииМаршрутныхЛистовКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "СведенияОВыполненииМаршрутныхЛистовКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Сведения о выполнении маршрутных листов';
														|en = 'Route sheet completion information'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.АнализВыполненияМаршрутныхЛистов");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("ПричинаЗадержки");
			ПоляРасшифровки.Добавить("Подразделение");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
			// Расшифровать отчетом -> Выполнение маршрутных листов с задержками
			#Область ВыполнениеМаршрутныхЛистовСЗадержками
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "ВыполнениеМаршрутныхЛистовСЗадержками");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Выполнение маршрутных листов с задержками';
														|en = 'Completion of route sheets with delays'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.АнализВыполненияМаршрутныхЛистов");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("ПричинаЗадержки");
			ПоляРасшифровки.Добавить("Подразделение");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Период");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
		КонецЕсли;
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.СверкаОборотовМУиБУ" Тогда
		СтандартнаяОбработка = Ложь;
		
		СчетСтрокиСверки = ОтчетыУПВызовСервераПереопределяемый.ПолучитьСчетСтрокиСверки(ЭтаФорма.ОтчетДанныеРасшифровки,Число(Расшифровка));
		Если ЗначениеЗаполнено(СчетСтрокиСверки) Тогда
			
			Если ТипЗнч(СчетСтрокиСверки) = Тип("ПланСчетовСсылка.Международный") Тогда
				ОткрытьОтчетАнализСчетаМеждународный(ЭтаФорма.Отчет, СчетСтрокиСверки);
			ИначеЕсли ТипЗнч(СчетСтрокиСверки) = Тип("ПланСчетовСсылка.Хозрасчетный") Тогда
				АдресРасшифровки = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
				ОткрытьОтчетАнализСчетаХозрасчетный(ЭтаФорма.Отчет, АдресРасшифровки, СчетСтрокиСверки);
			Иначе
				ПоказатьЗначение(Неопределено, СчетСтрокиСверки);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.БухгалтерскийОтчетМеждународный" Тогда
		
		Если КлючТекущегоВарианта = "АнализСчетаМеждународный" Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ПараметрыРасшифровки = Новый Структура;
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Счет");
			ПоляРасшифровки.Добавить("Субконто1");
			ПоляРасшифровки.Добавить("Субконто2");
			ПоляРасшифровки.Добавить("Субконто3");
			ПоляРасшифровки.Добавить("КорСчет");
			ПоляРасшифровки.Добавить("КорСубконто1");
			ПоляРасшифровки.Добавить("КорСубконто2");
			ПоляРасшифровки.Добавить("КорСубконто3");
			ПараметрыРасшифровки.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			ПараметрыРасшифровки.Вставить("СписокПараметров", СписокПараметров);
			
			ПараметрыФормыРасшифровки = ОтчетыУПВызовСервераПереопределяемый.ПараметрыКарточкиСчета(
												Расшифровка, 
												ЭтаФорма.ОтчетДанныеРасшифровки,
												ПараметрыРасшифровки.СписокПараметров, 
												ПараметрыРасшифровки.ПоляРасшифровки);
			
			ОткрытьОтчетКарточкаСчетаМеждународный(ЭтаФорма.Отчет, ПараметрыФормыРасшифровки);
		
		КонецЕсли;// КлючТекущегоВарианта = "АнализСчетаМеждународный"
	//-- НЕ УТКА
	
	//++ НЕ УТ
	ИначеЕсли ПолноеИмяОтчета = "Отчет.СверкаДанныхОУиБУ" Тогда

		ПараметрыРасшифровки = ОтчетыУПВызовСервераПереопределяемый.ПолучитьПараметрыРасшифровки(ЭтаФорма.ОтчетДанныеРасшифровки, Расшифровка);
		
		ПоляРасшифровки = ПараметрыРасшифровки.ПоляРасшифровки;
		АналитикаСтроки = ПараметрыРасшифровки.АналитикаСтроки;
		
		ИдентификаторПараметраПериод = ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьИдентификаторПоОбъекту(
			ЭтаФорма.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета")));
		НастройкаПериод = ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторПараметраПериод);
		АналитикаСтроки.Вставить("ПериодОтчета", НастройкаПериод.Значение);
		Для каждого ЭлементОтбора Из ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				АналитикаСтроки.Вставить("ПоказатьТолькоОтклонения", ЭлементОтбора.Использование);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ПолеРасшифровки Из ПоляРасшифровки Цикл
			
			Если ПолеРасшифровки = "НачальноеСальдо"	ИЛИ ПолеРасшифровки = "КонечноеСальдо"	ИЛИ
				 ПолеРасшифровки = "ОборотДт"			ИЛИ ПолеРасшифровки = "ОборотКт"		ИЛИ
				 ПолеРасшифровки = "НачальныйОстаток"	ИЛИ ПолеРасшифровки = "Приход"			ИЛИ
				 ПолеРасшифровки = "Расход"				ИЛИ ПолеРасшифровки = "КонечныйОстаток"	ИЛИ
				 ПолеРасшифровки = "НачОткл"			ИЛИ ПолеРасшифровки = "КонОткл"			ИЛИ
				 ПолеРасшифровки = "ПриходОткл"			ИЛИ ПолеРасшифровки = "РасходОткл"		Тогда
				 
				СтандартнаяОбработка = Ложь;
				
				Если ЭтаФорма.КлючТекущегоВарианта = "СверкаПоРегистраторам" Тогда
					Возврат;
				КонецЕсли;

				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("КлючВарианта",                     "СверкаПоРегистраторам");
				ПараметрыФормы.Вставить("СформироватьПриОткрытии",          Истина);
				ПараметрыФормы.Вставить("ВидимостьКомандВариантовОтчетов",  Ложь);
				ПараметрыФормы.Вставить("РежимРасшифровки",                 Истина);
				
				ПользовательскиеНастройки = ОтчетыУПВызовСервераПереопределяемый.ПараметрыРасшифровки(АналитикаСтроки);
				ПараметрыФормы.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
				
				ОткрытьФорму("Отчет.СверкаДанныхОУиБУ.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, Новый УникальныйИдентификатор);
			КонецЕсли;
			
		КонецЦикла;
	//-- НЕ УТ
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.РасшифровкаФормулыБюджетногоОтчета" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = БюджетнаяОтчетностьВызовСервера.ПараметрыФормыРасшифровкиОтчетаРасшифровки(
																		Расшифровка, ЭтаФорма.ОтчетДанныеРасшифровки);
		
		Если ПараметрыОткрытия = Неопределено Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Нет данных для расшифровки';
											|en = 'No data for decryption'"));
		Иначе
			БюджетнаяОтчетностьКлиент.ОткрытьФормуОтчета(ПараметрыОткрытия, ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ПроверкаСвязейПоказателейБюджетов" Тогда
		
		ДанныеРасшифровкиОтчета = БюджетированиеВызовСервера.ДанныеРасшифровкиОтчета(Расшифровка, ЭтаФорма.ОтчетДанныеРасшифровки);
		
		Если ДанныеРасшифровкиОтчета.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать Тогда
			СтандартнаяОбработка = Ложь;
			
			ДоступныеПоляРасшифровки  = Новый Структура;
			ДоступныеПоляРасшифровки.Вставить("ПоказательБюджетов");
			ДоступныеПоляРасшифровки.Вставить("Аналитика1");
			ДоступныеПоляРасшифровки.Вставить("Аналитика2");
			ДоступныеПоляРасшифровки.Вставить("Аналитика3");
			ДоступныеПоляРасшифровки.Вставить("Аналитика4");
			ДоступныеПоляРасшифровки.Вставить("Аналитика5");
			ДоступныеПоляРасшифровки.Вставить("Аналитика6");
			ДоступныеПоляРасшифровки.Вставить("Валюта");
			
			ПоляРасшифровки = Новый Структура;
			Для каждого Поле Из ДанныеРасшифровкиОтчета.ПоляРасшифровки Цикл
				Если ДоступныеПоляРасшифровки.Свойство(Поле.Ключ) Тогда
					ПоляРасшифровки.Вставить(Поле.Ключ, Поле.Значение);
				КонецЕсли;
			КонецЦикла;
			Для каждого Поле Из ДанныеРасшифровкиОтчета.ПоляРасшифровкиРодители Цикл
				Если ДоступныеПоляРасшифровки.Свойство(Поле.Ключ) Тогда
					ПоляРасшифровки.Вставить(Поле.Ключ, Поле.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Параметры = Новый Структура;
			Параметры.Вставить("ИмяОтчета"			, "СправкаРасчетПоказателяБюджетов");
			Параметры.Вставить("КлючВарианта"		, "СправкаРасчетПоказателяБюджетов");
			Параметры.Вставить("КомпоновщикНастроек", ЭтаФорма.Отчет.КомпоновщикНастроек);
			Параметры.Вставить("ПоляРасшифровки"	, ПоляРасшифровки);
			НастройкаРасчетПоказателяБюджетов = ОтчетыУТВызовСервераПереопределяемый.НастроитьОтчетРасшифровки(Параметры);
			
			ПараметрыДанныхОсновногоОтчета = ЭтаФорма.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных;
			
			ФиксированныеПараметры = Новый Структура("Период, Показатель", ДанныеРасшифровкиОтчета.ПараметрыДанных.Период);
			Для Каждого КлючИЗначение Из ФиксированныеПараметры Цикл
				
				ЗначениеПараметра = ?(КлючИЗначение.Ключ = "Период", КлючИЗначение.Значение,
					ПараметрыДанныхОсновногоОтчета.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(КлючИЗначение.Ключ)).Значение);
				
				Параметр = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкаРасчетПоказателяБюджетов.ФиксированныеНастройки, КлючИЗначение.Ключ);
				Если Параметр = Неопределено Тогда
					Параметр = НастройкаРасчетПоказателяБюджетов.ФиксированныеНастройки.ПараметрыДанных.Элементы.Добавить();
					Параметр.Параметр = Новый ПараметрКомпоновкиДанных(КлючИЗначение.Ключ);
				КонецЕсли;
				КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкаРасчетПоказателяБюджетов.ФиксированныеНастройки,
																					КлючИЗначение.Ключ, ЗначениеПараметра, Истина);
				
			КонецЦикла;
			
			КлючНастроек = "РасшифровкаПоказателяБюджетов";
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("КлючВарианта",                  "СправкаРасчетПоказателяБюджетов");
			ПараметрыФормы.Вставить("КлючНазначенияИспользования",    КлючНастроек);
			ПараметрыФормы.Вставить("КлючПользовательскихНастроек",   КлючНастроек);
			ПараметрыФормы.Вставить("ПользовательскиеНастройки",      НастройкаРасчетПоказателяБюджетов.ПользовательскиеНастройки);
			ПараметрыФормы.Вставить("ФиксированныеНастройки",         НастройкаРасчетПоказателяБюджетов.ФиксированныеНастройки);
			ПараметрыФормы.Вставить("СформироватьПриОткрытии",        Истина);
			ПараметрыФормы.Вставить("ВидимостьКомандВариантовОтчетов",Ложь);
			ПараметрыФормы.Вставить("РежимРасшифровки",               Истина);
			ОткрытьФорму("Отчет.СправкаРасчетПоказателяБюджетов.Форма", ПараметрыФормы, ЭтаФорма, Истина);
		КонецЕсли;
		
	//++ НЕ УТКА
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ИспользованиеСчетовИСубконтоМеждународныйУчет" Тогда
		
		ПараметрыРасшифровки = ОтчетыУПВызовСервераПереопределяемый.ПараметрыРасшифровкиНастроек(
									Расшифровка, ЭтаФорма.ОтчетДанныеРасшифровки);
		Если ПараметрыРасшифровки.ЭтоПроизводный Тогда
			СтандартнаяОбработка = Ложь;
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", ПараметрыРасшифровки.ЭлементОтчета);
			
			ПараметрыФормы.Вставить("ВидПоказателей", ПараметрыРасшифровки.ВидПоказателей);
			ПараметрыФормы.Вставить("АдресЭлементаВХранилище", "");
			ПараметрыФормы.Вставить("ИдентификаторГлавногоХранилища", ЭтаФорма.УникальныйИдентификатор);
			ПараметрыФормы.Вставить("ДополнительныйРежимФормы", ПредопределенноеЗначение("Перечисление.ДополнительныеРежимыЭлементовОтчетов.ВидОтчета"));
			ПараметрыФормы.Вставить("ПоказатьКодСтрокиПримечание", Истина);
			ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
			
			ОткрытьФорму("Справочник.ЭлементыФинансовыхОтчетов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
			
		ИначеЕсли ЗначениеЗаполнено(ПараметрыРасшифровки.ВидОтчета) И ЗначениеЗаполнено(ПараметрыРасшифровки.ЭлементОтчета) Тогда
			
			СтандартнаяОбработка = Ложь;
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", ПараметрыРасшифровки.ВидОтчета);
			ПараметрыФормы.Вставить("ТекущийЭлементОтчета", ПараметрыРасшифровки.ЭлементОтчета);
			ОткрытьФорму("Справочник.ВидыФинансовыхОтчетов.ФормаОбъекта",ПараметрыФормы);
			
		КонецЕсли;
	
	ИначеЕсли КлючТекущегоВарианта = "ОтклонениеЗаказаФактаОтКалькуляции" 
		Или КлючТекущегоВарианта = "ЗаказКонтекст" Тогда
		
		СписокПараметров = Новый Массив;
		
		СписокПараметров.Добавить("ДанныеОтчета");
		
		ПоляРасшифровки = Новый Массив;
		
		ПоляРасшифровки.Добавить("ЗаказНаПроизводство");
		ПоляРасшифровки.Добавить("КодСтрокиЗаказаНаПроизводство");
		ПоляРасшифровки.Добавить("СтатьяКалькуляции");
		
		// Поля для расшифровки К-З
		ПоляРасшифровкиКЗ = Новый Массив;
		ПоляРасшифровкиКЗ.Добавить("СуммаКалькуляция");
		ПоляРасшифровкиКЗ.Добавить("СуммаЗаказВсеЭтапы");
		ПоляРасшифровкиКЗ.Добавить("Отклонение_Сумма_Калькуляция_ЗаказВсеЭтапы");
		ПоляРасшифровкиКЗ.Добавить("Процент_Сумма_Калькуляция_ЗаказВсеЭтапы");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляРасшифровки, ПоляРасшифровкиКЗ);
		
		// Поля для расшифровки З-Ф
		ПоляРасшифровкиЗФ = Новый Массив;
		ПоляРасшифровкиЗФ.Добавить("СуммаЗаказВсеЭтапыДубль");
		ПоляРасшифровкиЗФ.Добавить("СуммаФактПрогнозДубль");
		ПоляРасшифровкиЗФ.Добавить("Отклонение_Сумма_ЗаказВсеЭтапы_ФактПрогноз");
		ПоляРасшифровкиЗФ.Добавить("Процент_Сумма_ЗаказВсеЭтапы_ФактПрогноз");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляРасшифровки, ПоляРасшифровкиЗФ);
		
		// Поля для расшифровки К-Ф
		ПоляРасшифровкиКФ = Новый Массив;
		ПоляРасшифровкиКФ.Добавить("СуммаКалькуляцияДубль");
		ПоляРасшифровкиКФ.Добавить("СуммаФактПрогноз");
		ПоляРасшифровкиКФ.Добавить("Отклонение_Сумма_Калькуляция_ФактПрогноз");
		ПоляРасшифровкиКФ.Добавить("Процент_Сумма_Калькуляция_ФактПрогноз");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляРасшифровки, ПоляРасшифровкиКФ);
		
		ДанныеРасшифровкиОтчета = КомпоновкаДанныхВызовСервера.ПараметрыФормыРасшифровки(
												Расшифровка,
												ЭтаФорма.ОтчетДанныеРасшифровки,
												СписокПараметров,
												ПоляРасшифровки);
		
		Если ДанныеРасшифровкиОтчета = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВариантОтчета = Неопределено;
		
		Для Каждого ПолеРасшифровки Из ПоляРасшифровкиКЗ Цикл
			Если ДанныеРасшифровкиОтчета.Свойство(ПолеРасшифровки) Тогда
				ВариантОтчета = "ОтклонениеСпецификацииЗаказаОтКалькуляции";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ВариантОтчета = Неопределено Тогда
			Для Каждого ПолеРасшифровки Из ПоляРасшифровкиЗФ Цикл
				Если ДанныеРасшифровкиОтчета.Свойство(ПолеРасшифровки) Тогда
					ВариантОтчета = "ОтклонениеФактаОтСпецификацииЗаказа";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ВариантОтчета = Неопределено Тогда
			Для Каждого ПолеРасшифровки Из ПоляРасшифровкиКФ Цикл
				Если ДанныеРасшифровкиОтчета.Свойство(ПолеРасшифровки) Тогда
					ВариантОтчета = "ОтклонениеФактаОтКалькуляции";
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ВариантОтчета = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		ФиксированныеНастройки = ПланированиеПроизводстваВызовСервера.ПараметрыРасшифровкиОтклонениеЗаказаФактаОтКалькуляции(ДанныеРасшифровкиОтчета);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("КлючВарианта",                   ВариантОтчета);
		ПараметрыФормы.Вставить("ФиксированныеНастройки",         ФиксированныеНастройки);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии",        Истина);
		ПараметрыФормы.Вставить("ВидимостьКомандВариантовОтчетов",Ложь);
		ПараметрыФормы.Вставить("РежимРасшифровки",               Истина);
		
		Отбор = Новый Структура;
		Отбор.Вставить("ДанныеОтчета", ДанныеРасшифровкиОтчета.ДанныеОтчета);
		
		Если ДанныеРасшифровкиОтчета.Свойство("СтатьяКалькуляции") Тогда
			Отбор.Вставить("СтатьяКалькуляции", ДанныеРасшифровкиОтчета.СтатьяКалькуляции);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ОткрытьФорму("Отчет.ОтклонениеЗаказаФактаОтКалькуляции.Форма", ПараметрыФормы, ЭтаФорма, Истина);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ПлановаяИФактическаяСебестоимостьПродукции" Тогда
		
		МенюОтчетов  = Новый Массив;
		МенюДействий  = Новый Массив;
		
		СписокПараметров = Новый Массив;
		СписокПараметров.Добавить("ДанныеПоСебестоимости");
		СписокПараметров.Добавить("Период");
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("Имя",              "ФактическаяСебестоимостьПродукции");
		ПараметрыОтчета.Вставить("Заголовок",        НСтр("ru = 'Фактическая себестоимость продукции';
															|en = 'Actual product cost'"));
		ПараметрыОтчета.Вставить("ИмяОтчета",        "Отчет.ФактическаяСебестоимостьПродукции");
		ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
		
		ПоляРасшифровки  = Новый Массив;
		ПоляРасшифровки.Добавить("Продукция");
		ПоляРасшифровки.Добавить("ХарактеристикаПродукции");
		ПоляРасшифровки.Добавить("НазначениеПродукции");
		ПоляРасшифровки.Добавить("ПартияПродукции");
		ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
		
		МенюОтчетов.Добавить(ПараметрыОтчета);
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("Имя",              "ПлановаяСебестоимостьПродукции");
		ПараметрыОтчета.Вставить("Заголовок",        НСтр("ru = 'Плановая себестоимость продукции';
															|en = 'Target product cost'"));
		ПараметрыОтчета.Вставить("ИмяОтчета",        "Отчет.ПлановаяСебестоимостьПродукции");
		ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
		
		ПоляРасшифровки  = Новый Массив;
		ПоляРасшифровки.Добавить("Продукция");
		ПоляРасшифровки.Добавить("ХарактеристикаПродукции");
		ПоляРасшифровки.Добавить("Калькуляция");
		ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
		
		МенюОтчетов.Добавить(ПараметрыОтчета);
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатраты"
		Или ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатратыПоПартиямПроизводства"
		Или ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатратыПостатейные"
		Или ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатратыТМЦ" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПоляРасшифровки = Новый Массив;
		
		СписокПараметров = Новый Массив;
		СписокПараметров.Добавить("ДанныеПоСебестоимости");
		СписокПараметров.Добавить("Период");
		СписокПараметров.Добавить("ПараметрПодразделение");
		
		ПоляГруппировкиСлужебные = Новый Массив;
		ПоляГруппировкиСлужебные.Добавить("ДвижениеМатериаловИРабот");
		ПоляГруппировкиСлужебные.Добавить("ДвижениеПродукцииИПолуфабрикатов");
		ПоляГруппировкиСлужебные.Добавить("ДвижениеПостатейныхРасходов");
		ПоляГруппировкиСлужебные.Добавить("РаспределениеЗатратНаПартииПроизводства");
		ПоляГруппировкиСлужебные.Добавить("ГруппировкаЗатратПоПартиямПроизводства");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляРасшифровки, ПоляГруппировкиСлужебные);
		
		ПоляГруппировки = Новый Массив;
		// для расшифровки ТМЦ
		ПоляГруппировки.Добавить("Организация");
		ПоляГруппировки.Добавить("СкладПодразделение");
		ПоляГруппировки.Добавить("АналитикаУчетаНоменклатуры");
		ПоляГруппировки.Добавить("Номенклатура");
		ПоляГруппировки.Добавить("Характеристика");
		ПоляГруппировки.Добавить("Серия");
		ПоляГруппировки.Добавить("Назначение");
		// для расшифровки постатейных
		ПоляГруппировки.Добавить("Подразделение");
		ПоляГруппировки.Добавить("СтатьяРасходов");
		ПоляГруппировки.Добавить("АналитикаРасходов");
		ПоляГруппировки.Добавить("НаправлениеДеятельности");
		// для расшифровки производства
		ПоляГруппировки.Добавить("ПартияПроизводства");
		ПоляГруппировки.Добавить("НаправлениеДеятельности");
		ПоляГруппировки.Добавить("СтатьяКалькуляции");
		ПоляГруппировки.Добавить("Затрата");
		ПоляГруппировки.Добавить("АналитикаЗатраты");
		ПоляГруппировки.Добавить("АналитикаЗатратыДополнительная");
		ПоляГруппировки.Добавить("ВидРабот");
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПоляРасшифровки, ПоляГруппировки);
		
		МассивПолейВарианта = Новый Массив;
		
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "аНачальныйОстатокКоличество,аНачальныйОстатокСтоимость,аНачальныйОстатокПР,аНачальныйОстатокВР", "НачальныйОстаток");
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "бПоступилоКоличество,бПоступилоСтоимость,бПоступилоПР,бПоступилоВР", "Поступления");
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "вПеремещенияКоличество,вПеремещенияСтоимость,вПеремещенияПР,вПеремещенияПР", "Перемещения");
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "жПереданоКоличество,жПереданоСтоимость,жПереданоПР,жПереданоВР", "ПередачиНаСклад");
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "гРасходНаПроизводствоКоличество,гРасходНаПроизводствоСтоимость,гРасходНаПроизводствоПР,гРасходНаПроизводствоВР", "РасходыНаПроизводство");
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "дСписаноКоличество,дСписаноСтоимость,дСписаноПР,дСписаноВР", "СписанияНаРасходы");
		ДобавитьГруппуКолонок(МассивПолейВарианта, ПоляРасшифровки, "еКонечныйОстатокКоличество,еКонечныйОстатокСтоимость,еКонечныйОстатокПР,еКонечныйОстатокВР", "КонечныйОстаток");
		
		ДанныеРасшифровкиОтчета = КомпоновкаДанныхВызовСервера.ПараметрыФормыРасшифровки(
												Расшифровка,
												ЭтаФорма.ОтчетДанныеРасшифровки,
												СписокПараметров,
												ПоляРасшифровки);
		
		Если ДанныеРасшифровкиОтчета = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВариантОтчета = Неопределено;
		
		Для Каждого Вариант Из МассивПолейВарианта Цикл
			Для Каждого ПолеРасшифровки Из Вариант.Поля Цикл
				Если ДанныеРасшифровкиОтчета.Свойство(ПолеРасшифровки) Тогда
					ВариантОтчета = Вариант.ВариантОтчета;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ЗначениеЗаполнено(ВариантОтчета) Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ТребуетсяПереопределениеПодразделения = Ложь;
		НеПереопределятьПользовательскиеПараметры = Ложь;
		Отбор = Новый Структура;
		ПользовательскиеПараметры = Новый Массив;
		Если ДанныеРасшифровкиОтчета.Свойство("ДвижениеМатериаловИРабот")
			Или ДанныеРасшифровкиОтчета.Свойство("ДвижениеПродукцииИПолуфабрикатов") Тогда
			
			ИмяОтчета = "Отчет.ПроизводственныеЗатратыТМЦ.Форма";
			
			Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
				
				Если СтрНайти(КлючТекущегоВарианта, "ПоПредприятию") = 0 Тогда
					
					ВариантОтчета = "ДвижениеМатериаловПродукцииРабот";
					Если ДанныеРасшифровкиОтчета.Свойство("Организация") Тогда
						Отбор.Вставить("Организация", ДанныеРасшифровкиОтчета.Организация);
					КонецЕсли;
					
				Иначе
					ВариантОтчета = "ДвижениеМатериаловПродукцииРаботПоПредприятию";
				КонецЕсли;
				
				НеПереопределятьПользовательскиеПараметры = Истина;
				
			КонецЕсли;
			
			Если ДанныеРасшифровкиОтчета.Свойство("ДвижениеМатериаловИРабот") Тогда
				Отбор.Вставить("Таблица",            "Материалы");
			Иначе
				Отбор.Вставить("Таблица",            "Продукция");
			КонецЕсли;
			
		ИначеЕсли ДанныеРасшифровкиОтчета.Свойство("ДвижениеПостатейныхРасходов")
			Или ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатратыПостатейные" Тогда
			
			ИмяОтчета = "Отчет.ПроизводственныеЗатратыПостатейные.Форма";
			
			Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
				
				Если СтрНайти(КлючТекущегоВарианта, "ПоПредприятию") = 0 Тогда
					
					ВариантОтчета = "ДвижениеПостатейныхРасходов";
					Если ДанныеРасшифровкиОтчета.Свойство("Организация") Тогда
						Отбор.Вставить("Организация", ДанныеРасшифровкиОтчета.Организация);
					КонецЕсли;
					
				Иначе
					ВариантОтчета = "ДвижениеПостатейныхРасходовПоПредприятию";
				КонецЕсли;
				
				НеПереопределятьПользовательскиеПараметры = Истина;
				
			КонецЕсли;
			
		ИначеЕсли ДанныеРасшифровкиОтчета.Свойство("РаспределениеЗатратНаПартииПроизводства")
			Или ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатратыПоПартиямПроизводства" Тогда
			
			ИмяОтчета = "Отчет.ПроизводственныеЗатратыПоПартиямПроизводства.Форма";
			
			Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
				
				Если СтрНайти(КлючТекущегоВарианта, "ПоПредприятию") = 0 Тогда
					
					ВариантОтчета = "РаспределениеЗатратНаПартииПроизводства";
					Если ДанныеРасшифровкиОтчета.Свойство("Организация") Тогда
						Отбор.Вставить("Организация", ДанныеРасшифровкиОтчета.Организация);
					КонецЕсли;
					
				Иначе
					ВариантОтчета = "РаспределениеЗатратНаПартииПроизводстваПоПредприятию";
				КонецЕсли;
				
				НеПереопределятьПользовательскиеПараметры = Истина;
				
			КонецЕсли;
			
		ИначеЕсли ДанныеРасшифровкиОтчета.Свойство("ГруппировкаЗатратПоПартиямПроизводства")
			Или ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатратыПоПартиямПроизводства" Тогда
			
			ИмяОтчета = "Отчет.ПроизводственныеЗатратыПоПартиямПроизводства.Форма";
			
			Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
				
				НеПереопределятьПользовательскиеПараметры = Истина;
				Если СтрНайти(КлючТекущегоВарианта, "ПоПредприятию") = 0 Тогда
					
					ВариантОтчета = "ГруппировкаЗатратПоПартиямПроизводства";
					Если ДанныеРасшифровкиОтчета.Свойство("Организация") Тогда
						Отбор.Вставить("Организация", ДанныеРасшифровкиОтчета.Организация);
					КонецЕсли;
					
				Иначе
					ВариантОтчета = "ГруппировкаЗатратПоПартиямПроизводстваПоПредприятию";
				КонецЕсли;
				
			КонецЕсли;
			
			ТребуетсяПереопределениеПодразделения = Истина;
			
		Иначе
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ВариантОтчета) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("КлючВарианта",                   ВариантОтчета);
		ПараметрыФормы.Вставить("СформироватьПриОткрытии",        Истина);
		
		Если ДанныеРасшифровкиОтчета.Свойство("ПараметрПодразделение") И ЗначениеЗаполнено(ДанныеРасшифровкиОтчета.ПараметрПодразделение) Тогда
			Если Не ТребуетсяПереопределениеПодразделения Тогда
				Отбор.Вставить("Подразделение",ДанныеРасшифровкиОтчета.ПараметрПодразделение);
			КонецЕсли;
		КонецЕсли;
		
		Если НеПереопределятьПользовательскиеПараметры Тогда
			ПользовательскийПараметр = Новый Структура("Имя, Значение", "ДанныеПоСебестоимости", ДанныеРасшифровкиОтчета.ДанныеПоСебестоимости);
			ПользовательскиеПараметры.Добавить(ПользовательскийПараметр);
			Если ДанныеРасшифровкиОтчета.Свойство("Период") И ЗначениеЗаполнено(ДанныеРасшифровкиОтчета.Период) Тогда
				ПользовательскийПараметр = Новый Структура("Имя, Значение", "Период", ДанныеРасшифровкиОтчета.Период);
				ПользовательскиеПараметры.Добавить(ПользовательскийПараметр);
			КонецЕсли;
		Иначе
			Отбор.Вставить("ДанныеПоСебестоимости", ДанныеРасшифровкиОтчета.ДанныеПоСебестоимости);
			Если ДанныеРасшифровкиОтчета.Свойство("Период") И ЗначениеЗаполнено(ДанныеРасшифровкиОтчета.Период) Тогда
				Отбор.Вставить("Период", ДанныеРасшифровкиОтчета.Период);
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого ПолеГруппировки Из ПоляГруппировки Цикл
			Если ДанныеРасшифровкиОтчета.Свойство(ПолеГруппировки) И ЗначениеЗаполнено(ДанныеРасшифровкиОтчета[ПолеГруппировки]) Тогда
				Отбор.Вставить(ПолеГруппировки,ДанныеРасшифровкиОтчета[ПолеГруппировки]);
			КонецЕсли;
		КонецЦикла;
		
		ПараметрыФормы.Вставить("Отбор", Отбор);
		ПараметрыФормы.Вставить("ПользовательскиеПараметры", ПользовательскиеПараметры);
		ПараметрыФормы.Вставить("НастройкиИсточник", ДанныеРасшифровкиОтчета);
		ОткрытьФорму(ИмяОтчета, ПараметрыФормы, ЭтаФорма, Истина);
		
	ИначеЕсли КлючТекущегоВарианта = "ПлановаяИФактическая"
		Или КлючТекущегоВарианта = "Отклонения" Тогда
		
		СписокПараметров = Новый Массив;
		
		СписокПараметров.Добавить("ДанныеОтчета");
		СписокПараметров.Добавить("ПериодОтчета");
		
		ПоляРасшифровки = Новый Массив;
		
		ПоляРасшифровки.Добавить("Продукция");
		ПоляРасшифровки.Добавить("ХарактеристикаПродукции");
		ПоляРасшифровки.Добавить("Калькуляция");
		ПоляРасшифровки.Добавить("СтатьяКалькуляции");
		ПоляРасшифровки.Добавить("СуммаКалькуляция");
		ПоляРасшифровки.Добавить("СуммаФакт");
		ПоляРасшифровки.Добавить("СуммаОтклонение");
		ПоляРасшифровки.Добавить("СуммаПроцентОтклонения");
		
		ДанныеРасшифровкиОтчета = КомпоновкаДанныхВызовСервера.ПараметрыФормыРасшифровки(
			Расшифровка,
			ЭтаФорма.ОтчетДанныеРасшифровки,
			СписокПараметров,
			ПоляРасшифровки);
		
		Если ДанныеРасшифровкиОтчета = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеРасшифровкиОтчета.Продукция)
			Или Не (ДанныеРасшифровкиОтчета.Свойство("СуммаКалькуляция")
			Или ДанныеРасшифровкиОтчета.Свойство("СуммаФакт")
			Или ДанныеРасшифровкиОтчета.Свойство("СуммаОтклонение")
			Или ДанныеРасшифровкиОтчета.Свойство("СуммаПроцентОтклонения")) Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("КлючВарианта",                   "ПлановаяИФактическаяРасшифровка");
		ПараметрыФормы.Вставить("СформироватьПриОткрытии",        Истина);
		ПараметрыФормы.Вставить("ВидимостьКомандВариантовОтчетов",Ложь);
		ПараметрыФормы.Вставить("РежимРасшифровки",               Истина);
		
		Отбор = Новый Структура;
		Отбор.Вставить("ДанныеОтчета", ДанныеРасшифровкиОтчета.ДанныеОтчета);
		Отбор.Вставить("ПериодОтчета", ДанныеРасшифровкиОтчета.ПериодОтчета);
		
		Если ДанныеРасшифровкиОтчета.Свойство("Продукция") Тогда
			Отбор.Вставить("Продукция", ДанныеРасшифровкиОтчета.Продукция);
		КонецЕсли;
		Если ДанныеРасшифровкиОтчета.Свойство("ХарактеристикаПродукции") Тогда
			Отбор.Вставить("ХарактеристикаПродукции", ДанныеРасшифровкиОтчета.ХарактеристикаПродукции);
		КонецЕсли;
		Если ДанныеРасшифровкиОтчета.Свойство("Калькуляция") Тогда
			Отбор.Вставить("Калькуляция", ДанныеРасшифровкиОтчета.Калькуляция);
		КонецЕсли;
		Если ДанныеРасшифровкиОтчета.Свойство("СтатьяКалькуляции") Тогда
			Отбор.Вставить("СтатьяКалькуляции", ДанныеРасшифровкиОтчета.СтатьяКалькуляции);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Отчет.ПлановаяИФактическаяСебестоимостьВыпущеннойПродукции.Форма", ПараметрыФормы, ЭтаФорма, Истина);
		
	ИначеЕсли КлючТекущегоВарианта = "АнализОтклоненийПоПодразделениям" Тогда
		
		СписокПараметров = Новый Массив;
		СписокПараметров.Добавить("ДанныеОтчета");
		
		ПоляРасшифровки = Новый Массив;
		
		ПоляРасшифровки.Добавить("ЗаказНаПроизводствоДубль");
		ПоляРасшифровки.Добавить("КодСтрокиЗаказаНаПроизводство");
		ПоляРасшифровки.Добавить("ЭтапДубль");
		ПоляРасшифровки.Добавить("СуммаЗаказВсеЭтапы");
		ПоляРасшифровки.Добавить("СуммаФактПрогноз");
		ПоляРасшифровки.Добавить("Отклонение_Сумма_ЗаказВсеЭтапы_ФактПрогноз");
		ПоляРасшифровки.Добавить("Процент_Сумма_ЗаказВсеЭтапы_ФактПрогноз");
		ПоляРасшифровки.Добавить("Значимость");
		
		ДанныеРасшифровкиОтчета = КомпоновкаДанныхВызовСервера.ПараметрыФормыРасшифровки(
			Расшифровка,
			ЭтаФорма.ОтчетДанныеРасшифровки,
			СписокПараметров,
			ПоляРасшифровки);
			
		Если ДанныеРасшифровкиОтчета = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Не ДанныеРасшифровкиОтчета.Свойство("ЗаказНаПроизводствоДубль")
			Или Не ДанныеРасшифровкиОтчета.Свойство("КодСтрокиЗаказаНаПроизводство")
			Или Не ДанныеРасшифровкиОтчета.Свойство("ЭтапДубль") 
			Или Не (ДанныеРасшифровкиОтчета.Свойство("СуммаЗаказВсеЭтапы")
			Или ДанныеРасшифровкиОтчета.Свойство("СуммаФактПрогноз")
			Или ДанныеРасшифровкиОтчета.Свойство("Отклонение_Сумма_ЗаказВсеЭтапы_ФактПрогноз")
			Или ДанныеРасшифровкиОтчета.Свойство("Процент_Сумма_ЗаказВсеЭтапы_ФактПрогноз")
			Или ДанныеРасшифровкиОтчета.Свойство("Значимость")) Тогда
			Возврат;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("КлючВарианта",                     "ОтклонениеФактаОтСпецификацииЗаказаБезПрогноза");
		ПараметрыФормы.Вставить("СформироватьПриОткрытии",          Истина);
		ПараметрыФормы.Вставить("ВидимостьКомандВариантовОтчетов",  Ложь);
		ПараметрыФормы.Вставить("РежимРасшифровки",                 Истина);
		
		Отбор = Новый Структура();
		
		Отбор.Вставить("ЗаказРасшифровка",                          ДанныеРасшифровкиОтчета.ЗаказНаПроизводствоДубль);
		Отбор.Вставить("КодСтрокиРасшифровка",                      ДанныеРасшифровкиОтчета.КодСтрокиЗаказаНаПроизводство);
		Отбор.Вставить("Этап",                                      ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеРасшифровкиОтчета.ЭтапДубль));
		Отбор.Вставить("ДанныеОтчета",                              ДанныеРасшифровкиОтчета.ДанныеОтчета);
		
		МассивТиповЗатрат = Новый Массив;
		МассивТиповЗатрат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЗатрат.Материальные"));
		МассивТиповЗатрат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЗатрат.ОплатаТруда"));
		МассивТиповЗатрат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыЗатрат.ВозвратныеОтходы"));
		
		Отбор.Вставить("ТипЗатрат",                                 МассивТиповЗатрат);
		
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Отчет.ОтклонениеЗаказаФактаОтКалькуляции.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, Истина);
	
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ПередачаМатериаловПоЗаказу" Тогда
		
		МенюОтчетов  = Новый Массив;
		МенюДействий = Новый Массив;
		
		Если КлючТекущегоВарианта = "ПередачаМатериаловПоЗаказуКонтекст"
			ИЛИ КлючТекущегоВарианта = "ПередачаМатериаловПоМаршрутномуЛистуКонтекст" Тогда
			
			// Расшифровать отчетом -> Маршрутные листы
			#Область МаршрутныеЛистыКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "МаршрутныеЛистыКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Маршрутные листы';
														|en = 'Route sheets'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.ПередачаМатериаловПоЗаказу");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Распоряжение");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
			// Расшифровать отчетом -> Передачи в производство
			#Область ПередачиВПроизводствоКонтекст
			ПараметрыОтчета = Новый Структура;
			ПараметрыОтчета.Вставить("Имя", "ПередачиВПроизводствоКонтекст");
			ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Передачи в производство';
														|en = 'Transfers to production'"));
			ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.ПередачаМатериаловПоЗаказу");
			
			ПоляРасшифровки  = Новый Массив;
			ПоляРасшифровки.Добавить("Подразделение");
			ПоляРасшифровки.Добавить("Номенклатура");
			ПоляРасшифровки.Добавить("Характеристика");
			ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
			
			СписокПараметров = Новый Массив;
			СписокПараметров.Добавить("Распоряжение");
			ПараметрыОтчета.Вставить("СписокПараметров", СписокПараметров);
			
			МенюОтчетов.Добавить(ПараметрыОтчета);
			#КонецОбласти
			
		КонецЕсли;
		
		ПараметрыРасшифровки = Новый Структура;
		ПараметрыРасшифровки.Вставить("МенюОтчетов",  МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка",  Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ПартииВПроизводстве" Тогда
		
		МенюОтчетов 			= Новый Массив;
		МенюДействий 			= Новый Массив;
		ПоляРасшифровки 		= Новый Массив;
		ПараметрыОтчета			= Новый Структура;
		ПараметрыДействия 		= Новый Структура;
		ПараметрыРасшифровки 	= Новый Структура;
		
		СтандартнаяОбработка = Ложь;
		ПоляРасшифровки.Добавить("ПартияПроизводства");
		
		ПараметрыОтчета.Вставить("Имя", "КонтрольДвиженияПродукцииИМатериаловПоЭтапуПроизводстваКонтекст");
		ПараметрыОтчета.Вставить("Заголовок", НСтр("ru = 'Контроль движения продукции и материалов по партии производства';
													|en = 'Control movement of manufactured products and materials by production batch'"));
		ПараметрыОтчета.Вставить("ИмяОтчета", "Отчет.КонтрольДвиженияПродукцииИМатериаловПоЭтапуПроизводства");
		ПараметрыОтчета.Вставить("ПоляРасшифровки", ПоляРасшифровки);
		
		МенюОтчетов.Добавить(ПараметрыОтчета);
		
		ПараметрыДействия.Вставить("Имя", "СостояниеОбеспечения");
		ПараметрыДействия.Вставить("Заголовок", НСтр("ru = 'Состояние обеспечения';
													|en = 'Supply state'"));
		ПараметрыДействия.Вставить("Действие", "ОткрытьФорму");
		ПараметрыДействия.Вставить("ИмяФормы", "Обработка.СостояниеОбеспечения.Форма");
		
		ПоляРасшифровки = Новый Массив;
		ПоляРасшифровки.Добавить("ЭтапПроизводства");
		
		СписокПараметров = Новый Массив;
		СписокПараметров.Добавить("ЭтапПроизводства");
		
		ДанныеРасшифровкиОтчета = КомпоновкаДанныхВызовСервера.ПараметрыФормыРасшифровки(
			Расшифровка,
			ЭтаФорма.ОтчетДанныеРасшифровки,
			СписокПараметров,
			ПоляРасшифровки);
		
		ПараметрыФормы = Новый Структура;
		
		СписокЗаказов = Новый СписокЗначений;
		СписокЗаказов.Добавить(ДанныеРасшифровкиОтчета.ЭтапПроизводства);
		
		ПараметрыФормы.Вставить("Заказы", СписокЗаказов);
		ПараметрыДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
		
		МенюДействий.Добавить(ПараметрыДействия);
		
		ПараметрыРасшифровки.Вставить("МенюОтчетов", МенюОтчетов);
		ПараметрыРасшифровки.Вставить("МенюДействий", МенюДействий);
		ПараметрыРасшифровки.Вставить("Расшифровка", Расшифровка);
		
		КомпоновкаДанныхКлиент.ОбработкаРасшифровкиСДополнительнымМеню(ЭтаФорма, ПараметрыРасшифровки, СтандартнаяОбработка);
		
	//-- НЕ УТКА
	
	ИначеЕсли ПолноеИмяОтчета = "Отчет.АнализРасходовПриУСН" Тогда
		ПараметрыРасшифровки = ОтчетыУПВызовСервераПереопределяемый.ПараметрыРасшифровкиАнализаРасходовПриУСН(
			Расшифровка, 
			ЭтаФорма.ОтчетДанныеРасшифровки, 
			ЭтаФорма.Отчет.КомпоновщикНастроек,
			КлючТекущегоВарианта);
		
		Если ПараметрыРасшифровки <> Неопределено Тогда
			СтандартнаяОбработка = Ложь;
			ОткрытьФорму("Отчет.АнализРасходовПриУСН.ФормаОбъекта", ПараметрыРасшифровки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик дополнительной расшифровки (меню табличного документа формы отчета).
//
// Параметры:
//   см. ОтчетыКлиентПереопределяемый.ОбработкаДополнительнойРасшифровки().
//
Процедура ОбработкаДополнительнойРасшифровки(ЭтаФорма, Элемент, Расшифровка, СтандартнаяОбработка) Экспорт
	
	ПолноеИмяОтчета = ЭтаФорма.НастройкиОтчета.ПолноеИмя;
	КлючТекущегоВарианта = ЭтаФорма.КлючТекущегоВарианта;
	
	Если ПолноеИмяОтчета = "Отчет.ПроизводственныеЗатраты" И КлючТекущегоВарианта = "ДвижениеТМЦиПроизводственныеЗатраты" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ДоступныеДействия = Новый Массив;
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Отфильтровать);
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Оформить);
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Сгруппировать);
		ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Упорядочить);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Расшифровка", Расшифровка);
		ДополнительныеПараметры.Вставить("ДанныеРасшифровки", ЭтаФорма.ОтчетДанныеРасшифровки);
		ДополнительныеПараметры.Вставить("АдресСхемы", ЭтаФорма.Отчет);
		ДополнительныеПараметры.Вставить("КлючОбъекта", ЭтаФорма.НастройкиОтчета.ПолноеИмя);
		ДополнительныеПараметры.Вставить("ПараметрыОбработчика", Неопределено);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаРасшифровкиСДополнительнымМенюЗавершение", КомпоновкаДанныхКлиент, ДополнительныеПараметры);
		
		ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(
			ЭтаФорма.ОтчетДанныеРасшифровки, 
			Новый ИсточникДоступныхНастроекКомпоновкиДанных(ЭтаФорма.Отчет));
		
		ОбработкаРасшифровки.ПоказатьВыборДействия(
			ОписаниеОповещения, 
			Расшифровка,
			ДоступныеДействия);
		
	КонецЕсли;
	
КонецПроцедуры

// Обработчик команд, добавленных динамически.
//
// Параметры:
//   см. ОтчетыКлиентПереопределяемый.ОбработчикКоманды().
//
Процедура ОбработчикКоманды(ЭтаФорма, Команда, Результат) Экспорт
	
	ПолноеИмяОтчета = ЭтаФорма.НастройкиОтчета.ПолноеИмя;
	
	//++ НЕ УТКА
	// Вызов подключаемых команд отчетов
	Если ПолноеИмяОтчета = "Отчет.СверкаОборотовМУиБУ" Тогда
		Если Команда.Имя = "НастройкаСоответствияСчетов" Тогда
			НастройкаСоответствияСчетов(ЭтаФорма);
		ИначеЕсли Команда.Имя = "НастройкаСоответствияСчетовИОборотов" Тогда
			НастройкаСоответствияСчетовИОборотов(ЭтаФорма);
		КонецЕсли;
		
	ИначеЕсли ПолноеИмяОтчета = "Отчет.ИспользованиеСчетовИСубконтоМеждународныйУчет" Тогда
		Если Команда.Имя = ИмяЭлемента(ЭтаФорма.НастройкиОтчета.ДопПараметры.ПланСчетов) Тогда
			УстановитьТипЗависимыхРеквизитов(ЭтаФорма);
		КонецЕсли;
		
	КонецЕсли;
	//-- НЕ УТКА
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Отчет", ПолноеИмяОтчета);
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда, ДополнительныеПараметры); 
	
КонецПроцедуры

// Обработчик результата выбора подчиненной формы.
//
// Параметры:
//   см. ОтчетыКлиентПереопределяемый.ОбработкаВыбора().
//
Процедура ОбработкаВыбора(ЭтаФорма, ВыбранноеЗначение, ИсточникВыбора, Результат) Экспорт
	
КонецПроцедуры

// Обработчик оповещения формы отчета.
//
// Параметры:
//   см. ОтчетыКлиентПереопределяемый.ОбработкаОповещения().
//
Процедура ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник) Экспорт
	
КонецПроцедуры
 
// Обработчик специальных действий при расшифровке отчетов.
// Например, открытие специализированных форм с параметризацией.
//
//	Параметры:
//		ПараметрыДействия - Структура
//			Имя - Строка - Имя выполняемого действия
//			Заголовок - Строка - Пользовательское представление выполняемого действия
//		ПараметрыРасшифровки - Структура - Параметры, передаваемые в форму.
//
Процедура ВыполнитьДействиеРасшифровки(ПараметрыДействия, ПараметрыРасшифровки) Экспорт
	
//++ НЕ УТКА
	Если ПараметрыДействия.Имя = "ИзменитьДоступностьВидаРЦ" Тогда
		
		ПланированиеПроизводстваКлиент.ИзменитьДоступностьВидаРЦ(
					ПараметрыРасшифровки.ВидРабочегоЦентра, 
					ПараметрыРасшифровки.ПериодГрафика);
	Иначе
//-- НЕ УТКА
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Не определен обработчик действия %1.';
											|en = '%1 action handler is not determined.'"),
										ПараметрыДействия.Заголовок);
//++ НЕ УТКА
	КонецЕсли;
//-- НЕ УТКА

КонецПроцедуры

// Метод вызывается из формы отчета после его формирования.
//
//	Параметры:
//		ФормаОтчета - УправляемаяФорма - форма отчета.
//
Процедура ПослеФормированияНаКлиенте(ФормаОтчета) Экспорт
	
	ДопСвойства = ФормаОтчета.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	ТребуетсяОбработчикОжидания = Ложь;
	Если ТипЗнч(ПараметрыПроверкиФоновыхЗаданий) <> Тип("Структура") Тогда
		ПараметрыПроверкиФоновыхЗаданий = Новый Структура;
		ПараметрыПроверкиФоновыхЗаданий.Вставить("Задания", Новый Структура());
		ПараметрыПроверкиФоновыхЗаданий.Вставить("Интервал", 7);
	КонецЕсли;
	Если ДопСвойства.Свойство("КоличествоДокументовКОтражениюВБюджетировании") Тогда
		ТребуетсяОбработчикОжидания = Истина;
		Если НЕ ПараметрыПроверкиФоновыхЗаданий.Задания.Свойство("ОтражениеДокументовВБюджетировании") Тогда
			ПараметрыТекущегоВызова = Новый Структура("НачалоПериода, КонецПериода", ДопСвойства.НачалоПериода, ДопСвойства.КонецПериода);
			ПараметрыЗадания = Новый Структура("Параметры, Формы", ПараметрыТекущегоВызова, Новый Соответствие());
			ПараметрыПроверкиФоновыхЗаданий.Задания.Вставить("ОтражениеДокументовВБюджетировании", ПараметрыЗадания);
		КонецЕсли;
		ПараметрыЗадания = ПараметрыПроверкиФоновыхЗаданий.Задания.ОтражениеДокументовВБюджетировании.Формы;
		ПараметрыФормы = Новый Структура("НачалоПериода, КонецПериода", ДопСвойства.НачалоПериода, ДопСвойства.КонецПериода);
		ПараметрыЗадания.Вставить(ФормаОтчета, ПараметрыФормы);
	КонецЕсли;
	Если ТребуетсяОбработчикОжидания Тогда
		ФормаОтчета.ПодключитьОбработчикОжидания("ПроверитьФоновыеЗадания", ПараметрыПроверкиФоновыхЗаданий.Интервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Метод вызывается из формы отчета после его формирования.
//
//	Параметры:
//		Ответ - КодВозвратаДиалога - При варианте "Да" будут выполнено переформирование форм
//		ПараметрыВыполнения - Структура - Содержит в себе перечень форм, которые необходимо переформировать.
//
Процедура ФормаОтчетаПослеПодтвержденияПереформирования(Ответ, ПараметрыВыполнения) Экспорт
	Перем НеобработанныеФормы;
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	Для Каждого ФормаОтчета Из ПараметрыВыполнения.Формы Цикл
		ФормаОтчета.ПодключитьОбработчикОжидания("Сформировать", 1, Истина);
	КонецЦикла;
	Если ПараметрыВыполнения.Свойство("НеобработанныеФормы", НеобработанныеФормы) Тогда
		Периоды = Новый Массив;
		Для Каждого КлючИЗначение Из НеобработанныеФормы Цикл
			Периоды.Добавить(КлючИЗначение.Значение.НачалоПериода);
			Периоды.Добавить(КлючИЗначение.Значение.КонецПериода);
		КонецЦикла;
		БюджетнаяОтчетностьВызовСервера.ОтразитьДокументыФоновымЗаданиемПоФормеСМаксимальнымПериодом(Периоды);
		Для Каждого КлючИЗначение Из НеобработанныеФормы Цикл
			ПослеФормированияНаКлиенте(КлючИЗначение.Ключ);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Возникает после окончания формирования отчета.
//
// Параметры:
//   ФормаОтчета - УправляемаяФорма - Форма отчета.
//   ОтчетСформирован - Булево - Истина если отчет был успешно сформирован.
//
Процедура ПослеФормирования(ФормаОтчета, ОтчетСформирован) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТКА

// Вызывается для отчета "СверкаОборотовМУиБУ"
//
Процедура НастройкаСоответствияСчетов(ЭтаФорма)
	
	ОткрытьФорму("Справочник.СоответствияСчетовМеждународногоУчета.Форма.ФормаНастройкиДляОтчетаСверки"
		, , ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Вызывается для отчета "СверкаОборотовМУиБУ"
//
Процедура НастройкаСоответствияСчетовИОборотов(ЭтаФорма)
	
	ОткрытьФорму("Обработка.НастройкаСоответствияСчетовИОборотовМеждународногоУчета.Форма.Форма"
		, , ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Вызывается для отчета "СверкаОборотовМУиБУ"
//
Процедура ОткрытьОтчетАнализСчетаХозрасчетный(Отчет, ДанныеРасшифровки, Счет)
	
	ПараметрыФормы = Новый Структура("Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии");
	ПараметрыФормы.СформироватьПриОткрытии = Истина;
	ПараметрыФормы.КлючВарианта = "АнализСчета";
	
	ИдентификаторПараметраПериод = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ПолучитьИдентификаторПоОбъекту(
		Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета")));
	НастройкаПериод = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторПараметраПериод);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Счет", Счет);
	
	Если НастройкаПериод.Использование Тогда
		Отбор.Вставить("Период", НастройкаПериод.Значение);
	КонецЕсли;
	
	Для Каждого Элемент Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если Не ПустаяСтрока(Элемент.ИдентификаторПользовательскойНастройки) Тогда
			ИдентификаторОтбора = Элемент.ИдентификаторПользовательскойНастройки;
			НастройкаОтбора = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторОтбора);
			Если НастройкаОтбора.Использование Тогда
				Отбор.Вставить(Строка(Элемент.ЛевоеЗначение), НастройкаОтбора.ПравоеЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыФормы.Отбор = Отбор;
	
	ОтчетыУПВызовСервераПереопределяемый.ЗаполнитьДанныеРасшифровкиАнализаСчетаХозрасчетный(ДанныеРасшифровки, Отбор);
	
	ЗаполняемыеНастройки = Новый Структура("Показатели, Группировка, Отбор, ВыводимыеДанные");
	ЗаполняемыеНастройки.Вставить("Показатели",			Ложь);
	ЗаполняемыеНастройки.Вставить("Группировка",		Истина);
	ЗаполняемыеНастройки.Вставить("Отбор",				Ложь);
	ЗаполняемыеНастройки.Вставить("ВыводимыеДанные",	Истина);
	
	ПараметрыФормы = Новый Структура("ВидРасшифровки, АдресНастроек, СформироватьПриОткрытии, ИДРасшифровки, ЗаполняемыеНастройки");
	ПараметрыФормы.Вставить("ВидРасшифровки",			1);
	ПараметрыФормы.Вставить("АдресНастроек",			ДанныеРасшифровки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии",	Истина);
	ПараметрыФормы.Вставить("ИДРасшифровки",			"АнализСчета");
	ПараметрыФормы.Вставить("ЗаполняемыеНастройки",		ЗаполняемыеНастройки);
	
	ОткрытьФорму("Отчет.АнализСчета.Форма.ФормаОтчета", ПараметрыФормы,, Истина);
	
КонецПроцедуры

// Вызывается для отчета "СверкаОборотовМУиБУ"
//
Процедура ОткрытьОтчетАнализСчетаМеждународный(Отчет, Счет)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Ресурс", "Сумма");
	Параметры.Вставить("Отбор", Новый Структура);
	Отбор = Параметры.Отбор;
	
	НастройкиОтчета = Отчет.КомпоновщикНастроек.Настройки;
	НастройкиПользователя = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	ИдентификаторПараметраПериод = НастройкиПользователя.ПолучитьИдентификаторПоОбъекту(
		НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодОтчета")));
	
	НастройкаПериод = НастройкиПользователя.Элементы.Найти(ИдентификаторПараметраПериод);
	
	Параметры.Вставить("Счет", Счет);
	Параметры.Вставить("ПериодОтчета", НастройкаПериод.Значение);
	
	Для Каждого Элемент Из Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если Не ПустаяСтрока(Элемент.ИдентификаторПользовательскойНастройки) Тогда
			ИдентификаторОтбора = Элемент.ИдентификаторПользовательскойНастройки;
			НастройкаОтбора = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторОтбора);
			Если НастройкаОтбора.Использование Тогда
				Отбор.Вставить(Строка(Элемент.ЛевоеЗначение), НастройкаОтбора.ПравоеЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НастройкаАнализаСчета = МеждународнаяОтчетностьВызовСервера.НастроитьАнализСчетаМеждународный(Параметры);
	КлючНастроек = "РасшифровкаОтчетаСверкаОборотовМУиБУ";
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "АнализСчетаМеждународный");
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНастроек);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", КлючНастроек);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", НастройкаАнализаСчета.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкаАнализаСчета.ФиксированныеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.БухгалтерскийОтчетМеждународный.Форма", ПараметрыФормы);
	
КонецПроцедуры

// Вызывается для отчета "АнализСчетаМеждународный"
//
Процедура ОткрытьОтчетКарточкаСчетаМеждународный(Отчет, ПоляРасшифровки)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИмяОтчета"			, "КарточкаСчетаМеждународный");
	Параметры.Вставить("КлючВарианта"		, "КарточкаСчетаМеждународный");
	Параметры.Вставить("КомпоновщикНастроек", Отчет.КомпоновщикНастроек);
	Параметры.Вставить("ПоляРасшифровки"	, ПоляРасшифровки);
	
	НастройкаКарточкиСчета = ОтчетыУТВызовСервераПереопределяемый.НастроитьОтчетРасшифровки(Параметры);
	
	КлючНастроек = "РасшифровкаАнализаСчета";
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "КарточкаСчетаМеждународный");
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", КлючНастроек);
	ПараметрыФормы.Вставить("КлючПользовательскихНастроек", КлючНастроек);
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", НастройкаКарточкиСчета.ПользовательскиеНастройки);
	ПараметрыФормы.Вставить("ФиксированныеНастройки", НастройкаКарточкиСчета.ФиксированныеНастройки);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.КарточкаСчетаМеждународный.Форма", ПараметрыФормы);
	
КонецПроцедуры

// Вызывается для отчета "ИспользованиеСчетовИСубконтоМеждународныйУчет"
//
Процедура УстановитьТипЗависимыхРеквизитов(ЭтаФорма)
	
	ДопПараметры = ЭтаФорма.НастройкиОтчета.ДопПараметры;
	
	ПользовательскиеНастройки = ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
	ДопСвойства = ЭтаФорма.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	ДопСвойства.Очистить();
	
	ДопСвойства.Вставить("ЭтоМеждународный", ЭтаФорма[ИмяЭлемента(ДопПараметры.ПланСчетов)]);
	ПланСчетов = ПользовательскиеНастройки.Найти(ДопПараметры.ПланСчетов);
	ПланСчетов.Значение = ДопСвойства.ЭтоМеждународный;
	
	Элементы = ЭтаФорма.Элементы;
	ЭлементСчет = Элементы[ИмяЭлемента(ДопПараметры.Счет)];
	ЭлементВидСубконто = Элементы[ИмяЭлемента(ДопПараметры.ВидСубконто)];
	
	Счет = ПользовательскиеНастройки.Найти(ДопПараметры.Счет);
	ВидСубконто = ПользовательскиеНастройки.Найти(ДопПараметры.ВидСубконто);
	СубконтоМежд = ПользовательскиеНастройки.Найти(ДопПараметры.СубконтоМежд);
	СубконтоРегл = ПользовательскиеНастройки.Найти(ДопПараметры.СубконтоРегл);
	Если НЕ ДопСвойства.ЭтоМеждународный Тогда
		Счет.Значение = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ПустаяСсылка");
		ВидСубконто.Значение = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка");
		
		ЭлементСчет.ОграничениеТипа = Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный");
		ЭлементВидСубконто.ОграничениеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные");
		Элементы.ГруппаЗначенийСубконто.ТекущаяСтраница = Элементы.СубконтоХозрасчетное;
	Иначе
		Счет.Значение = ПредопределенноеЗначение("ПланСчетов.Международный.ПустаяСсылка");
		ВидСубконто.Значение = ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка");
		
		ЭлементСчет.ОграничениеТипа = Новый ОписаниеТипов("ПланСчетовСсылка.Международный");
		ЭлементВидСубконто.ОграничениеТипа = Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ВидыСубконтоМеждународные");
		Элементы.ГруппаЗначенийСубконто.ТекущаяСтраница = Элементы.СубконтоМеждународное;
	КонецЕсли;
	СубконтоМежд.Значение = Неопределено;
	СубконтоРегл.Значение = Неопределено;
	
КонецПроцедуры

Функция ИмяЭлемента(Идентификатор)
	
	Возврат "ЗначениеПараметраНастроек_Значение_"+СтрЗаменить(Идентификатор,"-","");
	
КонецФункции

Процедура ДобавитьГруппуКолонок(МассивПолейВарианта, ОбщийМассивПолей, СтрокаПолей, ВариантОтчета)
	
	Поля = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаПолей,",", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбщийМассивПолей, Поля);
	
	СтруктураПолей = Новый Структура;
	СтруктураПолей.Вставить("Поля", Поля);
	СтруктураПолей.Вставить("ВариантОтчета", ВариантОтчета);
	
	МассивПолейВарианта.Добавить(СтруктураПолей);
	
КонецПроцедуры

//-- НЕ УТКА
#КонецОбласти 