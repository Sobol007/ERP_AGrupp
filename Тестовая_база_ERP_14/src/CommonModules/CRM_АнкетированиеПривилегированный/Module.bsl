
////////////////////////////////////////////////////////////////////////////////
// Анкетирование привилегированный (CRM)
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Процедура записывает реквизиты объекта анкетирования.
//
// Параметры:
//  Анкета	 - ДокументОбъект.Анкета - Анкета. 
//
Процедура ЗаписатьРеквизитыОбъектаАнкетирования(Анкета) Экспорт
	
	ТаблицаРеквизитовОбъекта = Новый ТаблицаЗначений;
	ТаблицаРеквизитовОбъекта.Колонки.Добавить("ИмяСправочника");
	ТаблицаРеквизитовОбъекта.Колонки.Добавить("ИмяРеквизита");
	ТаблицаРеквизитовОбъекта.Колонки.Добавить("ЗначениеРеквизита");
	ТаблицаРеквизитовОбъекта.Колонки.Добавить("ТипОтвета");
	
	// Выберем вопросы, ответы на которые необходимо записать в объект.
	
	Для Каждого СтрокаСостав Из Анкета.Состав Цикл
		
		Если ЗначениеЗаполнено(СтрокаСостав.ЭлементарныйВопрос) Тогда
			
			ИмяСправочника = СтрокаСостав.ЭлементарныйВопрос.CRM_ИмяСправочника;
			ИмяРеквизита   = СтрокаСостав.ЭлементарныйВопрос.CRM_ИмяРеквизита;
			
			Если ПустаяСтрока(ИмяСправочника) Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			СтрокаТаблицаРеквизитовОбъекта = ТаблицаРеквизитовОбъекта.Добавить();
			СтрокаТаблицаРеквизитовОбъекта.ИмяСправочника = ИмяСправочника;
			СтрокаТаблицаРеквизитовОбъекта.ИмяРеквизита = ИмяРеквизита;
			СтрокаТаблицаРеквизитовОбъекта.ЗначениеРеквизита = СтрокаСостав.Ответ;
			
			Если Найти(ИмяРеквизита, "ДополнительныйРеквизит_") = 1 Тогда
				
				// дополнительный реквизит.
				
				СтрокаТаблицаРеквизитовОбъекта.ТипОтвета = "ДР"
				
			ИначеЕсли Найти(ИмяРеквизита, "КонтактнаяИнформация_") = 1 Тогда
				
				// контактная информация.
				
				СтрокаТаблицаРеквизитовОбъекта.ТипОтвета = "КИ"
				
			Иначе
				
				// реквизит справочника.
				
				СтрокаТаблицаРеквизитовОбъекта.ТипОтвета = "РС"
				
			КонецЕсли;	
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаРеквизитовОбъекта.Количество() > 0 Тогда
		
		ОбъектАнкетирования = Анкета.Респондент.ПолучитьОбъект();
		
		ИмяСправочникаМетаданные = ОбъектАнкетирования.Метаданные().Имя;
		
		// запишем простые реквизиты.
		
		МассивСтрокРеквизитов = ТаблицаРеквизитовОбъекта.НайтиСтроки(Новый Структура("ТипОтвета", "РС")); 
		
		Для Каждого СтрокаТаблицаРеквизитовОбъекта Из МассивСтрокРеквизитов Цикл
			
			Если СтрокаТаблицаРеквизитовОбъекта.ИмяСправочника = ИмяСправочникаМетаданные Тогда
				
				Попытка
					
					ОбъектАнкетирования[СтрокаТаблицаРеквизитовОбъекта.ИмяРеквизита] = СтрокаТаблицаРеквизитовОбъекта.ЗначениеРеквизита;
					
				Исключение
					
					ЗаписьЖурналаРегистрации(НСтр("ru='Заполнение анкет';en='Filling of questionnaires'"), 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				КонецПопытки;
				
			КонецЕсли;	
			
		КонецЦикла;		
		
		// Запишем дополнительные реквизиты.
		
		МассивСтрокРеквизитов = ТаблицаРеквизитовОбъекта.НайтиСтроки(Новый Структура("ТипОтвета", "ДР")); 
		
		Для Каждого СтрокаТаблицаРеквизитовОбъекта Из МассивСтрокРеквизитов Цикл
			
			Если СтрокаТаблицаРеквизитовОбъекта.ИмяСправочника = ИмяСправочникаМетаданные Тогда
				
				Попытка
					
					ИмяДопРеквизита = СтрЗаменить(СтрокаТаблицаРеквизитовОбъекта.ИмяРеквизита, "ДополнительныйРеквизит_", ""); 
					
					ДопРеквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяДопРеквизита);
					
					ЗначениеДопРеквизита = СтрокаТаблицаРеквизитовОбъекта.ЗначениеРеквизита;
					
					СтрокаДополнительныеРеквизиты = ОбъектАнкетирования.ДополнительныеРеквизиты.Найти(ДопРеквизит, "Свойство");
					
					Если СтрокаДополнительныеРеквизиты = Неопределено Тогда
						
						СтрокаДополнительныеРеквизиты = ОбъектАнкетирования.ДополнительныеРеквизиты.Добавить();
						
						СтрокаДополнительныеРеквизиты.Свойство = ДопРеквизит;
						
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(ЗначениеДопРеквизита) ИЛИ (ТипЗнч(ЗначениеДопРеквизита) = Тип("Булево") И ЗначениеДопРеквизита = Ложь) Тогда
						
						ОбъектАнкетирования.ДополнительныеРеквизиты.Удалить(СтрокаДополнительныеРеквизиты);
						
					Иначе						
						
						СтрокаДополнительныеРеквизиты.Значение = ЗначениеДопРеквизита;
						
						// Поддержка строк неограниченной длины.
						Свойство = ДопРеквизит.ПолучитьОбъект();
						
						Если УправлениеСвойствамиСлужебный.ИспользоватьНеограниченнуюСтроку(Свойство.ТипЗначения, Свойство.МногострочноеПолеВвода) Тогда
							
							СтрокаДополнительныеРеквизиты.ТекстоваяСтрока = ЗначениеДопРеквизита;
							
						КонецЕсли;
						
					КонецЕсли;					
							
				Исключение
					
					ЗаписьЖурналаРегистрации(НСтр("ru='Заполнение анкет';en='Filling of questionnaires'"), 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				КонецПопытки;
				
			КонецЕсли;	
			
		КонецЦикла;		
		
		// Запишем контактную информацию.
		
		МассивСтрокРеквизитов = ТаблицаРеквизитовОбъекта.НайтиСтроки(Новый Структура("ТипОтвета", "КИ")); 
		
		// Создадим таблицу для поиска вида контактной информации.

		ИмяСправочника = ОбъектАнкетирования.Метаданные().Имя;
		
		ГруппаВидовКИ = Новый Массив;
		
		ГруппаВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации["Справочник" + ИмяСправочника]);
		
		Если ИмяСправочника = "Партнеры" Тогда
			
			ГруппаВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации["CRM_Справочник" + ИмяСправочника + "Компания"]);
			ГруппаВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации["CRM_Справочник" + ИмяСправочника + "ЧастноеЛицо"]);
			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст =					
		"ВЫБРАТЬ
		|	ВидыКонтактнойИнформации.Ссылка КАК ВидКонтактнойИнформации,
		|	ВидыКонтактнойИнформации.Наименование
		|ИЗ
		|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
		|ГДЕ
		|	ВидыКонтактнойИнформации.Родитель В (&ГруппаВидовКИ)";
		
		Запрос.УстановитьПараметр("ГруппаВидовКИ", ГруппаВидовКИ);
		
		ТаблицаВидовКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
		
		Для Каждого СтрокаТаблицаРеквизитовОбъекта Из МассивСтрокРеквизитов Цикл
			
			Если СтрокаТаблицаРеквизитовОбъекта.ИмяСправочника = ИмяСправочникаМетаданные Тогда
				
				Попытка
					
					ИмяДопРеквизита = СокрЛП(СтрЗаменить(СтрокаТаблицаРеквизитовОбъекта.ИмяРеквизита, "КонтактнаяИнформация_", ""));
									
					ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
					
					Для Каждого СтрокаТаблицыВидовКИ Из ТаблицаВидовКонтактнойИнформации Цикл
						
						Если СокрЛП(СтрокаТаблицыВидовКИ.Наименование) = ИмяДопРеквизита Тогда 
							
							ВидКонтактнойИнформации = СтрокаТаблицыВидовКИ.ВидКонтактнойИнформации;
							
							Прервать;							
							
						КонецЕсли;
						
					КонецЦикла;				
					
					ТипКонтактнойИнформации = ВидКонтактнойИнформации.Тип;
					
					ЗначениеКонтактнойИнформации = СтрокаТаблицаРеквизитовОбъекта.ЗначениеРеквизита;
					
					СтрокаКонтактнойИнформации = ОбъектАнкетирования.КонтактнаяИнформация.Найти(ВидКонтактнойИнформации, "Вид");
					
					Если СтрокаКонтактнойИнформации = Неопределено Тогда
						
						СтрокаКонтактнойИнформации = ОбъектАнкетирования.КонтактнаяИнформация.Добавить();
						
						СтрокаКонтактнойИнформации.Вид = ВидКонтактнойИнформации;
						
						СтрокаКонтактнойИнформации.Тип = ТипКонтактнойИнформации;
						
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(ЗначениеКонтактнойИнформации) Тогда
						
						ОбъектАнкетирования.КонтактнаяИнформация.Удалить(СтрокаКонтактнойИнформации);
						
					Иначе						
						
						СтрокаКонтактнойИнформации.Представление = ЗначениеКонтактнойИнформации;						
						
					КонецЕсли;
					
					// Заполним реквизиты, соответствующие типу контактной информации.
					
					Если ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
						
						
					ИначеЕсли ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
						
						
					ИначеЕсли ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
						
						
					ИначеЕсли ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Факс Тогда	
						
						
					ИначеЕсли ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
						
					
					ИначеЕсли ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Другое Тогда	
						
						
					КонецЕсли;	
							
				Исключение
					
					ЗаписьЖурналаРегистрации(НСтр("ru='Заполнение анкет';en='Filling of questionnaires'"), 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					
				КонецПопытки;
				
			КонецЕсли;	
			
		КонецЦикла;
		
		Попытка
			
			ОбъектАнкетирования.Записать();
			
			СтрокаСообщения = НСтр("ru='Записаны реквизиты %1 ""%2"".';en='Details %1 ""%2"" was written down.'");
			
			Если ИмяСправочникаМетаданные = "КонтактныеЛицаПартнеров" Тогда
				
				ПервыйПараметр = "контактного лица";
				
			ИначеЕсли ИмяСправочникаМетаданные = "Партнеры" Тогда
				
				ПервыйПараметр = "клиента";
				
			Иначе
				
				ПервыйПараметр = "пользователя";
				
			КонецЕсли;	
			
			СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, ПервыйПараметр, ОбъектАнкетирования.Наименование);
			
			ЗаписьЖурналаРегистрации(НСтр("ru='Заполнение анкет';en='Filling of questionnaires'"),
			УровеньЖурналаРегистрации.Информация, , ,
			СтрокаСообщения);
			
		Исключение
			
			ЗаписьЖурналаРегистрации(НСтр("ru='Заполнение анкет';en='Filling of questionnaires'"), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
			
	КонецЕсли;
	
КонецПроцедуры // ЗаписатьРеквизитыОбъектаАнкетирования(Анкета) Экспорт

// Функция получает реквизит из справочника.
//
// Параметры:
//  ОбъектАнкетирования	 - ОпределяемыйТип - Определяемы тип "Респодент".
//  Вопрос				 - СтрокаТаблицыЗначений - Строка таблицы значений.
// 
// Возвращаемое значение:
//  Неопределено, Строка - Значение реквизита. 
//
Функция ПолучитьРеквизитИзСправочника(ОбъектАнкетирования, Вопрос) Экспорт
	
	ИмяСправочника = Вопрос.CRM_ИмяСправочника;
	ИмяРеквизита   = Вопрос.CRM_ИмяРеквизита;
	
	ЗначениеИзСправочника = Неопределено;
	
	Попытка
		
		ИмяСправочникаМетаданные = ОбъектАнкетирования.Метаданные().Имя;
		
		Если ИмяСправочника = ИмяСправочникаМетаданные Тогда
			
			Если Найти(ИмяРеквизита, "ДополнительныйРеквизит_") = 1 Тогда
				
				// дополнительный реквизит
				
				ИмяДопРеквизита = СтрЗаменить(ИмяРеквизита, "ДополнительныйРеквизит_", ""); 
				
				ДопРеквизит = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию(ИмяДопРеквизита);
				
				СтрокаДополнительныеРеквизиты = ОбъектАнкетирования.ДополнительныеРеквизиты.Найти(ДопРеквизит, "Свойство");
				
				Если Не СтрокаДополнительныеРеквизиты = Неопределено Тогда
					
					ЗначениеИзСправочника = СтрокаДополнительныеРеквизиты.Значение;
					
				КонецЕсли;
			
			ИначеЕсли Найти(ИмяРеквизита, "КонтактнаяИнформация_") = 1 Тогда
				
				// контактная информация				
				
				ИмяДопРеквизита = СтрЗаменить(ИмяРеквизита, "КонтактнаяИнформация_", ""); 
				
				ГруппаВидовКИ = Новый Массив;
				
				ГруппаВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации["Справочник" + ИмяСправочникаМетаданные]);
				
				Если ИмяСправочника = "Партнеры" Тогда
					
					ГруппаВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации["CRM_Справочник" + ИмяСправочникаМетаданные + "Компания"]);
					ГруппаВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации["CRM_Справочник" + ИмяСправочникаМетаданные + "ЧастноеЛицо"]);
					
				КонецЕсли;
				
				Запрос = Новый Запрос;
				Запрос.Текст =					
				"ВЫБРАТЬ
				|	ВидыКонтактнойИнформации.Ссылка КАК ВидКонтактнойИнформации,
				|	ВидыКонтактнойИнформации.Наименование
				|ИЗ
				|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
				|ГДЕ
				|	ВидыКонтактнойИнформации.Родитель В (&ГруппаВидовКИ)";
				
				Запрос.УстановитьПараметр("ГруппаВидовКИ", ГруппаВидовКИ);
				
				ТаблицаВидовКонтактнойИнформации = Запрос.Выполнить().Выгрузить();
				
				ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
				
				Для Каждого СтрокаТаблицыВидовКИ Из ТаблицаВидовКонтактнойИнформации Цикл
					
					Если СокрЛП(СтрокаТаблицыВидовКИ.Наименование) = ИмяДопРеквизита Тогда 
						
						ВидКонтактнойИнформации = СтрокаТаблицыВидовКИ.ВидКонтактнойИнформации;
						
						Прервать;							
						
					КонецЕсли;
					
				КонецЦикла;				
							
				СтрокаКонтактнойИнформации = ОбъектАнкетирования.КонтактнаяИнформация.Найти(ВидКонтактнойИнформации, "Вид");
				
				Если Не СтрокаКонтактнойИнформации = Неопределено Тогда
					
					ЗначениеИзСправочника = СтрокаКонтактнойИнформации.Представление;
					
				КонецЕсли;
				
			Иначе
				
				// реквизит справочника
				
				ЗначениеИзСправочника = ОбъектАнкетирования[ИмяРеквизита];			
				
			КонецЕсли;	
			
		КонецЕсли;	
		
	Исключение
		
	КонецПопытки;	
	
	Возврат	ЗначениеИзСправочника;
	
КонецФункции // ПолучитьРеквизитИзСправочника(Реквизит, Вопрос)	
	
#КонецОбласти
