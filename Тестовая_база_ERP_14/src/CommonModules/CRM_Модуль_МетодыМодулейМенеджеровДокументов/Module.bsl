
#Область ОпределениеФормОбъектов

// Процедура - Обработка получения форм объектов CRM
//
// Параметры:
//  Источник				 - ДокументСсылка - Источник. 
//  ВидФормы				 - Строка - Имя стандартной формы. 
//  Параметры				 - Струтура	 - Параметры формы. 
//  ВыбраннаяФорма			 - Строка, УправляемаяФорма	 - Содержит имя открываемой формы или объект метаданных Форма.
//  ДополнительнаяИнформация - Струтура	 - Дополнительная информация открытия формы.
//  СтандартнаяОбработка	 - Булево - Признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаПолученияФормОбъектовCRMМодуль(Источник, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
	
	Если Параметры.Свойство("Ключ") Тогда
		ВыбраннаяФорма = Неопределено;
	КонецЕсли;
	
	#Область ПолученияФормВстреча
	
	Если ТипЗнч(Источник) = Тип("ДокументМенеджер.Встреча") Тогда
	
		Если ВидФормы = "ФормаОбъекта" Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "Документ.CRM_Взаимодействие.Форма.ФормаДокумента";
			Параметры.Вставить("ВидВзаимодействия", Справочники.CRM_ВидыВзаимодействий.Встреча);
			CRM_МетодыМодулейМенеджеровДокументов.ПодменитьПараметрыПартнера(Параметры);
		КонецЕсли;
	
	#КонецОбласти
	
	#Область ПолученияФормЗапланированноеВзаимодействие
	
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументМенеджер.ЗапланированноеВзаимодействие") Тогда
	
		Если ВидФормы = "ФормаОбъекта" Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "Документ.CRM_Взаимодействие.Форма.ФормаДокумента";
			ВидВзаимодействияПрочее = CRM_МетодыМодулейМенеджеровДокументов.ПолучитьВидВзаимодействия(Перечисления.CRM_ВидыСобытий.Прочее);
			Параметры.Вставить("ВидВзаимодействия", ВидВзаимодействияПрочее);
			CRM_МетодыМодулейМенеджеровДокументов.ПодменитьПараметрыПартнера(Параметры);
		КонецЕсли;
		
	КонецЕсли;
		
	#КонецОбласти
	
	Если ВыбраннаяФорма = Неопределено И Параметры.Свойство("Ключ") Тогда
		ВыбраннаяФорма = "Документ." + Параметры.Ключ.Метаданные().Имя +"."+ ВидФормы;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти // ОпределениеФормОбъектов

#Область CRM_Интерес

// Заполняет таблицу реквизитов, зависимых от хозяйственной операции
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция соглашения
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от хозяйственной операции
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от хозяйственной операции.
//
Процедура ИнтересЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Товары.ПроцентРучнойСкидки");
	МассивВсехРеквизитов.Добавить("Товары.СуммаРучнойСкидки");
	МассивВсехРеквизитов.Добавить("Товары.ПроцентАвтоматическойСкидки");
	МассивВсехРеквизитов.Добавить("Товары.СуммаАвтоматическойСкидки");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
		МассивРеквизитовОперации.Добавить("Товары.ПроцентРучнойСкидки");
		МассивРеквизитовОперации.Добавить("Товары.СуммаРучнойСкидки");
		МассивРеквизитовОперации.Добавить("Товары.ПроцентАвтоматическойСкидки");
		МассивРеквизитовОперации.Добавить("Товары.СуммаАвтоматическойСкидки");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров для заполнения налогообложения НДС продажи.
//
// Параметры:
//  Объект - ДокументОбъект.КоммерческоеПредложениеКлиенту - документ, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  Структура - Параметры заполнения, описание параметров см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
//
Функция ИнтересПараметрыЗаполненияНалогообложенияНДСПродажи(Объект) Экспорт
	
	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСПродажи();
	
	ПараметрыЗаполнения.Организация = Объект.Организация;
	ПараметрыЗаполнения.Дата = Объект.Дата;
	ПараметрыЗаполнения.ЭтоЗаказ = Истина;
	
	ПараметрыЗаполнения.РеализацияТоваров = Истина;
	ПараметрыЗаполнения.РеализацияРаботУслуг = Истина;
		
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти // CRM_Интерес