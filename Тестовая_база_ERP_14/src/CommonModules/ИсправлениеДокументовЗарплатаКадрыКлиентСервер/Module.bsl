////////////////////////////////////////////////////////////////////////////////
// ПОДСИСТЕМА ОСТАТКИ ОТПУСКОВ
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Устанавливает значения реквизитам формы с включенным механизмом исправлений.
// Параметры:
// 		Форма, 
//		РежимИсправления - допустимые значения 
//				"РасчетЗарплаты" - для расчетных документов
//				"ПериодическиеСведения" - для документов, которые вводят периодические сведения.
//		ПолеПериодРегистрации - по умолчанию "ПериодРегистрации".
//
Процедура УстановитьПоляИсправления(Форма, РежимИсправления = "РасчетЗарплаты", ПолеПериодРегистрации = "ПериодРегистрации") Экспорт
	
	Если НЕ Форма.ИспользоватьРасчетЗарплатыРасширенная Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОсновногоРеквизита = "Объект";
	
	ДокументУтвержден = УтверждениеДокумента(Форма, Форма.Элементы.Найти("Расчетчик"));
	ДоступноИсправлениеДокумента = ДокументУтвержден И Форма.ДоступноИсправлениеДокумента;
	ДоступноСторнированиеДокумента = ДокументУтвержден И Форма.ДоступноСторнированиеДокумента;
	
	Если Форма.ИспользоватьИсправлениеДокумента Тогда
		ИсправленныйДокумент = Форма[ИмяОсновногоРеквизита].ИсправленныйДокумент;
		ЭтоДокументИсправление = ЗначениеЗаполнено(ИсправленныйДокумент);
	Иначе
		ИсправленныйДокумент = Неопределено;
		ЭтоДокументИсправление = Ложь;
	КонецЕсли;
	
	ВидимостьПанелиИсправления = ЭтоДокументИсправление Или (
		((ДоступноИсправлениеДокумента И Форма.ИспользоватьИсправлениеДокумента) Или (ДоступноСторнированиеДокумента И Форма.ИспользоватьСторнированиеДокумента))
			И Форма[ИмяОсновногоРеквизита].Проведен И Форма.ИсправлениеДоступно);
	
	Форма.Элементы.ГруппаИсправление.Видимость = ВидимостьПанелиИсправления;
	
	Если Не ВидимостьПанелиИсправления Тогда 
		Возврат;
	КонецЕсли;
	
	ВидимостьКомандыИсправить = Ложь;
	Если Форма.ИспользоватьИсправлениеДокумента Тогда
		ВидимостьКомандыИсправить = Не (?(Форма.ИспользоватьСторнированиеДокумента, Форма.ДокументСторнирован, Ложь)
												Или ?(Форма.ИспользоватьИсправлениеДокумента, Форма.ДокументИсправлен, Ложь))
												И Форма.ИсправлениеДоступно
												И ДоступноИсправлениеДокумента;
		Форма.Элементы.Исправить.Видимость = ВидимостьКомандыИсправить;
		Если ЭтоДокументИсправление Тогда
			Форма.Элементы.Исправить.Заголовок = НСтр("ru = 'Исправить повторно';
														|en = 'Correct again'");
		Иначе
			Форма.Элементы.Исправить.Заголовок = НСтр("ru = 'Исправить';
														|en = 'Correct'");
		КонецЕсли;
	КонецЕсли;
	
	ВидимостьКомандыСторнировать = Ложь;
	Если Форма.ИспользоватьСторнированиеДокумента Тогда
		ВидимостьКомандыСторнировать = Не (?(Форма.ИспользоватьСторнированиеДокумента, Форма.ДокументСторнирован, Ложь)
												Или ?(Форма.ИспользоватьИсправлениеДокумента, Форма.ДокументИсправлен, Ложь))
												И Форма.ИсправлениеДоступно
												И ДоступноСторнированиеДокумента;
		Форма.Элементы.Сторнировать.Видимость = ВидимостьКомандыСторнировать;
	КонецЕсли;
	
	СтатусВнимание = Истина;
	КрасныйТекст = Ложь;
	ТекстНадписи = "";
	
	Если Форма.ИспользоватьИсправлениеДокумента И Форма.ДокументИсправлен Тогда
		Если ЭтоДокументИсправление Тогда
			ТекстНадписи = НСтр("ru = 'Документ является исправлением другого документа. В свою очередь документ повторно исправлен и его редактирование невозможно';
								|en = 'The document is a correction document of the previous document. In its turn, the document was corrected again and cannot be edited'");
		Иначе
			ТекстНадписи = НСтр("ru = 'Документ исправлен и его редактирование невозможно';
								|en = 'Document is corrected and cannot be modified'");
		КонецЕсли;
		КрасныйТекст = Истина;
		
	ИначеЕсли Форма.ИспользоватьСторнированиеДокумента И Форма.ДокументСторнирован Тогда
		Если ЭтоДокументИсправление Тогда
			ТекстНадписи = НСтр("ru = 'Документ является исправлением другого документа. В свою очередь документ сторнирован и его редактирование невозможно';
								|en = 'The document is a correction document of the previous period. In its turn, the document is reversed and cannot be edited'");
		Иначе
			ТекстНадписи = НСтр("ru = 'Документ сторнирован и его редактирование невозможно';
								|en = 'Document is reversed and cannot be edited'");
		КонецЕсли;
		КрасныйТекст = Истина;
		
	ИначеЕсли Форма.ИспользоватьИсправлениеДокумента И ЭтоДокументИсправление Тогда
		ТекстНадписи = НСтр("ru = 'Документ является исправлением другого документа';
							|en = 'The document is a correction document of the previous document'");
		
	ИначеЕсли РежимИсправления = "РасчетЗарплаты" И (Форма.ИспользоватьИсправлениеДокумента Или Форма.ИспользоватьСторнированиеДокумента) Тогда
		ПериодРегистрации = Форма[ИмяОсновногоРеквизита][ПолеПериодРегистрации];
		ПредставлениеПериода = Формат(ПериодРегистрации, "ДФ='ММММ гггг ""г""'");
		РедактированиеНеРекомендуется = Истина;
		
		Если Форма[ИмяОсновногоРеквизита].Свойство("Организация") Тогда
			Организация = Форма[ИмяОсновногоРеквизита].Организация;
			Ссылка = Форма[ИмяОсновногоРеквизита].Ссылка;
			Если Форма.ПроведенаВыплатаЗарплаты = НеОпределено Или Форма.ПроизведеноОтражение = НеОпределено Тогда
				ИсправлениеДокументовЗарплатаКадрыВызовСервера.ЗаполнитьВыплатаПроизводиласьОтражениеВУчетеПроизводилось(Организация, Ссылка, ПериодРегистрации, Форма.ПроведенаВыплатаЗарплаты, Форма.ПроизведеноОтражение)
			КонецЕсли;
			
			Если Форма.ПроведенаВыплатаЗарплаты И Форма.ПроизведеноОтражение Тогда
				ТекстНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'За %1 уже проведены выплата и отражение зарплаты в бухгалтерском учете.';
						|en = 'Salary is already paid and recorded in bookkeeping for %1.'"), ПредставлениеПериода);
					
			ИначеЕсли Форма.ПроведенаВыплатаЗарплаты Тогда
				ТекстНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Выплата зарплаты за %1 уже проведена.';
						|en = 'Salary for %1 is already paid.'"), ПредставлениеПериода);
					
			ИначеЕсли Форма.ПроизведеноОтражение Тогда
				ТекстНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Произведено отражение зарплаты в бухгалтерском учете за %1.';
						|en = 'Salary is recorded in bookkeeping for %1.'"), ПредставлениеПериода);
					
			Иначе
				РедактированиеНеРекомендуется = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ВидимостьКомандыИсправить Или ВидимостьКомандыСторнировать Тогда
			
			Если РедактированиеНеРекомендуется Тогда
				ТекстНадписи = ТекстНадписи + ?(ПустаяСтрока(ТекстНадписи), "", " ");
				ТекстНадписи = ТекстНадписи + НСтр("ru = 'Редактирование этого документа не рекомендуется.';
													|en = 'It is not recommended that you edit this document.'");
			Иначе
				СтатусВнимание = Ложь;
			КонецЕсли;
			
			ТекстНадписи = ТекстНадписи + ?(ПустаяСтрока(ТекстНадписи), "", Символы.ПС);
			
			Если Не РедактированиеНеРекомендуется И ВидимостьКомандыИсправить И ВидимостьКомандыСторнировать Тогда
				ТекстНадписи = ТекстНадписи
					+ НСтр("ru = 'Если необходимо внести исправление, но при этом сохранить данный экземпляр документа, воспользуйтесь командой Исправить или Сторнировать';
							|en = 'If you want to correct but retain this document copy, click ""Correct"" or ""Reverse""  '");
			ИначеЕсли ВидимостьКомандыИсправить И ВидимостьКомандыСторнировать Тогда
				ТекстНадписи = ТекстНадписи
					+ НСтр("ru = 'Воспользуйтесь командой Исправить для исправления этого документа или Сторнировать для его отмены';
							|en = 'Use the ""Correct"" command to correct this document or ""Reverse"" to cancel it'"); 
			ИначеЕсли ВидимостьКомандыИсправить Тогда
				ТекстНадписи = ТекстНадписи
					+ НСтр("ru = 'Воспользуйтесь командой Исправить для исправления этого документа';
							|en = 'To correct this document, use the ""Correct"" command'");
			ИначеЕсли ВидимостьКомандыСторнировать Тогда
				ТекстНадписи = ТекстНадписи
					+ НСтр("ru = 'Воспользуйтесь командой Сторнировать для отмены этого документа';
							|en = 'Use the Reverse command to cancel this document'");
			КонецЕсли;
		КонецЕсли;	
			
	ИначеЕсли РежимИсправления = "ПериодическиеСведения" И (Форма.ИспользоватьИсправлениеДокумента Или Форма.ИспользоватьСторнированиеДокумента) Тогда
		ТекстНадписи = НСтр("ru = 'Если необходимо внести исправление, но при этом сохранить данный экземпляр документа, воспользуйтесь командой Исправить';
							|en = 'If you want to correct but retain this document copy, click Correct   '");
		
	КонецЕсли;
	
	Форма.Элементы.ИсправлениеКартинка.Картинка = ?(СтатусВнимание, БиблиотекаКартинок.Предупреждение, БиблиотекаКартинок.Информация);	
	ИсправлениеИнфоНадпись = Форма.Элементы.ИсправлениеКартинка.РасширеннаяПодсказка;
	ИсправлениеИнфоНадпись.ЦветТекста = ?(КрасныйТекст, Форма.ИсправлениеЦветОсобогоТекста, Форма.ИсправлениеПоясняющийТекст);
	ИсправлениеИнфоНадпись.Заголовок = ТекстНадписи;
	
	Если Форма.ИспользоватьИсправлениеДокумента Тогда
		Форма.Элементы.ПерейтиКИсправлению.Видимость = Форма.ДокументИсправлен;
	КонецЕсли;
	
	Если Форма.ИспользоватьСторнированиеДокумента  Тогда
		Форма.Элементы.ПерейтиКСторно.Видимость = Форма.ДокументСторнирован И Форма.ДоступноЧтениеСторнирование;
	КонецЕсли;
	
	Если Форма.ИспользоватьИсправлениеДокумента Или Форма.ИспользоватьСторнированиеДокумента Тогда
		Форма.Элементы.ПерейтиКИсправленному.Видимость = ЭтоДокументИсправление;
	КонецЕсли;
	
КонецПроцедуры

Функция УтверждениеДокумента(Форма, ГруппаФормы, ДокументУтвержден = Истина)
	
	Если ГруппаФормы = Неопределено Тогда
		Возврат ДокументУтвержден;
	КонецЕсли;
	
	Для каждого ПодчиненныйЭлемент Из ГруппаФормы.ПодчиненныеЭлементы Цикл
		Если ПодчиненныйЭлемент.Вид = ВидПоляФормы.ПолеФлажка Тогда
			ДокументУтвержден = Форма.Объект[ПодчиненныйЭлемент.Имя];
		КонецЕсли;
		Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ГруппаФормы") Или ТипЗнч(ПодчиненныйЭлемент) = Тип("ТаблицаФормы") Тогда
			ДокументУтвержден = УтверждениеДокумента(Форма, ПодчиненныйЭлемент, ДокументУтвержден);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументУтвержден;
	
КонецФункции

#КонецОбласти
