
////////////////////////////////////////////////////////////////////////////////
// Напоминания клиент
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Процедура для вывода активных напоминаний.
//
// Параметры:
//  НеОткрыватьДляРабочегоСтола	 - Булево - Не открывать для рабочего стола.
//
Процедура ВывестиАктивныеНапоминания(НеОткрыватьДляРабочегоСтола = Истина) Экспорт
 	МассивНапоминаний = CRM_НапоминанияСервер.ПолучитьНапоминания();
	// +Рабочий стол
	РабочийСтолАктивен = Ложь;
	Если ск_глСтекФормРабочегоСтола <> Неопределено Тогда
		Для Каждого ФормаРабочегоСтола Из ск_глСтекФормРабочегоСтола Цикл
			Если ФормаРабочегоСтола.Значение.Открыта() Тогда
				CRM_РабочийСтолКлиент.ОбновитьТаблицуНапоминаний(ФормаРабочегоСтола.Значение, МассивНапоминаний);
				РабочийСтолАктивен = Истина
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	Если НеОткрыватьДляРабочегоСтола И РабочийСтолАктивен Тогда
		Возврат;
	КонецЕсли;
	// -Рабочий стол
#Если ВебКлиент Тогда
	ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
#Иначе	
	Если ГлФормаНапоминанийCRM = Неопределено Тогда
		ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
	КонецЕсли;
#КонецЕсли
	
	Если (МассивНапоминаний.Количество() = 0) И НЕ ГлФормаНапоминанийCRM.Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураНовыхНапоминаний = Новый Структура("ПрочиеНапоминания");
	СтруктураНовыхНапоминаний.Вставить("МассивНапоминаний", МассивНапоминаний);
	
	ГлФормаНапоминанийCRM.ОбновитьТаблицуНапоминаний(СтруктураНовыхНапоминаний);
КонецПроцедуры // ВывестиАктивныеНапоминания()

// Функция формирования ключа записи по строке.
//
// Параметры:
//	Строка	- Строка	- Строка с ключом записи.
//
// Возвращаемое значение:
//	Структура	- Ключ записи
//
Функция СформироватьКлючЗаписиПоСтроке(Строка) Экспорт
	Ключ = Новый Структура();
	Ключ.Вставить("Пользователь",	Строка.Пользователь);
	Ключ.Вставить("Предмет",		Строка.Предмет);
	Если ТипЗнч(Строка.Предмет) = Тип("СправочникСсылка.УчетныеЗаписиЭлектроннойПочты") И ЗначениеЗаполнено(Строка.ВидОповещения) Тогда
		Ключ.Вставить("Содержание",		Строка.Содержание);
	Иначе	
		Ключ.Вставить("ДатаНачала",		Строка.ДатаНачала);
		Ключ.Вставить("ДатаОповещения",	Строка.ДатаОповещения);
	КонецЕсли;
	Возврат Ключ;
КонецФункции // СформироватьКлючЗаписиПоСтроке()

// подключает обработчик оповещения о входящих письмах.
//
// Параметры:
//  ЗапуститьСразу	 - Булево - Запустить сразу. 
//
Процедура ПодключитьОбработчикОповещенияВходящиеПисьма(ЗапуститьСразу = Ложь) Экспорт
	
	// Получим период обновления напоминаний из настроек параметров учета.
	ВходящиеПисьмаПериодОбновления = CRM_НапоминанияСервер.ПолучитьПериодОбновленияНапоминанийОВходящихПисьмах();
	
	Если ВходящиеПисьмаПериодОбновления > 0 Тогда
		
		Если ЗапуститьСразу Тогда
		
			ВывестиАктивныеНапоминанияОВходящихПисьмах();	
		
		КонецЕсли;		
		
		ПодключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминанияОВходящихПисьмах", ВходящиеПисьмаПериодОбновления);
		
	КонецЕсли;	
	
КонецПроцедуры// ПодключитьОбработчикОжиданияВходящиеПисьма()
	
//  Подключает обработчик оповещения о входящих письмах.
//
Процедура ОтключитьОбработчикОповещенияВходящиеПисьма() Экспорт
	
	ОтключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминанияОВходящихПисьмах");
		
КонецПроцедуры// ПодключитьОбработчикОжиданияВходящиеПисьма()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик события "ПриНачалеРаботыСистемы".
//
// Параметры:
//	Нет.
//
Процедура ПриНачалеРаботыСистемы() Экспорт
	
	ВывестиАктивныеНапоминания();
	
	// Получим период обновления напоминаний из настроек параметров учета.
	ПериодОбновления = CRM_НапоминанияСервер.ПолучитьПериодОбновленияНапоминаний();
	
	ПодключитьОбработчикОжидания("CRM_ВывестиАктивныеНапоминания", ПериодОбновления);
	
	ПодключитьОбработчикОповещенияВходящиеПисьма(Истина);
	
КонецПроцедуры // ПриНачалеРаботыСистемы()

// Процедура для вывода активных напоминаний.
//
// Параметры:
//	Нет.
//
Процедура ВывестиАктивныеНапоминанияОВходящихПисьмах() Экспорт
 	МассивНапоминаний = CRM_НапоминанияСервер.ПолучитьНапоминания(Истина);
#Если ВебКлиент Тогда
	ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
#Иначе	
	Если ГлФормаНапоминанийCRM = Неопределено Тогда
		ГлФормаНапоминанийCRM = ПолучитьФорму("ОбщаяФорма.CRM_ФормаНапоминаний", , , "Уникум");
	КонецЕсли;
#КонецЕсли	
	Если (МассивНапоминаний.Количество() = 0) И НЕ ГлФормаНапоминанийCRM.Открыта() Тогда
		Возврат;
	КонецЕсли;
	СтруктураНовыхНапоминаний = Новый Структура("НапоминанияОВходящихПисьмах");
	СтруктураНовыхНапоминаний.Вставить("МассивНапоминаний", МассивНапоминаний);
	ГлФормаНапоминанийCRM.ОбновитьТаблицуНапоминаний(СтруктураНовыхНапоминаний);
КонецПроцедуры // ВывестиАктивныеНапоминания()

#КонецОбласти
