
////////////////////////////////////////////////////////////////////////////////
// CRM работа с мессенджерами сервер повт исп
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

#Область ОбщиеПроцедурыИФункции

// Функция - Найти контакт по контактной информации
//
// Параметры:
//  ИДПользователя	 - Строка - ID Пользователя.
//  УчетнаяЗапись	 - СправочникСсылка.CRM_УчетныеЗаписиМессенджеров - Учетная запись.
//  ТипКИ			 - ПеречислениеСсылка.ТипыКонтактнойИнформации	 - Тип контактной информации.
// 
// Возвращаемое значение:
//  СправочникСсылка.ВидыКонтактнойИнформации - Найденный контакт.
//
Функция НайтиКонтактПоКонтактнойИнформации(ИДПользователя, УчетнаяЗапись, ТипКИ) Экспорт

	СоответствиеКонтактов = Новый Соответствие;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Контакты.Ссылка,
	|	Контакты.ИДПользователя КАК ИДПользователя
	|ИЗ
	|	(ВЫБРАТЬ
	|		КонтактнаяИнформация.Ссылка КАК Ссылка,
	|		КонтактнаяИнформация.Представление КАК ИДПользователя
	|	ИЗ
	|		Справочник.Пользователи.КонтактнаяИнформация КАК КонтактнаяИнформация
	|	ГДЕ
	|		КонтактнаяИнформация.Представление = &ИДПользователя
	|		И КонтактнаяИнформация.Тип = &Тип
	|		И НЕ(КонтактнаяИнформация.Ссылка.ПометкаУдаления)
	|";
	
	МассивОписанияТиповКонтактов = ВзаимодействияКлиентСервер.ОписанияКонтактов();
	Для каждого ЭлементМассиваОписания Из МассивОписанияТиповКонтактов Цикл
		
		Если ЭлементМассиваОписания.Имя = "Пользователи" Тогда
			Продолжить;
		КонецЕсли;	
		
		Запрос.Текст = Запрос.Текст + "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|		КонтактнаяИнформация.Ссылка КАК Ссылка,
		|		КонтактнаяИнформация.Представление КАК ИДПользователя
		|ИЗ
		|	Справочник." + ЭлементМассиваОписания.Имя + ".КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Представление = &ИДПользователя
		|	И КонтактнаяИнформация.Тип = &Тип
		|	И (НЕ КонтактнаяИнформация.Ссылка.ПометкаУдаления)
		|";
		
	КонецЦикла;	
	
	Запрос.Текст = Запрос.Текст + ") КАК Контакты
	|ИТОГИ ПО
	|	ИДПользователя";

	Запрос.УстановитьПараметр("ИДПользователя", ИДПользователя);
	Запрос.УстановитьПараметр("Тип", ТипКИ);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		ВыборкаПоСсылкам = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если (ВыборкаПоСсылкам.Следующий()) Тогда
			Возврат ВыборкаПоСсылкам.Ссылка;
		КонецЕсли;
	КонецЦикла;

	Возврат Неопределено;

КонецФункции

// Получить вид контактной информации мессенджера
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.CRM_УчетныеЗаписиМессенджеров - Учетная запись.
//  Контакт		  - СправочникСсылка - Тип контактного лица.
// 
// Возвращаемое значение:
//  СправочникСсылка.ВидыКонтактнойИнформации - Найденный контакт.
//
Функция ПолучитьВидКИМессенджера(УчетнаяЗапись, Контакт) Экспорт
	
	МодульМенеджера = CRM_РаботаСМессенджерамиСерверПовтИсп.МенеджерМессенджера(УчетнаяЗапись.ТипМессенджера);
	Возврат МодульМенеджера.ПолучитьВидКИМессенджера(Контакт);
	
КонецФункции

// Тип контактной информации мессенджера
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.CRM_УчетныеЗаписиМессенджеров - Учетная запись.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыКонтактнойИнформации - Тип контакной информации. 
//
Функция ТипКИМессенджера(УчетнаяЗапись) Экспорт

	МодульМенеджера = CRM_РаботаСМессенджерамиСерверПовтИсп.МенеджерМессенджера(УчетнаяЗапись.ТипМессенджера);
	Возврат МодульМенеджера.ТипКИМессенджера();
	
КонецФункции

// Возвращает структуру с параметрами доступа.
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.CRM_УчетныеЗаписиМессенджеров - Учетная запись.
// 
// Возвращаемое значение:
//  Структура - Структура с параметрами доступа.
//
Функция СтруктураПараметровДоступа(УчетнаяЗапись) Экспорт
	Возврат УчетнаяЗапись.ХранилищеПараметровДоступа.Получить();
КонецФункции

// Функция - Начало адреса страницы пользователя
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.CRM_УчетныеЗаписиМессенджеров - Учетная запись.
// 
// Возвращаемое значение:
//  Строка - Строка с адресом страницы.
//
Функция НачалоАдресаСтраницыПользователя(УчетнаяЗапись) Экспорт
	МодульМенеджера = CRM_РаботаСМессенджерамиСерверПовтИсп.МенеджерМессенджера(УчетнаяЗапись.ТипМессенджера);
	Возврат МодульМенеджера.НачалоАдресаСтраницыПользователя();
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция МенеджерМессенджера(ТипМессенджера) Экспорт
	
	МодульМенеджера = Обработки["CRM_РаботаСМессенджером"+Строка(ТипМессенджера)];
	Возврат МодульМенеджера;
	
КонецФункции

#КонецОбласти