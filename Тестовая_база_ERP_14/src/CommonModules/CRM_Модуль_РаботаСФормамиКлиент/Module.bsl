Процедура ВыполнитьПереопределяемуюКоманду(Форма, Команда, ДополнительныеПараметры) Экспорт
	Если Команда.Имя = "СкрытьПоказатьИнтересы"	Тогда
		КомандаСкрытьПоказатьИнтересы(Форма);
	КонецЕсли;	
	
	Если Команда.Имя = "CRM_ИнтересСтрокой"	Тогда
		Если ПустаяСтрока(Форма.CRM_ИнтересСтрокой) И Форма.CRM_ТаблицаИнтересов.Количество() > 0 Тогда
			// нажата кнопка очистить
			ОчиститьТекущийИнтерес(Форма);
		Иначе
			// нажата кнопка выбора
			ИнтересНачалоВыбора(Форма);
		КонецЕсли;	
	КонецЕсли;
	
	Если Команда.Имя = "Основной"	Тогда
		ТекущиеДанные = Форма.Элементы.CRM_ТаблицаИнтересов.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Если ТекущиеДанные.Партнер <> Форма.Объект.Партнер Тогда
			Возврат;
		КонецЕсли;			
		Форма.CRM_ИнтересСтрокой	= CRM_ОбщегоНазначенияСервер.СформироватьПредставлениеИнтересаСтрокой(ТекущиеДанные.Ссылка);
		МассивСтрок = Форма.CRM_ТаблицаИнтересов.НайтиСтроки(новый Структура("Ссылка",ТекущиеДанные.Ссылка));
		Форма.CRM_ТаблицаИнтересов.Сдвинуть(Форма.CRM_ТаблицаИнтересов.Индекс(МассивСтрок[0]),-Форма.CRM_ТаблицаИнтересов.Индекс(МассивСтрок[0]));
		
		//УдалитьИнтересыПрочихПартнеров(Форма);
	КонецЕсли;	
	
	Если Команда.Имя = "ДобавитьИнтерес"	Тогда
		ИнтересНачалоВыбора(Форма, Ложь);
	КонецЕсли;
	
	Если Команда.Имя = "ОткрытьИнтерес"	Тогда
		Если Форма.CRM_ТаблицаИнтересов.Количество() > 0 Тогда
			ПоказатьЗначение(,Форма.CRM_ТаблицаИнтересов[0].Ссылка);
		КонецЕсли;
	КонецЕсли;
	
	Если Команда.Имя = "УдалитьИнтерес"	Тогда
		ТекущиеДанные = Форма.Элементы.CRM_ТаблицаИнтересов.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		МассивСтрок = Форма.CRM_ТаблицаИнтересов.НайтиСтроки(новый Структура("Ссылка",ТекущиеДанные.Ссылка));
		Форма.CRM_ТаблицаИнтересов.Удалить(Форма.CRM_ТаблицаИнтересов.Индекс(МассивСтрок[0]));
		Если Форма.CRM_ТаблицаИнтересов.Количество() > 0 Тогда
			Форма.CRM_ИнтересСтрокой	=  CRM_ОбщегоНазначенияСервер.СформироватьПредставлениеИнтересаСтрокой(Форма.CRM_ТаблицаИнтересов[0].Ссылка);
		Иначе	
			Форма.CRM_ИнтересСтрокой	= "";
		КонецЕсли;	
		//УдалитьИнтересыПрочихПартнеров(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура КомандаСкрытьПоказатьИнтересы(Форма)
	
	Форма.Элементы.СкрытьПоказатьИнтересы.Пометка = НЕ Форма.Элементы.СкрытьПоказатьИнтересы.Пометка;
	Форма.Элементы.ГруппаCRM_ТаблицаИнтересов.Видимость = Форма.Элементы.СкрытьПоказатьИнтересы.Пометка;
	
	//УдалитьИнтересыПрочихПартнеров(Форма);	
КонецПроцедуры

Процедура ИнтересНачалоВыбора(Форма, УстановитьОсновным = Истина)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ПометкаУдаления", Ложь);
	СтруктураОтбора.Вставить("Партнер", Форма.Объект.Партнер);
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("РежимВыбора"			, Истина);
	ПараметрыФормыВыбора.Вставить("МножественныйВыбор"	, Ложь);
	ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе"	, Истина);
	ПараметрыФормыВыбора.Вставить("ЭтоВыборИнтереса"	, Истина);	
	
	ПараметрыФормыВыбора.Вставить("Отбор"				, СтруктураОтбора);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИнтересОкончаниеВыбора", ЭтотОбъект, Новый Структура("Форма,УстановитьОсновным", Форма, УстановитьОсновным));
	
	ОткрытьФорму("Документ.CRM_Интерес.Форма.ФормаСписка", ПараметрыФормыВыбора, , Новый УникальныйИдентификатор, ВариантОткрытияОкна.ОтдельноеОкно,, ОписаниеОповещения);
	
КонецПроцедуры


Процедура ИнтересОкончаниеВыбора(ВыбранноеЗначение, ДопПараметры) Экспорт
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	Форма = ДопПараметры.Форма;	
	Если ДопПараметры.УстановитьОсновным Тогда
		Форма.CRM_ИнтересСтрокой	= CRM_ОбщегоНазначенияСервер.СформироватьПредставлениеИнтересаСтрокой(ВыбранноеЗначение);
	КонецЕсли;	
	
	МассивСтрок = Форма.CRM_ТаблицаИнтересов.НайтиСтроки(новый Структура("Ссылка",ВыбранноеЗначение));
	
	Если МассивСтрок.Количество() = 0 Тогда
		НоваяСтрока = Форма.CRM_ТаблицаИнтересов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, CRM_Модуль_РаботаСФормамиСервер.ПолучитьСтруктуруСтрокиИнтересов(ВыбранноеЗначение));
		Если ДопПараметры.УстановитьОсновным Тогда
			Форма.CRM_ТаблицаИнтересов.Сдвинуть(Форма.CRM_ТаблицаИнтересов.Индекс(НоваяСтрока),-Форма.CRM_ТаблицаИнтересов.Индекс(НоваяСтрока));
		КонецЕсли;	
	ИначеЕсли ДопПараметры.УстановитьОсновным Тогда	
		Форма.CRM_ТаблицаИнтересов.Сдвинуть(Форма.CRM_ТаблицаИнтересов.Индекс(МассивСтрок[0]),-Форма.CRM_ТаблицаИнтересов.Индекс(МассивСтрок[0]));
	КонецЕсли;
	
	//УдалитьИнтересыПрочихПартнеров(Форма);
КонецПроцедуры

Процедура ОчиститьТекущийИнтерес(Форма, УстановитьОсновным = Истина)
	
	Форма.CRM_ТаблицаИнтересов.Удалить(0);
	
	Если Форма.CRM_ТаблицаИнтересов.Количество() > 0 Тогда
		
		ТекущаяСтрока = Форма.CRM_ТаблицаИнтересов[0];
		
		Если ТекущаяСтрока.Партнер = Форма.Объект.Партнер Тогда
			Форма.CRM_ИнтересСтрокой	= CRM_ОбщегоНазначенияСервер.СформироватьПредставлениеИнтересаСтрокой(ТекущаяСтрока.Ссылка);			
		КонецЕсли;
		
	КонецЕсли;
	
	//УдалитьИнтересыПрочихПартнеров(Форма);
	
КонецПроцедуры

Процедура УдалитьИнтересыПрочихПартнеров(Форма)
	
	ИндексТекущейСтроки = 0;
	
	Пока ИндексТекущейСтроки < Форма.CRM_ТаблицаИнтересов.Количество() Цикл 
		
		ТекущаяСтрока = Форма.CRM_ТаблицаИнтересов[ИндексТекущейСтроки];
		
		Если ТекущаяСтрока.Партнер = Форма.Объект.Партнер Тогда
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
		Иначе	
			Форма.CRM_ТаблицаИнтересов.Удалить(ТекущаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры// УдалитьИнтересыПрочихПартнеров(Форма)	
	
