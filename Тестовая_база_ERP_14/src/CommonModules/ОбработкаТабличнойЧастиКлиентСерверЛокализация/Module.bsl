#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьСтруктуруКэшируемыеЗначения(КэшированныеЗначения) Экспорт

	//++ Локализация

	КэшированныеЗначения.Вставить("ПризнакиКатегорииЭксплуатации", Новый Соответствие);
	СтруктураПустойКатегории = Новый Структура;
	//++ НЕ УТ
	СтруктураПустойКатегории.Вставить("ИнвентарныйУчет", Ложь);
	СтруктураПустойКатегории.Вставить("СпособПогашенияСтоимостиБУ", ПредопределенноеЗначение("Перечисление.СпособыПогашенияСтоимостиТМЦ.ПоСроку"));
	//-- НЕ УТ
	СтруктураПустойКатегории.Вставить("СрокЭксплуатации", 0);
	СтруктураПустойКатегории.Вставить("СтатьяРасходов", ПредопределенноеЗначение("ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка"));
	КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Вставить(ПредопределенноеЗначение("Справочник.КатегорииЭксплуатации.ПустаяСсылка"), СтруктураПустойКатегории);
	
	//++ НЕ УТ
	КэшированныеЗначения.Вставить("ПризнакиПартииТМЦВЭксплуатации", Новый Соответствие);
	СтруктураПустойПартии = Новый Структура;
	СтруктураПустойПартии.Вставить("ИнвентарныйУчет", Ложь);
	КэшированныеЗначения.ПризнакиПартииТМЦВЭксплуатации.Вставить(ПредопределенноеЗначение("Справочник.ПартииТМЦВЭксплуатации.ПустаяСсылка"), СтруктураПустойПартии);
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры

#Область Локализация

//++ Локализация

// Заполняет признаки категории эксплуатации в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - Кэшированные значения табличной части.
//
Процедура ЗаполнитьПризнакиКатегорииЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакиКатегорииЭксплуатации") Тогда
		
		СтруктураПризнаков = КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Получить(
			ТекущаяСтрока.КатегорияЭксплуатации);
			
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Если СтруктураПризнаков = Неопределено Тогда
			СтруктураПризнаков = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ТекущаяСтрока.КатегорияЭксплуатации,
				"ИнвентарныйУчет, СпособПогашенияСтоимостиБУ, СрокЭксплуатации, СтатьяРасходов");
			КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Вставить(
				ТекущаяСтрока.КатегорияЭксплуатации,
				СтруктураПризнаков);
		КонецЕсли;
	#КонецЕсли
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураПризнаков);
		
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

// Заполняет признаки партии ТМЦ, используемой в эксплуатации, в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока - Структура - структура со свойствами строки документа.
//	СтруктураДействий - Структура - структура с действиями, которые нужно произвести.
//	КэшированныеЗначения - Структура - Кэшированные значения табличной части.
//
Процедура ЗаполнитьПризнакиПартииТМЦВЭксплуатации(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакиПартииТМЦВЭксплуатации") Тогда
		
		СтруктураПризнаков = КэшированныеЗначения.ПризнакиПартииТМЦВЭксплуатации.Получить(
			ТекущаяСтрока.ПартияТМЦВЭксплуатации);
			
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Если СтруктураПризнаков = Неопределено Тогда
			СтруктураПризнаков = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ТекущаяСтрока.ПартияТМЦВЭксплуатации,
				"ИнвентарныйУчет");
			КэшированныеЗначения.ПризнакиКатегорииЭксплуатации.Вставить(
				ТекущаяСтрока.ПартияТМЦВЭксплуатации,
				СтруктураПризнаков);
		КонецЕсли;
	#КонецЕсли
		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураПризнаков);
		
		СтруктураКоличества = Новый Структура("Количество, КоличествоУпаковок", 0, 0);
		ЗаполнитьЗначенияСвойств(СтруктураКоличества, ТекущаяСтрока);
		Если СтруктураКоличества.Количество = 0 Тогда
			СтруктураКоличества.Количество = 1;
		КонецЕсли;
		Если СтруктураКоличества.КоличествоУпаковок = 0 Тогда
			СтруктураКоличества.КоличествоУпаковок = 1;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураКоличества);
		
	КонецЕсли;
	
КонецПроцедуры

//-- НЕ УТ

//-- Локализация

#КонецОбласти

#КонецОбласти
