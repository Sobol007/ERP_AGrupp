
////////////////////////////////////////////////////////////////////////////////
// Маркетинг сервер
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

// Функция возвращает информацию о первичном интересе.
//
// Параметры:
//	Период	- Дата				- Период	
//	Сделка	- ДокументСсылка	- Сделка
//	Партнер	- СправочникСсылка	- Партнер.
//
// Возвращаемое значение:
//   Структура - Струкатура параметров первичного интереса.
//
Функция ПолучитьПервичныйИнтерес(Период = Неопределено, Сделка = Неопределено, Партнер = Неопределено) Экспорт
	Если ЗначениеЗаполнено(Сделка) ИЛИ ЗначениеЗаполнено(Партнер) Тогда
		СтрокаУсловия = "";
		Если ЗначениеЗаполнено(Сделка) Тогда
			СтрокаУсловия = ", Сделка = &Сделка";
		КонецЕсли;
		Если ЗначениеЗаполнено(Партнер) Тогда
			СтрокаУсловия = СтрокаУсловия + ?(СтрокаУсловия = "",", Партнер = &Партнер"," И Партнер = &Партнер");
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Сделка",		Сделка);
		Запрос.УстановитьПараметр("Партнер",	Партнер);
		Запрос.УстановитьПараметр("Период",		?(ЗначениеЗаполнено(Период),	Период,	CRM_ОбщегоНазначенияСервер.ПолучитьТекущуюДатуСеанса()));
		Запрос.Текст = "ВЫБРАТЬ
		|	ИсточникиПервичногоИнтересаСрезПоследних.КаналПервичногоИнтереса,
		|	ИсточникиПервичногоИнтересаСрезПоследних.ИсточникПервичногоИнтереса,
		|	ИсточникиПервичногоИнтересаСрезПоследних.Партнер,
		|	ИсточникиПервичногоИнтересаСрезПоследних.Сделка,
		|	ИсточникиПервичногоИнтересаСрезПоследних.Сделка.Дата
		|ИЗ
		|	РегистрСведений.ИсточникиПервичногоИнтереса.СрезПоследних(&Период" + СтрокаУсловия + ") КАК ИсточникиПервичногоИнтересаСрезПоследних ГДЕ КаналПервичногоИнтереса.ПометкаУдаления = ЛОЖЬ
		|	УПОРЯДОЧИТЬ ПО Период УБЫВ";
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		Если Выборка.Следующий() Тогда
			Возврат Выборка;
		КонецЕсли;
	КонецЕсли;
	Возврат Новый Структура("КаналПервичногоИнтереса,ИсточникПервичногоИнтереса,Партнер,Сделка", 
		ПланыВидовХарактеристик.КаналыРекламныхВоздействий.ПустаяСсылка(), Неопределено, Справочники.Партнеры.ПустаяСсылка(),Неопределено);
КонецФункции // ПолучитьПервичныйИнтерес()

// Процедура - Сформировать надпись первичного интереса
//
// Параметры:
//  Ссылка			 - ДокументСсылка - Ссылка на интерес.
//  Интерес			 - СтрокаТаблицыЗначений - Строка с параметрами первичного интереса.
//  АктуальноДо		 - Дата - Дата актуальности. 
//  Надпись			 - Строка - Выводимая надпись.
//  ВыводитьНадпись	 - Булево - Выводить надпись.
//
Процедура СформироватьНадписьПервичногоИнтереса(Ссылка, Интерес, АктуальноДо, Надпись, ВыводитьНадпись) Экспорт
	
	Если ТипЗнч(Интерес.КаналПервичногоИнтереса.ТипЗначения.ПривестиЗначение()) = Тип("ПеречислениеСсылка.CRM_КаналыБезУказанияИсточника") Тогда
		Надпись = НСтр("ru='Ранее зарегистрирован актуальный источник привлечения по каналу ""%КПИ%""';en='Previously, the actual source of attracting through the channel ""% KPI%""'");
		Надпись = СтрЗаменить(Надпись, "%КПИ%", Интерес.КаналПервичногоИнтереса);
	Иначе	
		Надпись = НСтр("ru='Ранее зарегистрирован актуальный источник привлечения по каналу ""%КПИ%"", источник ""%ИПИ%""';en='Earlier, the actual source of attraction was registered through the channel ""% KPI%"", the source ""% IPI%""'"); ;
		Надпись = СтрЗаменить(Надпись, "%КПИ%", Интерес.КаналПервичногоИнтереса);
		Надпись = СтрЗаменить(Надпись, "%ИПИ%", Интерес.ИсточникПервичногоИнтереса);
	КонецЕсли;
	Если Интерес.Сделка = Ссылка Тогда
		ВыводитьНадпись = Ложь;
	КонецЕсли;
	Если ТипЗнч(Интерес.КаналПервичногоИнтереса.ТипЗначения.ПривестиЗначение()) = Тип("СправочникСсылка.МаркетинговыеМероприятия") Тогда
		Если Не Интерес.Актуальность Тогда
			Надпись = Надпись + НСтр("ru='. Срок действия маркетингового мероприятия истек.';en='. The marketing action has expired.'");
		Иначе
			Надпись = Надпись + ?(ЗначениеЗаполнено(АктуальноДо), НСтр("ru = ' (актуально до '") + Формат(АктуальноДо, "ДФ=dd.MM.yyyy") + ")", "");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
